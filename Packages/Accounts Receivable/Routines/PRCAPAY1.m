PRCAPAY1 ;WASH-ISC@ALTOONA,PA/CMS-MAKE A PAYMENT, SUBPROGRAM ;6/17/93  8:57 AM
V ;;4.5;Accounts Receivable;**48,63**;Mar 20, 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
EN(PRCABN,PRCADT,PRPAYMT,BAL,PRCAREP,PINC) ;
 ;create payment trans for bill
 N PRCA,PRCAEN,PRCAP,PRCAPAY,PRCAPB,PRCAPB1,PRCAPPR,PRCATYPE,X,Y,Z
 D SETV^PRCAPAY2 Q:$G(ERR)]""
 D PATTR^PRCAUTL S PRCAP("PB")=PRCAPB
 S PRCABL=$S($D(^PRCA(430,PRCABN,7)):^(7),1:""),PRCAP("IB")=+$P(PRCABL,U,2),PRCAP("AB")=+$P(PRCABL,U,3),PRCAP("MF")=+$P(PRCABL,U,4),PRCAP("CC")=+$P(PRCABL,U,5)
 S PRCAP("PC")=+$P(PRCABL,U,7),PRCAP("IC")=$P(PRCABL,U,8),PRCAP("AC")=$P(PRCABL,U,9),PRCAP("MFP")=$P(PRCABL,U,10),PRCAP("CCP")=$P(PRCABL,U,11)
 S (PRCAP("PC1"),PRCAP("IC1"),PRCAP("AC1"),PRCAP("MC1"),PRCAP("CC1"),PRCAP("REMAIN"))=0
 S PRCAPV("PB")=PRCAP("PB"),PRCAPV("IB")=PRCAP("IB"),PRCAPV("AB")=PRCAP("AB"),PRCAPV("CC")=PRCAP("CC"),PRCAPV("MF")=PRCAP("MF")
MARSH ;check a marshal fee
 I PRCAP("MF")'>0 G COURT
 I PRCAP("MF")'<PRCA("PAYMT") S PRCAP("MF")=PRCAP("MF")-PRCA("PAYMT"),PRCAP("AC")=PRCAP("AC")+PRCA("PAYMT"),PRCAP("MC1")=PRCA("PAYMT"),PRCAP("MFP")=PRCAP("MFP")+PRCAP("MC1"),PRCA("PAYMT")=0 G SETEN
 S PRCAP("AC")=PRCAP("AC")+PRCAP("MF"),PRCA("PAYMT")=PRCA("PAYMT")-PRCAP("MF"),PRCAP("MC1")=PRCAP("MF"),PRCAP("MFP")=PRCAP("MFP")+PRCAP("MC1"),PRCAP("MF")=0
COURT ;check a court cost
 I PRCAP("CC")'>0 G ADMIN
 I PRCAP("CC")'<PRCA("PAYMT") S PRCAP("AC")=PRCAP("AC")+PRCA("PAYMT"),PRCAP("CC")=PRCAP("CC")-PRCA("PAYMT"),PRCAP("CC1")=PRCA("PAYMT"),PRCAP("CCP")=PRCAP("CCP")+PRCAP("CC1"),PRCA("PAYMT")=0 G SETEN
 S PRCAP("AC")=PRCAP("AC")+PRCAP("CC"),PRCA("PAYMT")=PRCA("PAYMT")-PRCAP("CC"),PRCAP("CC1")=PRCAP("CC"),PRCAP("CCP")=PRCAP("CCP")+PRCAP("CC1"),PRCAP("CC")=0
ADMIN ;check an administration balance
 I PRCAP("AB")'>0 G INTR
 I PRCAP("AB")'<PRCA("PAYMT") S PRCAP("AC")=PRCAP("AC")+PRCA("PAYMT"),PRCAP("AB")=PRCAP("AB")-PRCA("PAYMT"),PRCAP("AC1")=PRCA("PAYMT"),PRCA("PAYMT")=0 G SETEN
 S PRCAP("AC")=PRCAP("AC")+PRCAP("AB"),PRCA("PAYMT")=PRCA("PAYMT")-PRCAP("AB"),PRCAP("AC1")=PRCAP("AB"),PRCAP("AB")=0
INTR ;check an interest balance
 I PRCAP("IB")'>0 G PRIN
 I PRCAP("IB")'<PRCA("PAYMT") S PRCAP("IC")=PRCAP("IC")+PRCA("PAYMT"),PRCAP("IB")=PRCAP("IB")-PRCA("PAYMT"),PRCAP("IC1")=PRCA("PAYMT"),PRCA("PAYMT")=0 G SETEN
 S PRCAP("IC")=PRCAP("IC")+PRCAP("IB"),PRCA("PAYMT")=PRCA("PAYMT")-PRCAP("IB"),PRCAP("IC1")=PRCAP("IB"),PRCAP("IB")=0
PRIN ;check a principal balance
 I PRCA("PAYMT")>0 D PRIN1^PRCAPAY2
SETEN ;set entry items
 S ^PRCA(433,PRCAEN,3)=PRCAP("PC1")_U_PRCAP("IC1")_U_PRCAP("AC1")_U_PRCAP("MC1")_U_PRCAP("CC1")
 S DIE="^PRCA(433,",DA=PRCAEN,DR="3///"_0_";5////"_PRCADT_"" D ^DIE K DIE,DR,DA
 S $P(^PRCA(430,PRCABN,7),U,1,5)=PRCAP("PB")_U_PRCAP("IB")_U_PRCAP("AB")_U_PRCAP("MF")_U_PRCAP("CC"),$P(^PRCA(433,PRCAEN,8),U,1,5)=$P(^PRCA(430,PRCABN,7),U,1,5)
 S $P(^PRCA(430,PRCABN,7),U,7,11)=PRCAP("PC")_U_PRCAP("IC")_U_PRCAP("AC")_U_PRCAP("MFP")_U_PRCAP("CCP")
 I ($D(PRCAP("REPAY")))&(PRCAREP>0) S $P(^PRCA(430,PRCABN,5,PRCAREP,0),U,2,3)="1^0",$P(^PRCA(430,PRCABN,5,PRCAREP,0),U,4)=PRCAEN
PAYEND I PRCA("PAYMT")>0 S PRCAMT=PRCA("PAYMT") D MULTPAY^PRCAUT1,TRANUP^PRCAUTL
 S PRCAPB=0 F Z=1:1:5 S PRCAPB=PRCAPB+$P(^PRCA(430,PRCABN,7),U,Z)
 ;I no balance left change to Payment in full and Coll/Close
 I 'PRCAPB D
 .S PRCA("STATUS")=$O(^PRCA(430.3,"AC",108,""))
 .S PRCA("SDT")=PRCADT D UPSTATS^PRCAUT2 K PRCA("STATUS"),PRCA("SDT")
 .S DIE="^PRCA(433,",Z=$O(^PRCA(430.3,"AC",20,"")),DA=PRCAEN
 .S DR="12///^S X=Z" D ^DIE K DR,DIE,Z
 .K ^PRCA(433,"AEOB",PRCABN,DA)
 ;I no balance left on TP bill
 I 'PRCAPB,($P($G(^PRCA(430.2,+$P($G(^PRCA(430,+PRCABN,0)),U,2),0)),U,6)="T") D
 .D BULL^IBCNSBL2(PRCABN,$P($G(^PRCA(430,+PRCABN,0)),"^",3),$$PAID^PRCAFN1(+PRCABN))
 .D PF^RCRCAT("P")
DONE S $P(^PRCA(433,PRCAEN,0),U,4)=2
 S:+$G(PINC) $P(^PRCA(433,PRCAEN,0),U,10)=PINC,PINC=PRCAEN
 S:$G(DCDJ)="" DCDJ=$P($G(^PRCA(430,+PRCABN,6)),U,5)
 I $G(DCDJ)]"" S $P(^PRCA(433,PRCAEN,0),U,7)=$S(DCDJ="DC":"RC",1:DCDJ)
 I PRCAP("REMAIN")>0 S PAMT=+$G(PAMT)+PRCAP("REMAIN")
END K PRCABN,PRCABL,%,PRCAPREV,PRCAREP,PRCAMT,PRCAPB,PRCADT,PRCAXTY
 K PRCADT,PRCAEN,PRCALAST,Z1,PRCADAYS,PRCATY,X1,X2 Q
