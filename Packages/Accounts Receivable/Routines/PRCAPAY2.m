PRCAPAY2 ;WASH-ISC@ALTOONA,PA/CMS-PAYMENT 2ND SUBROUTINE ;7/5/95  3:26 PM
V ;;4.5;Accounts Receivable;**9,63**;Mar 20, 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
PRIN1 ;called by PRCAPAY1
 S PRCAP("PC")=$S(PRCAP("PB")<PRCA("PAYMT"):PRCAP("PB")+PRCAP("PC"),1:PRCAP("PC")+PRCA("PAYMT"))
 S PRCAP("PC1")=$S(PRCAP("PB")<PRCA("PAYMT"):PRCAP("PB"),1:PRCA("PAYMT")),PRCAP("PB")=PRCAP("PB")-PRCA("PAYMT"),PRCAP("REMAIN")=$S(PRCAP("PB")<0:-PRCAP("PB"),1:0) S:PRCAP("PB")<0 PRCAP("PB")=0
 Q
 ;
SETV ;Create payment transaction and update files called by PRCAPAY1
 D SETTR^PRCAUTL I '$D(PRCAEN) S ERR="TRANSACTION CANNOT BE ESTABLISHED" Q
 S PRCA("PAYMT")=PRPAYMT,PRCAPB1=$G(^PRCA(430,PRCABN,7)),PRCAPB1=+$P(PRCAPB1,U)+$P(PRCAPB1,U,2)+$P(PRCAPB1,U,3)+$P(PRCAPB1,U,4)+$P(PRCAPB1,U,5)
 I PRCAPB1'=+BAL S ERR="POSTING INTERRUPTION" Q
 S PRCATYPE=2,PRCA("STATUS")=$P(^PRCA(430,PRCABN,0),U,8),PRCA("SEG")=$P(^(0),U,21),PRCAPPR=$P(^(0),U,18),PRCAPB=+^PRCA(430,PRCABN,7)
 I PRCAREP S PRCAREP=0,PRCAP("REPAY")="" S X=0 F  S X=$O(^PRCA(430,PRCABN,5,X)) Q:'X  I +$P($G(^PRCA(430,PRCABN,5,X,0)),U,2)<1 S PRCAREP=X Q
 S DIE="^PRCA(433,",DR="[PRCA BATCH PAYMENT]",DA=PRCAEN D ^DIE
 Q
CKDAT ;check data
 N ABN,BAL,X,Y,%DT
 S %DT="XT",X=+$G(DOP) D ^%DT I Y<1 S ERR="INVALID DATE OF PAYMENT '"_$S('$G(DOP):"MISSING",1:DOP)_"'" Q
 S:'$D(PRCARN) PRCARN=""
 I $G(PRCARN)]"",(($L(PRCARN)>10)!($L(PRCARN)<7)) S ERR="INVALID RECEIPT NUMBER '"_PRCARN_"'" Q
 I $G(^VA(200,+$G(PRCAPER),0))']"" S ERR="INVALID PROCESSED BY '"_$S('$G(PRCAPER):"MISSING",1:PRCAPER)_"'" Q
 S:$G(PAMT)["$" PAMT=$P(PAMT,"$",2) I '$G(PAMT)!(+PAMT'?.N.1".".2N)!(+PAMT>999999.99)!(+PAMT'>0) S ERR="INVALID PAYMENT AMOUNT '"_$S($G(PAMT)']"":"MISSING",1:PAMT)_"'" Q
 I $G(ACCT)']""!('$G(ACCT)) S ERR=" MISSING BILL NUMBER OR DEBTOR " Q
 I (ACCT'[";DPT("),(ACCT'[";PRCA(430,") S ERR="INVALID BILL NUMBER OR DEBTOR "_ACCT Q
 I $G(@(U_$P(ACCT,";",2)_+ACCT_",0)"))']"" S ERR="INVALID BILL NUMBER OR DEBTOR "_ACCT Q
 I $G(DCDJ)]"" I (DCDJ'="RC")&(DCDJ'="DC")&(DCDJ'="DOJ")&(DCDJ'="IRS") S ERR="INVALID DC DOJ CODE "_DCDJ Q
 ;I $G(DCDJ),ACCT'["PRCA(430" K DCDJ
 I ACCT["PRCA(430" S ABN=+ACCT,BAL=0 D CHK^PRCAPAY(ABN,.BAL) I BAL'>0 S ACCT=$P($G(^RCD(340,+$P(^PRCA(430,ABN,0),U,9),0)),U,1) S:ACCT'["DPT"&('$D(ERR)) ERR="BILL HAS NO CURRENT BALANCE" I ACCT["DPT" K ERR
 Q
BILL ;Apply payment to bill
 N ABN,BAL,DAT
 S ABN=+ACCT,DAT=+$P(^PRCA(430,ABN,0),U,10),BAL=0 D CHK^PRCAPAY(ABN,.BAL) S ^TMP("PRCAPAY",$J,DAT,ABN)=BAL_"^"_$P($G(^PRCA(430,ABN,4)),U,3)_"^"
 I $G(PRCARN)]"" D ADIN^PRCAPAY(ABN,DAT,PAMT,.BAL) G:'$D(^TMP("PRCAPAY",$J,DAT,ABN)) PT
 I BAL>PAMT S $P(^TMP("PRCAPAY",$J,DAT,ABN),U,3)=PAMT S PAMT=0
 I PAMT,BAL'>PAMT S $P(^TMP("PRCAPAY",$J,DAT,ABN),U,3)=BAL S PAMT=PAMT-BAL
 I PRCARN="",$G(PRCACOM)="" S PRCACOM="Automatic Decrease Adjustment"
 D @$S(PRCARN="":"TRAN^PRCARFU(ABN,DOP,+$P(^TMP(""PRCAPAY"",$J,DAT,ABN),U,3),PRCACOM)",1:"EN^PRCAPAY1(ABN,DOP,+$P(^TMP(""PRCAPAY"",$J,DAT,ABN),U,3),+$P(^(ABN),U,1),+$P(^(ABN),U,2),$G(DCDJ))")
 I $G(ERR)]"" S PAMT=$G(PAMT)+$P(^TMP("PRCAPAY",$J,DAT,ABN),U,3) Q
PT I PAMT>0 S ACCT=$P(^RCD(340,+$P(^PRCA(430,ABN,0),U,9),0),U,1) I ACCT'["DPT" S ERR="OVERPAYMENT ON BILL"
 I ACCT["DPT",$O(^VA(200,"B",$P(^DPT($P(ACCT,";"),0),"^"),0)) S ERR="OVERPAYMENT ON BILL"
 K ^TMP("PRCAPAY",$J)
 Q
