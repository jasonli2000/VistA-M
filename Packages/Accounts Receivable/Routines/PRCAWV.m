PRCAWV ;SF-ISC/YJK-WAIVED IN PART TRANSACTION ;7/1/94  2:46 PM
V ;;4.5;Accounts Receivable;**67,71**;Mar 20, 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
 ;This is a routine for waived in part transaction.
WAIVED N OLDPRIN,PRCABN0
 D BEGIN^PRCAWO G:('$D(PRCABN))!('$D(PRCAEN)) END
 S PRCAENT=$S($D(^PRCA(433,PRCAEN,4,0)):+$P(^(0),U,4),1:0) Q:PRCAENT'>0  S PRCAENTY=$P(^(0),U,3) W !
 I PRCAENT>1 S PRCATEMP="[PRCA FY WAIVED1]" D DIE
 I PRCAENT'>1 D  S PRCATEMP="[PRCA FY WAIVED2]" D DIE D:'$D(PRCAD("DELETE")) UPFY K PRCAENT
    .N DA S DA=0
    .S DA=$O(^PRCA(433,PRCAEN,4,DA)) S OLDPRIN=$P($G(^(DA,0)),U,2)
    .Q
 I $D(PRCAD("DELETE")) S PRCACOMM="TRANSACTION INCOMPLETE" D DELETE^PRCAWO1,KILLV K PRCACOMM Q
 W:$D(IOF) @IOF S D0=PRCAEN K DXS D ^PRCATW2 K DXS
ASK S %=2 W !!,"Is this correct" D YN^DICN I %=0 D M1^PRCAMESG G ASK
 I (%<0)!(%=2) S PRCACOMM="USER CANCELED" D DELETE^PRCAWO1,KILLV K PRCACOMM Q
DONE I '$D(PRCAD("DELETE")) D 
   .D TRANUP^PRCAUTL,UPPRIN
   .I '$$ACCK^PRCAACC(PRCABN) D EN^PRCAFWO(PRCABN,DT,PRCAMT,$S($D(PRCA("SITE")):PRCA("SITE"),1:$$SITE^RCMSITE),PRCAEN)
 S X(1)=0,X=$G(^PRCA(430,+$G(PRCABN0),7)),X(1)=+X,X(1)=$P(X,"^",2)+X(1),X(1)=$P(X,"^",3)+X(1),X(1)=$P(X,"^",4)+X(1),X(1)=$P(X,"^",5)+X(1)
 K PRCA("STATUS")
 I X(1)=0,$G(PRCABN0) D
 .;Check for payment transactions
 .F X=0:0 S X=$O(^PRCA(433,"C",PRCABN0,X)) Q:'X  I ",2,7,20,"[(","_$P($G(^PRCA(430.3,+$P($G(^PRCA(433,X,1)),"^",2),0)),"^",3)_",") S PRCA("STATUS")=$O(^PRCA(430.3,"AC",108,0))
 .S:'$D(PRCA("STATUS")) PRCA("STATUS")=$O(^PRCA(430.3,"AC",111,0))
 .S DA=PRCABN0,DIE="^PRCA(430,",DR="8////"_PRCA("STATUS") D ^DIE
 D KILLV Q
UPPRIN Q:('$D(PRCABN))!('$D(PRCAMT))  Q:'$D(^PRCA(430,PRCABN,7))  S $P(^PRCA(430,PRCABN,7),U,1)=$P(^(7),U,1)-PRCAMT Q
DIE S DR=PRCATEMP,DIE="^PRCA(433,",DA=PRCAEN D ^DIE I '$D(PRCAMT) S PRCAD("DELETE")=1
 Q
UPFY Q:('$D(PRCAMT))!('$D(PRCAENTY))  S $P(^PRCA(433,PRCAEN,4,PRCAENTY,0),U,5)=PRCAMT,$P(^PRCA(433,PRCAEN,4,PRCAENTY,0),U,2)=$P(^PRCA(433,PRCAEN,4,PRCAENTY,0),U,2)-PRCAMT,$P(^(0),U,4)=1 Q
KILLV ;
END K %,DA,PRCA,PRCAEN,PRCABN,PRCAENTY,PRCATY,PRCAKEN1,DIE,DIC,PRCAD,PRCAMT,PRCATEMP,DR,PRCAENT,PRCAENY1,PRCATL1 Q
