RCMRGFIX ;SF-IRMFO/JLI,REM,TJK - ROUTINE TO FIX DUPLICATE ENTRIES IN DEBTOR FILE
 ;;4.5;Accounts Receivable;**132**;Mar 20, 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
 ;;
 ;;
 N RCARRAY,RCDATA,FROM,TO,RCROOT,DEBTOR,BILL,DA,DIE,DR
 K ^TMP("RCMRGFIX","A",$J)
 S RCROOT=";RCD(340,"
 S DEBTOR=0 F  S DEBTOR=$O(^RCD(340,"B",DEBTOR)) Q:'DEBTOR  I DEBTOR["DPT(" S TO=$O(^(DEBTOR,0)) D
    .S FROM=$O(^RCD(340,"B",DEBTOR,TO)) Q:'FROM
    .S ^TMP("RCMRGFIX","A",$J,FROM,TO,FROM_RCROOT,TO_RCROOT)=""
    .Q
 S RCARRAY=$NA(^TMP("RCMRGFIX","A",$J))
 I '$D(@RCARRAY) Q
 D EN^XDRMERG(340,RCARRAY)
REPOINT ;REPOINTS 430 BILLS TO CORRECT 340 ENTRY
 S FROM=0
 F  S FROM=$O(^TMP("RCMRGFIX","A",$J,FROM)) Q:'FROM  S TO=$O(^(FROM,0)) D
    .S BILL=0
    .F  S BILL=$O(^PRCA(430,"C",FROM,BILL)) Q:'BILL  S DIE="^PRCA(430,",DA=BILL,DR="9////"_TO D ^DIE
    .Q
 W !!,"DUPLICATE DEBTORS MERGE IS COMPLETED"
 Q
