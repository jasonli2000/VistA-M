RCNRBD2 ;Washington ISC@Altoona,Pa/TJK-BAD DEBT SUMMARY DOCUMENTS ;6/23/95  10:26 AM
V ;;4.5;Accounts Receivable;**4**;Mar 20, 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
 ;CALLED BY AR NIGHTLY PROCESS ON 4th OF MONTH
 ;SENDS BAD DEBT & COTRACT ADJUSTMENT SUMMARY DOCUMENTS TO FMS
 NEW FDATA,RECDT,X1,X2,X,APP,TRANTYP,SITE,FY,REC,END
 K ^TMP("RCNRBD2",$J)
 S SITE=$$SITE^RCMSITE()
 S X1=$E(DT,1,5)_"01",X2=-1 D C^%DTC S (END,RECDT)=X D SETFY
 S REC=0 F  S REC=$O(^RC(348.1,REC)) Q:REC'?1N.N  D
    .S FDATA=$G(^RC(348.1,REC,0)),APP=$P(FDATA,U)
    .D SV(APP,23)
    .Q:APP'=5014
    .D SV(5014,27)
    .Q
 K ^TMP("RCNRBD2",$J)
EXIT Q
SETFY S FY=$E(END,2,3) S:$E(END,4,5)>9 FY=FY+1 S:FY=100 FY="00"
 Q
SV(APP,TRANTYP) ;COMPILE DOCUMENTS
 NEW DOCNUM,DATA,TYPE,RCID,ENT347,FMSAMT
 S FMSAMT=$S(TRANTYP=23:$P(FDATA,U,12),1:$P(FDATA,U,11))
 Q:FMSAMT=0
 ;CALL TO GET DOCUMENT NUMBER
 ;DOCUMENT NUMBER IN VARIABLE DOCNUM
 S DOCNUM=$$ENUM^RCMSNUM(SITE)
 S DOCNUM1="SV-"_$$PADSPACE(DOCNUM)
 S TYPE=$O(^RC(347.1,"B","SUMMARY VOUCHER",0))
 S RCID="BDE"_$E(RECDT,4,7)_"SV"_APP_TRANTYP,ENT347=""
 D OPEN^RCFMDRV1(DOCNUM1,TYPE,RCID,.ENT347)
 D UPDATE(APP,FMSAMT)
SV1 D CONTROL^GECSUFMS("A",SITE,DOCNUM,"SV",10,0,"","STANDARD VOUCHER")
 D SETCODE^GECSSDCT(GECSFMS("DA"),"D RETN^RCFMFN02")
 S INCDEC="I"
 S:FMSAMT<0 INCDEC="D",FMSAMT=-FMSAMT
 S DATA="SV2^"_$E(RECDT,2,3)_"^"_$E(RECDT,4,5)_"^"_$E(RECDT,6,7)_"^^^E^^^^^^^^^"_$J(FMSAMT,0,2)_"^~"
 S ^TMP("RCNRBD2",$J,APP,1)=DATA
 S DATA="LIN^~",^(2)=DATA ;NAKED TO LINE ABOVE
 S DATA="SVA^001^"_TRANTYP_"^"_FY_"^^"_$S(APP="0160":"AMAF",1:APP)_"^^"_SITE_"^~",^(3)=DATA
 S DATA="SVB^"_$J(FMSAMT,0,2)_"^"_INCDEC_"^^G^~",^(4)=DATA
 D STACK(RCID)
SVQ Q
UPDATE(APP,AMT) Q:'ENT347  S DA=ENT347,(DIC,DIE)="^RC(347,"
 S DR="1.01////"_APP_";1.02////"_AMT
 D ^DIE Q
STACK(RCID) ;SENDS CODESHEETS TO THE STACKER
 N I
 F I=1:1:4 S DA=^TMP("RCNRBD2",$J,APP,I) D SETCS^GECSSTAA(GECSFMS("DA"),^(I))
 D SETSTAT^GECSSTAA(GECSFMS("DA"),"Q")
 D SSTAT^RCFMFN02(RCID,1)
STACKQ Q
FYQ Q FY
PADSPACE(ENTRY) ;  return entry with padded spaces
 S %=$E($TR(ENTRY,"-")_"           ",1,11)
PDSPQ Q %
EN1(ENT347) ;ENTRY POINT FOR RE-TRANSMISSION
 NEW N0,N1,ND,DOCNUM,DOCNUM1,SITE,TYPE,RCID,APP,END,AMT,FMSAMT
 NEW INCDEC,FY,I,RECDT
 S N0=$G(^RC(347,ENT347,0)),N1=$G(^(1))
 S DOCNUM1=$P(N0,U,9),DOCNUM=$P(DOCNUM1,"-",2),SITE=$E(DOCNUM,1,3)
 S TYPE=$P(N0,U,2)
 S RCID=$P(N0,U,6),APP=$E(RCID,10,13),TRANTYP=$E(RCID,14,15)
 S END=$S($E(DT,4,5)<$E(RCID,4,5):$E(DT,1,3)-1,1:$E(DT,1,3)),END=END_$E(RCID,4,7)
 S (AMT,FMSAMT)=$P(N1,U,2)
 D SETFY
 S RECDT=DT
 D SV1
 W !,"Code Sheet Retransmitted"
EN1Q Q
RETRAN ;ENTRY POINT FOR MENU OPTION TO RETRANSMIT
 N DIC
 S DIC="^RC(347,",DIC(0)="EQAMZ",DIC("A")="Select Bad Debt Document to Retransmit: "
 S DIC("S")="I $P(^(0),U,6)?1""BDE"".4N.2A.6N,$P(^(0),U,3)=3"
 D ^DIC
 Q:($D(DTOUT))!($D(DUOUT))!(Y<1)
 D EN1(+Y)
 Q
