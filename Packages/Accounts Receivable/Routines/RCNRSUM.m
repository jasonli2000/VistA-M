RCNRSUM ;Washington ISC@Altoona,Pa/TJK-NATIONAL ROLL-UP SUMMARY DOCUMENTS ;4/12/95  10:10 AM
V ;;4.5;Accounts Receivable;**3**;Mar 20, 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
 ;CALLED BY NATIONAL ROLL-UP TO SEND SUMMARY DOCUMENTS TO FMS
 N RCWR,RCSV,SITE,END
 D COMPILE^RCNRSUM1(.SITE,.END,.RCSV,.RCWR)
 Q:$D(^RC(347.2,"B",$E(END,2,5)))
 NEW DATA,FY,I,APP,ENT,PREVAMT,INCDEC,PY,PM,PREVDT
 NEW RECDT
 S RECDT=END
 S PY=$E(END,2,3),PM=$E(END,4,5)-1
 S:PM<1 PM=12 S:$L(PM)<2 PM="0"_PM
 S:PM=12 PY=PY-1 S:PY<10 PY="0"_PY
 S PREVDT=PY_PM
 I $O(^RC(347.2,0)),'$D(^RC(347.2,"B",PREVDT)) Q
 ;F APP=2431,5014 S RCSV(APP)=$G(^TMP("RCSV",$J,APP)),RCWR(APP)=$G(^TMP("RCWR",$J,APP))
 D SETFY F APP=5014,2431 S AMT=+RCSV(APP) D SV(APP,AMT)
 K ^TMP("RCNRSUM",$J)
 F APP=2431,5014 S AMT=+RCWR(APP) D:AMT WR(APP,AMT)
 D STORE
 K ^TMP("RCNRSUM",$J),^TMP("RCSV",$J),^TMP("RCWR",$J)
EXIT Q
SETFY S FY=$$FY(END),FY(1)=$P(FY,U),FY(2)=$P(FY,U,2)
SETFYQ Q
SV(APP,AMT) NEW DOCNUM,DATA,TYPE,RCID,ENT347,FMSAMT
 ;CALL TO GET DOCUMENT NUMBER
 ;DOCUMENT NUMBER IN VARIABLE DOCNUM
 D DIF Q:FMSAMT=0
 S DOCNUM=$$ENUM^RCMSNUM(SITE)
 S DOCNUM1="SV-"_$$PADSPACE(DOCNUM)
 S TYPE=$O(^RC(347.1,"B","SUMMARY VOUCHER",0))
 S RCID="NDB"_$E(END,4,7)_"SV"_APP,ENT347=""
 ;D DEL^RCFMFN02(RCID)
 D OPEN^RCFMDRV1(DOCNUM1,TYPE,RCID,.ENT347)
 D UPDATE(APP,FMSAMT)
SV1 D CONTROL^GECSUFMS("A",SITE,DOCNUM,"SV",10,0,"","STANDARD VOUCHER")
 D SETCODE^GECSSDCT(GECSFMS("DA"),"D RETN^RCFMFN02")
 S INCDEC="I"
 S:FMSAMT<0 INCDEC="D",FMSAMT=-FMSAMT
 S DATA="SV2^"_$E(RECDT,2,3)_"^"_$E(RECDT,4,5)_"^"_$E(RECDT,6,7)_"^^^E^^^^^^^^^"_$J(FMSAMT,0,2)_"^~"
 S ^TMP("RCNRSUM",$J,APP,1)=DATA
 S DATA="LIN^~",^(2)=DATA ;NAKED TO LINE ABOVE
 S DATA="SVA^001^21^"_FY(1)_"^^"_APP_"^^"_SITE_"^^^^^ARRV^~",^(3)=DATA
 S DATA="SVB^"_$J(FMSAMT,0,2)_"^"_INCDEC_"^^R^^"_$E(END,2,3)_"^"_$E(END,4,5)_"^"_$E(END,6,7)_"^~",^(4)=DATA
 D STACK(4,RCID)
SVQ Q
DIF N PREVAMT
 I '$O(^RC(347.2,0)) S PREVAMT=0 G DIF1
 S DIC="^RC(347.2,",DIC(0)="XMZ",X=PREVDT D ^DIC
 S PREVAMT=$S(APP=5014:$P(Y(0),U,2),1:$P(Y(0),U,3))
DIF1 S FMSAMT=AMT-PREVAMT
DIFQ Q
WR(APP,AMT) NEW DOCNUM,DATA,TYPE,RCID,ENT347
 S DOCNUM=$$ENUM^RCMSNUM(SITE)
 S DOCNUM1="WR-"_$$PADSPACE(DOCNUM)
 S TYPE=$O(^RC(347.1,"B","WRITE-OFF SUMMARY",0))
 S RCID="NDB"_$E(END,4,7)_"WR"_APP
 ;D DEL^RCFMFN02(RCID)
 D OPEN^RCFMDRV1(DOCNUM1,TYPE,RCID,.ENT347),UPDATE(APP,AMT)
WR1 D CONTROL^GECSUFMS("A",SITE,DOCNUM,"WR",10,0,"","WRITE OFF")
 D SETCODE^GECSSDCT(GECSFMS("DA"),"D RETN^RCFMFN02")
 S DATA="CR2^"_$E(RECDT,2,3)_"^"_$E(RECDT,4,5)_"^"_$E(RECDT,6,7)_"^^^^^^E^^^999999999999^^"_$J(AMT,0,2)_"^^"_$E(END,2,3)_"^"_$E(END,4,5)_"^"_$E(END,6,7)_"^~"
 S ^TMP("RCNRSUM",$J,APP,1)=DATA
 S DATA="LIN^~",^(2)=DATA ;NAKED TO LINE ABOVE
 S DATA="CRA^001^"_FY(1)_"^^"_APP_"^"_SITE_"^^^ARRV^^^^^^^^MISCN^^"_$J(AMT,0,2)_"^I^^04^~"
 S ^TMP("RCNRSUM",$J,APP,3)=DATA
 D STACK(3,RCID)
WRQ Q
STORE S X=$E(END,2,5),DIC(0)="MXL",DLAYGO=347.2,DIC="^RC(347.2," D ^DIC S DA=+Y
 S DIE=DIC,DR=".02////"_RCSV(5014)_";.03////"_RCSV(2431)_";.04////"_RCWR(5014)_";.05////"_RCWR(2431) D ^DIE
 Q
UPDATE(APP,AMT) Q:'ENT347  S DA=ENT347,(DIC,DIE)="^RC(347,"
 S DR="1.01////"_APP_";1.02////"_AMT
 D ^DIE Q
STACK(NUM,RCID) N I
 S I=0 F I=1:1:NUM S DA=^TMP("RCNRSUM",$J,APP,I) D SETCS^GECSSTAA(GECSFMS("DA"),^(I))
 D SETSTAT^GECSSTAA(GECSFMS("DA"),"Q")
 D SSTAT^RCFMFN02(RCID,1)
STACKQ Q
 N I
FY(DATE) ;COMPUTES FISCAL YEAR AND FISCAL MONTH FOR A GIVEN FILEMAN DATE
 N FY
 S FY(1)=$E(DATE,2,3),FY(2)=$E(DATE,4,5) S:FY(2)>9 FY(1)=FY(1)+1
 S FY(2)=$S(FY(2)<7:"0"_(FY(2)+3),FY(2)<10:FY(2)+3,1:"0"_(FY(2)-9))
 S:FY(1)=100 FY(1)="00"
 S FY=FY(1)_U_FY(2)
FYQ Q FY
PADSPACE(ENTRY) ;  return entry with padded spaces
 S %=$E($TR(ENTRY,"-")_"           ",1,11)
PDSPQ Q %
EN1(ENT347) ;ENTRY POINT FOR RE-TRANSMISSION
 NEW N0,N1,ND,DOCNUM,DOCNUM1,SITE,TYPE,RCID,APP,END,AMT,FMSAMT
 NEW INCDEC,FY,I,RECDT
 S N0=$G(^RC(347,ENT347,0)),N1=$G(^(1))
 S DOCNUM1=$P(N0,U,9),DOCNUM=$P(DOCNUM1,"-",2),SITE=$E(DOCNUM,1,3)
 S TYPE=$P(N0,U,2)
 S RCID=$P(N0,U,6),APP=$E(RCID,10,13)
 S END=$S($E(DT,4,5)<$E(RCID,4,5):$E(DT,1,3)-1,1:$E(DT,1,3)),END=END_$E(RCID,4,7)
 S (AMT,FMSAMT)=$P(N1,U,2)
 D SETFY S ND=$E(RCID,8,9)
 S RECDT=DT
 D @(ND_1)
 W !,"Code Sheet Retransmitted"
EN1Q Q
