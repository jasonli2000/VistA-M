RCNRSUM1 ;Washington ISC@Altoona,Pa/TJK-COMPILE/RETRANSMIT SUMMARY SV/WR TO FMS ;4/12/95  10:12 AM
V ;;4.5;Accounts Receivable;**3**;Mar 20, 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
RETRAN ;RETRANSMIT DOCUMENT
 ;ENTRY POINT FOR MENU OPTION TO RETRANSMIT
 N DIC
 S DIC="^RC(347,",DIC(0)="EQAMZ",DIC("A")="Select NDB Document to Retransmit: "
 S DIC("S")="I $P(^(0),U,6)?1""NDB"".4N.2A.4N,$P(^(0),U,3)=3"
 D ^DIC
 Q:($D(DTOUT))!($D(DUOUT))!(Y<1)
 ;I Y<0 W !,"No rejected NDB code sheets to retransmit" Q
 D EN1^RCNRSUM(+Y)
 Q
COMPILE(SITE,END,RCSV,RCWR) ;ENTRY POINT FOR BACKGROUND JOB TO COMPILE FIGURES
 ;CALLED BY RCNRSUM
 N BILL,TRANS,FUND,TYPE,TTYPE,STATUS,BEG
 F X=5014,2431 S (RCSV(X),RCWR(X))=0
 S SITE=$$SITE^RCMSITE()
 S X1=$S(+$E(DT,6,7)=1:DT,1:$E(DT,1,5)_"01"),X2=-1 D C^%DTC S END=X
 ;COMPILE STANDARD VOUCHER DATA
 F STATUS=16,42 D
    .S BILL=0
    .F  S BILL=$O(^PRCA(430,"AC",STATUS,BILL)) Q:BILL'?1N.N  D
       ..S FUND=$$FUND(BILL) Q:'FUND
       ..S AMT=+$G(^PRCA(430,BILL,7))
       ..S RCSV(FUND)=RCSV(FUND)+AMT
       ..;W !,FUND,"     ",$P(^PRCA(430,BILL,0),U),"    ",AMT
       ..Q
 ;COMPILE SUMMARY WRITE-OFF DATA
 S BEG=$E(END,1,5)_"01"
 F TTYPE=8:1:11 S TRANS=0 D
    .F  S TRANS=$O(^PRCA(433,"AW",TTYPE,TRANS)) Q:'TRANS  D
       ..S T0=$G(^PRCA(433,TRANS,0)) Q:$P(T0,U,4)'=2  S T1=$G(^(1))
       ..S TDATE=$P(T1,U,9) Q:(TDATE<BEG)!(TDATE>(END+.99999))
       ..S BILL=$P(T0,U,2),FUND=$$FUND(BILL) Q:'FUND
       ..S RCWR(FUND)=RCWR(FUND)+$$TAMT(TRANS)
       ..Q
    .Q
COMPILEQ Q
FUND(BILL) N N0,SEG,FUND
 I '$$ACCK^PRCAACC(BILL) S FUND=0 G FUNDQ
 S N0=$G(^PRCA(430,BILL,0))
 I $P(N0,U,12)'=SITE S FUND=0 G FUNDQ
 S SEG=$P(N0,U,21)
 S FUND=$S(SEG>242&(SEG<246):2431,SEG>294&(SEG<297):2431,1:5014)
FUNDQ Q FUND
TAMT(TRANS) ;
 N TRAMT
 S TRAMT=0 F X=0:0 S X=$O(^PRCA(433,TRANS,4,X)) Q:'X  S TRAMT=TRAMT+$S($P(^(X,0),U,5)<0:-$P(^(0),U,5),1:$P(^(0),U,5))
 Q TRAMT
