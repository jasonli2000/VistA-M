RCY173PO ;ALB/TMK - PRCA*4.5*173 POST-INSTALL ;31-OCT-2002
 ;;4.5;Accounts Receivable;**173**;Mar 20, 1995
 ;
POST ;Set up check points for post-init
 N %
 S %=$$NEWCP^XPDUTL("U342","U342^RCY173PO")
 S %=$$NEWCP^XPDUTL("ADDUSER","ADDUSERS^RCY173PO")
 S %=$$NEWCP^XPDUTL("ADDGMBR","ADDGMBR^RCY173PO")
 S %=$$NEWCP^XPDUTL("END","END^RCY173PO") ; Leave as last update
 Q
 ;
U342 N Z,DA,DIE,X,Y,DR
 D BMES^XPDUTL("Adding default values to 'aging' site parameters")
 S DIE="^RC(342,",DA=1,DR="7.02////5;7.03////7" D ^DIE
 D COMPLETE
 Q
 ;
ADDUSERS N DIC,X,Y,DO,DD,DLAYGO
 D BMES^XPDUTL("Adding new EDI Lockbox generic users")
 S DIC(0)="",DIC="^VA(200,",X="EDILOCKBOX,AUTOMATIC" D ^DIC
 I Y>0 D  G ADD1
 . D BMES^XPDUTL("User 'EDILOCKBOX,AUTOMATIC' already exists in the NEW PERSON file - not added")
 D BMES^XPDUTL("Adding new user 'EDILOCKBOX,AUTOMATIC' to the NEW PERSON file")
 S DLAYGO=200,DIC(0)="L",DIC="^VA(200,",DIC("DR")="1////EDI",X="EDILOCKBOX,AUTOMATIC" D FILE^DICN K DIC,DO,DD,DLAYGO
 I Y'>0 D
 . D BMES^XPDUTL("A PROBLEM WAS ENCOUNTERED TRYING TO ADD USER 'EDILOCKBOX,AUTOMATIC'")
 . D BMES^XPDUTL("THE ENTRY MUST BE ADDED MANUALLY TO THE NEW PERSON FILE")
 E  D
 . D BMES^XPDUTL("USER 'EDILOCKBOX,AUTOMATIC' WAS SUCCESSFULLY ADDED TO THE NEW PERSON FILE")
 ;
ADD1 S DIC(0)="",DIC="^VA(200,",X="PRCA,AUTOAUDIT" D ^DIC
 I Y>0 D  G ADDQ
 . D BMES^XPDUTL("User 'PRCA,AUTOAUDIT' already exists in the NEW PERSON file - not added")
 D BMES^XPDUTL("Adding new user 'PRCA,AUTOAUDIT' to the NEW PERSON file")
 S DLAYGO=200,DIC(0)="L",DIC="^VA(200,",DIC("DR")="1////EDI",X="PRCA,AUTOAUDIT" D FILE^DICN K DIC,DO,DD,DLAYGO
 I Y'>0 D
 . D BMES^XPDUTL("A PROBLEM WAS ENCOUNTERED TRYING TO ADD USER 'PRCA,AUTOAUDIT'")
 . D BMES^XPDUTL("THE ENTRY MUST BE ADDED MANUALLY TO THE NEW PERSON FILE")
 E  D
 . D BMES^XPDUTL("USER 'PRCA,AUTOAUDIT' WAS SUCCESSFULLY ADDED TO THE NEW PERSON FILE")
 ;
ADDQ D COMPLETE
 Q
 ;
ADDGMBR ; Add S.RCDPE EDI LOCKBOX SERVER server option to mailgroup MLB
 D BMES^XPDUTL("Note: SERVER OPTION S.RCDPE EDI LOCKBOX SERVER@<your site's domain>"),MES^XPDUTL("      MUST BE ADDED MANUALLY TO MAIL GROUP MLB")
 D COMPLETE
 Q
 ;
COMPLETE ;
 D BMES^XPDUTL("Step complete.")
 Q
 ;
END ;
 D BMES^XPDUTL("Post install complete.")
 Q
 ;
