LABCX7B ;SLC/DLG/FHS - BECKMAN CX7 UNI AND BIDIRECTIONAL ;4/29/91  07:25 ; [ 12/16/96  9:56 AM ]
 ;;5.02;LAB;;10/30/90 10:51
 ;CROSS LINK BY ID OR IDE
LA1 S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
 S I=0 F J=0:0 S I=$O(TC(I)) Q:I<1  S:$D(TC(I,4))#2 TEST(TC(I,4))=I
LA2 K TV,Y S (BAD,TRAY,CUP,ID,IDE)="",TOUT=0,A=1 D IN G QUIT:TOUT,LA2:$P(IN,",",2)'="702"
 I $P(IN,",",3)="01" D QC,IN,QC,HDR G:BAD LA2 G LA3
 I $P(IN,",",3)="03" D QC,RES G:BAD LA2 G LA3
 G LA2
LA3 G:ID<1 LA2 X LAGEN G LA2:'ISQN ;Can be changed by the cross-link code
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 G LA2
QC S Y(A)=IN,A=A+1 Q
HDR S:$P(Y(1),",",12)="NO" BAD=1 Q:BAD  S V=$P(Y(1),",",6) D NUM S IDE=+V,V=$P(Y(1),",",13) D NUM S ID=+V,V=$P(Y(1),",",8) D NUM S TRAY=V,V=$P(Y(1),",",9) D NUM S CUP=V
 Q
RES S V=$P(Y(1),",",6) D NUM S IDE=+V,V=$P(Y(1),",",8) D NUM S TRAY=+V,V=$P(Y(1),",",9) D NUM S CUP=+V,V=$P(Y(1),",",10) D NUM S ID=+V
 I $P(Y(1),",",22)'="NR" S BAD=1 Q:BAD
 S V=$P(Y(1),",",11) D NUM S TEST=V,V=$P(Y(1),",",16) D NUM S ZZ=V S:"*#"[V V="" I V>0!(V="0.0"),($D(TEST(TEST))#2) S @TC(TEST(TEST),1)=$S(ZZ="0.0":0,1:+V) ;SLC/JL
 Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>9  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
OUT S CNT=^LA(TSK,"O")+1,^("O")=CNT,^("O",CNT)=OUT
 LOCK ^LA("Q") S Q=^LA("Q")+1,^("Q")=Q,^("Q",Q)=TSK LOCK
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK) I $D(ZTSK) D KILL^%ZTLOAD K ZTSK
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
