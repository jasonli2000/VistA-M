LAH747BD ;DALLAS/DAF;SLC/RAF- HITACHI 747 DIRECT CONNECT BIDIRECTIONAL ;8/6/93 13:00 [05/27/94]
 ;;5.1;LAB;;04/11/91 11:06
 ;This routine was written by Dave French at Dallas VAMC and
 ;generously given to the lab package.
 D:$D(ZTSK) KILL^%ZTLOAD K ZTSK S LANM=$T(+0),(T,TSK,HOME)=$O(^LAB(62.4,"C",LANM,0)) Q:HOME<1
 Q:$D(^LA("LOCK",HOME))
INIT S U="^",IOP=$P(^LAB(62.4,HOME,0),U,2) G:IOP="" H^XUS
 S IOP=IOP_";255",%ZIS="" D ^%ZIS G:POP H^XUS U IO X ^%ZOSF("TYPE-AHEAD"),^%ZOSF("LABOFF")
 I '$D(^LA(T,"ENV")) D GETENV^%ZOSV S ^LA(T,"ENV")=Y
 ;^LA(T,"ENV")=UCI^VOLUME SET^VAX NODE 
 S:'$D(^LA(T,"I"))#2 ^LA(T,"I")=0,^("I",0)=0
 S:'$D(^LA(T,"O"))#2 ^LA(T,"O")=0,^("O",0)=0
 S ^LA("LOCK",HOME)=$J,T=HOME
 S ECHOALL=0,X=^LAB(62.4,TSK,0),U="^",LWL=$P(X,U,4),WL=$P(X,U,11),METH=$P(X,U,10),LROVER=+$P(X,U,12),LALCT=$P(X,U,5) I 'WL K ^LA("LOCK",TSK) S TSK=0 Q
 S LATRANS=$P(^LRO(68,WL,0),U,3),LADT=$S(LATRANS="D":DT,LATRANS="M":$E(DT,1,5)_"00",LATRANS="Y":$E(DT,1,3)_"0000")
 S LAGEN="S "_$P(X,U,6)_"="_$P(X,U,7)_" D "_$P(X,U,6)_"^LAGEN"
 I "T"[LALCT F I=0:0 S I=$O(^LAB(62.4,TSK,3,I)) Q:I<1  S X=^(I,0),TC=I,TC(I,0)=+X,TC(I,1)=^(1),TC(I,2)=$P(X,U,2),TC(I,3)=$P(X,U,3),TC(I,4)=$P(X,U,4)
 S STX=$C(2),ETX=$C(3),SP=" ",CR=$C(13)
 S LRINST=T,LRLL=$P(^LAB(62.4,T,0),"^",4) D ^LADOWN1 S %X="^UTILITY($J,",%Y="^UTILITY(LANM," D %XY^%RCR S T=HOME
 D ^%ZISC S X="TRAP^"_LANM,@^%ZOSF("TRAP")
 R X:1,X:1 ;FLUSH BUFFER
RD L  I $D(^LA("STOP",HOME)) K ^LA("LOCK",HOME),^LA(HOME),^LA("STOP",HOME),^UTILITY(LANM) G H^XUS
 S ^LA(HOME,"R")=$H R STR:15 G RD:'$T  ;; read time increased from 5 to 15
 S STR=$P(STR,STX,2),(IN,^LA(T,"I",2))=$P(STR,ETX),(CKSUM,^LA(T,"I",3))=$P(STR,ETX,2)
 I '$D(^LA(T,"ST")) S ^("ST")="S",^("CT")=0
 S ST=^LA(T,"ST"),CT=^("CT")
REP I CKSUM["@@" D  G RD
 . I ST="TS" S CN=$G(^LA(T,"C",0)) I CN>0 S ^(0)=CN-1
 . S ^LA(T,"ST")="S",^LA("CT")=0
FTN S FTN="",INODED=^LA(T,"I",2),FTN=$A($E(INODED,1)) G:"<>?@A;12:="'[$E(INODED,1) RD
 I FTN="" G RD
 G @ST
S ;SYNC
 I FTN=63 S OUT=STX_">"_ETX_"3E" G OUT
 I FTN=64 G RD
 S ^LA(T,"ST")="R" G BADSEND
R ;RESYNC
 I FTN'=63 S (CT,^LA(T,"CT"))=CT+1 S:CT>3 ^("ST")="S",^("CT")=0 G RD
 S ^LA(T,"ST")="OK",^("CT")=0
 G ACCEPT
TS ;TRANSMITTING TEST SELECTIONS
OK ;OK TO RECEIVE RESULTS
 G @FTN:FTN>58
 D RESULT G ACCEPT
63 ;REPEAT
64 ;DATA BAD AND SUSPEND
 I ST="TS" S CN=$G(^LA(T,"C",0)) I CN>0 S ^(0)=CN-1
 I FTN=63 G 62
 S ^LA(T,"ST")="S",^("CT")=0 G RD
62 ;DATA ACCEPTED
59 ;SINGLE TEST SELECT ALSO
 I FTN=59 D ^LAH747Q I $L(LRECORD)>26 S OUT=LRECORD G OUT
ACCEPT ;ACCEPT PACKET
 S:ST'="OK" ^LA(T,"ST")="OK" S OUT=STX_">"_ETX_"3E"
 G OUT
BADSEND ;SEND A BAD PACKET TO SYNC
 S OUT=STX_"BAD"_ETX_"@@"
OUT ;
 W OUT_CR
 G RD
RESULT Q:'$D(^LA(TSK,"I",0))
 S TP=0,X="NOW",%DT="T" D ^%DT S NOW=Y
 D DT^LRX S LADT=$S(LATRANS="D":DT,LATRANS="M":$E(DT,1,5)_"00",LATRANS="Y":$E(DT,1,3)_"0000")
 K TEST S J="" F  S J=$O(TC(J)) Q:J=""  S TEST(TC(J,4))=J
LA2 K TV,Y Q:"AEG"'[$E(IN,2)  S Y(1)=IN
 D CHK Q:'CKSUM  S (TRAY,ID,IDE,CUP)=0 S V=$E(Y(1),4,7) D NUM S TRAY=+V,V=$E(Y(1),8,12) D NUM S IDE=+V,CUP=+$E(Y(1),13),V=$E(Y(1),14,26) D NUM S ID=+V
 S V=$E(Y(1),27,28) D NUM S NT=+V Q:'NT
 S Y(1)=$E(Y(1),29,255) F I=1:1:NT Q:'$L(Y(1))  S Y=$E(Y(1),1,9),Y(1)=$E(Y(1),10,255) I $E(Y,9)="@" S V=$E(Y,1,2) D NUM S TEST=+V,V=$E(Y,3,8) D NUM S VAL=+V D LA4
LA3 X LAGEN Q:'ISQN  ;Can be changed by the cross-link code
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 Q
LA4 Q:'$D(TEST(TEST))  I VAL S II=TEST(TEST),@TC(II,1)=VAL
 Q
CHK S SUM=0 F CHAR=1:1:$L(Y(1)) S SUM=SUM+$A($E(Y(1),CHAR))
 S SUM=SUM#256,%HD=CKSUM D ^%HD S CKSUM=$S(%HD=SUM:1,1:0)
 Q
NUM ;
 S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X
 Q
TRAP D ^LABERR
 S T=HOME U IO R X:1,X:1 G @("RD^"_LANM)
EXIT LOCK ^LA(TSK) H 1 K ^LA(TSK,"I"),^LA("LOCK",TSK),A,CKSUM,CNT,CTRL D
 .K CUP,I,ID,IDE,II,IN,ISQN,J,JJ,LAGEN,LRAN,LANM,LWL,NT,OUT,Q,T,TC,TEST
 .K CHAR,EN,CR,CT,ECHOALL,ETX,STX,FTN,HOME,IONODED,IO,IOP,LADT,LALCT
 .K LRECORD,LRINST,LRLL,LROVER,METH,NOW,POP,SP,ST,STR,SUM,TC,TEST,TP
 .K TV,WL,LATRANS,TOUT,TRAY,TSK,TV,V,VAL,X,Y,ACC,FR,SOR,TESTC,TST,LRAA
 .I $D(ZTSK) D KILL^%ZTLOAD
 Q
