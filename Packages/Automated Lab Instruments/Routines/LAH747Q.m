LAH747Q ;DALLAS/DAF;SLC/RAF -  HITACHI 747 QUERY ROUTINE ; 8/6/93 13:00
 ;;5.1;LAB;;04/11/91 11:06
 ;This routine was written by Dave French at Dallas VAMC and
 ;generously given to the lab package.
 S (FR,LRECORD)=$E(INODED,1,26),V=$E(INODED,14,26) D NUM S LRAN=+V
 Q:'LRAN
 D DT^LRX
 S LRAA=$P(^LAB(62.4,T,0),"^",11) Q:'LRAA
 S ACC=$G(^LRO(68,LRAA,1,DT,1,LRAN,4,0)) Q:ACC=""
 S SOR=+$G(^LRO(68,LRAA,1,DT,1,LRAN,5,1,0))
 S STX=$C(2),ETX=$C(3),SP=" ",TST="",$P(TST,"0",50)=""
 S TESTC=0,TESTS=TST
 D TEST,JOIN
 Q
TEST F LRTEST=0:0 S LRTEST=$O(^LRO(68,LRAA,1,DT,1,LRAN,4,LRTEST)) Q:'LRTEST  D
 . Q:'$D(^UTILITY(LANM,LRTEST))
 . F I=0:0 S I=$O(^UTILITY(LANM,LRTEST,I)) Q:'I  S Y=^(I) I Y<50&(Y'=27) S $E(TESTS,Y)=1 S:Y>TESTC TESTC=Y I Y=8 I SOR=71!(SOR=74) S $E(TESTS,Y)=0
 S TESTS=$E(TESTS,1,TESTC)
 Q
JOIN Q:TESTS'["1"
 S LRECORD=FR_$E(100+TESTC,2,3)_TESTS_"0"
 S SUM=0 F CHAR=1:1:$L(LRECORD) S SUM=SUM+$A($E(LRECORD,CHAR))
 S %DH=SUM#256 D ^%DH S CHKSM="",$P(CHKSM,"0",3-$L(%DH))=%DH
 S LRECORD=STX_LRECORD_ETX_CHKSM
 Q
NUM ;
 S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X
 Q
