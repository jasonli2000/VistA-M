LAH911BD ;DALMC/DAF/RVAMC/PLS- HITACHI 911 DIRECT CONNECT BIDIRECTIONAL ; [ 11/12/93  6:42 AM ]
 ;;5.1;LAB;;04/11/91 11:06
 ;THIS ROUTINE HAS TWO DIFFERENT DATA TRAPS. THE FIRST DATA TRAP IS TURNED ON BY SETTING ^LA("D"_T,0)=0 WHERE T IS THE AUTOINSTRUMENT ENTRY NUMBER. THIS TRAP WILL TRAP JUST DATA NODES.
 ;THE SECOND TRAP IS TURNED ON BY SETTING ^LA("A"_T,0)=0. THIS TRAP WILL TRAP ALL COMMUNICATION INCLUDING THE DATA ACCEPT('>') PACKETS.
 ;TO TURN THE TRAPS OFF: K ^LA("D"_T) OR ^LA("A"_T)
 D:$D(ZTSK) KILL^%ZTLOAD K ZTSK S LANM=$T(+0),(T,TSK,HOME)=$O(^LAB(62.4,"C",LANM,0)) Q:HOME<1
 Q:$D(^LA("LOCK",HOME))
 S DEB="D"_T
 S DEBA="A"_T
INIT S U="^",IOP=$P(^LAB(62.4,HOME,0),U,2) G:IOP="" H^XUS
 S IOP=IOP_";255",%ZIS="" D ^%ZIS G:POP H^XUS U IO X ^%ZOSF("TYPE-AHEAD"),^%ZOSF("LABOFF")
 I '$D(^LA(T,"ENV")) D GETENV^%ZOSV S ^LA(T,"ENV")=Y
 S:'$D(^LA(T,"I"))#2 ^LA(T,"I")=0,^("I",0)=0
 S:'$D(^LA(T,"O"))#2 ^LA(T,"O")=0,^("O",0)=0
 S ^LA("LOCK",HOME)=$J,T=HOME
 S ECHOALL=0,X=^LAB(62.4,TSK,0),U="^",LWL=$P(X,U,4),WL=$P(X,U,11) I 'WL K ^LA("LOCK",TSK) S TSK=0 Q
 S METH=$P(X,U,10),LROVER=+$P(X,U,12),LALCT=$P(X,U,5),LAZZ=$P(^LRO(68,WL,0),U,3),LADT=$S(LAZZ="D":DT,LAZZ="M":$E(DT,1,5)_"00",LAZZ="Y":$E(DT,1,3)_"0000")
 S LAGEN="S "_$P(X,U,6)_"="_$P(X,U,7)_" D "_$P(X,U,6)_"^LAGEN"
 I "T"[LALCT F I=0:0 S I=$O(^LAB(62.4,TSK,3,I)) Q:I<1  S X=^(I,0),TC=I,TC(I,0)=+X,TC(I,1)=^(1),TC(I,2)=$P(X,U,2),TC(I,3)=$P(X,U,3),TC(I,4)=$P(X,U,4)
 S STX=$C(2),ETX=$C(3),SP=" ",CR=$C(13)
 S LRINST=T,LRLL=$P(^LAB(62.4,T,0),"^",4) D ^LADOWN1 S %X="^TMP($J,",%Y="^TMP(LANM," D %XY^%RCR S T=HOME
 D:IO(0)'=IO ^%ZISC S X="TRAP^"_LANM,@^%ZOSF("TRAP")
 R X:1,X:1 ;FLUSH BUFFER
RD L  IF $D(^LA("STOP",HOME)) K ^LA("LOCK",HOME),^LA(HOME),^LA("STOP",HOME),^TMP(LANM) G H^XUS
 S ^LA(HOME,"R")=$H R STR:10 G RD:'$T
 I $D(^LA(DEB,0)) S:STR'[">" (QDEB,^LA(DEB,0))=^LA(DEB,0)+1,^(QDEB)="IN: "_STR_"%^%"_$H
 I $D(^LA(DEBA,0)) S (QDEBA,^LA(DEBA,0))=^LA(DEBA,0)+1,^(QDEBA)="IN: "_STR_"%^%"_$H
 S STR=$P(STR,STX,2),(IN,^LA(T,"I",2))=$P(STR,ETX),(CKSUM,^LA(T,"I",3))=$P(STR,ETX,2)
 I '$D(^LA(T,"ST")) S ^("ST")="S",^("CT")=0
 S ST=^LA(T,"ST"),CT=^("CT")
REP I CKSUM["@@" D  G RD
 . I ST="TS" S CN=$G(^LA(T,"C",0)) I CN>0 S ^(0)=CN-1
 . S ^LA(T,"ST")="S",^LA("CT")=0
FTN S FTN="",INODED=^LA(T,"I",2),FTN=$A($E(INODED,1)) G:"<>?@A;12:="'[$E(INODED,1) RD
 I FTN="" G RD
 G @ST
S ;SYNC
 I FTN=63 S OUT=STX_">"_ETX_"3E" G OUT
 I FTN=64 G RD
 S ^LA(T,"ST")="R" G BADSEND
R ;RESYNC
 I FTN'=63 S (CT,^LA(T,"CT"))=CT+1 S:CT>3 ^("ST")="S",^("CT")=0 G RD
 S ^LA(T,"ST")="OK",^("CT")=0
 G ACCEPT
TS ;TRANSMITTING TEST SELECTIONS
OK ;OK TO RECEIVE RESULTS
 G @FTN:FTN>58
 D RESULT G ACCEPT
63 ;REPEAT
64 ;DATA BAD AND SUSPEND
 I ST="TS" S CN=$G(^LA(T,"C",0)) I CN>0 S ^(0)=CN-1
 I FTN=63 G 62
 S ^LA(T,"ST")="S",^("CT")=0 G RD
62 ;DATA ACCEPTED
59 ;SINGLE TEST SELECT ALSO
 I FTN=59 D ^LAH911Q I $L(LRECORD)>37 S OUT=LRECORD G OUT
ACCEPT ;ACCEPT PACKET
 S:ST'="OK" ^LA(T,"ST")="OK" S OUT=STX_">"_ETX_"3E"
 G OUT
BADSEND ;SEND A BAD PACKET TO SYNC
 S OUT=STX_"BAD"_ETX_"@@"
OUT ;
 W OUT_CR
 I $D(^LA(DEB,0)) S:OUT'[">" (QDEB,^LA(DEB,0))=^LA(DEB,0)+1,^(QDEB)="OUT: "_OUT_"%^%"_$H
 I $D(^LA(DEBA,0)) S (QDEBA,^LA(DEBA,0))=^LA(DEBA,0)+1,^(QDEBA)="OUT: "_OUT_"%^%"_$H
 G RD
RESULT Q:'$D(^LA(TSK,"I",0))
 S TP=0,X="NOW",%DT="T" D ^%DT S NOW=Y
 D DT^LRX S LADT=$S(LAZZ="D":DT,LAZZ="M":$E(DT,1,5)_"00",LAZZ="Y":$E(DT,1,3)_"0000")
 K TEST S J="" F  S J=$O(TC(J)) Q:J=""  S TEST(TC(J,4))=J
LA2 K TV,Y Q:"ABCDEFGHKLabghkl"'[$E(IN,2)  S Y(1)=IN
 D CHK Q:'CKSUM  S (TRAY,ID,IDE,CUP)=0 S TRAY=$E(Y(1),7),V=$E(Y(1),4,6) D NUM S IDE=+V,CUP=+$E(Y(1),8,9),V=$E(Y(1),10,22) D NUM S ID=+V
 S V=$E(Y(1),38,39) D NUM S NT=+V Q:'NT
 S Y(1)=$E(Y(1),40,255) F I=1:1:NT Q:'$L(Y(1))  S Y=$E(Y(1),1,9),Y(1)=$E(Y(1),10,255) I $E(Y,9)=" " S V=$E(Y,1,2) D NUM S TEST=+V,V=$E(Y,3,8) D NUM S VAL=+V D LA4
LA3 X LAGEN Q:'ISQN  ;Can be changed by the cross-link code
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 Q
LA4 Q:'$D(TEST(TEST))  I VAL S II=TEST(TEST),@TC(II,1)=VAL
 Q
CHK S SUM=0 F CHAR=1:1:$L(Y(1)) S SUM=SUM+$A($E(Y(1),CHAR))
 S SUM=SUM#256,%HD=$$BCV^LAH911Q(CKSUM,10,16) S CKSUM=$S(%HD=SUM:1,1:0)
 Q
NUM ;
 S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X
 Q
DQ ;DETASK JOB
 S X="T" D ^%DT S DT=Y S U="^",LANM=$T(+0),T=$O(^LAB(62.4,"C",LANM,0)) Q:T<1  K ^LA("LOCK",T) G LAH911BD
TRAP D ^LABERR
 S T=HOME U IO R X:1,X:1 G @("RD^"_LANM)
EXIT LOCK ^LA(TSK) H 1
 K ^LA(TSK,"I"),^LA("LOCK",TSK),A,CHAR,CKSUM,CN,CNT,CR,CT.CTRL,CUP D
 .K DEB,DEBA,ECHOALL,ETX,FTN,HOME,I,ID,IDE,II,IN,INODED,IOP,ISQN,J,JJ
 .K LADT,LALCT,LAZZ,LRECORD,LRINST,LRLL,LROVER,LAGEN,LANM,LWL,METH,NT
 .K NOW,OUT,POP,Q,QDEB,QDEBA,SP,ST,STR,STX,SUM,TC,T,TEST,TOUT,TP,TRAY
 .K TSK,TV,V,VAL,WL,X,Y I $D(ZTSK) D KILL^%ZTLOAD
 Q
