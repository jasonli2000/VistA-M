LAH911Q ;DALMC/DAF/RVAMC/PLS   HITACHI 911 QUERY ROUTINE [ 11/12/93  7:05 AM ]
 ;;5.1;LAB;;09/07/93
 S (FR,LRECORD)=$E(INODED,1,37),V=$E(INODED,10,22) D NUM S LRAN=+V
 Q:'LRAN
 D DT^LRX
 S LRAA=$P(^LAB(62.4,T,0),"^",11) Q:'LRAA
 S ACC=$G(^LRO(68,LRAA,1,DT,1,LRAN,4,0)) Q:ACC=""
 S AGE="   ",SEX=0,NAME=""
 S DEFSPEC=$G(^LAB(69.9,1,1)),DEFSPEC=$P(DEFSPEC,U)_U_$P(DEFSPEC,U,3)_U_$P(DEFSPEC,U,4)
 S X1=$G(^LRO(68,LRAA,1,DT,1,LRAN,0)),LRDFN=+X1,LRDPF=+$P(X1,U,2),DFN=$P($G(^LR(LRDFN,0)),U,3)
 S CDT=$J(+$G(^LRO(68,LRAA,1,DT,1,LRAN,3)),10,4)
 S SPEC=+$G(^LRO(68,LRAA,1,DT,1,LRAN,5,1,0))
 I DEFSPEC'[SPEC,$A($E(FR,2))#2 S $E(FR,2)=$C($A($E(FR,2))+1)
 I LRDPF=2 D ^VADPT S NAME=VADM(1),SSN=$P(VADM(2),U,2)_"    ",AGE=$J("",3-$L(VADM(4)))_VADM(4),SEX=$S($P(VADM(5),U)="F":2,$P(VADM(5),U)="M":1,1:0)
 E  I LRDPF=62.3 S NAME=$P(^LAB(LRDPF,0),U)
 E  I LRDPF=67!(LRDPF=67.1) S NAME=$P(^LRT(LRDPF,0),U)
 S $E(FR,23,25)=AGE,$E(FR,27)=SEX
 S STX=$C(2),ETX=$C(3),SP=" ",TST="",$P(TST,"0",54)=""
 S TESTC=48,TESTS=TST
 I CDT]0 S CDT=$E(CDT,4,7)_$E(CDT,2,3)_$P(CDT,".",2) S $E(FR,28,37)=CDT
 D TEST
 ;Comment 1=Name, Comment 4=SSN
 S:$G(NAME)]"" $E(TESTS,49)=1,TESTS=TESTS_NAME_$J("",30-$L(NAME))
 S:$G(SSN)]"" $E(TESTS,52)=1,TESTS=TESTS_SSN
 D JOIN
 Q
TEST F LRTEST=0:0 S LRTEST=$O(^LRO(68,LRAA,1,DT,1,LRAN,4,LRTEST)) Q:'LRTEST  D
 . Q:'$D(^TMP(LANM,LRTEST))
 . F I=0:0 S I=$O(^TMP(LANM,LRTEST,I)) Q:'I  S Y=^(I) I Y<50 S $E(TESTS,Y)=1
 S:$E(TESTS,47,49)["1" $E(TESTS,47,49)=100
 Q
JOIN Q:TESTS'["1"
 S LRECORD=FR_TESTC_TESTS
 S SUM=0 F CHAR=1:1:$L(LRECORD) S SUM=SUM+$A($E(LRECORD,CHAR))
 S %DH=$$BCV(SUM#256,16) S CHKSM="",$P(CHKSM,"0",3-$L(%DH))=%DH
 S LRECORD=STX_LRECORD_ETX_CHKSM
 Q
NUM ;
 S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X
 Q
BCV(X,X1,X2) ;CONVERT NUMBER TO DIFF BASE VALUE-BASE 2,8,10,16
 ;X=NUMBER TO CONVERT
 ;X1=BASE TO CONVERT TO
 ;X2=BASE OF NUMBER (X) - OPTIONAL - WILL DEFAULT TO BASE 10
 N Y
 S:'$D(X2) X2=10
 S Y=""
 D:X2'=10  G:X1=10 EXIT S Y="" F I=1:1 S Y=$E("0123456789ABCDEF",X#X1+1)_Y,X=X\X1 Q:X<1
 .F I=1:1:$L(X) S Y=Y*X2+($F("0123456789ABCDEF",$E(X,I))-2)
 .S X=Y
 K ACC,AGE,CDT,CHAR,CHKSM,DEFSPEC,DFN,ETX,FR,I,INODED,JJ,LANM,LRAA D
 . K LRAN,LRDFN,LRDPF,LRECORD,LRTEST,NAME,SEX,SP,SPEC,SSN,STX,SUM,T
 . K TESTC,TESTS,TST,V,VADM,X,X1,X2
EXIT Q Y
