LAZTDXF1 ;SLC/RWF/PLS- ABBOTT TDX/FLX WITH SPEC ID VERSION 1.2 ;10/25/90  14:31 ;;;PLS 55532,37175
 ;;5.12;LAB;;04/11/91 11:06
 ;;MODIFIED TO INCLUDE THE CAROUSEL LOADLIST REPORTED TEST RESULTS
 ;;USED WITH THE TDX/FLX INSTRUMENT
 ;CROSS LINK BY ID,CUP OR IDE
LA1 S LANM=$T(+0),TSK=$O(^LAB(62.4,"C",LANM,0)) Q:TSK<1
 Q:'$D(^LA(TSK,"I",0))
 K LATOP D ^LASET Q:'TSK  S X="TRAP^"_LANM,@^%ZOSF("TRAP")
 D LDTC
LA2 K TV S TOUT=0 D IN G QUIT:TOUT,LA2:($E(IN,1,5)'="ASSAY"&($E(IN,1,10)'="TDXFLX RUN"))&($E(IN,1,11)'="TDXFLX SPLS") G:$E(IN,1,10)="TDXFLX RUN"!($E(IN,1,11)="TDXFLX SPLS") WLA2 S V=$P(IN,":",2) D NUM S TEST=V
 F III=0:0 S III=$O(TC(III)) Q:III<1  I TEST=TC(III,4) D LA2A Q
 Q:TOUT  G LA2
LA2A F I=0:0 S TOUT=0 D IN G QUIT:TOUT I $E(IN,1,8)="CAROUSEL" S V=$P(IN,":",2) D NUM S TRAY=+V Q
 F I=0:0 S TOUT=0 D IN G QUIT:TOUT I $E(IN,1,3)="LOC" D LA2B Q
 Q
LA2B F I=0:0 S TOUT=0 D IN G QUIT:TOUT Q:IN'["ID#"  S V=$E(IN,1,2) D NUM S (CUP,ID)=+V,IDE=+$E(IN,9,15) S TOUT=0 D IN G QUIT:TOUT S V=$E(IN,4,13) D NUM S V=$J(V,0,+TC(III,3)),@TC(III,1)=$S(V<LRLN(III):"<"_LRLN(III),1:V) D LA3
 Q
LA3 X LAGEN Q:'ISQN
 F I=0:0 S I=$O(TV(I)) Q:I<1  S:TV(I,1)]"" ^LAH(LWL,1,ISQN,I)=TV(I,1)
 K TV Q
WLA ;LOADLIST PROCESSING
WLA2 ;
 D WLA2A Q:TOUT  G LA2
WLA2A ;
 F I=0:0 S TOUT=0 D IN G QUIT:TOUT I $E(IN,1,8)="CAROUSEL" S V=$P(IN,":",2) D NUM S TRAY=+V Q
 F I=0:0 S TOUT=0 D IN G QUIT:TOUT I $E(IN,1,9)="--- -----" D WLA2B Q
 Q
WLA2B ;
 F I=0:0 S TOUT=0 D IN Q:IN']""  G QUIT:TOUT S TEST=$E(IN,5,9) D WLA2C  F XX=1:1:3 D IN G QUIT:TOUT
 Q
WLA2C ;
 F III=0:0 S III=$O(TC(III)) Q:III<1  I TEST=TC(III,2) S V=$E(IN,1,2) D NUM S (CUP,ID)=+V,IDE=+$E(IN,12,21) S V=$E(IN,24,30) D NUM S V=$J(V,0,+TC(III,3)),@TC(III,1)=$S(V<LRLN(III):"<"_LRLN(III),1:V) D LA3
 Q
LDTC F III=0:0 S III=$O(TC(III)) Q:III<1  I $D(TC(III,2)) X TC(III,2)   ;SET-UP ABBREVIATIONS FOR TESTS. The statement S TC(III,2)="TEST ABBREVIATION" should be entered in PARAM 1 of each test.
 Q
NUM S X="" F JJ=1:1:$L(V) S:$A(V,JJ)>32 X=X_$E(V,JJ)
 S V=X Q
IN S CNT=^LA(TSK,"I",0)+1 IF '$D(^(CNT)) S TOUT=TOUT+1 Q:TOUT>300  H 5 G IN
 S ^LA(TSK,"I",0)=CNT,IN=^(CNT),TOUT=0
 S:IN["~" CTRL=$P(IN,"~",2),IN=$P(IN,"~",1)
 Q
QUIT LOCK ^LA(TSK) H 1 K ^LA(TSK),^LA("LOCK",TSK),^UTILITY($J),^UTILITY("LA",$J) I $D(ZTSK) D KILL^%ZTLOAD K ZTSK
 Q
TRAP D ^LABERR S T=TSK D SET^LAB G @("LA2^"_LANM) ;ERROR TRAP
