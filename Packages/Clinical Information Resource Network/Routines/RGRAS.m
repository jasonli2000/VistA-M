RGRAS ;BIR/CML-CIRN PRE-SEEDING REPORT FOR TREATING FACILITY UPDATE ;04/06/00
 ;;1.0;CLINICAL INFO RESOURCE NETWORK;**3**;30 Apr 99
BEGIN ;
 W !!,"This report prints the following:"
 W !,"1. Total number of ""active"" patients (i.e., total number of patients with a"
 W !,"   local or national ICN (Integration Control Number))."
 W !,"2. Total number of patients who are shared with remote facilities."
 W !,"3. Total number of remote treating facility entries found for shared patients."
 ;W !,"   (Remember, patients can be shared with multiple facilities.)"
 W !!,"This information will be used to calculate the approximate number of HL7"
 W !,"messages that will be generated by the seeding process to update ""Last"
 W !,"Treatment Date"" for entries in the TREATING FACILITY LIST file (#391.91)."
 ;
DEV W !!,"The right margin for this report is 80.",!!
 D EN^XUTMDEVQ("START^RGRAS","CIRN - Pre-Seeding Report for TF Update") I 'POP Q
 W !,"NO DEVICE SELECTED OR REPORT PRINTED!!"
 G QUIT
 ;
START ;
 S HOME=$$SITE^VASITE()   ;institution file ptr^station name^station number
 S LOCIEN=$P(HOME,"^"),LOCSITE=$P(HOME,"^",2)
 ;
LOOP ;Search ^DPT("AICN" for ICNs that are not in "history".
 ;For each found:
 ;-set ACTCNT = # of active patients (patients with a local or national ICN)
 ;  for each of these patients, determine if they are shared by checking ^DGCN(391.91,"APAT",DFN
 ;  for any entries that are not the local site.
 ;  - SPTCNT = # of shared patients (this number will be the number of msgs to go to the MPI)
 ;  - RTFCNT = # of remote TFs found (SPTCNT+RTFCNT = # of HL msgs to be generated)
 ;
 S (ACTCNT,SPTCNT,RTFCNT)=0
 ;
 S ICN=0 F  S ICN=$O(^DPT("AICN",ICN)) Q:'ICN  D
 .S DFN=$O(^DPT("AICN",ICN,0)) S CURICN=$P($G(^DPT(DFN,"MPI")),"^") I ICN=CURICN D
 ..S ACTCNT=ACTCNT+1,SHARE=0
 ..I $E(IOST,1,2)="C-",'(ACTCNT#500) W "."
 ..S TF=0 F  S TF=$O(^DGCN(391.91,"APAT",DFN,TF)) Q:'TF  D
 ...I TF'=LOCIEN S SHARE=1,RTFCNT=RTFCNT+1
 ..I SHARE S SPTCNT=SPTCNT+1
 S TOT=RTFCNT+SPTCNT
 ;
PRT ;Print report
 D NOW^%DTC S HDT=$$FMTE^XLFDT($E(%,1,12)),$P(LN,"-",81)=""
 ;
 W:$Y!($E(IOST,1,2)="C-") @IOF
 W !,"Treating Facility Update Seeding Statistics"
 W !,"Printed at ",LOCSITE," on ",HDT,!,LN
 ;
 W !,"Total number of active patients with an ICN:",?70,$J(ACTCNT,7)
 W !!,"Total number of shared patients:",?60,$J(SPTCNT,7)
 W !,"  (# of messages to send to MPI)"
 W !,"Total number of remote Treating Facilities found:",?60,$J(RTFCNT,7)
 W !,"  (# of messages to send to remote sites)",?58,"---------"
 W !?11,"Approximate # of HL7 messages to be generated:",?58,$J(TOT,9)
 ;
QUIT ;
 I $E(IOST,1,2)="C-" S DIR(0)="E" D  D ^DIR K DIR
 .S SS=22-$Y F JJ=1:1:SS W !
 K %,%I,ACTCNT,CURICN,DFN,DIR,HDT,HOME,ICN,JJ,LN,LOCIEN,LOCSITE,RTFCNT,SHARE,SPTCNT,SS,TF,TOT,X,Y,ZTSK
 D ^%ZISC S:$D(ZTQUEUED) ZTREQ="@"
 Q
