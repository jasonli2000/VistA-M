PXRMZ001 ;WPB/CAM  use in the routine filed in file 811.9 for reminders ;06/26/2002  10:40
 ;;1.5;;1/29/2001
REM1(DFN,TEST,DATE,VALUE,TEXT) ;Check Progress Note for title contained in the condition field
 ;Read Reminder Def file for title node 20,,0 piece 11
 N ARHD,ARHD0,ARHD1,ARHDCASE,ARHDFIND,ARHDTITL,ARHDTX
 S (TEST,VALUE)=0,DATE="",TEXT=""
 S ARHDTX=$P($G(D00,"~"),U)
 S ARHD0=+$O(^PXD(811.9,"B",ARHDTX,0))
 ; Pointer to the REMINDER COMPUTED FINDINGS file (#811.4)
 ; 'TITLE-CONTAINS' entry
 S ARHDFIND=539007
 S ARHD1=0
 F  S ARHD1=$O(^PXD(811.9,ARHD0,20,"E","PXRMD(811.4,",ARHDFIND,ARHD1)) Q:ARHD1'>0  D
 . S ARHD=$G(^PXD(811.9,ARHD0,20,ARHD1,3))
 . S ARHDTITL=$TR($P($P(ARHD,U),"""",2),"~"," ")
 . S ARHDCASE=$P(ARHD,U,2)
 . D REM1A(ARHDTITL,ARHDCASE)
 . Q
 Q
REM1A(TITLE,CASE) I $G(TITLE)="" Q
 N ARHD0,ARHDDATE,ARHDIEN,ARHDT,ARHDTX
 K ^TMP($J,"ARHDTIU")
 S ARHDTX=""
 F  S ARHDTX=$O(^TIU(8925.1,"B",ARHDTX)) Q:ARHDTX=""  D
 . I $S(CASE:ARHDTX[TITLE,1:$$UP^XLFSTR(ARHDTX)[$$UP^XLFSTR(TITLE)) D
 .. S ARHD0=0
 .. F  S ARHD0=$O(^TIU(8925.1,"B",ARHDTX,ARHD0)) Q:ARHD0'>0  D
 ... S ^TMP($J,"ARHDTIU",ARHD0)=ARHDTX
 ... Q
 .. Q
 . Q
 S ARHD0=0
 F  S ARHD0=$O(^TMP($J,"ARHDTIU",ARHD0)) Q:ARHD0'>0  D
 . S ARHDT=+$O(^TIU(8925,"APT",DFN,ARHD0,7,0))
 . S ARHDIEN=+$O(^TIU(8925,"APT",DFN,ARHD0,7,ARHDT,0))
 . S ARHDTX=$G(^TIU(8925,ARHDIEN,0))
 . S ARHDDATE=$P($G(^TIU(8925,ARHDIEN,15)),U)
 . I $P(ARHDTX,U)]"",ARHDDATE>DATE D
 .. S TEST=1
 .. S VALUE=^TMP($J,"ARHDTIU",ARHD0)
 .. S DATE=ARHDDATE
 .. Q
 . Q
 K ^TMP($J,"ARHDTIU")
 Q
 ;
REM2(DFN,TEST,DATE,VALUE,TEXT) ;Patient diet order of NPO or CLEAR LIQUIDS
 ;for greater than or equal to three days.
 N ARHD0,ARHD1,ARHD2,ARHDCANC,ARHDFHDI,ARHDNOW
 N ARHDNPO,ARHDPROD,ARHDTEFF,VADMVT,VAERR,VAINDT
 S TEST=0,(VALUE,DATE,TEXT)=""
 D ADM^VADPT2
 I ($G(VADMVT)'>0)!($G(VAERR)>0) Q  ;      *** Can't find admission
 S ARHD0=+DFN
 S ARHD1=+VADMVT
 S ARHD2=+$P($G(^FHPT(ARHD0,"A",ARHD1,0)),U,2)
 S ARHDFHDI=$G(^FHPT(ARHD0,"A",ARHD1,"DI",ARHD2,0))
 S ARHDTEFF=$P(ARHDFHDI,U,9)
 S ARHDNOW=$$NOW^XLFDT
 I ARHDTEFF>ARHDNOW Q  ;                   *** Effective date > Now
 I $$FMDIFF^XLFDT(ARHDNOW,ARHDTEFF)<3 Q  ; *** Less than three days
 S ARHDNPO=$$GET1^DIQ(115.01,ARHD2_","_ARHD1_","_ARHD0_",",6)
 S ARHDCANC=$P(ARHDFHDI,U,18)
 I (ARHDNPO="NPO")&(ARHDCANC'>0) D  Q  ;   *** Found a NPO
 . S TEST=1
 . S VALUE=ARHDNPO
 . S DATE=ARHDTEFF
 . S TEXT="Patient diet: "_ARHDNPO
 . Q
 S ARHDPROD=$$GET1^DIQ(115.01,ARHD2_","_ARHD1_","_ARHD0_",",12)
 S ARHDPROD=$$UP^XLFSTR(ARHDPROD)
 I ARHDPROD["CLEAR LIQUID" D  Q  ;         *** Found a CLEAR LIQUID
 . S TEST=1
 . S VALUE=ARHDPROD
 . S DATE=ARHDTEFF
 . S TEXT="Patient diet: "_ARHDPROD
 . Q
 Q
 ;
REM3(DFN,TEST,DATE,VALUE,TEXT) ;Patient tubefeeding orders
 N ARHD0,ARHD1,ARHD2,ARHDFHTF,VADMVT,VAERR,VAINDT
 S TEST=0,(VALUE,DATE,TEXT)=""
 D ADM^VADPT2
 I ($G(VADMVT)'>0)!($G(VAERR)>0) Q  ;      *** Can't find admission
 S ARHD0=+DFN
 S ARHD1=+VADMVT
 S ARHD2=+$P($G(^FHPT(ARHD0,"A",ARHD1,0)),U,4)
 S ARHDFHTF=$P($G(^FHPT(ARHD0,"A",ARHD1,"TF",ARHD2,0)),U)
 I ARHDFHTF>0 S TEST=1,DATE=ARHDFHTF ;     *** Tubefeeding found
 Q
 ;
REM4(DFN,TEST,DATE,VALUE,TEXT) ;Patient with ATTENDING INPATIENT MULTIDISCIPLINARY PLAN OF CARE NOTE
 N ARHDDATE,ARHDOCDF,VADMVT,VAERR,VAINDT
 S TEST=0,(VALUE,DATE,TEXT)=""
 S ARHDOCDF=+$O(^TIU(8925.1,"B","ATTENDING INPATIENT MULTIDISCIPLINARY PLAN OF CARE NOTE",0))
 I ARHDOCDF'>0 Q  ;                        *** Can't find doc definition
 D ADM^VADPT2
 I ($G(VADMVT)'>0)!($G(VAERR)>0) Q  ;      *** Can't find admission
 S (ARHDDATE,ARHDDATE(0))=$P($G(^DGPM(VADMVT,0)),U)
 I ARHDDATE'>0 Q  ;                        *** Can't find admission date
 S ARHDDATE=+$O(^TIU(8925,"APT",DFN,ARHDOCDF,7,(9999999-ARHDDATE)),-1)
 I $O(^TIU(8925,"APT",DFN,ARHDOCDF,7,ARHDDATE,0))>0 D
 . S TEST=1
 . S DATE=ARHDDATE(0)
 . Q
 Q
 ;
REM5(DFN,TEST,DATE,VALUE,TEXT) ;InPatient?
 N ARHDDATE,VADMVT,VAERR,VAINDT
 S TEST=0,(DATE,TEXT,VALUE)=""
 D ADM^VADPT2
 I $G(VAERR)>0 S VALUE="Unknown" Q  ;      *** Error
 I $G(VADMVT)'>0 S VALUE="Outpatient" Q  ; *** Outpatient
 S ARHDDATE=$P($G(^DGPM(VADMVT,0)),U)
 I ARHDDATE'>0 S VALUE="Unknown" Q  ;      *** Can't find admission date
 S TEST=1
 S DATE=ARHDDATE
 S VALUE="Inpatient"
 Q
 ;
REM6(DFN,TEST,DATE,VALUE,TEXT) ;GYN PAP/CERVICAL SMEAR
 N LRDFN,LRIDT,LRPAP,LRSP
 S TEST=0,(DATE,TEXT,VALUE)=""
 I $P($G(^DPT(DFN,0)),U,2)'="F" Q
 S LRDFN=$P($G(^DPT(DFN,"LR")),U)
 I LRDFN'>0 Q
 ; PAP Smear array, subscript = IEN from Laboratory Test file (#60)
 S LRPAP(5323)=""
 S LRIDT=9999999-$$NOW^XLFDT
 F  S LRIDT=$O(^LR(LRDFN,"CY",LRIDT)) Q:(LRIDT'>0)!(TEST>0)  D
 . S LRSP=0
 . F  S LRSP=$O(^LR(LRDFN,"CY",LRIDT,.1,LRSP)) Q:(LRSP'>0)!(TEST>0)  D
 .. I $D(LRPAP(+$P($G(^LR(LRDFN,"CY",LRIDT,.1,LRSP,0)),U,2)))#2 D
 ... S TEST=1
 ... S DATE=$P($G(^LR(LRDFN,"CY",LRIDT,0)),U)
 ... Q
 .. Q
 . Q
 Q
