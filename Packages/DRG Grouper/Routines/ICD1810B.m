ICD1810B ;ALB/MRY - ADD NEW DRG/DIAG/PROC ; 9/4/03 2:21pm
 ;;18.0;DRG Grouper;**10**;Oct 13,2000
 ;
 Q
 ;
ADDDRG ;-- Add any new DRGs
 N DIC,X,Y,LINE,ICDDRG,DA,DRGX,DRGY,MDC,DINUM,ICDDIC,ICDDA1
 D BMES^XPDUTL(">>> Adding New DRGs - Please verify that 13 added")
 I '$D(^ICM(98)) D  Q:Y=-1
 .S DIC="^ICM(",DIC(0)=""
 .S X="PRE",DINUM=98
 .K DO D FILE^DICN
 F LINE=1:1 S X=$T(ADD+LINE) S ICDDRG=$P(X,";;",2) Q:ICDDRG="EXIT"  D
 .S DIC="^ICD(",DIC(0)=""
 .S MDC=$P(ICDDRG,U,2) I MDC="PRE" S MDC=98
 .; check whether #900 field needs an entry of a routine name
 .S DIC("DR")="5///"_MDC_";.06///"_$P(ICDDRG,U,3)_";900///"_$P(ICDDRG,U,5)
 .S X="DRG",X=X_$P(ICDDRG,U)
 .; check for duplicates in case install is being rerun
 .I +$O(^ICD("B",X,0)) Q
 .K DO D FILE^DICN
 .I Y=-1 Q
 .K DIC("DR")
 .S DA(1)=+Y
 .S ICDDIC=DIC,ICDDA1=+Y
 .S DIC=DIC_DA(1)_",1,"
 .S DIC(0)="L"
 .S X=$P(ICDDRG,U,4)
 .K DO D FILE^DICN
 .I Y=-1 Q
 .K DIC("DR")
 .S DA(1)=+ICDDA1,DIC=ICDDIC_DA(1)_",66,"
 .S DIC(0)="L",DIC("P")=$P(^DD(80.2,66,0),"^",2)
 .S DIC("DR")=".03///"_$P(ICDDRG,U,7)_";.05///"_MDC_";.06///"_$P(ICDDRG,U,3)
 .S X=$P(ICDDRG,U,6)
 .K DO D FILE^DICN
 .I Y=-1 Q
 . ; displays listing
 .S DRGX=$P(ICDDRG,U),DRGY=$P(ICDDRG,U,4)
 .D MES^XPDUTL("  DRG"_DRGX_"     "_DRGY_" added.")
 Q
 ;
ADD ;New DRGs - descriptions should not exceed 80 char.
 ;;528^1^1^INTRACRANIAL VASCULAR PROCEDURE W PRINCIPAL DX OF HEMORRHAGE^^3031001^1
 ;;529^1^1^VENTRICULAR SHUNT PROCEDURES W CC^^3031001^1
 ;;530^1^1^VENTRICULAR SHUNT PROCEDURES W/O CC^^3031001^1
 ;;531^1^1^SPINAL PROCEDURES W CC^ICDTLB6^3031001^1
 ;;532^1^1^SPINAL PROCEDURES W/O CC^ICDTLB6^3031001^1
 ;;533^1^1^EXTRACRANIAL PROCEDURES W CC^ICDTLB6^3031001^1
 ;;534^1^1^EXTRACRANIAL PROCEDURES W/O CC^ICDTLB6^3031001^1
 ;;535^5^1^CARDIAC DEFIB IMPLANT W CARDIAC CATH AND W ACUTE MI, HEART FAILURE, OR SHOCK^ICDTLB6^3031001^1
 ;;536^5^1^CARDIAC DEFIB IMPLANT W CARDIAC CATH AND W/O ACUTE MI, HEART FAILURE, OR SHOCK^ICDTLB6^3031001^1
 ;;537^8^1^LOCAL EXCISION/REMOVAL INTERNAL FIXATION DEVICES EXCEPT HIP AND FEMUR W CC^ICDTLB6^3031001^1
 ;;538^8^1^LOCAL EXCISION/REMOVAL INTERNAL FIXATION DEVICES EXCEPT HIP AND FEMUR W/O CC^ICDTLB6^3031001^1
 ;;539^17^1^LYMPHOMA AND LEUKEMIA W MAJOR O.R. PROCEDURE W CC^ICDTLB6^3031001^1
 ;;540^17^1^LYMPHOMA AND LEUKEMIA W MAJOR O.R. PROCEDURE W/O CC^ICDTLB6^3031001^1
 ;;EXIT
 Q
