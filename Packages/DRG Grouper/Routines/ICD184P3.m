ICD184P3 ;;BPIOFO/ESW - INACTIVATE/REVISE DIAG & PROC CODES ; 10/24/02 4:17pm
 ;;18.0;DRG Grouper;**4**;Oct 13,2000
 Q
 ;
CHGDIAG ; Inactivate Diagnoses (info taken from Fed Reg Table 6C)
 ; Revise titles of Diagnoses (info from Table 6E)
 ;;
 D BMES^XPDUTL(">>>Inactivating 19 Diagnoses - please verify")
 N LINE,X,ICDDIAG,ENTRY,DA,DIE,DR,DIAG,DESC,CNT S CNT=0
 F LINE=1:1 S X=$T(INACT+LINE) S ICDDIAG=$P(X,";;",2) Q:ICDDIAG="EXIT"  D
 .S ENTRY=+$O(^ICD9("BA",ICDDIAG_" ",0)) I ENTRY D
 ..S DA=ENTRY,DIE="^ICD9(",DR="100///1;102///3021001"
 ..D ^DIE
 ..S CNT=CNT+1
 .I 'ENTRY D BMES^XPDUTL(">>>Diagnosis "_ICDDIAG_" to be inactivated not on file")
 D BMES^XPDUTL(">>> "_CNT_" diagnoses were inactivated")
 ;
 D BMES^XPDUTL(">>>Revising 23 Diagnoses - please verify")
 N MDC,CC,DRG,DRG1,DRG2,DRG3,DRG4,DRG5,DRG6,CNT,LINE S CNT=0
 F LINE=1:1 S X=$T(REVD+LINE) S ICDDIAG=$P(X,";;",2) Q:ICDDIAG="EXIT"  D
 .S ENTRY=+$O(^ICD9("BA",$P(ICDDIAG,U)_" ",0)) I ENTRY D
 ..S DA=ENTRY,DIE="^ICD9("
 ..S DIAG=$P(ICDDIAG,U,2) I DIAG="" S DIAG=$P(ICDDIAG,U,3)
 ..S DESC=$P(ICDDIAG,U,3),MDC=$P(ICDDIAG,U,5)
 ..S CC="" I $P(ICDDIAG,U,4)="Y" S CC=1
 ..S DRG=$P(ICDDIAG,U,6)
 ..S (DRG1,DRG2,DRG3,DRG4,DRG5,DRG6)=""
 ..S DRG1=$P(DRG,"~",1) I $P(DRG,"~",2) S DRG2=$P(DRG,"~",2) I $P(DRG,"~",3) S DRG3=$P(DRG,"~",3) I $P(DRG,"~",4) S DRG4=$P(DRG,"~",4) I $P(DRG,"~",5) S DRG5=$P(DRG,"~",5) I $P(DRG,"~",6) S DRG6=$P(DRG,"~",6)
 ..S DR="3///"_DIAG_";5///"_MDC_";10///"_DESC_";60///"_DRG1_";61///"_DRG2_";62///"_DRG3_";63///"_DRG4_";64///"_DRG5_";65///"_DRG6_";70///"_CC
 ..D ^DIE
 ..S CNT=CNT+1
 .I 'ENTRY D BMES^XPDUTL(">>>Diagnosis "_ICDDIAG_" to be revised not on file")
 D BMES^XPDUTL(">>> "_CNT_" diagnoses were revised")
 Q
INACT ; Inactive Diagnoses
 ;;357.8
 ;;359.8
 ;;459.1
 ;;633.0
 ;;633.1
 ;;633.2
 ;;633.8
 ;;633.9
 ;;770.8
 ;;771.8
 ;;779.8
 ;;780.9
 ;;795.0
 ;;795.3
 ;;998.3
 ;;V01.8
 ;;V13.2
 ;;V23.4
 ;;V54.8
 ;;EXIT
 Q
 ;
REVD ; Revise diagnoses/descriptions
 ;;402.00^HYP HEART DIS MALIGN W/O FAIL^HYPERTENSIVE HEART DISEASE, MALIGNANT, WITHOUT HEART FAILURE^Y^5^134
 ;;402.01^HYP HEART DIS MALIGN WITH FAIL^HYPERTENSIVE HEART DISEASE, MALIGNANT, WITH HEART FAILURE^Y^5^115~121~124~127
 ;;402.10^HYP HEART DIS BENING W/0 FAIL^HYPERTENSIVE HEART DISEASE, BENING, WITHOUT HEART FAILURE^N^5^134
 ;;402.11^HYP HEART DIS BENING WITH FAIL^HYPERTENSIVE HEART DISEASE, BENING, WITH HEART FAILURE^Y^5^115~121~124~127
 ;;402.90^HYP HEART DIS UNSPECI W/O FAIL^HYPERTENSIVE HEART DISEASE, UNSPECIFIED, WITHOUT HEART FAILURE^N^5^134
 ;;402.91^HYP HEART DIS UNSP WITH FAIL^HYPERTENSIVE HEART DISEASE, UNSPECIFIED, WITH HEART FAILURE^Y^5^115~121~124~127
 ;;404.00^HYP HRT & REN DIS MALIG W/O ME^HYPERTENSIVE HEART AND RENAL DISEASE, MALIGNANT, WITHOUT MENTION OF HEART FAILURE OR RENAL FAILURE^Y^5^134
 ;;404.01^HYP HRT & REN DIS MAL W/ HEART^HYPERTENSIVE HEART AND RENAL DISEASE, MALIGNANT, WITH HEART FAILURE^Y^5^115~121~124~127
 ;;404.03^HYP HRT & REN MAL W/HRT & RENL^HYPERTENSIVE HEART AND RENAL DISEASE, MALIGNANT, WITH HEART FAILURE AND RENAL FAILURE^Y^5^115~121~124~127
 ;;404.10^HYP HRT & REN DIS BENIGN W/O^HYPERTENSIVE HEART AND RENAL DISEASE, BENING, WITHOUT MENTION OF HEART FAILURE OR RENAL FAILURE^N^5^134
 ;;404.11^HYP HRT & REN BENIGN W/ HEART^HYPERTENSIVE HEART AND RENAL DISEASE, BENIGN, WITH HEART FAILURE^Y^5^115~121~124~127
 ;;404.13^HYP HRT & REN BEN W/HRT & REN^HYPERTENSIVE HEART AND RENAL DISEASE, BENIGN, WITH HEART FAILURE AND RENAL FAILURE^Y^5^115~121~124~127
 ;;404.90^HYP HRT & REN DIS UNSPECIFIED^HYPERTENSIVE HEART AND RENAL DISEASE, UNSPECIFIED, WITHOUT MENTION OF HEART FAILURE OR RENAL FAILURE^N^5^134
 ;;404.91^HYP HRT & REN UNSP W/HEART^HYPERTENSIVE HEART AND RENAL DISEASE, UNSPECIFIED, WITH HEART FAILURE^Y^5^115~121~124~127
 ;;404.93^HYP HRT & REN UNS W/HRT & REN^HYPERTENSIVE HEART AND RENAL DISEASE, UNSPECIFIED, WITH HEART FAILURE AND RENAL FAILURE^Y^5^115~121~124~127
 ;;414.10^ANEURYSM OF HEART^ANEURYSM OF HEART^N^5^121~144~145
 ;;414.11^ANEURYSM OF CORONARY VESSELS^ANEURYSM OF CORONARY VESSELS^N^5^121~144~145
 ;;414.19^OTHER ANEURYSM OF HEART^OTHER ANEURYSM OF HEART^N^5^121~144~145
 ;;428.0^CONGEST HEART FAIL UNSPESIFIED^CONGESTIVE HEART FAILURE, UNSPECIFIED^Y^5^115~121~124~127
 ;;454.9^ASYMPTOMATIC VARICOSE VEINS^ASYMPTOMATIC VARICOSE VEINS^N^5^130~131
 ;;627.2^SYMPT MENOP OF FEMALE CLIMAC^SYMPTOMATIC MENOPAUSAL OR FEMALE CLIMACTERIC STATES^N^13^358~359~369
 ;;627.4^SYMP STATE ASS W/ARTIF MENOP^SYMPTOMATIC STATES ASSOCIATED WITH ARTIFICIAL MENOPAUSE^N^13^358~359~369
 ;;V49.81^ASYMPT POSTMENOP STAT (AGE)^ASYMPTOMATIC POSTMENOPAUSAL STATUS (AGE-RELATED) (NATURAL)^N^23^467
 ;;EXIT
 Q
 ;
CHGPROC ; Inactivate Procedures (info taken from Fed Reg Table 6D)
 ; Revise descriptions of Procedures ( info from Table 6F)
 D BMES^XPDUTL(">>>No Procedures to inactivate")
 N LINE,X,ICDPROC,ENTRY,DA,DIE,DR,PROC,DESC
 F LINE=1:1 S X=$T(INAC+LINE) S ICDPROC=$P(X,";;",2) Q:ICDPROC="EXIT"  D
 .S ENTRY=+$O(^ICD0("BA",ICDPROC_" ",0)) I ENTRY D
 ..S DA=ENTRY,DIE="^ICD0(",DR="100///1;102///3021001"
 ..D ^DIE
 ;
 D BMES^XPDUTL(">>>Revising descriptions of 4 Procedures - please verify")
 N LINE,CNT S CNT=0
 F LINE=1:1 S X=$T(REVP+LINE) S ICDPROC=$P(X,";;",2) Q:ICDPROC="EXIT"  D
 .S ENTRY=+$O(^ICD0("BA",$P(ICDPROC,U)_" ",0)) I ENTRY D
 ..S DA=ENTRY,DIE="^ICD0("
 ..S PROC=$P(ICDPROC,U,2) I PROC="" S PROC=$P(ICDPROC,U,3)
 ..S DESC=$P(ICDPROC,U,3)
 ..S DR="4///"_PROC_";10///"_DESC
 ..D ^DIE
 ..S CNT=CNT+1
 .I 'ENTRY D
 ..D BMES^XPDUTL(">>>Missing "_$P(ICDPROC,U)_"Procedure to be revised")
 D BMES^XPDUTL(">>>> "_CNT_" procedures were revised")
 Q
 ;
INAC ; Inactive Procedures
 ;;EXIT
 Q
 ;
REVP ; Revise descriptions 
 ;;02.41^IRRIG & EXPL OF VENTRIC SHUNT^IRRIGATION AND EXPLORATION OF VENTRICULAR SHUNT
 ;;36.06^INS NON-DRUG-ELUT COR AR STEN^INSERTION OF NON-DRUG-ELUTING CORONARY ARTERY STENTS(S)
 ;;39.79^OTH ENDOV REPAIR ANEUR VESELS^OTHER ENDOVASCULARREPAIR OF ANEURYSM OF OTHER VESSELS
 ;;39.90^INS NON-DR-ELU NONCOR AR STEN^INSERTION OF NON-DRUG-ELUTING, NONCORONARY ARTERY STENTS(S)
 ;;EXIT
 Q
 ;
