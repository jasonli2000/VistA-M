ENLOG11 ;(WASH ISC)/DH-Meld LOG1 with AEMS/MERS ;1-30-92
 ;;;;
 ;CLASS 3 SOFTWARE - Not officially supported by the ISC's
 L ^ENG(6914,ENMERS) S EN=^ENZ(6914.5,ENLOG,0),ENNXRN=$P(EN,U),$P(^ENG(6914,ENMERS,2),U,7)=ENNXRN
 S ENCMR=$P(EN,U,4) G:ENCMR="" USE
 I '$D(EN("CMR")),$D(^ENG(6914,ENMERS,2)),$P(^(2),U,9)]"" G USE
 S ENCMR(0)=$S($D(^ENG(6914.1,"B",ENCMR)):$O(^ENG(6914.1,"B",ENCMR,0)),1:"") D:ENCMR(0)="" ADDCMR S $P(^ENG(6914,ENMERS,2),U,9)=$S(ENCMR(0)]"":ENCMR(0),1:ENCMR)
USE S ENUSE=$P(EN,U,5) G:ENUSE="" OWN
 I '$D(EN("USE")),$D(^ENG(6914,ENMERS,3)),$P(^(3),U)]"" G OWN
 S $P(^ENG(6914,ENMERS,3),U)=ENUSE
OWN S ENOWN=$P(EN,U,6) G:ENOWN="" PO
 I '$D(EN("OWN")),$D(^ENG(6914,ENMERS,3)),$P(^(3),U,4)]"" G PO
 S $P(^ENG(6914,ENMERS,3),U,4)=ENOWN
PO G:'$D(^ENZ(6914.5,ENLOG,1)) SN S EN=^ENZ(6914.5,ENLOG,1)
 S ENPO=$P(EN,U) G:ENPO="" AD
 I '$D(EN("PO")),$D(^ENG(6914,ENMERS,2)),$P(^(2),U,2)]"" G AD
 S $P(^ENG(6914,ENMERS,2),U,2)=ENPO I $D(^ENG("VERSION")),^("VERSION")]"6.4" S X=ENPO,DA=ENMERS D PO^ENLIB2
AD S ENAD=$P(EN,U,2) G:ENAD="" COST
 I '$D(EN("AD")),$D(^ENG(6914,ENMERS,2)),$P(^(2),U,4)]"" G COST
 S $P(^ENG(6914,ENMERS,2),U,4)=ENAD
COST S ENCOST=$P(EN,U,3) G:ENCOST="" LIFE
 I '$D(EN("COST")),$D(^ENG(6914,ENMERS,2)),$P(^(2),U,3)]"" G LIFE
 I '$D(EN("COST")),$D(^ENG(6914,ENMERS,2)),$P(^(2),U,12)]"" G LIFE
 I ENCOST["." S X=$P(ENCOST,".",2) I $L(X)<2 S ENCOST=$S($L(X)=0:ENCOST_"00",1:ENCOST_"0")
 I ENCOST>0,ENOWN=2 S $P(^ENG(6914,ENMERS,2),U,3)="",$P(^(2),U,12)=ENCOST
 I ENCOST>0,ENOWN'=2 S $P(^ENG(6914,ENMERS,2),U,3)=ENCOST,$P(^(2),U,12)=""
LIFE S ENLIFE=$P(EN,U,4) G:ENLIFE="" RD
 I '$D(EN("LIFE")),$D(^ENG(6914,ENMERS,2)),$P(^(2),U,6)]"" G RD
 S $P(^ENG(6914,ENMERS,2),U,6)=ENLIFE
RD S ENRD=$P(EN,U,5) G:ENRD="" CSN
 I '$D(EN("RD")),$D(^ENG(6914,ENMERS,2)),$P(^(2),U,10)]"" G CSN
 S $P(^ENG(6914,ENMERS,2),U,10)=ENRD
CSN S ENCSN=$P(EN,U,6) G:ENCSN="" SN
 I '$D(EN("CSN")),$D(^ENG(6914,ENMERS,2)),$P(^(2),U,8)]"" G SN
 D CSNPTR
SN G:'$D(^ENZ(6914.5,ENLOG,2)) SPEX S EN=^ENZ(6914.5,ENLOG,2)
 S ENSN=$P(EN,U,3) G:ENSN="" SORC
 I '$D(EN("SN")),$D(^ENG(6914,ENMERS,1)),$P(^(1),U,3)]"" G SORC
 S $P(^ENG(6914,ENMERS,1),U,3)=ENSN
SORC S ENSORC=$P(EN,U,5) G:ENSORC="" SPEX
 I '$D(EN("SORC")),$D(^ENG(6914,ENMERS,2)),$P(^(2),U,14)]"" G SPEX
 I ENSORC]"",ENSORC'=9,ENSORC'=10 S $P(^ENG(6914,ENMERS,2),U,14)=ENSORC
SPEX I $D(ENADSPX),$D(^ENZ(6914.5,ENLOG,21)) D SPEX^ENLOG13
 S $P(^ENG(6914,ENMERS,0),U,4)="NX",$P(^ENZ(6914.5,ENLOG,31),U,2)=1
 S ENLDGR=$S($D(^ENZ(6914.5,ENLOG,2)):$P(^(2),U,7),1:"") I ENLDGR]"",'$D(^ENG(6914,ENMERS,8)) S $P(^ENG(6914,ENMERS,8),U,6)=ENLDGR
 I ENLDGR]"",$P(^ENG(6914,ENMERS,8),U,6)="" S $P(^(8),U,6)=ENLDGR
 L
 Q  ;Return to ENLOG10 for next record
 ;
CSNPTR I ENCSN?10N S X=$E(ENCSN,1,4)_"-"_$E(ENCSN,5,10)
 E  S X=ENCSN
 I $D(^ENCSN(6917,"B",X)) S ENCSN(0)=$O(^(X,0)) I ENCSN(0)>0,$D(^ENCSN(6917,ENCSN(0),0)) S X=ENCSN(0)
 S $P(^ENG(6914,ENMERS,2),U,8)=X
 Q
 ;
ADDCMR S DIC="^ENG(6914.1,",DIC(0)="LX",X=ENCMR K DD,DO D FILE^DICN
 I Y>0 S ENCMR(0)=+Y
 Q
 ;ENLOG11
