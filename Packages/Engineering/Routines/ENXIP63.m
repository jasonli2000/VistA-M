ENXIP63 ;WCIOFO/SAB- PATCH INSTALL ROUTINE ;2/7/2000
 ;;7.0;ENGINEERING;**63**;Aug 17, 1993
 ;
 ;Environmental Check
 N ENX
 S ENX=$$GET1^DID(6914,99,"","LABEL")
 I ENX]"",ENX'="REMAIN CAPITALIZED SET BY" D
 . W $C(7),!,"WARNING: Unexpected Field #99 found in EQUIPMENT INV (#6914) file."
 . W !,"This patch includes a new field REMAIN CAPITALIZED SET BY (#99)"
 . W !,"that will be added to the EQUIPMENT INV. (#6914) file."
 . W !,"There appears to be a locally added field "_ENX
 . W !,"already assigned with field number 99.  The definition of"
 . W !,"field #99 will be overwritten when this patch is installed."
 Q
 ;
PR ;Pre Install Entry Point
 ;
 ;delete screens ENEQ3 and ENEQ3D from file 6910.9 (will be reloaded)
 N DA,DIK,ENI
 S DIK="^ENG(6910.9,"
 F ENI="ENEQ3","ENEQ3D" D
 . S DA=0 F  S DA=$O(^ENG(6910.9,"B",ENI,DA)) Q:'DA  D ^DIK
 Q
 ;
PS ;Post Install Entry Point
 N ENI,ENSN
 ;
 ; only perform during 1st install
 I $$PATCH^XPDUTL("EN*7.0*63") D BMES^XPDUTL("  Skipping post install since patch was previously installed.") Q
 I DT>3000430 D BMES^XPDUTL("  Skipping post install since Today is after Apr 30, 2000.") Q
 ; check if legacy computer system (due to facility consolidation)
 ;   We don't want to generate new FD Documents on the legacy since
 ;   FD Documents are manually entered on Fixed Assets/FMS within
 ;   a month of having the equipment data transferred to the consolidated
 ;   computer system. Any new FD Documents sent from the legacy
 ;   Engineering software would reject in Austin.
 S ENI=$O(^DIC(6910,0)) ; ien of entry in file 6910 (should be 1)
 S ENSN=$S(ENI:$P($G(^DIC(6910,ENI,0)),U,2),1:"") ; station number
 I $$LEGACY(ENSN) D  Q
 . D BMES^XPDUTL("  Skipping post install because patch is being installed on a legacy system.")
 . D MES^XPDUTL("  Any FD Documents generated by a legacy system would reject in Austin.")
 ;
QTASK ; Queue Task to expense capitalized equipment that does not meet the
 ; new capitalization threshold ($25,000)
 ;   
 S ZTRTN="TASK^ENFACTT"
 S ZTDESC="ENG Capitalization Threshold Task"
 S ZTDTH="3000427.170000"
 S ZTIO=""
 S ZTSAVE("ENIO")=XPDQUES("POS1","B")
 D ^%ZTLOAD
 ;
 I '$G(ZTSK) D
 . D BMES^XPDUTL("ERROR. The one-time task was not successfully queued.")
 . D MES^XPDUTL("Please contact National VISTA Support for assistance.")
 . ; send mail message to developer and installer
 ;
 I $G(ZTSK) D
 . D BMES^XPDUTL("  The one-time task was successfully queued.")
 . D MES^XPDUTL("  The task number is "_ZTSK)
 . D MES^XPDUTL("  It will start on "_$$HTE^XLFDT(ZTSK("D")))
 . D MES^XPDUTL("  After the task completes a summary report will")
 . D MES^XPDUTL("  be printed on "_XPDQUES("POS1","B"))
 Q
 ;
LEGACY(ENSN) ; Legacy Station Extrinsic Function
 ; input ENSN - station number
 ; returns 1 if legacy station or 0 if not legacy station
 ; list updated through calendar year 1999 facility consolidations
 N ENRET
 S ENRET=0
 S ENSN=$G(ENSN)
 I "^599^505^627^641^566^611^685^591^569^513^579^604^645^535^522^680^752^533^592^574^686^617^665^594^525^532^527^"[(U_ENSN_U) S ENRET=1
 Q ENRET
 ;
 ;ENXIP63
