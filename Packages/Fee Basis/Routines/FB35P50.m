FB35P50 ;ALBANY/JEH-POST INIT ROUTINE TO IDENTIFY CORRUPT VENDOR FILE ;11/16/02
 ;;3.5;FEE BASIS;**50**;JAN 30, 1995
 ; Loop through the Fee Basis Vender File looking for records with the
 ; "LOCAL VENDOR NAME" missing and if we have the "AUSTIN VENDOR NAME"
 ; write the "INTERNAL RECORD NUMBER","AUSTIN VENDOR NAME","ID NUMBER"
 ; and "CHAIN"(for pharmacy vendor only) into the INSTALL file.
 N AMSNM,CHAIN,FBNMB,ID,TEMPOUT
 S FBNMB=0
 F  S FBNMB=$O(^FBAAV(FBNMB)) Q:'FBNMB  D  ;
 .I $P(^FBAAV(FBNMB,0),"^",1)=""  D  ;IF THE LOCAL VENDOR NAME MISSING
 ..S DIC=161.2,DR="30.01;1;8",DA=FBNMB,DIQ(0)="E",DIQ="TEMPOUT"
 ..D EN^DIQ1 K DIC,DR,DA,DIQ
 ..S AMSNM=$G(TEMPOUT(161.2,FBNMB,30.01,"E"))
 ..S ID=$G(TEMPOUT(161.2,FBNMB,1,"E"))
 ..S CHAIN=$G(TEMPOUT(161.2,FBNMB,8,"E"))
 ..I AMSNM'=""  D  ;
 ...D BMES^XPDUTL("INTERNAL ENTRY NUMBER: "_FBNMB)
 ...D MES^XPDUTL("AUSTIN NAME: "_AMSNM)
 ...D MES^XPDUTL("ID NUMBER: "_ID)
 ...D MES^XPDUTL("CHAIN: "_CHAIN)
 Q
