DVB430PT ;ALB/JAD POST-INSTALL FOR PATCH DVB*4*30 ; 12/29/97
 ;;4.0;HINQ;**30**;03/25/92
 ;
 ;This routine will be run as a post-installation for patch DVB*4*30.
 ;This routine will add new disability codes to and modify existing
 ;names/descriptions in the DISABILITY CONDITION file (#31).
 ;
POST ;entry point for post-install, setting up checkpoints
 N %
 S %=$$NEWCP^XPDUTL("DVBLINE","EN^DVB430PT",1)
 Q
 ;
EN ;begin processing
 ;
 N DVBLINE
 ;
 D BMES^XPDUTL("  >> *** Updating DISABILITY CONDITION file (#31)")
 D MES^XPDUTL("  ")
 ;
 ;get value from checkpoints, previous run
 S DVBLINE=+$$PARCP^XPDUTL("DVBLINE")
 ;
DVBNEW ;add new codes or modify name
 ;
 F DVBI=DVBLINE:1 S DVBJ=$P($T(NEWCODE+DVBI),";;",2) Q:DVBJ["$EXIT"  D
 .S DVBCODE=+DVBJ,DVBNAME=$E($P(DVBJ,"^",2),1,45),DVBLINE=DVBI
 .S DVBDESC=$E($P(DVBJ,"^",3),1,200)
 .;
 .;add new code
 .I '$D(^DIC(31,"C",DVBCODE)) D  G UPDATECH
 ..K DD,DO
 ..S DIC="^DIC(31,",DIC(0)="L",DIC("DR")="2////"_DVBCODE_";10////"_DVBDESC
 ..S X=DVBNAME,DLAYGO=31
 ..D FILE^DICN
 ..D MES^XPDUTL(DVBCODE_":  adding  "_DVBCODE_"  "_X)
 ..K DLAYGO,DIC,X
 ..Q
 .;
 .;modify name
 .S DVBIEN=+$O(^DIC(31,"C",DVBCODE,0))
 .S DVBREC=$G(^DIC(31,DVBIEN,0)),DVBOLDSC=$G(^DIC(31,DVBIEN,1)) I DVBREC']"" D  G UPDATECH
 ..D MES^XPDUTL("  >>>> error "_DVBCODE_" in C x-reference and not in file 31")
 ..Q
 .S DVBOLDN=$P(DVBREC,"^") I DVBOLDN=DVBNAME&(DVBOLDSC=DVBDESC) G UPDATECH
 .S DA=DVBIEN,DIE="^DIC(31,",DR=".01////"_DVBNAME_";10////"_DVBDESC
 .D ^DIE
 .K DR,DA,DIE
 .I DVBOLDN'=DVBNAME D
 ..D MES^XPDUTL(DVBCODE_": changing name from "_DVBOLDN)
 ..D MES^XPDUTL("                    to   "_DVBNAME)
 .I DVBOLDSC'=DVBDESC D
 ..D MES^XPDUTL(DVBCODE_": changing description...")
 ..D MES^XPDUTL("          from:  "_DVBOLDSC)
 ..D MES^XPDUTL("            to:  "_DVBDESC)
 .;
UPDATECH .;update checkpoint
 .S %=$$UPCP^XPDUTL("DVBLINE",DVBLINE)
 .Q
 K DVBCODE,DVBI,DVBIEN,DVBJ,DVBNAME,DVBOLDN,DVBREC,DVBDESC,DVBOLDSC
 Q
 ;
NEWCODE ; codes to be added or changed
 ;;7000^VALVULAR HEART DISEASE^VALVULAR HEART DISEASE (INCLUDING RHEUMATIC HEART DISEASE)
 ;;7002^PERICARDITIS^PERICARDITIS, BACTERIAL OR RHEUMATIC
 ;;7004^HEART DISEASE, SY.^SYPHILITIC HEART DISEASE
 ;;7005^ARTERIOSCLEROTIC HEART DISEASE^ARTERIOSCLEROTIC HEART DISEASE (CORONARY ARTERY DISEASE)
 ;;7006^MYOCARDIAL INFARCTION^MYOCARDIUM, INFARCTION OF
 ;;7010^SUPRAVENTRICULAR ARRHYTHMIAS^SUPRAVENTRICULAR ARRHYTHMIAS - PAROXYSMAL ATRIAL FIBRILLATION OR OTHER SUPRAVENTRICULAR TACHYCARDIA
 ;;7011^VENTRICULAR ARRHYTHMIAS (SUSTAINED)^SUSTAINED VENTRICULAR ARRHYTHMIAS
 ;;7012^ATRIAL FIBRILLATION^ATRIAL FIBRILLATION, PERMANENT
 ;;7013^TACHYCARDIC HEART CONDITION^TACHYCARDIA, PAROXYSMAL
 ;;7015^ATRIOVENTRICULAR BLOCK^ATRIOVENTRICULAR BLOCK
 ;;7016^HEART VALVE REPLACEMENT^HEART VALVE REPLACEMENT (PROSTHESIS)
 ;;7017^CORONARY ARTERY BYPASS^CORONARY ARTERY BYPASS SURGERY
 ;;7018^CARDIAC PACEMAKER^IMPLANTABLE CARDIAC PACEMAKER
 ;;7019^CARDIAC TRANSPLANT^CARDIAC TRANSPLANTATION
 ;;7020^CARDIOMYOPATHY^CARDIOMYOPATHY
 ;;7101^HYPERTENSIVE VASCULAR DISEASE^HYPERTENSIVE VASCULAR DISEASE (HYPERTENSION AND ISOLATED SYSTOLIC HYPERTENSION)
 ;;7110^AORTIC ANEURYSM^ANEURYSM, AORTIC, FUSIFORM, SACULAR, DISSECTION AND/OR WITH STENOSIS
 ;;7111^ANEURYSM, ANY LARGE ARTERY^ANEURYSM OF ANY LARGE ARTERY
 ;;7112^ANEURYSM, ANY SMALL ARTERY^ANEURYSM OF ANY SMALL ARTERY
 ;;7113^ARTERIOVENOUS FISTULA, TRAUMATIC^ARTERIOVENOUS FISTULA (ANEURYSM), TRAUMATIC
 ;;7114^ARTERIOSCLEROSIS^ARTERIOSCLEROSIS OBLITERANS
 ;;7115^BUERGER'S DISEASE^THROMBOANGIITIS OBLITERANS (BUERGER'S DISEASE)
 ;;7116^CLAUDICATION^CLAUDICATION, INTERMITTENT
 ;;7117^REYNAUD'S SYNDROME^REYNAUD'S SYNDROME
 ;;7118^ANGIONEUROTIC EDEMA^ANGIONEUROTIC EDEMA
 ;;7119^ERYTHROMELALGIA^ERYTHROMELALGIA
 ;;7121^POST-PHLEBITIC SYNDROME^POST-PHLEBITIC SYNDROME OF ANY ETIOLOGY
 ;;7122^COLD INJURY RESIDUALS^RESIDUAL SYMPTOMS (PAIN, ARTHRALGIA, ETC.) OF COLD INJURY
 ;;$EXIT
 Q
