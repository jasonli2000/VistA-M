GMTSP37 ; CIO/SLC - Post Install GMTS*2.7*37    ; 07/18/2000
 ;;2.7;Health Summary;**37**;Oct 20, 1995
 ;
 Q
POST ; Post Install
 D BM(" Setting up CPT Modifiers in Health Summary Components"),BL K ^TMP("GMTSP37")
 N GMTSABR F GMTSABR="RP","OE","SR","SRO","NSR","SNSR" D COM,TYP K ^TMP("GMTSP37")
MSG N GMTSBLD,GMTSINST,GMTSHORT I +$$ROK("GMTSXPS1")>0 S GMTSHORT="",GMTSINST="",GMTSBLD="GMTS*2.7*37" D SEND^GMTSXPS1
 Q
COM ; Update Health Summary Components
 Q:'$L($G(GMTSABR))  N GMTSCI,GMTSCNM,GMTST,GMTSTI,GMTSSI
 S GMTSCI=+($O(^GMT(142.1,"C",GMTSABR,0))) Q:GMTSCI=0  S GMTSCNM=$P($G(^GMT(142.1,+GMTSCI,0)),"^",9) S:'$L(GMTSCNM) GMTSCNM=$P($G(^GMT(142.1,+GMTSCI,0)),"^") Q:'$L(GMTSCNM)  S GMTST=GMTSCNM F  Q:$L(GMTST)>22  S GMTST=GMTST_" "
 S GMTST="    "_GMTST_"  "_GMTSABR D M(GMTST),EDC S GMTSTI=0 F  S GMTSTI=$O(^GMT(142,"AE",GMTSCI,GMTSTI)) Q:+GMTSTI=0  S GMTSSI=0 F  S GMTSSI=$O(^GMT(142,"AE",GMTSCI,GMTSTI,GMTSSI)) Q:+GMTSSI=0  S ^TMP("GMTSP37",GMTSTI,GMTSSI,GMTSCI)=""
 Q
EDC ; Edit Component
 N X,Y,DIC,DIE,DTOUT,DA S DA=+($G(GMTSCI)) Q:DA=0  Q:'$D(^GMT(142.1,DA,0))
 S (DIC,DIE)="^GMT(142.1,",DR="14///Y"
 ; DBIA 10018 call ^DIE
 D ^DIE
 Q
TYP ; Update Health Summary Types
 N GMTSTI,GMTSTN,GMTSSI,GMTSCI,GMTSCN,GMTST,GMTSC,GMTSCT S (GMTSC,GMTSCT,GMTSTI)=0 F  S GMTSTI=$O(^TMP("GMTSP37",GMTSTI)) Q:+GMTSTI=0  D
 . S (GMTSCT,GMTSSI)=0 F  S GMTSSI=$O(^TMP("GMTSP37",GMTSTI,GMTSSI)) Q:+GMTSSI=0  D
 . . S GMTSCT=GMTSCT+1,GMTSCI=0 F  S GMTSCI=$O(^TMP("GMTSP37",GMTSTI,GMTSSI,GMTSCI)) Q:+GMTSCI=0  D
 . . . S GMTSTN=$P($G(^GMT(142,GMTSTI,0)),"^",1),GMTSC=GMTSC+1 D:GMTSC=1 BM(" Setting up CPT Modifiers in Health Summary Types"),BL S GMTST="    "_GMTSTN_" " D:GMTSCT=1 M(GMTST) D EDT
 Q
EDT ; Edit Type
 N X,Y,DIC,DIE,DTOUT,DA Q:+($G(GMTSTI))=0  Q:+($G(GMTSSI))=0  S DA(1)=+($G(GMTSTI)),DA=+($G(GMTSSI)),(DIC,DIE)="^GMT(142,"_DA(1)_",1,",DR="9///Y" D ^DIE
 Q
BM(X) ;   Blank Line with Message
 Q:$D(GMTSQT)
 ; DBIA 10141 call BMES^XPDUTL
 D:$D(XPDNM) BMES^XPDUTL($G(X)) W:'$D(XPDNM) !!,$G(X) Q
BL ;   Blank Line with w/o Message
 Q:$D(GMTSQT)
 ; DBIA 10141 call MES^XPDUTL
 D:$D(XPDNM) MES^XPDUTL(" ") W:'$D(XPDNM) !," " Q
M(X) ;   Message
 Q:$D(GMTSQT)
 ; DBIA 10141 call MES^XPDUTL
 D:$D(XPDNM) MES^XPDUTL($G(X)) W:'$D(XPDNM) !,$G(X) Q
ROK(X) ; Routine OK
 S X=$G(X) Q:'$L(X) 0 Q:$L(X)>8 0
 ; DBIA 10096 access ^%ZOSF
 X ^%ZOSF("TEST")
 Q:$T 1 Q 0
