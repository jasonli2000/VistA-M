HBHCRP1 ; LR VAMC(IRMS)/MJT-HBHC report on files 634.1, 634.2, & 634.3, (Form 3/4/5 Error(s)), sorted by patient & includes:  patient name, SSN, form, & corresponding date ;9204
 ;;1.0;HOSPITAL BASED HOME CARE;**2**;NOV 01, 1993
 S %ZIS="Q",HBHCCC=0 K IOP,ZTIO,ZTSAVE D ^%ZIS Q:POP
 I $D(IO("Q")) S ZTRTN="DQ^HBHCRP1",ZTSAVE("HBHC*")="",ZTDESC="HBHC Form Errors Report" D ^%ZTLOAD G EXIT
DQ ; De-queue
 U IO
 K ^TMP("HBHC",$J)
 ; Max length for HBHCHEAD = 50
 S $P(HBHCY,"-",81)="",HBHCPAGE=0,HBHCHEAD="Form Errors",HBHCHDR="W ?7,""Patient Name"",?42,""SSN"",?58,""Form"",?72,""Date""",HBHCCOLM=(80-(30+$L(HBHCHEAD))\2) S:HBHCCOLM'>0 HBHCCOLM=1 D TODAY^HBHCUTL
LOOP ; Loop thru files 634.1, 634.2 & 634.3 "B" cross-ref
 F HBHCFILE=634.1,634.2,634.3 S HBHCDPT="" F  S HBHCDPT=$O(^HBHC(HBHCFILE,"B",HBHCDPT)) Q:HBHCDPT=""  D SETUP S HBHCIEN="" F  S HBHCIEN=$O(^HBHC(HBHCFILE,"B",HBHCDPT,HBHCIEN)) Q:HBHCIEN=""  D PROCESS
 D PRTLOOP D:$D(^HBHC(634.5,"B")) PSEUDO^HBHCUTL1 D ENDRPT^HBHCUTL1
EXIT ; Exit module
 D ^%ZISC
 K HBHCCOLM,HBHCDATE,HBHCDPT,HBHCDPT0,HBHCFILE,HBHCFL,HBHCFORM,HBHCHDR,HBHCHEAD,HBHCIEN,HBHCINFO,HBHCNAME,HBHCNDX,HBHCPAGE,HBHCPC,HBHCTDY,HBHCTXT,HBHCY,HBHCZ,Y,^TMP("HBHC",$J)
 Q
SETUP ; Setup variables
 S HBHCFORM=$S(HBHCFILE=634.1:3,HBHCFILE=634.2:4,1:5),HBHCFL=$S(HBHCFORM=4:632,1:631),HBHCPC=$S(HBHCFORM=5:40,1:2)
 Q
PROCESS ; Process report data
 S HBHCDPT0=^DPT(HBHCDPT,0),HBHCDATE=$P($G(^HBHC(HBHCFL,($P(^HBHC(HBHCFILE,HBHCIEN,0),U,2)),0)),U,HBHCPC)
 S ^TMP("HBHC",$J,($P(HBHCDPT0,U)),HBHCFORM,HBHCIEN)=$E($P(HBHCDPT0,U,9),1,3)_"-"_$E($P(HBHCDPT0,U,9),4,5)_"-"_$E($P(HBHCDPT0,U,9),6,9)_U_$E(HBHCDATE,4,5)_"-"_$E(HBHCDATE,6,7)_"-"_$E(HBHCDATE,2,3)
 Q
PRTLOOP ; Print loop
 D:IO'=IO(0)!($D(IO("S"))) HDRPAGE^HBHCUTL
 I '$D(IO("S")),(IO=IO(0)) S HBHCCC=HBHCCC+1 D HDRPAGE^HBHCUTL
 S HBHCNAME=""
 F  S HBHCNAME=$O(^TMP("HBHC",$J,HBHCNAME)) Q:HBHCNAME=""  S HBHCFORM="" F  S HBHCFORM=$O(^TMP("HBHC",$J,HBHCNAME,HBHCFORM)) Q:HBHCFORM=""  D SETTXT S HBHCNDX="" F  S HBHCNDX=$O(^TMP("HBHC",$J,HBHCNAME,HBHCFORM,HBHCNDX)) Q:HBHCNDX=""  D PRINT
 Q
SETTXT ; Set form text
 S HBHCTXT=$S(HBHCFORM=3:"Eval/Adm",HBHCFORM=4:"Visit Log",1:"Discharge")
 Q
PRINT ; Print report
 S HBHCINFO=^TMP("HBHC",$J,HBHCNAME,HBHCFORM,HBHCNDX)
 I ($D(ZTRTN)!(HBHCCC=0))&((IOSL-$Y)<5) W @IOF D HDRPAGE^HBHCUTL
 W !?7,HBHCNAME,?42,$P(HBHCINFO,U),?58,HBHCTXT,?72,$P(HBHCINFO,U,2),!,HBHCY
 Q
