PRCPEITO ;WISC/RFJ-edit inventory items options ;31 Jan 92
 ;;4.0;IFCAP;;9/23/93
 D ^PRCPUSEL Q:'$G(PRCP("I"))
 N %,PRCPX,PRCPZ1
 S %="" F  S %=$O(PRCP(%)) Q:%=""  S PRCPZ1(%)=PRCP(%)
 D START(.PRCPZ1)
 Q
 ;
 ;
START(PRCPZ) ;prcpz=prcp standard variables
 N %,%H,%I,DIRUT,ITEMDA,LOCK,LINE,OPTION,PART,ROUTINE,UPFRONT,X,Y
 I PRCPZ("DPTYPE")="W" S X="PRCOSS2" X:$D(^%ZOSF("TEST")) ^("TEST") I $T D DISP^PRCOSS2(.PRCPZ)
 K UPFRONT
BEFORE W !!,"--select item upfront, return to enter/edit selection--" S ITEMDA=$$ITEM^PRCPUITM(PRCPZ("I"),1,"","") I ITEMDA["^" D EXIT Q
 I 'ITEMDA,$G(UPFRONT) D EXIT Q
 I ITEMDA S UPFRONT=1,LOCK(1)="^PRCP(445,"_PRCPZ("I")_",1,"_ITEMDA_")" D LOCK^PRCPU4(.LOCK,1,10) I '$D(LOCK) K UPFRONT G BEFORE
OPTION S DIR(0)="SO^",OPTION=1 K PART F LINE=1:1 S X=$P($T(PART+LINE),";",3,99) Q:X=""  D
 .   I $P(X,";",3)="*" S %="PRCP"_$TR(PRCPZ("DPTYPE"),"WSP","W2")_" MGRKEY" Q:'$$KEY^PRCPUREP(%,DUZ)
 .   I $P(X,";",4)'="" X $P(X,";",4) I '$T Q
 .   S PART(OPTION)=$P(X,";",1,2),DIR(0)=DIR(0)_OPTION_":"_$P(X,";")_";",OPTION=OPTION+1
 S DIR("A")="Select Field Type to Edit (from "_PRCPZ("IN")_")" W ! D ^DIR K DIR I '+Y D LOCK^PRCPU4(.LOCK,0,0) G:$G(UPFRONT) BEFORE D EXIT Q
 S ROUTINE=$P($G(PART(+Y)),";",2) I ROUTINE="" W !!,"EDIT TYPE NOT AVAILABLE AT THIS TIME." G OPTION
 I $G(UPFRONT)!(ROUTINE="SECOND") D  G:'$G(UPFRONT) BEFORE G OPTION
 .   D @ROUTINE
 .   I $G(ITEMDA) K:'$D(^PRCP(445,PRCPZ("I"),1,ITEMDA,0)) UPFRONT
 .   D:'$G(UPFRONT) LOCK^PRCPU4(.LOCK,0,0)
AFTER W !! S ITEMDA=$$ITEM^PRCPUITM(PRCPZ("I"),1,"","") I ITEMDA S LOCK(1)="^PRCP(445,"_PRCPZ("I")_",1,"_ITEMDA_")" D LOCK^PRCPU4(.LOCK,1,10) G:'$D(LOCK) AFTER D @ROUTINE,LOCK^PRCPU4(.LOCK,0,0) G AFTER
 G OPTION
 ;
 ;
EXIT ;     |-> exit routine
 I PRCPZ("DPTYPE")="W" S X="PRCOSS2" X:$D(^%ZOSF("TEST")) ^("TEST") I $T D CHECK^PRCOSS2(.PRCPZ)
 Q
 ;
 ;
SECOND ;secondary item edit
 N DISTRPT,PRCPFLAG,PRCPX,PRCPZ2
 F  S DISTRPT=+$$DISTRPT^PRCPUINV(PRCPZ("I")) Q:'DISTRPT  D  Q:$G(PRCPFLAG)
 .   I '$D(^PRCP(445,DISTRPT,4,+$G(DUZ),0)) W !?2,"YOU ARE NOT AN AUTHORIZED USER FOR THIS INVENTORY POINT." Q
 .   S PRCPFLAG=1
 I '$G(DISTRPT) Q
 K PRCPFLAG,PRCPZ2 S PRCPZ2("I")=DISTRPT,PRCPZ2("DPTYPE")=$P($G(^PRCP(445,DISTRPT,0)),"^",3),PRCPZ2("IN")=$P($P($G(^PRCP(445,DISTRPT,0)),"^"),"-",2,99) D START(.PRCPZ2)
 Q
 ;
 ;
PART ;enter edit inventory item options
 ;;All Fields (for NEW Items);ALL^PRCPEITE(PRCPZ("I"),ITEMDA)
 ;;Costing Data;COSTEDIT^PRCPEITC(PRCPZ("I"),ITEMDA);*;
 ;;Descriptive;DESCRIP^PRCPEITE(PRCPZ("I"),ITEMDA)
 ;;Drug Accountability;DISPUNIT^PRCPEITE(PRCPZ("I"),ITEMDA);*;I PRCPZ("DPTYPE")="P",$P($G(^PRCP(445,PRCPZ("I"),0)),"^",20)="D"
 ;;Edit Secondary Items;SECOND;;I PRCPZ("DPTYPE")="P"
 ;;Issue Units;UNITISS^PRCPEPKG(PRCPZ("I"),ITEMDA)
 ;;Levels;LEVELS^PRCPEITE(PRCPZ("I"),ITEMDA)
 ;;Outstanding Due-Ins;DUEIN^PRCPEQTY(PRCPZ("I"),ITEMDA);*
 ;;Procurement and Mandatory Sources;SOURCES^PRCPEPSU(PRCPZ("I"),ITEMDA)
 ;;Quantities;QTY^PRCPEQTY(PRCPZ("I"),ITEMDA);;I PRCPZ("DPTYPE")'="W"
 ;;Remove Item from Inventory;DELETE^PRCPUITM(PRCPZ("I"),ITEMDA);*
 ;;Special Parameters;SPECIAL^PRCPEITE(PRCPZ("I"),ITEMDA)
