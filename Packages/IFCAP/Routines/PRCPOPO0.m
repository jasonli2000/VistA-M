PRCPOPO0 ;WISC/RFJ-post distribution orders from primary to secondary ;28 Sep 92
 ;;4.0;IFCAP;;9/23/93
 D ^PRCPUSEL Q:'$G(PRCP("I"))
 I PRCP("DPTYPE")'="P" W !,"THIS OPTION CAN ONLY BE USED BY A PRIMARY INVENTORY POINT." Q
 S IOP="HOME" D ^%ZIS K IOP
 F  D ORDERNO Q:'$G(ORDERNO)  D TOP(ORDERNO)
 Q
 ;
 ;
TOP(V1) ;     |-> call this entry point to start posting order
 ;     |-> v1 = ordernumber
 I '$D(^PRCP(445.3,+V1,0)) Q
 N %,D,ERROR,ITEMDA,NUMBER,ORDERNO,OVERPOST,PRCPFLAG,PRCPQUIT,QTY,SECOND,SECONDA,UNITP,UNITS,X,XH,XP,Y
 S ORDERNO=+V1,SECONDA=+$P($G(^PRCP(445.3,ORDERNO,0)),"^",3),SECOND=$$INVNAME^PRCPUX1(+SECONDA) I SECOND="" W !,"THERE IS NO SECONDARY INVENTORY POINT FOR THIS ORDER NUMBER." Q
 W !?5,"DISTRIBUTION TO SECONDARY: ",SECOND
 S XP="Are you sure you want to POST this order to "_SECOND,XH="Enter 'YES' to start posting the order to the secondary inventory point",XH(1)="Enter 'NO' or '^' to exit.",%=1 W ! D YN^PRCPU4 I %'=1 Q
 D LOCK^PRCPOENU(ORDERNO) I $G(PRCPFLAG) Q
 ;
 W !!,"Checking items and quantities to post..."
 S IOP="HOME" D ^%ZIS K IOP
 K ^TMP($J,"POST"),^TMP($J,"ERROR"),OVERPOST,PRCPFLAG
 S (ITEMDA,NUMBER)=0 F  S ITEMDA=$O(^PRCP(445.3,ORDERNO,1,ITEMDA)) Q:'ITEMDA!($G(PRCPFLAG))  S D=^(ITEMDA,0),QTY=+$P(D,"^",2) D
 .   S ERROR=1,X=$G(^PRCP(445,PRCP("I"),1,ITEMDA,0)) I X="" S ^TMP($J,"ERROR",ITEMDA,ERROR)="ITEM NOT FOUND IN PRIMARY INVENTORY POINT.",ERROR=ERROR+1,QTY=0
 .   S Y=$G(^PRCP(445,SECONDA,1,ITEMDA,0)) I Y="" S ^TMP($J,"ERROR",ITEMDA,ERROR)="ITEM NOT FOUND IN SECONDARY INVENTORY POINT.",ERROR=ERROR+1
 .   S %=$P(X,"^",7)-$P(D,"^",2) I %<0 S ^TMP($J,"ERROR",ITEMDA,ERROR)="QUANTITY ORDERED IS GREATER THAN QUANTITY ON-HAND.",ERROR=ERROR+1,OVERPOST=1
 .   S %=$$GETVEN^PRCPUVEN(SECONDA,ITEMDA,PRCP("I")_";PRCP(445,",1),UNITP=$$UNIT^PRCPUX1(PRCP("I"),ITEMDA,"/"),UNITS=$$UNITVAL^PRCPUX1($P(%,"^",3),$P(%,"^",2),"/") I UNITS'=UNITP D
 .   .   S ^TMP($J,"ERROR",ITEMDA,ERROR)="PRIMARY UNIT OF ISSUE ["_UNITP_"] DOES NOT EQUAL SECONDARY",ERROR=ERROR+1
 .   .   S ^TMP($J,"ERROR",ITEMDA,ERROR)="        UNIT OF RECPT ["_UNITS_"], CONVERSION FACTOR = "_$P(%,"^",4),ERROR=ERROR+1
 .   I $O(^TMP($J,"ERROR",ITEMDA,0)) S ^TMP($J,"ERROR",ITEMDA)=+$P(D,"^",2)
 .   I QTY S ^TMP($J,"POST",ITEMDA)=QTY
 .   W !,"ITEM #: ",ITEMDA,?15,$E($$DESCR^PRCPUX1(PRCP("I"),ITEMDA),1,20),?37,"ON-HAND: ",+$P(X,"^",7),?53,"ORDERED: ",+$P(D,"^",2),?69,"POST: ",QTY
 .   S NUMBER=NUMBER+1 I (NUMBER#20)=0 D P^PRCPU4 S NUMBER=0
 I $G(PRCPFLAG) G CONT
 ;
 I $O(^TMP($J,"ERROR",0)) K PRCPQUIT D TOP^PRCPOPOR I $G(PRCPQUIT) G CONT
 ;
 D POST^PRCPOPO1
 ;
CONT ;     |-> unlock and continue
 D UNLOCK^PRCPOENU(ORDERNO,PRCP("I"),SECONDA)
 K ^TMP($J,"POST"),^TMP($J,"ERROR")
 Q
 ;
 ;
ORDERNO ;get distribution order number
 N %,PRCPPRIV,DIC,X,Y
 K ORDERNO S DIC(0)="AEQM",DIC="^PRCP(445.3,",DIC("S")="I $P(^(0),U,2)=PRCP(""I""),(($P(^(0),U,6)=""R"")!($P(^(0),U,6)=""B""))",DIC("A")="Select DISTRIBUTION ORDER: ",PRCPPRIV=1 W !! D ^DIC K PRCPPRIV Q:Y<0
 S ORDERNO=+Y Q
