PRCPPO1 ;WISC/RFJ-primary receive issue book ;17 July 91
 ;;4.0;IFCAP;;9/23/93
 D ^PRCPUSEL Q:'$G(PRCP("I"))  I PRCP("DPTYPE")'="P" W !,"ONLY PRIMARIES CAN RECEIVE POSTED STOCK!" Q
 I $P($G(^PRCP(445,PRCP("I"),0)),"^",2)'="Y" W !,"THE PRIMARY MUST BE KEEPING A PERPETUAL INVENTORY." Q
 I $P($G(^PRCP(445,PRCP("I"),0)),"^",6)'="Y" W !,"THE PRIMARY MUST BE KEEPING A DETAILED TRANSACTION HISTORY." Q
 I $P(^PRCP(445,PRCP("I"),0),"^",16)'="N" W !,"BEFORE USING THIS OPTION, THE 'PRIMARY UPDATED BY WAREHOUSE' MUST BE SET TO NO." Q
 N %,DA,DIC,DIR,PRCPDA,PRCPFLAG,PRCPITEM,PRCPLIDA,PRCPLOCK,PRCPOPT,PRCPPVNO,PRCPQTY,PRCPREPT,PRCPTRAN,PRCPTYPE,PRCPX,WHSE,X,Y
 S X="" W ! D ESIG^PRCUESIG(DUZ,.X) I X'>0 Q
TOP S PRCPPVNO=+$O(^PRC(440,"AC","S",0))_";PRC(440," I '$D(^PRC(440,+PRCPPVNO,0)) W !!,"THERE IS NOT A VENDOR IN THE VENDOR FILE (#440) DESIGNATED AS A SUPPLY WHSE." Q
 S DIC="^PRCS(410,",DIC(0)="QEAMZ",DIC("A")="Select TRANSACTION NUMBER: "
 S DIC("S")="I $P(^(0),U,6)=PRCP(""I""),$P(^(0),U,2)=""O"",$P(^(0),U,4)=5,$P($G(^(3)),U,4)=+PRCPPVNO,$P($G(^(7)),U,6)]"""",$S('$D(^PRC(443,+Y,0)):1,$P(^(0),U,3)]"""":1,1:0)"
 W ! D ^PRCSDIC K DIC Q:Y<1  S PRCPDA=+Y,PRCPTRAN=$P(^PRCS(410,PRCPDA,0),"^")
 ;     |-> find the warehouse
 K WHSE S %=0 F  S %=$O(^PRCP(445,"AC","W",%)) Q:'%  I +$G(^PRCP(445,+%,0))=+PRCPTRAN S WHSE=% Q
 I '$D(WHSE) W !,"THERE IS NOT A WAREHOUSE DESIGNATED FOR STATION '",+PRCPTRAN,"'." G TOP
 W !!,"DISTRIBUTION FROM THE WAREHOUSE: ",$$INVNAME^PRCPUX1(WHSE)
 ;
 S DIR(0)="S^1:ALL Items on Request;2:EXCEPTION (Enter only items NOT received);3:SELECTION (Enter only items received);",DIR("A")="Select METHOD of Receiving",DIR("B")="SELECTION"
 S DIR("?",1)="Enter ALL to receive ALL the items exactly as the primary ordered them."
 S DIR("?",2)="Enter EXCEPTION to enter the items NOT received as ordered.  ALL other",DIR("?",3)="NON selected items will be received as ordered by the primary."
 S DIR("?",4)="Enter SELECTION to select ONLY the items you want to receive.  All other",DIR("?")="items NOT selected will NOT be received as ordered by the primary." D ^DIR K DIR I Y'=1,Y'=2,Y'=3 G TOP
 S PRCPOPT=Y,PRCPTYPE="Receiving" D REPASK^PRCPWPOR G:'$D(PRCPREPT) TOP
 ;
 K ^TMP($J) W !!,"LOCKING INVENTORY POINT: ",$P($$INVNAME^PRCPUX1(PRCP("I")),"-",2,99) S PRCPLOCK(1)="^PRCP(445,"_PRCP("I")_",1)" D LOCK^PRCPU4(.PRCPLOCK,1,10) G:'$D(PRCPLOCK) TOP
 W !,"INVENTORY POINT LOCKED.  OTHER JOBS WILL NOT BE ABLE TO ACCESS INVENTORY",!,"POINT UNTIL RECEIVING IS COMPLETED."
 I PRCPOPT=1 G START
ITEM S PRCPFLAG=0,DIC("W")="S PRCPNODE=13 D DICW^PRCPWPO1 K PRCPNODE",DIC="^PRCS(410,"_PRCPDA_",""IT"",",DA(1)=PRCPDA,DIC(0)="QEAMZ",DIC("A")="Select LINE ITEM Number: " W !! D ^DIC K DIC,DA I +Y<0 G START
 S PRCPLIDA=+Y,PRCPX=Y(0),PRCPITEM=$P(PRCPX,"^",5)
 I '$D(^PRCP(445,PRCP("I"),1,PRCPITEM,0)) W !,"THIS ITEM IS NOT IN THE PRIMARY INVENTORY AND WILL NOT BE POSTED." S ^TMP($J,"NOPOST",PRCPLIDA)="" G ITEM
 I $P(PRCPX,"^",14)'="" W !?5,"ITEM IS CANCELLED",$S($P(PRCPX,"^",14)["S":" AND SUBSTITUTED WITH LINE #(S): "_$P($P(PRCPX,"^",14),",",2,99),1:""),!?5,"YOU CANNOT RECEIVE IN THIS ITEM." S ^TMP($J,"NOPOST",PRCPLIDA)="" G ITEM
 S PRCPQTY=$P(PRCPX,"^",2)-$P(PRCPX,"^",13) S:PRCPQTY<0 PRCPQTY=0 W !,$J("QUANTITY ORDERED:",30),$J(+$P(PRCPX,"^",2),7),!,$J("QUANTITY RECEIVED:",30),$J(+$P(PRCPX,"^",13),7),!,$J("QUANTITY OUTSTANDING:",30),$J(PRCPQTY,7)
QTY W !,$J("Enter QUANTITY to RECEIPT:",30),$J(PRCPQTY,7),"// " R X:DTIME I '$T!(X["^") W:$D(^TMP($J,"POST",PRCPLIDA)) "  *** RECEIVING ",^(PRCPLIDA)," ***" G ITEM
 S:X="" X=PRCPQTY I +X'=X!(X>PRCPQTY)!(X<0)!(X?.E1"."3N.N) W !,"ENTER THE QUANTITY TO RECEIVE FROM 0 TO ",PRCPQTY G QTY
 K ^TMP($J,"NOPOST",PRCPLIDA) W "   *** RECEIVING ",X," ***" S ^TMP($J,"POST",PRCPLIDA)=X G ITEM
 ;
 ;
START ;     |-> loop items for errors
 D TOP^PRCPPO3,LOCK^PRCPU4(.PRCPLOCK,0,0) G TOP
