PRCPRINA ;WISC/RFJ-inactivity item report ;4 Feb 92
 ;;4.0;IFCAP;;9/23/93
 D ^PRCPUSEL Q:'$G(PRCP("I"))
 N %,DATA,DATE,DEFAULT,END,ENDDT,FLAG,ITEMDA,LASTMO,LASTR,LASTU,MAXDT,MONTHS,NOW,NOWDT,PAGE,PRCPEND,PRCPFLAG,SCREEN,START,STARTDT,X,Y
 D NOW^%DTC S NOWDT=X,Y=% X ^DD("DD") S NOW=Y,Y=$E(NOWDT,1,5)_"00" X ^DD("DD") S END=Y,X1=$E(NOWDT,1,5)_"15",X2=-30 D C^%DTC S (LASTMO,Y)=$E(X,1,5)_"00" X ^DD("DD") S DEFAULT=Y
 S PRCPEND=$P("31^28^31^30^31^30^31^31^30^31^30^31","^",+$E(NOWDT,4,5)) I PRCPEND=28,$E(NOWDT,2,3)#4=0 S PRCPEND=29
 S MAXDT=$E(NOWDT,1,5)_PRCPEND
START S %DT="AEP",%DT("A")="Start Item INACTIVITY with Date (Month Year): ",%DT("B")=DEFAULT,%DT(0)=-MAXDT W ! D ^%DT K %DT Q:Y<0  S STARTDT=$E(Y,1,5)
 S %DT="AEP",%DT("A")="  End Item INACTIVITY with Date (Month Year): ",%DT("B")=END,%DT(0)=-MAXDT D ^%DT K %DT Q:Y<0  S ENDDT=$E(Y,1,5)
 I ENDDT<STARTDT W !,"END DATE MUST BE GREATER THAN OR EQUAL TO THE START DATE." G START
 S %ZIS="Q" D ^%ZIS Q:POP  I $D(IO("Q")) D  D ^%ZTLOAD K IO("Q"),ZTSK Q
 .   S ZTDESC="Inactive Item Report",ZTRTN="DQ^PRCPRINA"
 .   S ZTSAVE("PRCP*")="",ZTSAVE("START*")="",ZTSAVE("END*")="",ZTSAVE("NOW*")="",ZTSAVE("ZTREQ")="@"
 W !!,"<*> please wait <*>"
DQ ;queue comes here
 K ^TMP($J,"INACT") S X1=ENDDT_"00",X2=STARTDT_"00" D ^%DTC S MONTHS=(X+12)\30 S:'MONTHS MONTHS=1
 S ITEMDA=0 F  S ITEMDA=$O(^PRCP(445,PRCP("I"),1,ITEMDA)) Q:'ITEMDA  D
 .   S DATE=STARTDT-1,FLAG=0 F  S DATE=$O(^PRCP(445,PRCP("I"),1,ITEMDA,2,DATE)) Q:'DATE!(DATE>ENDDT)  I +$P($G(^(DATE,0)),"^",2) S FLAG=1 Q
 .   Q:FLAG  S (DATE,LASTU)=0 F  S DATE=$O(^PRCP(445,PRCP("I"),1,ITEMDA,2,DATE)) Q:'DATE  I +$P($G(^(DATE,0)),"^",2) S LASTU=DATE
 .   I LASTU S Y=$E(LASTU,1,5)_"00" X ^DD("DD") S LASTU=Y
 .   S (DATE,LASTR)=0 F  S DATE=$O(^PRCP(445,PRCP("I"),1,ITEMDA,3,DATE)) Q:'DATE  I +$P($G(^(DATE,0)),"^",2) S LASTR=DATE
 .   I LASTR S Y=$E(LASTR,1,5)_"00" X ^DD("DD") S LASTR=Y
 .   S ^TMP($J,"INACT",ITEMDA)=$S(LASTU=0:"",1:LASTU)_"^"_$S(LASTR=0:"",1:LASTR)
 S Y=STARTDT_"00" X ^DD("DD") S START=Y,Y=ENDDT_"00" X ^DD("DD") S END=Y,PAGE=1,SCREEN=$$SCRPAUSE^PRCPUREP U IO D H
 S ITEMDA=0 F  S ITEMDA=$O(^TMP($J,"INACT",ITEMDA)) Q:'ITEMDA!($D(PRCPFLAG))  S DATA=^(ITEMDA) D
 .   S FLAG="" I $P($G(^PRCP(445,PRCP("I"),1,ITEMDA,0)),"^",26)="Y" S FLAG=+$P(^(0),"^",7)
 .   W !,ITEMDA,?10,$E($$DESCR^PRCPUX1(PRCP("I"),ITEMDA),1,20),?32,$J($P(DATA,"^"),14),$J($P(DATA,"^",2),14),$J(+$P($G(^PRCP(445,PRCP("I"),1,ITEMDA,0)),"^",20),10),$J(FLAG,10)
 .   I $Y>(IOSL-4) D:SCREEN P^PRCPU4 Q:$D(PRCPFLAG)  D H
 .   I $G(ZTQUEUED),$$S^%ZTLOAD S PRCPFLAG=1 W !?10,"<<< TASKMANAGER JOB TERMINATED BY USER >>>"
 I '$D(PRCPFLAG) D END^PRCPUREP
 D ^%ZISC K ^TMP($J,"INACT") Q
 ;
H S %=NOW_"  PAGE "_PAGE,PAGE=PAGE+1 I PAGE'=2!(SCREEN) W @IOF
 W $C(13),"INACTIVE ITEM REPORT FOR: ",PRCP("IN"),?(80-$L(%)),%
 W !?5,"INACTIVE USAGE FROM ",START," TO ",END,"  (",MONTHS," MONTHS)"
 W !?5,"DELETE = delete item when QUANTITY ON HAND (shown if DELETE is YES) is 0"
 S %="",$P(%,"-",81)="" W !,"MI#",?10,"DESCRIPTION",?32,$J("LAST USAGE",14),$J("LAST RECPT",14),$J("DUE-OUT",10),$J("DELETE",10),!,% Q
