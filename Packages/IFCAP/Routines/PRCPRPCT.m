PRCPRPCT ;WISC/RFJ-percent stock on hand before receiving report ;10 Feb 92
 ;;4.0;IFCAP;;9/23/93
 D ^PRCPUSEL Q:'$G(PRCP("I"))
 N %,D,DATEEND,DATESTR,DEFAULT,NOW,NOWDT,PRCPINV,X,Y S PRCPINV=$$INVNAME^PRCPUX1(PRCP("I")) D NOW^%DTC S Y=X X ^DD("DD")
 S NOWDT=X,NOW=Y,X1=X,X2=-180 D C^%DTC S Y=$E(X,1,5)_"01" X ^DD("DD") S DEFAULT=Y,%DT="AEP",%DT("A")="START with DATE: ",%DT("B")=DEFAULT,%DT(0)=-NOWDT W ! D ^%DT K %DT Q:Y<0  S DATESTR=Y
ENDDATE S %DT="AEP",%DT("A")="  END with DATE: ",%DT("B")=NOW,%DT(0)=-NOWDT D ^%DT K %DT Q:Y<0  I Y<DATESTR W !,?2,"END DATE MUST BE EQUAL TO OR AFTER DATE: " S Y=DATESTR X ^DD("DD") W Y G ENDDATE
 S DATEEND=Y,Y=DATESTR X ^DD("DD") W !!,"I will print the receipts from ",Y," to " S Y=DATEEND X ^DD("DD") W Y,!!
 S DIR(0)="S^1:NSN;2:GROUP CATEGORY",DIR("A")="Sort BY",DIR("B")="NSN" D ^DIR K DIR I Y'=1,Y'=2 Q
 I Y=1 W !?2,"START WITH NSN: FIRST// @    <<-- ENTER '@' TO PRINT ITEMS WITHOUT A NSN" S BY="[PRCP SORT:NSN]"
 E  W !?2,"START WITH GROUP CATEGORY CODE: FIRST// @   <<-- ENTER '@' TO PRINT ITEMS",!?51,"WITHOUT A GROUP CATEGORY CODE" S BY="[PRCP SORT:GROUP]"
 S DIC="^PRCP(445,",L=0,FLDS="[PRCP REPORT:RECEIPT]",DIS(0)="I D0=PRCP(""I"")",DIOEND="D END^PRCPUREP" D EN1^DIP Q
 ;
PRINT ;called from print template
 N COUNT,D,DATE,DATESTR,DATEEND,DD,X,Y I '$D(DATESTR) S DATESTR=.1,DATEEND=DT
 S DD=^DD("DD"),COUNT=1,DATE=DATESTR-.1 F  S DATE=$O(^PRCP(445,D0,1,D1,3,DATE)) Q:'DATE!(DATE>DATEEND)  S D=$G(^(DATE,0)) I D'="" D  Q:$G(Y)=U
 .   I COUNT=1 W !?10,$J("DATE RECD",9),$J("QTY",7),$J("PERCENT",10),?50,"DATE RECD",$J("QTY",7),$J("PERCENT",10)
 .   I +$P(D,"^",3)>9999 S $P(D,"^",3)=">9999.00"
 .   S COUNT=COUNT+1,Y=DATE X DD W:COUNT#2=0 !?10 W:COUNT#2'=0 ?50 W $J($P(Y,",")_","_$E($P(Y,",",2),3,4),9),$J(+$P(D,"^",2),7),$J($P(D,"^",3),10,2) Q:COUNT#2=0
 .   I $D(IOSL),$Y'<(IOSL-3),$D(^UTILITY($J,1))#2,^(1)?1U1P1E.E X ^(1)
 Q
 ;
DT ;called from ^utility($j,"de",2..4) when printing page break
 I Y W $P("JAN^FEB^MAR^APR^MAY^JUN^JUL^AUG^SEP^OCT^NOV^DEC",U,$E(Y,4,5))_" " W:Y#100 $J(Y#100\1,2)_"," W Y\10000+1700 W:Y#1 "  "_$E(Y_0,9,10)_":"_$E(Y_"000",11,12) Q
 W Y Q
