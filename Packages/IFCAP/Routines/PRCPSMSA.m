PRCPSMSA ;WISC/RFJ-isms adjustment transaction ;22 Oct 91
 ;;4.0;IFCAP;;9/23/93
 Q
 ;
 ;
ADJUST ;     |-> adjust inventory items
 N %,ADJUST,COUNT,DATA,DESCR,ITEMDA,NSN,PRCPFLAG,STRING,UNIT,VALUE,X,Y
 S IOP="HOME" D ^%ZIS K IOP,^TMP($J,"ADJUST"),^TMP($J,"STRING")
 F  W !! S ITEMDA=$$ITEM^PRCPUITM(PRCP("I"),0,"","") Q:'ITEMDA  D
 .   S NSN=$$NSN^PRCPUX1(ITEMDA) I NSN="" W !,"ITEM DOES NOT HAVE AN NSN!" Q
 .   I $$SKU^PRCPUX1(PRCP("I"),ITEMDA)["?" W !,"SKU IS NOT DEFINED!" Q
 .   S DESCR=$$DESCR^PRCPUX1(PRCP("I"),ITEMDA),DATA=$G(^PRCP(445,PRCP("I"),1,ITEMDA,0)),VALUE=$J($P(DATA,"^",27),0,3),UNIT=$$UNITVAL^PRCPUX1($P(DATA,"^",14),$P(DATA,"^",5)," per ")
 .   W @IOF,!!,ITEMDA,?7,$E(DESCR,1,50),?60,NSN,!!?5,"UNIT per ISSUE  : ",UNIT,!?5,"QUANTITY DUE-OUT: ",+$P(DATA,"^",20),!?5,"QUANTITY ON-HAND: ",+$P(DATA,"^",7),!?5,"QUANTITY NON-ISSUABLE: ",+$P(DATA,"^",19)
 .   W !?5,"AVERAGE COST    : ",$J(+$P(DATA,"^",22),0,3),!?5,"ON-HAND VALUE   : ",VALUE
 .   W !!,"ADJUSTMENTS WILL NOT AFFECT THE DATA LISTED (STORED IN DHCP) ABOVE.",!,"IT WILL ONLY CHANGE THE DATA CURRENTLY STORED IN AUSTIN."
 .   K DIR S DIR(0)="SO^1:Due-Out Quantity;2:On-Hand Quantity;3:Value On-Hand",DIR("A")="Adjust" D ^DIR I '+Y Q
 .   S ADJUST=+Y I $D(^TMP($J,"ADJUST",ITEMDA,"CS",ADJUST)) W !,"REMOVING ISMS CODE SHEET WHICH WILL ADJUST ",Y(0)," BY: ",$G(^(ADJUST,"V")) K ^TMP($J,"ADJUST",ITEMDA,"CS",ADJUST)
 .   K DIR S DIR(0)="N^"_$S(ADJUST=1:"-99999999:999999999",ADJUST=2:"-999999999:9999999999",1:"-9999999.99:99999999.99:2"),DIR("A")="Adjust "_Y(0)_" by Amount" W ! D ^DIR S Y=+Y I '+Y Q
 .   I ADJUST=1 D ADJUST^PRCPSMA0(PRCP("I"),ITEMDA,"","","",Y)
 .   I ADJUST=2 D ADJUST^PRCPSMA0(PRCP("I"),ITEMDA,Y,"","","")
 .   I ADJUST=3 D ADJUST^PRCPSMA0(PRCP("I"),ITEMDA,"",Y,"","")
 .   I STRING("AT")="" W !,"UNABLE TO CREATE CODE SHEET!" Q
 .   S ^TMP($J,"ADJUST",ITEMDA)=DESCR_"^"_NSN_"^"_UNIT_"^"_+$P(DATA,"^",7)_"^"_+$P(DATA,"^",20)_"^"_VALUE,^TMP($J,"ADJUST",ITEMDA,"CS",ADJUST)=STRING("AT"),^TMP($J,"ADJUST",ITEMDA,"CS",ADJUST,"V")=Y
 .   W !,"CS: ",STRING("AT")
 I ITEMDA["^" D Q Q
 I '$O(^TMP($J,"ADJUST",0)) W !,"NO ITEMS SELECTED." D Q Q
 W @IOF,!,"YOU HAVE SELECTED THE FOLLOWING ITEMS TO ADJUST:" S ITEMDA=0,COUNT=1 F  S ITEMDA=$O(^TMP($J,"ADJUST",ITEMDA)) Q:'ITEMDA!($D(PRCPFLAG))  S %=^(ITEMDA) I %'="" D
 .   W !,ITEMDA,?7,$E($P(%,"^"),1,50),?60,$P(%,"^",2)
 .   I $D(^TMP($J,"ADJUST",ITEMDA,"CS",1)) S ^TMP($J,"STRING",COUNT)=^(1),COUNT=COUNT+1 W !?7,"ADJ QTY DUE-OUT (DHCP Value: ",$P(%,"^",5)," in ",$P(%,"^",3),") BY: ",$G(^TMP($J,"ADJUST",ITEMDA,"CS",1,"V"))
 .   I $D(^TMP($J,"ADJUST",ITEMDA,"CS",2)) S ^TMP($J,"STRING",COUNT)=^(2),COUNT=COUNT+1 W !?7,"ADJ QTY ON-HAND (DHCP Value: ",$P(%,"^",4)," in ",$P(%,"^",3),") BY: ",$G(^TMP($J,"ADJUST",ITEMDA,"CS",2,"V"))
 .   I $D(^TMP($J,"ADJUST",ITEMDA,"CS",3)) S ^TMP($J,"STRING",COUNT)=^(3),COUNT=COUNT+1 W !?7,"ADJ ON-HAND DOLLAR$ VALUE (DHCP Value: ",$P(%,"^",6),") BY: ",$G(^TMP($J,"ADJUST",ITEMDA,"CS",3,"V"))
 .   I $Y>(IOSL-5) D P^PRCPU4
 I $D(PRCPFLAG) D Q Q
 I '$O(^TMP($J,"STRING",0)) W !!,"NO CODE SHEETS CREATED." D Q Q
 S XP="*** ARE YOU SURE YOU WANT TO CREATE THE ISMS TRANSACTION",XP(1)="    AND TRANSMIT IT TO AUSTIN",XH="ENTER 'YES' TO CREATE THE ISMS TRANSACTION AND TRANSMIT IT TO AUSTIN",XH(1)="ENTER 'NO' OR '^' TO EXIT.",%=1 W !! D YN^PRCPU4
 I %'=1 D Q Q
 D CODESHT^PRCPSMGO(PRC("SITE"),"ADJ","")
Q K ^TMP($J,"ADJUST"),^TMP($J,"STRING") Q
