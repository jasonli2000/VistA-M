PRCPSPA0 ;BOISE/TKW,WISC/AKS-assemble set packs ;17 Feb 92
 ;;4.0;IFCAP;;9/23/93
 ;
 K ^TMP("PRCPMAIN",$J)
 N I,X,Y,PRCPAVG,PRCPCOST,PRCPDTI,PRCPI,PRCPITEM,PRCPMY,PRCPOAVG,PRCPODI,PRCPONH,ORDERNO,PRCPQ,PRCPQTY,PRCPSET,PRCPUR,FLG,ITEMDA,NSETS,ONHQTY,RSETS,SITEM,PRCPSRC1,%,PRCPNSN,PRCPSUB,CASE
 ; ASSEMBLE NEW SETS/PACKS--UPDATES SET/PACK INVENTORY
 D ^PRCPUSEL Q:'$G(PRCP("I"))  D NOW^%DTC S PRCPDTI=$P(%,".",1),PRCPODI=PRCPDTI,PRCPMY=$E(%,1,5),ORDERNO=$$ORDERNO^PRCPUTR(PRCP("I"))
 ; SET DIC("W") TO DISPLAY ITEM DESCRIPTION FROM INVENTORY.  CALLED FROM INPUT TRANSFORM ON FILE 445, FIELD 1, ITEM NO.
 S DIC("W")="S Z=^PRC(441,+Y,0),PRCPDESC=$P(Z,U,2) S:$D(^PRCP(445,DA(1),1,+Y,6)) PRCPDESC=$S($P(^(6),U,1)'="""":$P(^(6),U,1),1:$P(Z,U,2)) D WD^PRCPSPA0"
E2 W !!! S DIC(0)="AEQMZ",DIC("S")="I $P(^PRC(441,+Y,0),U,6)=""S"",$P(^(0),U,7)=PRCP(""I"")",PRCPSET=DIC("S"),DIC("A")="Select Set/Pack: " D  G:PRCPITEM=-1 EXIT
 .   S:'$D(DIC("A")) DIC("A")="Select Item: " S:'$D(PRCPSET) PRCPSET="I 1" I DIC(0)["L" S DLAYGO=445
 .   S:'$D(^PRCP(445,PRCP("I"),1,0)) ^(0)="^445.01IP^^"
 .   S DIC="^PRCP(445,"_PRCP("I")_",1,",DA(1)=PRCP("I") D ^DIC K DIC,DLAYGO,DA,DO,PRCPSET S PRCPITEM=+Y Q:Y=-1  D
 .   .   ; FIND ITEM IN ITEM MASTER FILE, RETURN DESCRIPTION, SUBACCOUNT, NATL.STOCK NO., FLAG INDICATING WHETHER ITEM IS PURCHASEABLE OR A SET/PACK.
 .   .   K PRCPITYP S:'$D(PRCPDESC) PRCPDESC="" S PRCPSUB="",PRCPNSN=""
 .   .   S X=$S($D(^PRC(441,+PRCPITEM,0)):^(0),1:"") Q:'X  S PRCPDESC=$S(PRCPDESC="":$P(X,U,2),1:PRCPDESC),PRCPSUB=+$P(X,U,10),PRCPNSN=$P(X,U,5),PRCPITYP=$S($P(X,U,6)'="":$P(X,U,6),1:"P")
 S PRCPONH=$P(Y(0),U,7),PRCPOAVG=$P(Y(0),U,22),PRCPUR=$$UNIT^PRCPUX1(PRCP("I"),PRCPITEM,"/"),ITEMDA=PRCPITEM
Q W !,"  ENTER QUANTITY ASSEMBLED:",$S($G(DEF):DEF_"//",1:"") R X:DTIME G:('$T)!(X["^") E2 S:($G(DEF)&(X="")) X=DEF G:X="" E2 I +X'=X!(X<1)!(X>999999)!(X?.E1"."1N.N) D W G Q
 K RSETS,DEF S NSETS=X W !,"Are you sure you wish to assemble this many sets/packs " S %=1 D YN^DICN G:%=-1 EXIT G:%=0 W G:%'=1 Q
UI ; LOOP THROUGH THE SET/PACK MUTILPLE AND CHECK FOR NEGATIVE BALANCES FOR
 ; EACH ITEM THAT MAKES UP THE SET/PACK.
 S FLG=0,SITEM=0 F  S SITEM=$O(^PRCP(445,PRCP("I"),1,ITEMDA,8,SITEM)) G:('SITEM&'FLG) PROC^PRCPSPA1 G:'SITEM Q S PRCPQTY=+$P(^(SITEM,0),U,2) D:$D(^PRCP(445,PRCP("I"),1,SITEM,0))  I '$D(^PRCP(445,PRCP("I"),1,SITEM)) D ERR G EXIT
 .   S ONHQTY=$P(^PRCP(445,PRCP("I"),1,SITEM,0),U,7),PRCPQTY=NSETS*PRCPQTY
 .   I PRCPQTY>ONHQTY S FLG=1,RSETS=$P(ONHQTY/PRCPQTY,".") W $C(7),!,"**WARNING--INVENTORY HAS GONE NEGATIVE ON ITEM NO. "_+SITEM W:RSETS>0 !," Please decrease the number of set/packs to "_+RSETS_" or less, to correct the problem." D:RSETS>0
 .   .   S:'$D(DEF) DEF=RSETS S:RSETS<DEF DEF=RSETS
W W $C(7),!!,"Enter the number of sets/packs to be assembled (number between 1 and 999999)"
 W !!,"This will increment the 'quantity on hand' for the Set/Pack specified, by the",!,"quantity of new Sets/Packs assembled.",!
 Q
ERR ;
 W !!,"Item "_SITEM_" is not in this inventory location. Please correct this problem before the set/packs could be assembled" Q
ID ; DISPLAY ITEM DESCRIPTION ON ITEMS THAT MAKE UP SETS/PACKS
 S Z=^PRC(441,+Y,0),PRCPDESC=$P(Z,U,2) S:$D(^PRCP(445,DA(1),1,+Y,6)) PRCPDESC=$S($P(^(6),U,1)'="""":$P(^(6),U,1),1:$P(Z,U,2)) S DIC("W")="D ID^PRCPSPA0"
 W $E("     ",$L(X),5),PRCPDESC K Z,ZX,PRCPDESC Q
WD S Z3="",$P(Z3," ",31)=""
 S Z1=$E(PRCPDESC,1,30),Z2=$E(PRCPDESC,31,60) S:$L(Z1)'=30 Z1=Z1_$E(Z3,1,30-$L(Z1))
 W $E("     ",$L(X),5) S ZX=$X+1 W Z1_"     NSN: "_$P(Z,U,5) W:Z2'="" !,?ZX,Z2 K Z,ZX,Z1,Z2,Z3 Q
EXIT ;
 QUIT
