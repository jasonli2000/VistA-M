PRCPSPD1 ;WISC/AKS-disassemble set packs for secondaries (cont) ;5 May 92
 ;;4.0;IFCAP;;9/23/93
 S DIR(0)="S^1:All;2:Exception (all items used except these);3:Selection"
 S DIR("A")="Choose one type" D ^DIR K DIR G:Y=2 EXC G:Y=3 SEL G:Y["^" EXIT
 S PRCPITEM=0 F  S PRCPITEM=$O(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM)) G:PRCPITEM=""!(PRCPITEM'?1.N) UI S ^TMP($J,PRCPITEM)=$P(^(PRCPITEM,0),U,2)_U_0
SEL ; Create a temporary global if set/pack is disassembled by selection.
 S (D0,DA(1))=ITEMDA,SLCT=3
ITEM1 S DIC("A")="SELECT ITEM: ",DIC="^PRCP(445,"_PRCP("I")_",1,"_ITEMDA_",8,",DIC(0)="AEQZ" D ^DIC K DIC,DA,D0 G:((Y=-1)&(X="")) LOOP1 G:Y=-1 EXIT S PRCPITEM=+Y
 W !,"  QUANTITY USED:",$P(Y(0),U,2),"//" R X:DTIME G:('$T)!(X["^") ITEM1 S:X="" X=$P(Y(0),U,2) I +X'=X!(X<1)!(X>999999)!(X?.E1"."1N.N)!(X>$P(Y(0),U,2)) D W^PRCPSPD2
 S ^TMP($J,PRCPITEM)=X_U_($P($G(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM,0)),U,2)-X) G SEL
LOOP1 S PRCPITEM=0 F  S PRCPITEM=$O(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM)) G:PRCPITEM=""!(PRCPITEM'?1.N) UI S:'$D(^TMP($J,PRCPITEM)) ^TMP($J,PRCPITEM)=0_U_$P($G(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM,0)),U,2)
EXC ; Create a temporary global if set/pack is disassembled by Exception.
 S (D0,DA(1))=ITEMDA,SLCT=2
 S DIC("A")="SELECT ITEM: ",DIC="^PRCP(445,"_PRCP("I")_",1,"_ITEMDA_",8,",DIC(0)="AEQZ" D ^DIC K DIC,DA,D0 G:((Y=-1)&(X="")) LOOP2 G:Y=-1 EXIT S PRCPITEM=+Y
 W !,"  QUANTITY UNUSED:",$P(Y(0),U,2),"//" R X:DTIME Q:('$T)!(X["^")  S:X="" X=$P(Y(0),U,2) I +X'=X!(X<1)!(X>999999)!(X?.E1"."1N.N)!(X>$P(Y(0),U,2)) D W^PRCPSPD2
 S ^TMP($J,PRCPITEM)=($P($G(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM,0)),U,2)-X)_U_X G EXC
LOOP2 S PRCPITEM=0 F  S PRCPITEM=$O(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM)) G:PRCPITEM=""!(PRCPITEM'?1.N) UI S:'$D(^TMP($J,PRCPITEM)) ^TMP($J,PRCPITEM)=$P($G(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM,0)),U,2)_U_0
UI ; DECREMENT THE QTY.ON HAND FOR THE SET/PACK ASSEMBLED, AND RECALCULATE WEIGHTED AVG.COST PER SET/PACK, UPDATE LAST COST, AVG.COST.
 S PRCPCOST=0 D CST^PRCPSPD0
 I $G(ORDERNO) K PRCPSPD1 S PRCPSPD1("QTY")=-1,(PRCPSPD1("SELVAL"),PRCPSPD1("INVVAL"))=PRCPCOST D ADDTRAN^PRCPUTR(PRCP("I"),ITEMDA,"S",ORDERNO,.PRCPSPD1) K PRCPSPD1
 S $P(^(0),U,7)=$P(^PRCP(445,PRCP("I"),1,ITEMDA,0),U,7)-1,PRCPAVG=PRCPOAVG
 ;S PRCPQTY=-1,PRCPITEM=ITEMDA D UPH^PRCPSPD2
 ; LOOP THROUGH AND DECREMENT THE INVENTORY QTY. ON ITEMS THAT MADE UP THE SET/PACK
 S PRCPITEM=0 F  S PRCPITEM=$O(^TMP($J,PRCPITEM)) G:'PRCPITEM E2^PRCPSPD0 S PRCPQTY=+^TMP($J,PRCPITEM) I $D(^PRCP(445,PRCP("I"),1,PRCPITEM,0)) S Y(0)=^(0) D SEC^PRCPSPD2
EXIT ;
 QUIT
