PRCPSPD3 ;WISC/AKS-disassemble set packs ;22 Sep 92
 ;;4.0;IFCAP;;9/23/93
 ;
 K ^TMP($J)
 N ITEMDA,CST,ITEMZ,Y,PRCPAVG,PRCPCOST,PRCPDTI,PRCPI,PRCPITEM,PRCPITYP,PRCPMY,PRCPOAVG,PRCPODI,PRCPONH,PRCPORD,PRCPQ,PRCPQTY,PRCPUIM
 N PRCPSET,PRCPUI,PRCPUR,PRCPVPNT,PRCPDESC,PRCPNSN,PRCPSRC1,PRCPSUB,PCE,PRCPUN,PRCPVEN,SLCT,PRCPCC,PRCPHIS2,PRCPXCST,%,ORDERNO,X
 ; DISASSEMBLE RETURNED SETS/PACK--DECREMENTS SET/PACK INVENTORY BY 1, DECREMENTS PRIMARY'S INVENTORY FOR USED ITEMS AND INCREMENTS SECONDARY'S INVENTORY BY THE SAME QUANTITY
 D ^PRCPUSEL Q:'$G(PRCP("I"))  D NOW^%DTC S PRCPDTI=$P(%,".",1),PRCPODI=PRCPDTI,PRCPMY=+$E(%,1,5),ORDERNO=$$ORDERNO^PRCPUTR(PRCP("I"))
 ; SET DIC("W") TO DISPLAY ITEM DESCRIPTION FROM INVENTORY.  CALLED FROM INPUT TRANSFORM ON FILE 445, FIELD 1, ITEM NO.
 S DIC("W")="S Z=^PRC(441,+Y,0),PRCPDESC=$P(Z,U,2) S:$D(^PRCP(445,DA(1),1,+Y,6)) PRCPDESC=$S($P(^(6),U,1)'="""":$P(^(6),U,1),1:$P(Z,U,2)) D WD^PRCPSPD5"
E2 W !!! S DIC(0)="AEQMZ",DIC("S")="I $P(^PRC(441,+Y,0),U,6)=""S"",$P(^(0),U,7)=PRCP(""I""),$P(^PRCP(445,PRCP(""I""),1,+Y,0),U,7)",PRCPSET=DIC("S"),DIC("A")="Select Set/Pack to be disassembled: " D  G:ITEMDA=-1 EXIT
 .   ; LOOK UP OR ADD AN ITEM ON GENERAL INVENTORY FILE (445)
 .   S:'$D(DIC("A")) DIC("A")="Select Item: " S:'$D(PRCPSET) PRCPSET="I 1" I DIC(0)["L" S DLAYGO=445
 .   S:'$D(^PRCP(445,PRCP("I"),1,0)) ^(0)="^445.01IP^^"
 .   S DIC="^PRCP(445,"_PRCP("I")_",1,",DA(1)=PRCP("I") D ^DIC K DIC,DLAYGO,DA,DO,PRCPSET S ITEMDA=+Y Q:Y=-1  D
 .   .   ; FIND ITEM IN ITEM MASTER FILE, RETURN DESCRIPTION, SUBACCOUNT, NATL.STOCK NO., FLAG INDICATING WHETHER ITEM IS PURCHASEABLE OR A SET/PACK.
 .   .   K PRCPITYP S:'$D(PRCPDESC) PRCPDESC="" S PRCPSUB="",PRCPNSN=""
 .   .   S ITEMZ=$S($D(^PRC(441,+ITEMDA,0)):^(0),1:"") Q:'ITEMZ  S PRCPDESC=$S(PRCPDESC="":$P(ITEMZ,U,2),1:PRCPDESC),PRCPSUB=+$P(ITEMZ,U,10),PRCPNSN=$P(ITEMZ,U,5),PRCPITYP=$S($P(ITEMZ,U,6)'="":$P(ITEMZ,U,6),1:"P")
 S PRCPONH=$P(Y(0),U,7),PRCPOAVG=$P(Y(0),U,22),PRCPUI=$P(Y(0),U,5),PRCPUI=$S($D(^PRCD(420.5,+PRCPUI,0)):$P(^(0),U),1:""),PRCPUR=$S($P(Y(0),U,14):$P(Y(0),U,14)_"/",1:"")_PRCPUI
 W !!,"Which Secondary Inventory Point would you like these items expensed to?" S PRCPSRC1=-1
 S DIC(0)="AEQM" S:'$D(DIC("S")) DIC("S")="" I '$D(DIC("A")) S DIC("A")="Select SECONDARY INVENTORY/DISTRIBUTION POINT: "
 S DIC="^PRCP(445,"_PRCP("I")_",2,",DA(1)=PRCP("I"),D0=PRCP("I"),DIC("S")="Q:'$D(^PRCP(445,+Y,0))  I +^(0)=PRC(""SITE"")"
 D ^DIC K DIC,DA,D0 G:Y=-1 EXIT S PRCPSRC1=+Y I '$D(^PRCP(445,PRCPSRC1,0)) S Y=-1 G EXIT
 G ^PRCPSPD4
EXIT ;
 QUIT
CST S PRCPITEM=0 F  S PRCPITEM=$O(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM)) Q:'PRCPITEM  S PRCPQTY=+$P(^(PRCPITEM,0),U,2) I $D(^PRCP(445,PRCP("I"),1,PRCPITEM,0)) D
 .   S CST=^PRCP(445,PRCP("I"),1,PRCPITEM,0),CST=$S($P(CST,U,15)>$P(CST,U,22):$P(CST,U,15),1:$P(CST,U,22)),PRCPCOST=PRCPCOST+(PRCPQTY*CST)
 Q
