PRCPSPD4 ;WISC/AKS-disassemble set packs (cont) ;28 Sep 92
 ;;4.0;IFCAP;;9/23/93
 S DIR(0)="S^1:All;2:Exception (all items used except these);3:Selection"
 S DIR("A")="Choose one type" D ^DIR K DIR G:Y=2 EXC G:Y=3 SEL G:Y["^" EXIT
 S PRCPITEM=0 F  S PRCPITEM=$O(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM)) G:PRCPITEM=""!(PRCPITEM'?1.N) UI S ^TMP($J,PRCPITEM)=$P(^(PRCPITEM,0),U,2)_U_0
SEL ; Create a temporary global if set/pack is disassembled by selection.
 S (D0,DA(1))=ITEMDA,SLCT=3
ITEM1 S DIC("A")="SELECT ITEM: ",DIC="^PRCP(445,"_PRCP("I")_",1,"_ITEMDA_",8,",DIC(0)="AEQZ" D ^DIC K DIC,DA,D0 G:((Y=-1)&(X="")) LOOP1 G:Y=-1 EXIT S PRCPITEM=+Y
 W !,"  QUANTITY USED:",$P(Y(0),U,2),"//" R X:DTIME G:('$T)!(X["^") ITEM1 S:X="" X=$P(Y(0),U,2) I +X'=X!(X<1)!(X>999999)!(X?.E1"."1N.N)!(X>$P(Y(0),U,2)) D W^PRCPSPD5
 S ^TMP($J,PRCPITEM)=X_U_($P($G(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM,0)),U,2)-X) G SEL
LOOP1 S PRCPITEM=0 F  S PRCPITEM=$O(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM)) G:PRCPITEM=""!(PRCPITEM'?1.N) UI S:'$D(^TMP($J,PRCPITEM)) ^TMP($J,PRCPITEM)=0_U_$P($G(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM,0)),U,2)
EXC ; Create a temporary global if set/pack is disassembled by Exception.
 S (D0,DA(1))=ITEMDA,SLCT=2
 S DIC("A")="SELECT ITEM: ",DIC="^PRCP(445,"_PRCP("I")_",1,"_ITEMDA_",8,",DIC(0)="AEQZ" D ^DIC K DIC,DA,D0 G:((Y=-1)&(X="")) LOOP2 G:Y=-1 EXIT S PRCPITEM=+Y
 W !,"  QUANTITY UNUSED:",$P(Y(0),U,2),"//" R X:DTIME Q:('$T)!(X["^")  S:X="" X=$P(Y(0),U,2) I +X'=X!(X<1)!(X>999999)!(X?.E1"."1N.N)!(X>$P(Y(0),U,2)) D W^PRCPSPD5
 S ^TMP($J,PRCPITEM)=($P($G(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM,0)),U,2)-X)_U_X G EXC
LOOP2 S PRCPITEM=0 F  S PRCPITEM=$O(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM)) G:PRCPITEM=""!(PRCPITEM'?1.N) UI S:'$D(^TMP($J,PRCPITEM)) ^TMP($J,PRCPITEM)=$P($G(^PRCP(445,PRCP("I"),1,ITEMDA,8,PRCPITEM,0)),U,2)_U_0
UI ; DECREMENT THE QTY.ON HAND FOR THE SET/PACK ASSEMBLED, AND RECALCULATE WEIGHTED AVG.COST PER SET/PACK, UPDATE LAST COST, AVG.COST.
 S PRCPCOST=0 D CST^PRCPSPD3
 I $G(ORDERNO) K PRCPSPD4 S PRCPSPD4("QTY")=-1,(PRCPSPD4("SELVAL"),PRCPSPD4("INVVAL"))=PRCPCOST D ADDTRAN^PRCPUTR(PRCP("I"),ITEMDA,"S",ORDERNO,.PRCPSPD4) K PRCPSPD4
 S $P(^(0),U,7)=$P(^PRCP(445,PRCP("I"),1,ITEMDA,0),U,7)-1,PRCPAVG=PRCPOAVG
 ;S PRCPQTY=-1,PRCPITEM=ITEMDA D UPH^PRCPSPD5
 ; LOOP THROUGH AND DECREMENT THE INVENTORY QTY. ON ITEMS THAT MADE UP THE SET/PACK
 S PRCPITEM=0 F  S PRCPITEM=$O(^TMP($J,PRCPITEM)) G:'PRCPITEM E2^PRCPSPD3 S PRCPQTY=+^TMP($J,PRCPITEM) I $D(^PRCP(445,PRCP("I"),1,PRCPITEM,0)) S Y(0)=^(0) D PRIM^PRCPSPD5 D:'$D(^PRCP(445,PRCPSRC1,1,PRCPITEM,0))  D SEC^PRCPSPD5
 .   ; Setting one item at a time
 .   ;
 .   S:$D(^PRCP(445,PRCPSRC1,1,0)) $P(^(0),U,3)=PRCPITEM,$P(^(0),U,4)=$P(^(0),U,4)+1
 .   I '$D(^PRCP(445,PRCPSRC1,1,0)) S ^(0)="^445.01IP^"_PRCPITEM_U_1
 .   S ^PRCP(445,PRCPSRC1,1,PRCPITEM,0)=^PRCP(445,PRCP("I"),1,PRCPITEM,0),PRCPUN=$P(^PRCP(445,PRCP("I"),1,PRCPITEM,0),"^",5),PRCPUIM=$P(^(0),U,14)
 .   I $D(^PRCP(445,PRCP("I"),1,PRCPITEM,6)) S ^PRCP(445,PRCPSRC1,1,PRCPITEM,6)=^(6)
 .   F PCE=2,3,4,6,7,8,9,10,11,13,16,18,19,20,23,24,26 S $P(^PRCP(445,PRCPSRC1,1,PRCPITEM,0),"^",PCE)=""
 .   S ^PRCP(445,PRCPSRC1,1,PRCPITEM,5,0)="^445.07I^1^1",PRCPVEN=PRCP("I")_";"_"PRCP(445,",$P(^PRCP(445,PRCPSRC1,1,PRCPITEM,0),"^",12)=PRCPVEN
 .   S ^PRCP(445,PRCPSRC1,1,PRCPITEM,5,1,0)=PRCPVEN_"^"_PRCPUN_"^"_PRCPUIM_"^"_PRCPUIM,^PRCP(445,PRCPSRC1,1,PRCPITEM,5,"B",PRCPVEN,1)="",^PRCP(445,PRCPSRC1,1,"AC",PRCPVEN,PRCPITEM)=""
 .   S ^PRCP(445,PRCPSRC1,1,"B",PRCPITEM,PRCPITEM)=""
EXIT ;
 QUIT
