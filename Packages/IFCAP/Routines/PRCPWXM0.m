PRCPWXM0 ;WISC/RFJ-build repetitive item list from confirmation message ;1 May 91
 ;;4.0;IFCAP;**7**;9/23/93
 N %,D,DIC,DIR,DLAYGO,I,PRC,PRCPCNT,PRCPCOST,PRCPDA,PRCPDATE,PRCPDESC,PRCPDT,PRCPENT,PRCPFLAG,PRCPI,PRCPITEM,PRCPNAME,PRCPNSN,PRCPPAGE,PRCPPKG,PRCPQTY,PRCPSRC,PRCPTOT,PRCPTYPE,PRCPUI,PRCPXMZ,PRCSDA,PRCSIP,PRCSNO,X,X1,Y,Z,ZTSK
 N PRCPPVNO,PRCPLOCK S PRCPPVNO=$O(^PRC(440,"AC","S",0)) I PRCPPVNO="" W !!,"THERE IS NOT A VENDOR IN THE VENDOR FILE (#440) DESIGNATED AS A SUPPLY WHSE." G Q
 S IOP="HOME" D ^%ZIS K IOP
ASK S DIC="^XMB(3.9,",DIC(0)="QEA" W ! D ^DIC Q:Y<0
 I $E($P(Y,"^",2),1,11)'="MSC MESSAGE" G ASK
 S PRCPXMZ=+Y,%=0 W !!,"Searching for '** TRANSACTION CODE  500'..." F  S %=$O(^XMB(3.9,PRCPXMZ,2,%)) Q:'%  I $G(^(%,0))["** TRANSACTION CODE  500" Q
 I '% W ?50,"NOT FOUND IN MESSAGE." G ASK
 W ?50,"FOUND ON LINE: ",%,! S PRCPI=%,DIC(0)="QEAM",DIC="^PRC(440,",DIC("S")="I $P($G(^PRC(440,Y,2)),U,2)=1",DIC("A")="Select 'DEPOT' SOURCE: " W ! D ^DIC G:+Y<0 ASK S PRCPSRC=+Y,PRCPNAME=$P($G(^PRC(440,PRCPSRC,0)),"^") K DIC
 W !!,"Extracting Items from message..." K ^TMP($J) S PRCPFLAG=1 F PRCPI=PRCPI:1 Q:'$D(^XMB(3.9,PRCPXMZ,2,PRCPI,0))  S D=^(0) S:D["** TRANSACTION CODE" PRCPFLAG=$S(D[500:1,1:0) I $L(D),PRCPFLAG,D'["QTY ADJ" D
 .   S %=$E(D,1,18) D  Q:%'?4N1"-"2UN1"-"3N1"-"4N.A
 .   .   F I=1:1 Q:$E(%,I)'=" "
 .   .   S %=$TR($E(%,I,245),"^")
 .   S PRCPNSN=%,%=$E(D,33,53) D  Q:%=""  S PRCPDESC=%,PRCPUI=$TR($E(D,56,57)," ") Q:PRCPUI=""  S PRCPPKG=$TR($E(D,61,62)," "),PRCPCOST=+$TR($E(D,69,78)," ,"),PRCPTOT=+$TR($E(D,81,93)," ,")
 .   .   F I=$L(%):-1 Q:I=0!($E(%,I)'=" ")
 .   .   S %=$TR($E(%,1,I),"^")
 .   S PRCPQTY=+$TR($E(D,107,118)," ,") I $D(^TMP($J,"ITEM",PRCPNSN)) S %=^(PRCPNSN),PRCPTOT=PRCPTOT+$P(%,"^",5),PRCPQTY=PRCPQTY+$P(%,"^",6),$P(^(PRCPNSN),"^",5,6)=PRCPTOT_"^"_PRCPQTY
 .   I '$D(^TMP($J,"ITEM",PRCPNSN)) S ^(PRCPNSN)=PRCPDESC_"^"_PRCPUI_"^"_PRCPPKG_"^"_PRCPCOST_"^"_PRCPTOT_"^"_PRCPQTY
 .   S PRCPITEM=$O(^PRC(441,"BB",PRCPNSN,0)) I 'PRCPITEM S ^TMP($J,"ADD",PRCPNSN)="ITEM NEEDS TO BE ADDED TO ITEM MASTER FILE (#441)" Q
 .   S %=$G(^PRC(441,PRCPITEM,2,PRCPSRC,0)) I %="" S ^TMP($J,"ADD",PRCPNSN)=$P($G(^PRC(440,PRCPSRC,0)),"^")_" NEEDS TO BE ADDED AS A SOURCE FOR THIS ITEM" Q
 .   S X=$$UNITCODE^PRCPUX1($P(%,"^",7)) I X'="",X'=PRCPUI S ^TMP($J,"ADD",PRCPNSN)="UNIT OF PURCHASE SHOULD BE "_PRCPUI_"  (NOT "_X_")" Q
 .   S $P(^TMP($J,"ITEM",PRCPNSN),"^",7)=PRCPITEM
 W " ...Finished!" I $O(^TMP($J,"ADD",0))'="" D  G Q
 .   W !!,"THERE ARE ERRORS IN BUILDING THE REPETITIVE ITEM LIST FROM THIS MESSAGE.",!,"THE ERRORS MUST BE CORRECTED BEFORE THE REPETITIVE ITEM LIST CAN BE BUILT.",!,"SELECT THE DEVICE TO PRINT THE LISTING OF ERRORS."
 .   S %ZIS="Q" D ^%ZIS Q:POP  I $D(IO("Q")) K IO("Q") S ZTRTN="DQ^PRCPWXM0",ZTDESC="Building Repetitive Item List Errors",ZTSAVE("PRCP*")="",ZTSAVE("^TMP($J,")="" D ^%ZTLOAD Q
 .   D DQ
 I $O(^TMP($J,"ITEM",0))="" W !!,"NO ITEMS FOUND IN THIS MESSAGE!" G ASK
 D NEW^PRCPWXM1 Q:'$D(PRCPDA)  D TYPE^PRCPWXM1 Q:'$D(PRCPTYPE)
 W ! S XP="READY TO BUILD REPETITIVE ITEM LIST",XH="ENTER 'YES' TO BUILD THE REPETITIVE ITEM LIST, 'NO' OR '^' TO EXIT.",%=1 D YN^PRCPU4 G:%'=1 Q D BUILD^PRCPWXM1
Q D LOCK^PRCPU4(.PRCPLOCK,0,0),^%ZISC K ^TMP($J) Q
DQ ;queue comes here
 N PRCPFLAG,SCREEN
 D NOW^%DTC S Y=% X ^DD("DD") S PRCPDATE=Y,PRCPPAGE=0,SCREEN=$$SCRPAUSE^PRCPUREP,PRCPNSN="" U IO D H F  S PRCPNSN=$O(^TMP($J,"ADD",PRCPNSN)) Q:'PRCPNSN  I $D(^TMP($J,"ITEM",PRCPNSN)) S D=^(PRCPNSN) D  Q:$D(PRCPFLAG)
 .   W !!,PRCPNSN,?20,$P(D,"^"),?44,$P(D,"^",2),?49,$P(D,"^",3),?51,$J($P(D,"^",6),10),$J($P(D,"^",4),8,2),$J($P(D,"^",5),10,2),!?5,"ERROR: ",^TMP($J,"ADD",PRCPNSN)
 .   I $Y>(IOSL-5) D:SCREEN P^PRCPU4 Q:$D(PRCPFLAG)  D H
 W:'$D(PRCPFLAG) !,"<<END OF REPORT>>" Q
H S PRCPPAGE=PRCPPAGE+1,X=PRCPDATE_"  PAGE "_PRCPPAGE I PRCPPAGE'=1!(SCREEN) W @IOF
 W $C(13),"ERRORS WITH BUILDING REPETITIVE ITEM LIST",?(79-$L(X)),X,!?42,"UNIT",?65,"UNIT",?74,"TOTAL"
 W !,"NSN",?20,"DESCRIPTION",?41,"ISSUE",?48,"PKG",?53,"QUANTITY",?65,"COST",?75,"COST",! S X="",$P(X,"-",80)="" W X Q
