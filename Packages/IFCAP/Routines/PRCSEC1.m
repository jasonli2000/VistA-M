PRCSEC1 ;WISC/SAW/CTB-CONTINUATION OF PRESEC ;1/29/93  3:35 PM
V ;;4.0;IFCAP;;9/23/93
 D NOW^%DTC S PRCS=%
 S PRCSCP=$S($D(^PRC(420,PRC("SITE"),1,+PRC("CP"),0)):$P(^(0),U,12),1:"")
 N PPMFLG S:$D(PPMFLG1) PPMFLG=10
 S $P(^PRCS(410,DA,10),U,4)=$S(PRCSCP=1!(PRCHQ=1):$O(^PRCD(442.3,"C",10,0)),1:$O(^PRCD(442.3,"C",60,0))),$P(^(11),U,3)=""
 K ^PRCS(410,"F",+PRCSN_"-"_+PRC("CP")_"-"_$P($P(PRCSN,U),"-",5),DA),^PRCS(410,"F1",$P($P(PRCSN,U),"-",5)_"-"_+PRCSN_"-"_+PRC("CP"),DA),^PRCS(410,"AQ",1,DA)
 ;
 S MESSAGE=""
 D ENCODE^PRCSC1(DA,DUZ,.MESSAGE)
 K MESSAGE
 S X=PRCST D TRANS^PRCSES
 ;
 S PRCSSCP=0 F PRCSSI=1:1 S PRCSSCP=$O(^PRCS(410,DA,12,PRCSSCP)) Q:PRCSSCP'>0  I $D(^PRCS(410,DA,12,PRCSSCP,0)) S X=$P(^(0),U,2) I X S DA(1)=DA,DA=PRCSSCP D TRANS^PRCSEZZ S DA=DA(1)
 K PRCSSCP,PRCSSI L
 I $P(PRCSN,U,4)>1 S X=$P(PRCSN,U,1),DIC="^PRC(443,",DIC(0)="L",DLAYGO=443 D ^DIC K DIC,DLAYGO,X
 I $P(PRCSN,U,4)>1 S X=$O(^PRCD(442.3,"C",60,0)) S:PRCSCP=1 X=$O(^PRCD(442.3,"C",10,0)) S $P(^PRC(443,DA,0),U,7)=X,^PRC(443,"AC",X,DA)="",$P(^PRC(443,DA,0),U,11)=$P(PRCSN,U,6) I PRCST1-PRCST<0 S $P(^(0),U,8)=1
 D EN2^PRCPWI
 I $P(PRCSN,U,4)=.5 Q
 S D0=DA,PRCHQ=$S(PRCHQ=1:"QUE^PRCE58P2",PRCHQ=5:"DQ^PRCPRIB0",1:"QUE^PRCSP12"),PRCHQ("DEST")=$S(PRCSCP=1!(PRCHQ="QUE^PRCE58P2"):"F",1:"S") D ^PRCHQUE Q
 Q
CP ;APPROVE REQUEST CP LOOK-UP
 S:'$D(PRCSC) PRCSC=1 S DIC="^PRC(420,"_PRC("SITE")_",1,",DIC(0)="AEMNQZ",DIC("A")="Select CONTROL POINT: " I $D(PRC("CP")) S DIC("B")=$S($D(^PRC(420,"A",DUZ,PRC("SITE"),+PRC("CP"),PRCSC)):PRC("CP"),1:"")
 S DIC("S")="I '$P(^(0),U,19),$S($D(^PRC(420,""A"",DUZ,PRC(""SITE""),+Y,PRCSC)):1,1:0)" D ^DIC K DIC("A"),DIC("B"),DIC("S") Q:Y<0  S PRC("CP")=$P(Y(0),U) S:$P(Y(0),U,16)'="" PRCSIP=$P(Y(0),U,16)
 Q
OCCK ;STATION AND CONTROL POINT OVERCOMMIT CHECK
 K PRCSOCK S PRCSCQT=$S($D(PRCSQT(1)):PRCSQT(1),1:"") S:PRCSCQT="" PRCSCQT=$E(DT,4,5),PRCSCQT=$S(PRCSCQT>9:1,PRCSCQT<4:2,PRCSCQT>6&(PRCSCQT<10):4,1:3)
 S PRCSOCS=$S($D(^PRC(420,PRC("SITE"),0)):$P(^(0),U,2),1:""),PRCSOCP=$S($D(^PRC(420,PRC("SITE"),1,+PRC("CP"),0)):$P(^(0),U,13),1:"")
 I PRCSOCS=0 W !!,$C(7),"The Station currently does not allow Control Points to overcommit funds." S PRCSOCK=1 G EXIT
 I PRCSOCS=""!(PRCSOCS=4) G CPCK
 I PRCSOCS=1&(PRCSCQT'=PRC("QTR")) G ERR
 I PRCSOCS=2&(PRCSCQT=4&(PRC("QTR")=1)!(PRCSCQT<PRC("QTR"))) G EXIT
 I PRCSOCS=3 G EXIT
 G ERR
CPCK I PRCSOCP="" G EXIT
 I PRCSOCP=1&(PRCSCQT'=PRC("QTR")) G ERR
 I PRCSOCP=2&(PRCSCQT=4&(PRC("QTR")=1)!(PRCSCQT<PRC("QTR"))) G EXIT
 I PRCSOCP=3 G EXIT
ERR S PRCSOCK=1 W !!,$C(7),"This Control Point cannot overcommit funds." H 3
EXIT K PRCSCQT,PRCSOCP,PRCSOCS Q
