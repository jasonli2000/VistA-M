PRCUX4 ;WISC@ALTOONA/CTB-SIGNATURE CONVERSION ROUTINE FOR FILE 443 ; 05/18/93  10:44 AM
V ;;4.0;IFCAP;;9/23/93
CVT(X) ;CONVERT RANGE OF RECORDS - X?.N1"-".N
 N BEGIN,END,DA
 S BEGIN=$P(X,"-"),END=$P(X,"-",2)
 S DA=BEGIN-1 L +^PRC(443)
 F  S DA=$O(^PRC(443,DA)) Q:'DA  Q:DA>END  I $D(^(DA,0)) D ONE(DA) W:DA#1000=0 "."
 L -^PRC(443) QUIT
ONE(DA) ;ENTRY POINT TO CONVERT ONE RECORD
 D CVT1^PRCHES11(DA)
 QUIT
BUILD ;BUILD ENTRIES IN 443.1 FOR FILE 443 CONVERSION
 NEW X,IX,VAR,MSG
 W !,"Building conversion list for 443"
 S IX=0
 F  S IX=$O(^PRC(443,IX)) Q:'IX  DO
 . S VAR=IX_"-"_(IX+99),IX=IX+99
 . D ADD^PRCUPM1("CVT^PRCUX4",VAR,.MSG)
 . W "." K MSG
 . QUIT
 W !,"Conversion list for 443 completed.",!
 QUIT
