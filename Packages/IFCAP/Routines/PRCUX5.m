PRCUX5 ;WISC@ALTOONA/CTB-SIGNATURE CONVERSION ROUTINE FOR FILE 423 ; 05/18/93  10:45 AM
V ;;4.0;IFCAP;;9/23/93
CVT(X) ;CONVERT RANGE OF RECORDS - X?.N1"-".N
 N BEGIN,END,DA
 S BEGIN=$P(X,"-"),END=$P(X,"-",2)
 S DA=BEGIN-1 L +^PRCF(423)
 F  S DA=$O(^PRCF(423,DA)) Q:'DA  Q:DA>END  I $D(^(DA,0)) D ONE(DA) W:DA#1000=0 "."
 L -^PRCF(423) QUIT
ONE(DA) ;ENTRY POINT TO CONVERT ONE RECORD
 D CVT1^PRCFES1(DA)
 QUIT
BUILD ;BUILD ENTRIES IN 443.1 FOR FILE 423 CONVERSION
 NEW X,FIRST,LAST,IX,VAR,MSG
 S FIRST=$O(^PRCF(423,0)) Q:FIRST=""
 S LAST=$P($G(^PRCF(423,0)),"^",3)
 W !,"Finding Last Entry in 423"
 F  S X=$O(^PRCF(423,LAST)) Q:'X  S LAST=X
 W "   Last Entry = ",LAST
 W !,"Building conversion list for 423"
 F IX=FIRST:100:LAST DO
 . S VAR=IX_"-"_(IX+99)
 . D ADD^PRCUPM1("CVT^PRCUX5",VAR,.MSG)
 . W "." K MSG
 . QUIT
 W !,"Conversion list for 423 completed.",!
 QUIT
