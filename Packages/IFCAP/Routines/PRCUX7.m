PRCUX7 ;WISC@ALTOONA/CTB-SIGNATURE CONVERSION ROUTINE FOR FILE 410 ; 05/18/93  10:47 AM
V ;;4.0;IFCAP;;9/23/93
CVT(X) ;CONVERT RANGE OF RECORDS - X?.N1"-".N
 N BEGIN,END,DA
 S BEGIN=$P(X,"-"),END=$P(X,"-",2)
 S DA=BEGIN-1 L +^PRCS(410)
 F  S DA=$O(^PRCS(410,DA)) Q:'DA  Q:DA>END  I $D(^(DA,0)) D ONE(DA) W:DA#1000=0 "."
 L -^PRCS(410) QUIT
ONE(DA) ;ENTRY POINT TO CONVERT ONE RECORD
 NEW D1
 ;CONVERT FIELD 63
 D CVT1^PRCSC4(DA)
 ;CONVERT FIELD 44.8
 D CVT1^PRCSC3(DA)
 ;CONVERT FIELD 44.5
 D CVT1^PRCSC1(DA)
 ;CONVERT FIELD 29.5
 D CVT1^PRCSC2(DA)
 QUIT
BUILD ;BUILD ENTRIES IN 443.1 FOR FILE 410 CONVERSION
 NEW X,IX,VAR,MSG
 W !,"Building conversion list for 410"
 S IX=0
 F  S IX=$O(^PRCS(410,IX)) Q:'IX  DO
 . S VAR=IX_"-"_(IX+99),IX=IX+99
 . D ADD^PRCUPM1("CVT^PRCUX7",VAR,.MSG)
 . W "." K MSG
 . QUIT
 W !,"Conversion list for 410 completed.",!
 QUIT
