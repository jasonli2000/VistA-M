PRCUX8 ;WISC@ALTOONA/CTB-SIGNATURE CONVERSION ROUTINE FOR FILE 442 ; 05/18/93  10:48 AM
V ;;4.0;IFCAP;;9/23/93
CVT(X) ;CONVERT RANGE OF RECORDS - X?.N1"-".N
 N BEGIN,END,DA
 S BEGIN=$P(X,"-"),END=$P(X,"-",2)
 S DA=BEGIN-1 L +^PRC(442)
 F  S DA=$O(^PRC(442,DA)) Q:'DA  Q:DA>END  I $D(^(DA,0)) D ONE(DA) W:DA#1000=0 "."
 L -^PRC(442) QUIT
ONE(DA) ;ENTRY POINT TO CONVERT ONE RECORD
 NEW D1
 ;CONVERT FIELD .05
 S D1=0 F  S D1=$O(^PRC(442,DA,13,D1)) Q:'D1  D CVT1^PRCHES0(DA,D1)
 ;CONVERT FIELD .6
 S D1=0 F  S D1=$O(^PRC(442,DA,11,D1)) Q:'D1  D CVT1^PRCHES1(DA,D1),CVT1^PRCHES2(DA,D1),CVT1^PRCHES3(DA,D1)
 ;CONVERT FIELD 2.4
 S D1=0 F  S D1=$O(^PRC(442,DA,10,D1)) Q:'D1  D CVT1^PRCHES4(DA,D1)
 ;CONVERT FIELD 16.5
 D CVT1^PRCHES5(DA)
 ;CONVERT FIELD 50
 S D1=0 F  S D1=$O(^PRC(442,DA,6,D1)) Q:'D1  D CVT1^PRCHES6(DA,D1),CVT1^PRCHES7(DA,D1)
 ;CONVERT FIELD 105
 D CVT1^PRCHES8(DA)
 ;CONVERT FIELD 114
 D CVT1^PRCHES9(DA)
 QUIT
BUILD ;BUILD ENTRIES IN 443.1 FOR FILE 442 CONVERSION
 NEW X,IX,VAR,MSG
 W !,"Building conversion list for 442"
 S IX=0
 F  S IX=$O(^PRC(442,IX)) Q:'IX  DO
 . S VAR=IX_"-"_(IX+99),IX=IX+99
 . D ADD^PRCUPM1("CVT^PRCUX8",VAR,.MSG)
 . W "." K MSG
 . QUIT
 W !,"Conversion list for 442 completed.",!
 QUIT
