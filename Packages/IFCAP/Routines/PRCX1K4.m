PRCX1K4 ;WISC/PLT-ENCODE MISSING ISSUE BOOK ESIG FOR FY-1995 ONLY ;
V ;;4.0;IFCAP;**35**;9/23/93
 QUIT  ;invalid entry
 ;
EN ;ENCODE MISSING POSTED ISSUE BOOK ESIG
 N PRC,PRCRI,PRCA,PRCB,PRCC,PRCD,PRCG,PRCH
 N A,B,X,Y
 W @IOF D EN^DDIOL("ENCODE MISSING POSTED ISSUE BOOK ESIG FOR FISCAL YEAR 1995 ONLY!")
 S PRCA=1995
 S PRCRI(410.5)=0 F  S PRCRI(410.5)=$O(^PRCS(410.5,PRCRI(410.5))) Q:'PRCRI(410.5)  S A=$G(^(PRCRI(410.5),0)) QUIT:A["ISSUE BOOK"
 I $G(PRCRI(410.5))'=5 D EN^DDIOL("ISSUE BOOK form type code is not 5 in file 410.5") G EXIT
 S PRCRI(442.3)=$O(^PRCD(442.3,"C",40,0))
 I 'PRCRI(442.3) D EN^DDIOL("Issue Book complete status is not in file 442.3")
 S PRCRI(411)=0 F  S PRCRI(411)=$O(^PRC(411,PRCRI(411))) Q:'PRCRI(411)  D
 . S PRC("SITE")=$P($G(^PRC(411,PRCRI(411),0)),"^") QUIT:'PRC("SITE")
 . S PRCB=PRC("SITE")_"-"_$E(PRCA,3,4)_"-",PRCC=PRCB_"~"
 . S PRCD=PRCB
 . ;check form type 5 and status 40 for final post
 . F  S PRCD=$O(^PRCS(410,"B",PRCD)) Q:PRCD=""!(PRCD]PRCC)  S PRCRI(410)=$O(^(PRCD,0)) I PRCRI(410) S PRCF=$G(^PRCS(410,PRCRI(410),0)) I $P(PRCF,"^",4)=PRCRI(410.5),$P($G(^(10)),"^",4)=PRCRI(442.3) D
 .. S PRCG=$G(^PRCS(410,PRCRI(410),4)) I $P(PRCG,"^",10)="" S A=$P(PRCD,"-",1,4) W !,$P(PRCF,"^"),?20,"$",$P(PRCG,"^",3) D ENCODE^PRCSC2(PRCRI(410),DUZ)
 .. QUIT
 . QUIT
 ;
 D EN^DDIOL(" ")
 D EN^DDIOL("ENCODE MISSING ISSUE BOOK FOR FISCAL YEAR 1995 DONE")
 D EN^DDIOL(" ")
EXIT QUIT
 ;
