PRCXCPN ;WISC/RFJ-change dd(420.01,.01,0) input transform ;25 Apr 95
 ;;4.0;IFCAP;**Class III**;9/23/93
 N NEWVALUE,OLDVALUE
 W !,"THIS PATCH WILL CHANGE THE INPUT TRANSFORM IN THE CONTROL POINT FILE #420,"
 W !,"CONTROL POINT NAME SUB-FIELD #1 TO PREVENT ADDING CONTROL POINTS AFTER"
 W !,"THE DATE OF MAY 5, 1995."
 ;
 I '$D(^DD(420.01,.01,0)) W !!,"CONTROL POINT FILE #420, CONTROL POINT NAME SUB-FIELD #1 DOES NOT EXIST." D NO Q
 I +$$VERSION^XPDUTL("PRC")'=4 W !!,"THIS ROUTINE SHOULD ONLY BE RUN ON SYSTEMS WITH IFCAP VERSION 4.0 INSTALLED." D NO Q
 S OLDVALUE=$E($T(OLDVALUE+1),5,999)
 I $P(^DD(420.01,.01,0),"^",5,99)'=OLDVALUE D
 .  W !!,"THERE IS A DISCREPANCY WITH THE ORIGINAL INPUT TRANSFORM IN ^DD(420.01,.01,0)."
 .  W !,"SITES ARE NOT SUPPOSE TO MAKE LOCAL MODIFICATIONS TO IFCAP CODE AND THIS PATCH"
 .  W !,"WILL OVERWRITE LOCAL MODIFICATIONS.  THE CURRENT AND EXPECTED VALUES ARE"
 .  W !,"LISTED BELOW:"
 .  W !?5,"THE EXPECTED VALUE IS:",!?5,OLDVALUE
 .  W !?5,"THE CURRENT VALUE IS:",!?5,$P(^DD(420.01,.01,0),"^",5,99)
 S NEWVALUE=$E($T(NEWVALUE+1),5,999)
 S $P(^DD(420.01,.01,0),"^",5,99)=NEWVALUE
 W !!,"THE INPUT TRANSFORM HAS BEEN CHANGED CORRECTLY."
 Q
 ;
 ;
NO ;  patch not installed
 W !!,"THIS PATCH WAS NOT INSTALLED."
 Q
 ;
 ;
NEWVALUE ;;  new value of input transform
 ;;=K:X[""""!($A(X)=45) X I $D(X) K:$L(X)>30!($L(X)<3)!(X'?3.4N1" "1AN.ANP) X I $D(X) S DINUM=+X I DT>2950505 K X,DINUM W !,"NEW CONTROL POINTS CANNOT BE ADDED AFTER MAY 5, 1995."
 ;;
OLDVALUE ;;  old value of input transform
 ;;=K:X[""""!($A(X)=45) X I $D(X) K:$L(X)>30!($L(X)<3)!(X'?3.4N1" "1AN.ANP) X I $D(X) S DINUM=+X
