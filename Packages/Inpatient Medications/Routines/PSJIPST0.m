PSJIPST0 ;BIR/MLM-CONVERT UD PICK LISTS TO FILEMAN COMPATIBLE STRUCTURE ;14 Jul 94 / 11:24 AM
 ;;4.5; Inpatient Medications ;;7 Oct 94
 ;
ENNV ;
 K ZTSAVE,ZTSK S ZTRTN="ENQN^PSJIPST0",ZTDTH=$H,ZTDESC="PICK LIST FILE CONVERSION (INPATIENT MEDS POST-INIT)",ZTIO="" D ^%ZTLOAD
 W !,"The conversion of the Pick List data to the VA FileMan compatible format is",$S($D(ZTSK):"",1:" NOT")," queued",!
 I $D(ZTSK) W " (to start NOW).",!!,"YOU WILL RECEIVE A MAILMAN MESSAGE WHEN TASK #"_ZTSK_" HAS COMPLETED."
 Q
 ;
ENQN ; convert Pick List file.
 S CNODE=$G(^PS(59.7,1,20.4)) G:$P(CNODE,U,13) DONE^PSJIPST
 S PLNO=+$P(CNODE,U,14) D:'PLNO CU I PLNO S X=+$P(CNODE,U,15) I X S X=X_"("_PLNO_")" D @X
 K ^PS(53.5,"A"),^("B"),^("F"),^("O")
 D NOW^%DTC S PSJC=0,PSJSTART=%,(PSJCR,X1)=$E(%,1,7),X2=7 D C^%DTC S PSJPURGE=X
 F  S PLNO=$O(^PS(53.5,PLNO)) Q:'PLNO  D PROCESS
 S PSGPLUPD="" F  S PSGPLUPD=$O(^PS(53.5,PSGPLUPD)) Q:PSGPLUPD'<0  K ^PS(53.5,PSGPLUPD)
 D NOW^%DTC S $P(^PS(59.7,1,20.4),U,13,15)=%_"^^"
 D SENDMSG^PSJIPST1
 Q
 ;
PROCESS ; Move pick list to ^XTMP and back.
 K ^XTMP("PSJ") S ^XTMP("PSJ",0)=PSJPURGE_U_PSJCR,$P(^PS(59.7,1,20.4),U,15)=1
 D 1(PLNO) D:$D(^PS(53.5,"-"_PLNO)) 1("-"_PLNO) S $P(^PS(57.5,20.4),U,15)=2 D 2(PLNO) S $P(^PS(59.7,1,20.4),U,14,15)=+PLNO_U_0,PSJC=PSJC+1,^PS(53.5,0)="PICK LIST^53.5I^"_PLNO_U_PSJC
 Q
 ;
1(PLNO) ;Move Pick List data from ^PS(53.5 to ^XTMP.
 S X=$G(^PS(53.5,PLNO,0)) Q:'X
 F I=1,2,3,8 I '$P(X,"^",I) K ^PS(53.5,PLNO) S X="" Q
 Q:'X  S ^XTMP("PSJ",$TR(PLNO,"-"),0)=$TR(PLNO,"-")_U_X
 S TEAM=0 F  S TEAM=$O(^PS(53.5,PLNO,TEAM)) Q:TEAM=""  D
 .S WARD=0 F  S WARD=$O(^PS(53.5,PLNO,TEAM,WARD)) Q:WARD=""  D
 ..S ROOM=0 F  S ROOM=$O(^PS(53.5,PLNO,TEAM,WARD,ROOM)) Q:ROOM=""  D
 ...S PAT=0 F  S PAT=$O(^PS(53.5,PLNO,TEAM,WARD,ROOM,PAT)) Q:PAT=""  D
 ....S ND1=$G(^PS(53.5,PLNO,TEAM,WARD,ROOM,PAT)),DFN=+$P(PAT,U,2),WARDN=$S(WARD="zns":$P(ND1,U),1:WARD)
 ....S ^XTMP("PSJ",$TR(PLNO,"-"),1,DFN,0)=$P(PAT,U,2)_U_TEAM_U_WARDN_U_ROOM
 ....S:PLNO>0 ONCNT=0 S ST=0 F  S ST=$O(^PS(53.5,PLNO,TEAM,WARD,ROOM,PAT,ST)) Q:ST=""  D
 .....S PD=0 F  S PD=$O(^PS(53.5,PLNO,TEAM,WARD,ROOM,PAT,ST,PD)) Q:PD=""  D
 ......S ND2=$G(^PS(53.5,PLNO,TEAM,WARD,ROOM,PAT,ST,PD)),ON=+$P(PD,U,2),PDPTR=+$G(^PS(55,DFN,5,ON,.1))
 ......S ONCNT=+$G(ONCNT)+1,^XTMP("PSJ",$TR(PLNO,"-"),1,DFN,1,ONCNT,0)=ON_U_ST_U_PDPTR_U_$P(ND2,U)_$S($E(PLNO)="-":U_1,1:"")
 ......S (PDD,PDDCNT)=0 F  S PDD=$O(^PS(53.5,PLNO,TEAM,WARD,ROOM,PAT,ST,PD,PDD)) Q:PDD=""  D
 .......S PDDPTR=+$P(PDD,U,2),ND3=$G(^PS(53.5,PLNO,TEAM,WARD,ROOM,PAT,ST,PD,PDD))
 .......S PDDCNT=+PDDCNT+1,^XTMP("PSJ",$TR(PLNO,"-"),1,DFN,1,ONCNT,1,PDDCNT,0)=+PDDPTR_U_$P(ND3,U)_U_$P(ND3,U,2)_U_$P(ND3,U,3)
 Q
 ;
2(PLNO) ;Move data from ^XTMP("PSJ", to ^PS(53.5,.
 K ^PS(53.5,PLNO) Q:'$D(^XTMP("PSJ",PLNO,0))
 S ^PS(53.5,PLNO,0)=^XTMP("PSJ",PLNO,0)
 S DFN="" F  S DFN=$O(^XTMP("PSJ",PLNO,1,DFN)) Q:'DFN  D
 .S ^PS(53.5,PLNO,1,DFN,0)=^XTMP("PSJ",PLNO,1,DFN,0),^PS(53.5,PLNO,1,0)="^53.51PA^"_DFN_U_($P($G(^PS(53.5,PLNO,1,0)),U,4)+1)
 .S ON="" F  S ON=$O(^XTMP("PSJ",PLNO,1,DFN,1,ON)) Q:'ON  D
 ..S X=+^XTMP("PSJ",PLNO,1,DFN,1,ON,0),PON=+$S($D(^PS(53.5,PLNO,1,DFN,1,"B",X)):$O(^PS(53.5,PLNO,1,DFN,1,"B",X,0)),1:ON)
 ..S ^PS(53.5,PLNO,1,DFN,1,PON,0)=^XTMP("PSJ",PLNO,1,DFN,1,ON,0),^PS(53.5,PLNO,1,DFN,1,0)="^53.52PA^"_ON_U_($P($G(^PS(53.5,PLNO,1,DFN,1,0)),U,4)+1)
 ..S ^PS(53.5,PLNO,1,DFN,1,"B",+^PS(53.5,PLNO,1,DFN,1,PON,0),PON)=""
 ..S PDD="" F  S PDD=$O(^XTMP("PSJ",PLNO,1,DFN,1,ON,1,PDD)) Q:'PDD  D
 ...S ^PS(53.5,PLNO,1,DFN,1,PON,1,PDD,0)=^XTMP("PSJ",PLNO,1,DFN,1,ON,1,PDD,0),^PS(53.5,PLNO,1,DFN,1,PON,1,0)="^53.53PA^"_PDD_U_($P($G(^PS(53.5,PLNO,1,DFN,1,PON,1,0)),U,4)+1)
 ...S ^PS(53.5,PLNO,1,DFN,1,PON,1,"B",+^PS(53.5,PLNO,1,DFN,1,PON,1,PDD,0),PDD)=""
 ..I $P(^PS(53.5,PLNO,1,DFN,1,PON,0),U,5) D EN5352^PSGPLXR(PLNO,DFN,PON,"S","AU")
 .S ^PS(53.5,PLNO,1,"B",DFN,DFN)=""
 K ^XTMP("PSJ",PLNO),DA,DIK S DIK="^PS(53.5,",DA=PLNO F PSG=.01,.02,.05 S DIK(1)=PSG D EN1^DIK
 Q
 ;
CU ; Clean up filed away pick lists.
 S (Q1,Q2,Q3)=0 F  S Q1=$O(^PS(53.5,"O",Q1)) Q:'Q1  F  S Q2=$O(^PS(53.5,"O",Q1,Q2)) Q:'Q2  F  S Q3=$O(^PS(53.5,"O",Q1,Q2,Q3)) Q:'Q3  K ^(Q3),^PS(53.5,Q3),^PS(53.5,"A",Q1,Q3)
 K Q1,Q2,Q3 Q
