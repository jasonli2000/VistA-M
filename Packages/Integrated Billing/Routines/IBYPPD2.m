IBYPPD2 ;ALB/ARH - IB*2*175 POST INIT: ADD TORT/INTERAGENCY RATES JAN 2003 ; 03/06/02
 ;;2.0;INTEGRATED BILLING;**175**;21-MAR-94
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
 ; 
 ;
 ; Add Jan 2003 Tort and Interagency Charges to the Charge Master, 61 Charge Items
 Q
ADDCI(EFFDAT) ; Add Charge Items (363.2) needs Charge Sets, pass in the effective date of the new charges
 N IBA,IBCNT,IBCNT1,IBI,IBLN,IBFN,IBCS,IBXRF,IBCI,IBRVCD,IBCHG,DD,DO,DLAYGO,DIC,DIE,DA,DR,X,Y,IBX,IBZ,IBDFLTDT,IBDT S (IBCNT,IBCNT1)=0
 ;
 S IBDFLTDT=+$G(EFFDAT) I 'IBDFLTDT D MSG("** Error: No Date, No Charges Added") G CIQ
 ;
 F IBI=1:1 S IBLN=$P($T(CIF+IBI),";;",2) Q:+IBLN!(IBLN="")  I $E(IBLN)?1A D SETCI
 ;
 I +IBCNT1 D MSG(IBCNT1_" Duplicate Charge Items already exist, not re-added")
 ;
CIQ S IBA(1)="      >> "_IBCNT_" Tort/Interagency Charge Items added (363.2)" D MES^XPDUTL(.IBA) K IBA
 Q
 ;
SETCI ; set Charge Item (duplicates based on item, CS, eff dt, rev cd)
 ;
 S IBCS=$P(IBLN,U,2),IBCS=+$O(^IBE(363.1,"B",IBCS,0)) I 'IBCS D MSG("** Error: CS "_$P(IBLN,U,2)_" undefined") Q
 S IBCI=+$$MCCRUTL($P(IBLN,U,1),5) I 'IBCI D MSG("** Error: BS "_$P(IBLN,U,1)_" undefined") Q
 S IBDT=IBDFLTDT I +$P(IBLN,U,3) S IBDT=+$P(IBLN,U,3)
 S IBRVCD=$$RVCD($P(IBLN,U,4))
 S IBCHG=+$P(IBLN,U,5)
 S IBXRF="AIVDTS"_IBCS
 ;
 S IBX=0 F  S IBX=$O(^IBA(363.2,IBXRF,IBCI,-IBDT,IBX)) Q:'IBX  S IBZ=$G(^IBA(363.2,IBX,0)) I $P(IBZ,U,6)=IBRVCD D
 . S IBCI=0,IBCNT1=IBCNT1+1 I +$P(IBZ,U,5)'=IBCHG D MSG("** Error: Item exists, wrong charge: "_IBLN)
 Q:'IBCI
 ;
 K DD,DO S DLAYGO=363.2,DIC="^IBA(363.2,",DIC(0)="L",X=IBCI_";DGCR(399.1," D FILE^DICN K DIC I Y<1 K X,Y Q
 S IBFN=+Y,IBCNT=IBCNT+1
 ;
 S DR=".02////"_IBCS_";.03////"_IBDT_";.05////"_IBCHG I +IBRVCD S DR=DR_";.06////"_IBRVCD
 S DIE="^IBA(363.2,",DA=+IBFN D ^DIE K DIE,DA,DR,X,Y
 Q
 ;
 ;
MCCRUTL(X,P) ; returns IFN of item in 399.1 if Name is found and piece P is true
 N IBX,IBY S IBY=""
 I $G(X)'="" S IBX=0 F  S IBX=$O(^DGCR(399.1,"B",X,IBX)) Q:'IBX  I $P($G(^DGCR(399.1,IBX,0)),U,+$G(P)) S IBY=IBX
 Q IBY
 ;
RVCD(RVCD) ; returns IFN if revenue code is valid and active
 N IBX,IBY S IBY=""
 I +$G(RVCD) S IBX=$G(^DGCR(399.2,+RVCD,0)) I +$P(IBX,U,3) S IBY=+RVCD
 Q IBY
 ;
MSG(X) ;
 N IBX S IBX=$O(IBA(999999),-1) S:'IBX IBX=1 S IBX=IBX+1
 S IBA(IBX)="         "_$G(X)
 Q
 ;
 ;
 ;
 ;
 ;
 ;
CIF ; Charge Items (363.2):  Bedsection ^ Charge Set ^Effective Date ^ Revenue Code ^ Charge
 ;;     
TORT ;; 2002 Tortiously Liable All Inclusive
 ;;        
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (INCLUSIVE)^^^1206
 ;;BLIND REHABILITATION^TL-INPT (INCLUSIVE)^^^1254
 ;;GENERAL MEDICAL CARE^TL-INPT (INCLUSIVE)^^^1815
 ;;INTERMEDIATE CARE^TL-INPT (INCLUSIVE)^^^801
 ;;NEUROLOGY^TL-INPT (INCLUSIVE)^^^2289
 ;;NURSING HOME CARE^TL-INPT (INCLUSIVE)^^^451
 ;;PRRTP^TL-INPT (INCLUSIVE)^^^276
 ;;PSYCHIATRIC CARE^TL-INPT (INCLUSIVE)^^^971
 ;;REHABILITATION MEDICINE^TL-INPT (INCLUSIVE)^^^1723
 ;;SPINAL CORD INJURY CARE^TL-INPT (INCLUSIVE)^^^1237
 ;;SURGICAL CARE^TL-INPT (INCLUSIVE)^^^3513
 ;;             
 ;; Tortiously Liable Non-Professional
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (NPF)^^101^812
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (NPF)^^240^279
 ;;BLIND REHABILITATION^TL-INPT (NPF)^^101^530
 ;;BLIND REHABILITATION^TL-INPT (NPF)^^240^623
 ;;GENERAL MEDICAL CARE^TL-INPT (NPF)^^101^1125
 ;;GENERAL MEDICAL CARE^TL-INPT (NPF)^^240^473
 ;;INTERMEDIATE CARE^TL-INPT (NPF)^^101^644
 ;;INTERMEDIATE CARE^TL-INPT (NPF)^^240^118
 ;;NEUROLOGY^TL-INPT (NPF)^^101^1350
 ;;NEUROLOGY^TL-INPT (NPF)^^240^604
 ;;NURSING HOME CARE^TL-INPT (NPF)^^101^376
 ;;NURSING HOME CARE^TL-INPT (NPF)^^240^61
 ;;PRRTP^TL-INPT (NPF)^^101^230
 ;;PRRTP^TL-INPT (NPF)^^240^29
 ;;PSYCHIATRIC CARE^TL-INPT (NPF)^^101^726
 ;;PSYCHIATRIC CARE^TL-INPT (NPF)^^240^153
 ;;REHABILITATION MEDICINE^TL-INPT (NPF)^^101^1001
 ;;REHABILITATION MEDICINE^TL-INPT (NPF)^^240^526
 ;;SPINAL CORD INJURY CARE^TL-INPT (NPF)^^101^773
 ;;SPINAL CORD INJURY CARE^TL-INPT (NPF)^^240^311
 ;;SURGICAL CARE^TL-INPT (NPF)^^101^2061
 ;;SURGICAL CARE^TL-INPT (NPF)^^240^1065
 ;;              
 ;; Tortiously Liable Professional
 ;;ALCOHOL AND DRUG TREATMENT^TL-INPT (PF)^^^115
 ;;BLIND REHABILITATION^TL-INPT (PF)^^^101
 ;;GENERAL MEDICAL CARE^TL-INPT (PF)^^^217
 ;;INTERMEDIATE CARE^TL-INPT (PF)^^^39
 ;;NEUROLOGY^TL-INPT (PF)^^^335
 ;;NURSING HOME CARE^TL-INPT (PF)^^^14
 ;;PRRTP^TL-INPT (PF)^^^17
 ;;PSYCHIATRIC CARE^TL-INPT (PF)^^^92
 ;;REHABILITATION MEDICINE^TL-INPT (PF)^^^196
 ;;SPINAL CORD INJURY CARE^TL-INPT (PF)^^^153
 ;;SURGICAL CARE^TL-INPT (PF)^^^387
 ;;                    
 ;; Tortiously Liable Other
 ;;OUTPATIENT VISIT^TL-OPT VST^^^300
 ;;PRESCRIPTION^TL-RX FILL^^^45
 ;;OUTPATIENT DENTAL^TL-OPT DENTAL^^^185
 ;;          
 ;;         
IA ;; 2002 Interagency
 ;;            
 ;;ALCOHOL AND DRUG TREATMENT^IA-INPT^^^1106
 ;;BLIND REHABILITATION^IA-INPT^^^1162
 ;;GENERAL MEDICAL CARE^IA-INPT^^^1668
 ;;INTERMEDIATE CARE^IA-INPT^^^733
 ;;NEUROLOGY^IA-INPT^^^2098
 ;;NURSING HOME CARE^IA-INPT^^^411
 ;;PRRTP^IA-INPT^^^252
 ;;PSYCHIATRIC CARE^IA-INPT^^^888
 ;;REHABILITATION MEDICINE^IA-INPT^^^1574
 ;;SPINAL CORD INJURY CARE^IA-INPT^^^1136
 ;;SURGICAL CARE^IA-INPT^^^3255
 ;;            
 ;;OUTPATIENT VISIT^IA-OPT VST^^^282
 ;;PRESCRIPTION^IA-RX FILL^^^45
 ;;OUTPATIENT DENTAL^IA-OPT DENTAL^^^167
 ;;
 Q
