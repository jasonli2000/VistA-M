IBYSB2 ;ALB/ARH - IB*2.0*124 POST INIT: MCCR UTILITY CODES (#399.1) ; 2/9/00
 ;;2.0;INTEGRATED BILLING;**124**;21-MAR-94
 ;
 ;
 Q
MUCODES ; Add MCCR UTILITY Codes to 399.1
 N DLAYGO,DIC,DIE,DD,DO,DA,DR,X,Y,IBA,IBI,IBLN,IBCNT,IBTYPE,IBFN S IBCNT=-1,IBTYPE=""
 ;
 F IBI=1:1 S IBLN=$P($T(MUCF+IBI),";;",2) Q:IBLN=""  I $E(IBLN)'=" " D
 . ;
 . I $P(IBLN,U,1)="" D:IBCNT'<0 MSG(IBCNT,IBTYPE) S IBCNT=0,IBTYPE=$P(IBLN,U,2) Q
 . ;
 . I +$$MCCRUTL($P(IBLN,U,3),+IBLN) Q  ; if code exists quit
 . ;
 . K DD,DO S DLAYGO=399.1,DIC="^DGCR(399.1,",DIC(0)="L",X=$E($P(IBLN,U,8),1,60) D FILE^DICN K DIC I Y<1 K X,Y Q
 . S IBFN=+Y,IBCNT=IBCNT+1
 . ;
 . S DR=".02////"_$P(IBLN,U,3)_";"_$P(IBLN,U,2)_"////"_1
 . S DR=DR_$S($P(IBLN,U,4)'="":";.03////"_$P(IBLN,U,4),1:"")_$S($P(IBLN,U,5)'="":";.16////"_$P(IBLN,U,5),1:"")
 . S DR=DR_$S($P(IBLN,U,6)'="":";.17////"_$P(IBLN,U,6),1:"")_$S($P(IBLN,U,7)'="":";.19////"_$P(IBLN,U,7),1:"")
 . ;
 . S DIE="^DGCR(399.1,",DA=+IBFN D ^DIE K DIE,DA,DR,X,Y
 ;
BSQ D MES^XPDUTL(.IBA)
 Q
 ;
MSG(CNT,TYPE) ; add message to install giving count and type of codes added
 N IBX S IBX=$O(IBA(9999),-1)+1 S IBA(IBX)="      * "_$J(CNT,3)_"  "_TYPE_" added (#399.1)"
 Q
 ;
MCCRUTL(X,P) ; returns IFN of item in 399.1 if Code is found and piece P is true
 ;
 N IBX,IBY S IBY=""
 I $G(X)'="" S IBX=0 F  S IBX=$O(^DGCR(399.1,"C",X,IBX)) Q:'IBX  I $P($G(^DGCR(399.1,IBX,0)),U,+$G(P)) S IBY=IBX
 Q IBY
 ;
 ;
MUCF ; TYPE # ^ TYPE FLD ^ .02 CODE ^ .03 ABBR ^ .16 OCC REL ^ .17 OCC SPAN ^ .19 VC AMNT ^ .01 NAME
 ;; 
 ;;^UB-92 Condition Codes
 ;;15^.22^25^^^^^PATIENT IS NON-U.S. RESIDENT
 ;;15^.22^42^^^^^CONTINUING CARE NOT RELATED TO INPATIENT ADMISSION
 ;;15^.22^43^^^^^CONTINUING CARE NOT WITHIN PRESCRIBED POSTDISCHARGE WINDOW
 ;;15^.22^69^^^^^IME PAYMENT ONLY BILL
 ;; 
 ;;^UB-92 Occurrence Span Codes
 ;;4^.11^M1^^^1^^PROVIDER LIABILITY - NO UTILIZATION
 ;; 
 ;;^UB-92 Value Codes
 ;;11^.18^39^^^^^PINTS OF BLOOD REPLACED
 ;;11^.18^61^^^^^LOCATION WHERE SERVICE IS FURNISHED (HHA AND HOSPICE)
 ;;11^.18^67^^^^^PERITONEAL DIALYSIS
 ;;11^.18^A4^^^^1^COVERED SELF-ADMINISTRABLE DRUGS - EMERGENCY
 ;;11^.18^A5^^^^1^COVERED SELF-ADMINISTRABLE DRUGS - NOT AS GIVEN TO PATIENT
 ;;11^.18^A6^^^^1^COVERED SELF-ADMINISTRABLE DRUGS - DIAGNOSTIC STUDY/OTHER
 ;;11^.18^E1^^^^1^DEDUCTIBLE PAYER D
 ;;11^.18^E2^^^^1^COINSURANCE PAYER D
 ;;11^.18^E3^^^^1^ESTIMATED RESPONSIBILITY PAYER D
 ;;11^.18^F1^^^^1^DEDUCTIBLE PAYER E
 ;;11^.18^F2^^^^1^COINSURANCE PAYER E
 ;;11^.18^F3^^^^1^ESTIMATED RESPONSIBILITY PAYER E
 ;;11^.18^G1^^^^1^DEDUCTIBLE PAYER F
 ;;11^.18^G2^^^^1^COINSURANCE PAYER F
 ;;11^.18^G3^^^^1^ESTIMATED RESPONSIBILITY PAYER F
 ;; 
 ;;^UB-92 Discharge Status Codes
 ;;6^.13^09^^^^^ADMITTED AS AN INPATIENT TO THIS HOSPITAL
 ;;6^.13^40^^^^^EXPIRED AT HOME
 ;;6^.13^41^^^^^EXPIRED IN A MEDICAL FACILITY
 ;;6^.13^42^^^^^EXPIRED - PLACE UNKNOWN
 ;;6^.13^50^^^^^HOSPICE - HOME
 ;;6^.13^51^^^^^HOSPICE - MEDICAL FACILITY
 ;;6^.13^61^^^^^DISCHARGED WITHIN FACILITY TO MEDICARE APPROVED SWING BED
 ;;6^.13^71^^^^^DISCHARGED TO ANOTHER FACILITY FOR OPT SV PER DISCHARGE PLAN
 ;;6^.13^72^^^^^DISCHARGED TO THIS FACILITY FOR OPT SV PER DISCHARGE PLAN
 ;;^
 ;;
