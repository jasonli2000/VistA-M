IBYXXX1 ; ALB/TMK - Identify and allow resubmit of bills sent to the wrong payer ;21-AUG-02
 ;;2.0;INTEGRATED BILLING;**193**;21-MAR-94
 ;
RESUB ; Resubmits bills in the ^XTMP("IBRESUBMIT") global
 N IBY,IBQUIT,IBQUIT1,DIR,X,Y,Z,IBDA1,Q,CT,IBIFN,IBQ
 I '$D(^XTMP("IBRESUBMIT")) D  Q
 . W !,"THERE ARE NO BILLS IDENTIFIED TO BE RESUBMITTED - NOTHING RESUBMITTED!!" S DIR(0)="E" S Y=$$DIR^IBYXXX(.DIR)
 S DIR("A",1)="There are currently "_$G(^XTMP("IBRESUBMIT","CT"))_" bills waiting to be resubmitted",DIR("A")="DO YOU WANT TO CONTINUE?: ",DIR(0)="YA"
 S Y=$$DIR^IBYXXX(.DIR,.IBQUIT,.IBQUIT1)
 Q:IBQUIT!IBQUIT1!'Y
 W !!!,"You may now choose any bills on the list you do NOT want to resubmit",!!,"WHEN FINISHED SELECTING BILLS TO REMOVE FROM THE LIST, JUST PRESS THE ENTER KEY",!!,"ENTER ?? AT ANY TIME TO PRINT THE LIST OF BILLS REMAINING ON THE LIST"
 ; loop: ask to select bill not to resubmit ... ?? will give a list of
 ;   current bills.  Set "NO" node in XTMP for selections, "DONE" node
 ;   in XTMP for resubmitted bills
 S (IBQUIT,IBQUIT1)=0
 F  S DIR("A")="SELECT A BILL TO PERMANENTLY REMOVE FROM THE RESUBMIT LIST: ",DIR(0)="FAO",DIR("??")="^N DIR,Y D PRINT1^IBYXXX(0)" S Y=$$DIR^IBYXXX(.DIR,.IBQUIT,.IBQUIT1) Q:IBQUIT!IBQUIT1!(Y="")  D  Q:IBQUIT!IBQUIT1
 . S IBY=$S(Y["-":$P(Y,"-",2),1:Y)
 . S Z=$O(^DGCR(399,"B",IBY,0))
 . I 'Z D  Q
 .. K DIR
 .. S DIR(0)="E" W !,"BILL "_Y_" IS NOT A VALID BILL #" S Y=$$DIR^IBYXXX(.DIR,.IBQUIT,.IBQUIT1) W !
 . ;
 . I '$D(^XTMP("IBRESUBMIT",Z)) D  Q
 .. K DIR
 .. S DIR(0)="E" W !,"BILL "_Y_" WAS NOT FOUND IN THE LIST" S Y=$$DIR^IBYXXX(.DIR,.IBQUIT,.IBQUIT1) W !
 . ;
 . S (IBQ,Q)=0 F  S Q=$O(^XTMP("IBRESUBMIT",Z,Q)) Q:'Q  I $D(^XTMP("IBRESUBMIT",Z,Q,"DONE"))!$D(^XTMP("IBRESUBMIT",Z,Q,"NO")) D  Q
 .. K DIR
 .. I $D(^XTMP("IBRESUBMIT",Z,Q,"DONE")) S DIR(0)="E" W !,"BILL "_Y_" WAS ALREADY RESUBMITTED" S Y=$$DIR^IBYXXX(.DIR,.IBQUIT,.IBQUIT1) S IBQ=1 W ! Q
 .. I $D(^XTMP("IBRESUBMIT",Z,Q,"NO")) S DIR(0)="E" W !,"BILL "_Y_" WAS ALREADY REMOVED FROM THE LIST" S Y=$$DIR^IBYXXX(.DIR,.IBQUIT,.IBQUIT1) S IBQ=1 W !
 . Q:IBQ
 . ;
 . S CT=$G(^XTMP("IBRESUBMIT","CT"))
 . S Q=0 F  S Q=$O(^XTMP("IBRESUBMIT",Z,Q)) Q:'Q  S ^(Q,"NO")=1,CT=CT-1
 . I CT'=^XTMP("IBRESUBMIT","CT") W "  ...REMOVED" S ^XTMP("IBRESUBMIT","CT")=CT
 ; If no more bills selected, ask to resubmit the bills remaining in
 ;   XTMP, if yes, do it
 Q:IBQUIT
 I $G(^XTMP("IBRESUBMIT","CT"))'>0 W !,"THERE ARE NO BILLS LEFT IN THE LIST TO RESUBMIT" S DIR(0)="E" S Y=$$DIR^IBYXXX(.DIR) Q
 W !,"THERE ARE "_^XTMP("IBRESUBMIT","CT")_" BILLS IN THIS LIST",!,"ENTER ?? TO SEE THE LIST OF REMAINING BILLS",!
 S DIR("B")="NO",DIR(0)="YA",DIR("A")="DO YOU WANT TO RESUBMIT THE "_^XTMP("IBRESUBMIT","CT")_" BILLS NOW?: ",DIR("??")="^N DIR,Y D PRINT1^IBYXXX(1)" S Y=$$DIR^IBYXXX(.DIR,.IBQUIT,.IBQUIT1)
 I Y'=1 Q
 K ^TMP("IBONE",$J),^TMP("IBCE-BATCH",$J)
 S IBIFN=0 F  S IBIFN=$O(^XTMP("IBRESUBMIT",IBIFN)) Q:'IBIFN  S Z=$O(^XTMP("IBRESUBMIT",IBIFN,0))  I Z,'$D(^XTMP("IBRESUBMIT",IBIFN,Z,"NO")),'$D(^XTMP("IBRESUBMIT",IBIFN,Z,"DONE")) S Z=$O(^XTMP("IBRESUBMIT",IBIFN,""),-1) I Z D
 . S IBDA1=Z
 . I $P($G(^IBA(364,Z,0)),U,3)'="X" D  Q:'IBDA1
 .. S IBDA1=$$LAST364^IBCEF4(IBIFN)
 .. I $P($G(^IBA(364,+IBDA1,0)),U,3)'="X" D
 ... S IBDA1=+$$ADDTBILL^IBCB1(IBIFN)
 ... S ^XTMP("IBRESUBMIT",IBIFN,Z,"DONE")=IBDA1
 ... S ^XTMP("IBRESUBMIT","CT")=^XTMP("IBRESUBMIT","CT")-1
 ... D UPDEDI^IBCEM(Z,"R")
 . S ^TMP("IBONE",$J,IBDA1)=Z
 S ^TMP("IBONE",$J)=0
 D ONE
 W !,"BILL(s) TRANSMITTED ... BATCH #(s): "
 S Z=0 F  S Z=$O(^TMP("IBCE-BATCH",$J,Z)) Q:'Z  W Z,$S($O(^(Z)):", ",1:"")
 I '$O(^TMP("IBCE-BATCH",$J,0)) W !,"NO BILL(S) TRANSMITTED - CHECK ALERTS/MAIL FOR DETAILS"
 I $G(^XTMP("IBRESUBMIT","CT"))>0 W !,^XTMP("IBRESUBMIT","CT")," BILLS WERE STILL NOT RESUBMITTED",!,"RUN THE PRINT OPTION OR RUN THIS OPTION AGAIN TO GET A LIST",!
 D CLEANUP^IBCE837A
 K ^TMP("IBONE",$J),^TMP("IBCE-BATCH",$J)
 D PAUSE^VALM1
 Q
 ;
ONE ; Txmt bills
 K ^TMP("IBXMSG",$J),^TMP("IBTXMT",$J),^TMP("IBHDR",$J),^TMP("IBHDR1",$J),^TMP("IBXERR",$J),IBXERR,^TMP("IBXINS",$J),^TMP("IBTX",$J)
 D FIND^IBCE837,OUTPUT^IBCE837
 Q
 ;
