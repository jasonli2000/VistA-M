ZISLDATE ;WILM/RJ - List Micom Date and Time; 6-9-85
 ;;7.1;KERNEL;;May 11, 1993
 ;;Version 4.51
 D 1^%ZISLVR I X=U W !,"No connection made." K IOP,W,X,Y,ZISLSITE Q
 S ZISLTYPE=$S(X["?":600,1:6600) U IO(0) W !,"Listing Micom ",ZISLTYPE," Time and Date..." G:ZISLTYPE=6600 6600 F I=1:1:4 U IO W *10 R X:1
 S ZISLDT="",R="DMP 969F 3" D T,R U IO(0) S:$P(ZISLDT,U,2)'=10 ZISLDT=$P(ZISLDT,U,1)_U_U_U_$P(ZISLDT,U,3,50) S (ZISLT,ZISLTIM)=""
 F ZISLT=$P(ZISLDT,U,25,26),$P(ZISLDT,U,28,29),$P(ZISLDT,U,31,32) D X S I=H+L S:$L(I)=1 I=0_I S ZISLTIM=ZISLTIM_I
 W !!,"Time (HH:MM:SS) ->> ",$E(ZISLTIM,1,2),":",$E(ZISLTIM,3,4),":",$E(ZISLTIM,5,6)
 S ZISLDT="",R="DMP 969D 2" D T,R U IO(0) D CP^%ZISLDIS X ^%ZIS("C")
 S ZISLT=$P(ZISLDT,U,24,25) D X S R=$C($P(ZISLDT,U,28))*16*16+H+L
 S ZISLMON="Jan^Feb^Mar^Apr^May^Jun^Jul^Aug^Sep^Oct^Nov^Dec",ZISLDAY="31^59^90^120^151^181^212^243^273^304^334^365"
 S %DT="",X="T" D ^%DT X ^DD("DD") S Y=$P(Y,",",2)
 I Y#4=0 F I=2:1:12 S ZISLDAY=$P(ZISLDAY,U,1,I-1)_U_($P(ZISLDAY,U,I)+1)_U_$P(ZISLDAY,U,I+1,12)
 D M S ZISLDAT=R-$P(ZISLDAY,U,I-1)_"-"_$P(ZISLMON,U,I)_"-"_Y W !!,"Date ->> ",ZISLDAT
Q K ZISLDAY,ZISLMON,ZISLDT,ZISLDAT,ZISLTIM,ZISLT,I,H,L,R,%DT,P,Y,ZISLSITE,ZISLCPU,ZISLTYPE Q
T U IO(0) F J=1:1 D P S X=$E(R,J) Q:X=""  U IO W X
 U IO W *13 Q
R F J=0:0 U IO R *X:1 Q:X=-1  S:X'=0 ZISLDT=ZISLDT_U_X
 Q
P F I=1:1:50
 Q
M F I=1:1:12 Q:$P(ZISLDAY,U,I)>(R-1)
 Q
X S H=$C($P(ZISLT,U,1)),H=$S(H="A":10,H="B":11,H="C":12,H="D":13,H="E":14,H="F":15,1:H)*16,L=$C($P(ZISLT,U,2)),L=$S(L="A":10,L="B":11,L="C":12,L="D":13,L="E":14,L="F":15,1:L) Q
6600 S R="SHO TD" D T^ZISLDIAL D CP^%ZISLDIS X ^%ZIS("C") G Q
