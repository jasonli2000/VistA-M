ZISLPC ;WILM/RJ - Micom Printer Contention; SAVE AS %ZISLPC IN MGR UCI; 9-1-86
 ;;7.1;KERNEL;;May 11, 1993
 ;;Version 4.51
 S ZISLTASK=$S($D(ZTSK):0,1:1) W:ZISLTASK !?2,"Printer Contention, wait a second->" D 1 I POP K ZISLP X:$D(ZISLC2) ZISLC2 W:ZISLTASK&($L(ZISLE)) !?2,ZISLE G X
 K Y X:$D(^%ZOSF("UCI")) ^("UCI") W:ZISLTASK !?2,"Connected ",ZISLL," to ",ZISLP,".  Output Device is ",IO I ZISLTASK W:$D(Y) " on System ",$P(Y,",",2),"."
 S I=$O(^%ZISL(107.3,"B",ZISLP,0)) I +I=0 S I=$P(^%ZISL(107.3,0),"^",4)+1,$P(^%ZISL(107.3,0),"^",3,4)=I_"^"_I,^%ZISL(107.3,I,0)=ZISLP_"^"_IO_"^"_$P(Y,",",2),^%ZISL(107.3,"B",ZISLP,I)=""
 S $P(^%ZISL(107.3,I,0),"^",4)=1
X K ZISLL,ZISLE,ZISLIO,ZISLIO1,ZISLR,ZISLSITE,ZISLY,ZISLPORT,ZISLSITE,ZISLPAUS,ZISLTASK,ZISLZ,ZISLC1,ZISLC2,ZISLCPU,ZISLTYPE Q
1 S (DX,DY)=0,ZISLE="",POP=1 D ^%ZISLSIT S ZISLIO=$P(ZISLSITE,"^",2) I ZISLIO="" S ZISLE="Unable to find Micom Command Port Device Number." Q
 D INIT X ZISLC1 I ZISLZ="^" H 5 X ZISLC1 I ZISLZ="^" S ZISLE="Micom Command Port is busy.  Try Later." Q
 S Y="" U ZISLIO X ^%ZOSF("EOFF"),^%ZOSF("TYPE-AHEAD") X:$D(^%ZOSF("ZMAXBUF")) ^("ZMAXBUF") W *13 F I=1:1 R ZISLZ#1:1 S:'$T ZISLZ="^" Q:ZISLZ="^"  S Y=Y_ZISLZ D P
 I Y["?"!(Y["A>")!(Y["B>") G SC
 I Y'["GO",Y'["CONNECTED" G ERCP
 W *13 F I=1:1:4 R Y#1:1 D P Q:Y="?"!(Y=">")
 I Y'="?",Y'=">" G ERCP
SC S ZISLTYPE=$S(Y[">":6600,1:""),R=$S(ZISLTYPE=6600:"CONN FORC ",1:"CON L")_ZISLL_$S(ZISLTYPE=6600:" ",1:" P")_ZISLP D T I ZISLY["?" D T
 I ZISLTYPE=6600 S:ZISLY'["!" ZISLY="TASK COMPLETE" G:ZISLY["!"&(ZISLY'["BUSY") ER5 I ZISLY["BUSY" D STA
 I ZISLTYPE'=6600,ZISLY'["TASK COMPLETE" D STA
CP S:ZISLY["TASK COMPLETE" POP=0 S ZISLIO1=IO,IO=ZISLIO,X="" D CP^%ZISLDIS S IO=ZISLIO1 X ZISLC2 Q
T U ZISLIO X ^%ZOSF("XY") S Y="" F J=1:1 D P S ZISLZ=$E(R,J) Q:ZISLZ=""  U ZISLIO W ZISLZ
 W *13 S ZISLY="" F J=0:0 U ZISLIO R ZISLZ#1:1 Q:'$T  S ZISLY=ZISLY_ZISLZ
 Q
INIT S ZISLC1="S ZISLZ=""^"" F I=1:1:5 O ZISLIO:"""":1 I $T S ZISLZ="""" Q",ZISLC2="C ZISLIO"
 Q
STA ;get status of line to printer
 S ZISLR=R,R=$S(ZISLTYPE=6600:"SHO CH ",1:"STA L")_ZISLL D T I ZISLTYPE'=6600 S ZISLPORT=+$E($P(ZISLY,"CONN",2),2,5)
 I ZISLTYPE=6600 S ZISLPORT=$E($P(ZISLY,"CALLING",2),4,9) S:$E(ZISLPORT,1)=0 ZISLPORT=$E(ZISLPORT,2,6)
 I ZISLPORT="" S ZISLY="" G ER4
 S Y=$O(^%ZISL(107.3,"B",ZISLPORT,0)) I +Y=0 S ZISLY="" G ER21
 S Y=$S($D(^%ZISL(107.3,Y,0)):^(0),1:"") I ZISLP=$P(Y,"^",1)!(ZISLPORT=0) S ZISLY="TASK COMPLETE" Q
 S ZISLY="" G:$P(Y,"^",4) ER2 G:Y="" ER21
 S R=$S(ZISLTYPE=6600:"DISC CH ",1:"DIS P")_ZISLP D T I ZISLY["?" G ER3
 S R=ZISLR D T I ZISLY'["TASK COMPLETE",ZISLY'["A>",ZISLY'["B>" G ER4
 S ZISLY="TASK COMPLETE"
 Q
P F ZISLPAUS=1:1:100
 Q
ERCP S ZISLE="Unable to access Micom Command Port.  Try Later.",ZISLY="" G CP
ER2 S ZISLE="Currently in use by Device "_$P(Y,"^",2)_" connected to "_ZISLPORT_" on CPU "_$P(Y,"^",3)_"." Q
ER21 S ZISLE="This printer is connected to "_ZISLPORT_" which is not defined in file 107.3." Q
ER3 S ZISLE="Unable to disconnect "_ZISLL_" from "_ZISLPORT_".  Try Again Later." Q
ER4 S ZISLE="Unable to connect "_ZISLL_" to "_ZISLP_".  Try Again Later." Q
ER5 D ER4 S ZISLE=ZISLE_$C(10)_$C(13)_$P(ZISLY,$C(10),2) Q
C ;Disconnect Printer
 Q:'$D(ZISLP)  S Y=$O(^%ZISL(107.3,"B",ZISLP,0)) Q:+Y=0  I $D(^%ZISL(107.3,Y,0)) S $P(^(0),"^",4)=0
 D ^%ZISLSIT S ZISLIO=$P(ZISLSITE,"^",2) G:ZISLIO="" CX
 D INIT X ZISLC1 I ZISLZ="^" X ZISLC1 G:ZISLZ="^" CX
 S Y="",(DX,DY)=0 U ZISLIO X ^%ZOSF("EOFF"),^%ZOSF("TYPE-AHEAD") X:$D(^%ZOSF("ZMAXBUF")) ^("ZMAXBUF") W *13 F I=1:1 R X#1:1 S:'$T X="^" Q:X="^"  S Y=Y_X D P
 G:Y["?"!(Y["A>")!(Y["B>") C1 W *13 F I=1:1:4 R Y#1:1 D P Q:Y="?"!(Y=">")
 I Y'="?",Y'=">" G CC
C1 S ZISLTYPE=$S(Y[">":6600,1:""),R=$S(ZISLTYPE=6600:"DISC CH ",1:"DIS P")_ZISLP D T I ZISLY["?" D T
CC S ZISLIO1=IO,IO=ZISLIO,X="" D CP^%ZISLDIS S IO=ZISLIO1 X ZISLC2
CX K ZISLIO,ZISLIO1,ZISLP,R,ZISLSITE,ZISLY,ZISLSITE,ZISLPAUS,ZISLZ,ZISLC1,ZISLC2,ZISLCPU,ZISLTYPE Q
