LRZLIST ;SLC/RWF/CJS - LAB RESULTS LIST ;2/19/91  10:39 [ 02/28/95  4:11 PM ]
 ;;5.2;LAB SERVICE;;Sep 27, 1994
 W !,"Summary List (Supervisors')  >>> NOT FOR WARD USE <<<",! K ^TMP($J) D DATE^LRWU G END:Y<1 S LRAD=Y,DIC="^LRO(68,",DIC(0)="AEQ",LRNL=0
 S LRRDT=$$DDDATE^LRAFUNC1(Y,1)
 F J=0:0 D ^DIC Q:Y<1  D CHKDAT^LRLSTWRL Q:Y<1  S DIC("A")="ANOTHER ONE: ",LRNL=LRNL+1,LRAA(LRNL)=+Y,LRAA(LRNL,1)=$P(Y,U,2),LRSS(LRNL)=$P(^LRO(68,LRAA(LRNL),0),U,2)
 K DIC G LRLIST:LRNL<1
 S LRDATE=-1 I $P(^LRO(68,LRAA(1),0),U,2)="MI" S %DT("A")="Report date approved to display: " D DATE^LRWU G END:LREND S LRDATE=Y
C R !,"1 ACCESSION NUMBER",!,"2 PATIENT",!,"LIST BY: ",LRX:DTIME G END:LRX["^"!(LRX=""),C:"12"'[LRX!(LRX>2)
 D RANGE
ALL W !!?5,"Do you wish to see all tests including Common Accessions " S %=1 D YN^DICN G:%=0 ALL G:%<1 END S:%=1 LRALL=""
 S %ZIS="MQ" D ^%ZIS G END:POP
 I $D(IO("Q")) S ZTRTN="DQ^LRZLIST",ZTIO=ION,ZTDESC="Summary List (Supervisors')",ZTSAVE("LR*")="" D ^%ZTLOAD G END
C2 U IO D HDR S DTOUT=0 G L10:LRX=1,L20:LRX=2,END
L10 I $D(LRALL) F LRAA=1:1:LRNL F L=LRFAN-1:0 S L=$O(^LRO(68,LRAA(LRAA),1,LRAD,1,L)) Q:(L>LRLAN)!(L<LRFAN)  S ^TMP($J,L,LRAA)=""
 I '$D(LRALL) F LRAA=1:1:LRNL S L=LRFAN-1 F  S L=$O(^LRO(68,LRAA(LRAA),1,LRAD,1,L)) Q:(L>LRLAN)!(L<LRFAN)  I $O(^(L,4,0)) S ^TMP($J,L,LRAA)=""
 F LRAN=0:0 S LRAN=$O(^TMP($J,LRAN)) Q:LRAN<1  F LRAA=0:0 S LRAA=$O(^TMP($J,LRAN,LRAA)) Q:LRAA<1  D PR G:$G(DTOUT)!($G(DUOUT)) END
 W !!,"END OF REPORT",! G END
L20 F LRAA=1:1:LRNL D L22
 S LRPNM=""
 F J=0:0 S LRPNM=$O(^TMP($J,LRPNM)) Q:LRPNM=""  S PNM=$P(LRPNM,U),SSN=$P(LRPNM,U,2) D L26 Q:$G(DTOUT)!($G(DUOUT))
 G END
L22 F LRAN=LRFAN-1:0 S LRAN=$O(^LRO(68,LRAA(LRAA),1,LRAD,1,LRAN)) Q:LRAN<1!(LRAN>LRLAN)  D L23
 Q
L23 I '$D(LRALL),'$O(^LRO(68,LRAA(LRAA),1,LRAD,1,LRAN,4,0)) Q
 Q:'$D(^LRO(68,LRAA(LRAA),1,LRAD,1,LRAN,0))  Q:'$D(^(3))  S LRDFN=+^(0),LRDPF=$P(^LR(LRDFN,0),U,2),DFN=$P(^(0),U,3) D DEM^LRX S:$L(PNM) ^TMP($J,PNM_U_SSN,LRAA,LRAN)=DOB Q
L26 F LRAA=0:0 S LRAA=$O(^TMP($J,LRPNM,LRAA)) Q:LRAA<1  D L28 Q:$G(DTOUT)!($G(DUOUT))
 Q
L28 F LRAN=0:0 S LRAN=$O(^TMP($J,LRPNM,LRAA,LRAN)) Q:LRAN<1  D PR Q:$G(DTOUT)!($G(DUOUT))
 Q
PR ;
 I '$D(^LRO(68,LRAA(LRAA),1,LRAD,1,LRAN,0)) W !!?10," Accession ",LRAN," deleted ",!!,$C(7) Q
 Q:'$D(^LRO(68,LRAA(LRAA),1,LRAD,1,LRAN,3))
 S LRIDT=9999999-^(3),LRDFN=+^LRO(68,LRAA(LRAA),1,LRAD,1,LRAN,0),LRINT=$P(^(0),U,5),LRODT=$P(^(0),U,4) G PR1:LRDATE<1
 S LRSET=0 F I=1,5,8,11,16 I $D(^LR(LRDFN,"MI",LRIDT,I)),+^(I)=LRDATE S LRSET=1 Q
 Q:'LRSET
PR1 S LRDPF=$P(^LR(LRDFN,0),U,2),DFN=$P(^(0),U,3) D:$G(LRX)=1 DEM^LRX
 I IOST?1"P".E&($Y>(IOSL-16)) W @IOF D HDR ;ONLY FOR USE ON A PRINTER
 D DASH^LRX W !!,PNM,?40,SSN,!,LRAA(LRAA,1)," ACC:  ",$S($D(^LRO(68,+LRAA(LRAA),1,LRAD,1,LRAN,.2)):^(.2),1:"no acc")
 I LRINT S LRINT=$S($D(^LRO(69,LRODT,1,LRINT,0)):$P(^(0),U,2),1:"") I LRINT S LRINT=$P($G(^VA(200,LRINT,0)),U,1) W !,"Person placing order: ",LRINT,!
 I $D(^LRO(68,+LRAA(LRAA),1,LRAD,1,LRAN,4)) S LRIN=+$O(^(4,"B",0)) I LRIN,$D(^LRO(68,+LRAA(LRAA),1,LRAD,1,LRAN,4,LRIN)) S LRIN=$P($G(^(LRIN,0)),U,4) S LRZIN=$S($L(LRIN):$P($G(^VA(200,LRIN,0)),U,2),1:"") 
 I LRIN="" W "Person performing test: ACCESSION NOT VERIFIED"
 I $L(LRIN) W "Person performing test: ",LRIN_" [",LRZIN_"]"
 W ?38,$S($D(^LRO(68,+LRAA(LRAA),1,LRAD,1,LRAN,.1)):"  ORD: "_^(.1),1:"")
 I '$D(LRSS(+$G(LRAA))) W !," ACCESSION #: ",LRAN," HAS AN ERROR NOTIFY SYSTEM MANAGER >>> ",!! Q
 IF '$D(^LR(LRDFN,LRSS(LRAA),LRIDT,0)) W !," ACCESSION #: ",LRAN,"  >>>>ERROR PLEASE NOTIFY SYSTEM MANAGER<<<<<",! Q
 W ! S DIC="^LR("_LRDFN_","""_LRSS(LRAA)_""",",DR="0"_$S(LRLONG:":99999999",1:""),DA=LRIDT D EN^LRDIQ
 Q
END W:$E(IOST,1,2)="P-" @IOF D ^%ZISC
 K DTOUT,DUOUT,LR,LRDFN,LRDPF,LRIDT,LRODT,LRPRAC,LRRB,LRTREA,LRWRD,PNM
 K SSN,VA
 K ^TMP($J),LRPNM,LRDATE,LRLONG,ZTRTN,ZTIO,ZTDESC,ZTSAVE,ZTSK,%H
 K C1,D0,DA,DICS,DL,DSC,DX,L,LAST,LRAA,LRAD,LRALL,LRDX,LREDT,LREND,LRFAN
 K LRIN,LRINT,LRLAN,LRLINE,LRNL,LRSET,LRSS,LRWDTL,LRRDT,LRRPG,LRX,POP,IO("Q")
 D KVAR^VADPT Q
HDR I '$D(LRRPG) S LRRPG=1 G HD1
HD1 W !,"SUMMARY LIST (SUPERVISORS') FOR DATE: ",LRRDT,?(IOM-12),"PAGE: ",LRRPG,!
 W "     >> NOT FOR WARD USE <<" W:$L(LRRDT)=4 ?40,"Report for date: ",$$DDDATE^LRAFUNC1(LRDATE,0) W !
 W !,"ACCESSION AREA(S) :" F ZZ=1:1:LRNL W LRAA(ZZ,1),"   "
 W ! F ZZ=1:1:(IOM-1) W "="
 S LRRPG=LRRPG+1
 Q
RANGE R !,"(L)ONG OR (S)HORT LISTING: S//",X:DTIME S LRLONG=(X["L"!(X["l")) I X["?" W !?5,"Long listing shows verified results where short list does not",! G RANGE
 D LRAN^LRWU3 Q
 ;
DQ U IO S:$D(ZTQUEUED) ZTREQ="@" G C2
