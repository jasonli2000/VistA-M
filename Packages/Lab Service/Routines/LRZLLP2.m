LRZLLP2 ;SLC/RWF - TRAY LIST PRINT ;2/5/91  14:37 ; [ 02/17/95  3:36 PM ]
 ;;5.2;LAB SERVICE;;Sep 27, 1994
 ; MJB-12/94 
 ;This routine was modifed for E3R 5519 which requested the ID field
 ; not be concatenated to the patient name if the entry is in the 
 ; REFERRAL or RESEARCH files.
 S LRPROF=0,X=$O(^LRO(68.2,LRINST,1,LRTRAY)),Y=$O(^(X,1,0)) I X,Y S LRPROF=+$P(^(Y,0),U,4)
LOOP S LRTRAY=$O(^LRO(68.2,LRINST,1,LRTRAY)) G END:LRTRAY<1!(LRTRAY>LRLLT&LRTYPE) D HED
 F LRCUP=1:1:LRMAXCUP D LP2
 G LOOP
LP2 S LRLL=$S($D(^LRO(68.2,LRINST,1,LRTRAY,1,LRCUP,0)):^(0),1:""),LRTEST="" I LRPROF'=+$P(LRLL,U,4) S LRPROF=+$P(LRLL,U,4),LRDC=LRPROF
 Q:LRLL=""  D HED:$Y+8>IOSL!(LRDC)
 W ! W:'LRALTH "TRAY:",$J(LRTRAY,3)," CUP:",$J(LRCUP,3) D LRLINE Q
LRLINE S LRAA=+LRLL,LRAD=+$P(LRLL,U,2),LRAN=+$P(LRLL,U,3) D MOVE^LRLLP4:$D(^TMP($J,LRPROF))=0
 S LRDFN=$S($D(^LRO(68,LRAA,1,LRAD,1,LRAN,0)):+^(0),1:"")
 I LRDFN="" K ^LRO(68.2,LRINST,1,LRTRAY,1,LRCUP) D DASH^LRX Q  ; IF NOTHING THERE, GET RID OF IT
 K ^TMP("LR",$J,"T"),LRTSTS S LRTEST="",LRURG=99 G BLANK:LRLL=""
 S J=0 F  S J=$O(^LRO(68.2,LRINST,1,LRTRAY,1,LRCUP,1,J)) Q:J<1  S X=$P(^(J,0),U,2),^TMP("LR",$J,"T",J)="",LRTEST=LRTEST_J_U S:X<LRURG LRURG=+X
 I LRXPD K ^TMP("LR",$J,"T"),LRTSTS,LRORD D ^LREXPD
 K LRTEST,LRORD F I=0:0 S I=$O(^TMP("LR",$J,"T",I)) Q:I'>0  S LRORD($S($D(^TMP($J,LRPROF,I)):^(I),1:I+999))=I
LP4 S LRACC=^LRO(68,LRAA,1,LRAD,1,LRAN,.2) W:$X=0 LRACC
 I $L(LRDFN) S LRLLOC=$P(^LRO(68,LRAA,1,LRAD,1,LRAN,0),U,7),LRDOC=$P(^(0),U,8),LRODNUM=$S($D(^(.1)):^(.1),1:""),LRIDT=$S($D(^(3)):9999999-^(3),1:0),LRSPEC=$S($D(^(5,1,0)):+^(0),1:0),LRSISPEC=+$P(^(0),U,2)
 I $L(LRDFN) S LRSPEC=$S($D(^LAB(61,LRSPEC,0)):$P(^(0),U),1:""),LRSISPEC=$S($D(^LAB(62,LRSISPEC,0)):$P(^(0),U),1:"")
 S X=LRDOC,LRLLOC=LRLLOC_"  "_$S($D(LRURG(LRURG)):LRURG(LRURG),1:"") D DOC^LRX
 S DFN=+$P(^LR(LRDFN,0),U,3),LRDPF=+$P(^(0),U,2),LRV=0,LRV=$S($D(^LR(LRDFN,"CH",LRIDT,0)):$P(^(0),U,3),1:0) D PT^LRX
 ;W ?18 W:LRDPF=2 $E(PNM,1,17),?36,$E(SSN,$L(SSN)-3,$L(SSN)) W:LRDPF'=2 $E(PNM_" "_SSN,1,22) W ?42,LRACC W:LRV ?52,"Ver" W ?60," #:",LRORDNUM S Y=9999999-LRIDT D ADD^LRX W !,?18,Y,?42,LRDOC D INF^LRX
 W ?18,$E(PNM,1,17),?36,$E(SSN,$L(SSN)-3,$L(SSN)) W ?42,LRACC W:LRV ?52,"Ver" W ?60," #:",LRODNUM S Y=9999999-LRIDT D ADD^LRX W !,?18,Y,?42,LRDOC D INF^LRX
LP3 ;
 I 'LRSHORT F J=0:0 S J=$O(LRORD(J)) Q:J<1  S I=LRORD(J) W !,?18,$E(LRLINE,1,31) W:LRLLOC]"" ?56,LRLLOC S LRLLOC="" W !,?18,$P(^LAB(60,I,0),"^",1),?50,LRSPEC D LRSPEC
 I LRSHORT W !?18,$E(LRLINE,1,31),?56,LRSPEC D LRSPEC W !?18 F J=0:0 S J=$O(LRORD(J)) Q:J<1  S I=LRORD(J) W:$X>19 ", " W $P(^LAB(60,I,0),"^",1) I $X>50 W !?18
 I $D(LRAA),$D(^LRO(68,+LRAA,0)),$P(^(0),U,2)="MI" W:$D(^LR(LRDFN,"MI",LRIDT,99)) !?20,^(99)
 W !,LRLINE,$E(LRLINE,1,39) Q
LP5 S L=$P(^TMP("LR",$J,"T",I),U,5),L=$P(L,";",2) I LRIDT,$D(^LR(LRDFN,"CH",LRIDT,L)) W ?37,$J(^(L),8)
 W:LRV ?45,"Ver" Q
 Q
BLANK W !,LRLINE,$E(LRLINE,1,39) Q
HED ;
 W:LRDC!(IOSL\2<$Y) @IOF
 W !!,$S(LRTYPE>0:"LOAD",1:"WORK"),"-LIST FOR ",$P(^LRO(68.2,LRINST,0),U,1),$S($D(^LRO(68.2,LRINST,10,LRPROF,0)):"  (Profile: "_$P(^(0),U,1)_")",1:""),?55,LRNOW
 W !,?18,"Name",?36,"ID#",?42,"Acc #",?60,"Order #",!,?18,"Collection Date/Time",?42,"Provider",!,LRLINE,$E(LRLINE,1,39)
 S LRDC=0 Q
END W ! W:$E(IOST,1,2)="P-" @IOF D ^%ZISC Q
LRSPEC ;
 I $D(LRAA),$D(^LRO(68,+LRAA,0)),$P(^(0),U,2)="MI",$D(LRSISPEC),$L(LRSISPEC) S TAB=$S(LRSHORT:56,1:50) W !,?TAB,LRSISPEC K TAB
 Q
