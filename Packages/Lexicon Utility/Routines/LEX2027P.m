LEX2027P ; ISL/KER - Pre/Post Install; 10/27/2003
 ;;2.0;LEXICON UTILITY;**27**;Sep 23, 1996
 ;
 ; External References
 ;   DBIA 10086  HOME^%ZIS
 ;   DBIA  2052  $$GET1^DID
 ;   DBIA  2055  PRD^DILFD
 ;   DBIA 10014  EN^DIU2
 ;   DBIA 10141  BMES^XPDUTL
 ;   DBIA 10141  MES^XPDUTL
 ;                      
 Q
 ;                      
POST ; LEX*2.0*27 Post-Install
 N LEXEDT S LEXEDT=$G(^LEXM(0,"CREATED")) D AFIL,IMP,CON,PRO,MSG
 Q
AFIL ; Re-create cross-reference ^LEXC(757.9,"AFIL")
 D BM^LEX2027("  Building Cross-Reference ^LEXC(757.9,""AFIL"")")
 N DA,DIK S DIK="^LEXC(757.9," K ^LEXC(757.9,"AFIL")
 S DA=0 F  S DA=$O(^LEXC(757.9,DA)) Q:+DA=0  D IX1^DIK
 Q
IMP ; Import Data from Routines
 D BM^LEX2027("  Installing Data") D:$L($T(EN^LEX2027A)) EN^LEX2027A
 D:$L($T(DATA^LEX2027A)) DATA^LEX2027A D:$L($T(DATA^LEX2027B)) DATA^LEX2027B
 D:$L($T(DATA^LEX2027C)) DATA^LEX2027C D:$L($T(DATA^LEX2027D)) DATA^LEX2027D
 Q:'$D(^%ZOSF("DEL"))  S:$L($T(DATA^LEX2027A)) X="LEX2027A" X:$L($T(DATA^LEX2027A)) ^%ZOSF("DEL")
 S:$L($T(DATA^LEX2027B)) X="LEX2027B" X:$L($T(DATA^LEX2027B)) ^%ZOSF("DEL")
 S:$L($T(DATA^LEX2027C)) X="LEX2027C" X:$L($T(DATA^LEX2027C)) ^%ZOSF("DEL")
 S:$L($T(DATA^LEX2027D)) X="LEX2027D" X:$L($T(DATA^LEX2027D)) ^%ZOSF("DEL")
 Q
CON ; Conversion of data (Sep 30 Inactivation Date)
 D BM^LEX2027("  Fixing Sep 30, 2003 Inactivation Date") W !,"   "
 N FDA,CODEIEN,SUBIEN,CTR,ZZERR
 S CODIEN=0,CTR=0 F  S CODIEN=$O(^ICD0(CODIEN)) Q:+CODIEN=0  D
 . S CTR=CTR+1 I CTR>1100 W "." S CTR=0
 . Q:'$D(^ICD0(CODIEN,66,"B",3030930))
 . S SUBIEN=$O(^ICD0(CODIEN,66,"B",3030930,""))
 . K FDA S FDA(80.166,SUBIEN_","_CODIEN_",",.01)="@"
 . D UPDATE^DIE(,"FDA",,"ZZERR")
 . K FDA S FDA(80.166,"+1,"_CODIEN_",",.01)=3031001
 . S FDA(80.166,"+1,"_CODIEN_",",.02)=0
 . D UPDATE^DIE(,"FDA",,"ZZERR")
 S CODIEN=0 F  S CODIEN=$O(^ICD9(CODIEN)) Q:+CODIEN=0  D
 . S CTR=CTR+1 I CTR>1100 W "." S CTR=0
 . Q:'$D(^ICD9(CODIEN,66,"B",3030930))
 . S SUBIEN=$O(^ICD9(CODIEN,66,"B",3030930,""))
 . K FDA S FDA(80.066,SUBIEN_","_CODIEN_",",.01)="@"
 . D UPDATE^DIE(,"FDA",,"ZZERR")
 . K FDA S FDA(80.066,"+1,"_CODIEN_",",.01)=3031001
 . S FDA(80.066,"+1,"_CODIEN_",",.02)=0
 . D UPDATE^DIE(,"FDA",,"ZZERR")
 S CODIEN=0 F  S CODIEN=$O(^LEX(757.02,CODIEN)) Q:+CODIEN=0  D
 . S CTR=CTR+1 I CTR>1100 W "." S CTR=0
 . Q:'$D(^LEX(757.02,CODIEN,4,"B",3030930))
 . S SUBIEN="" F  S SUBIEN=$O(^LEX(757.02,CODIEN,4,"B",3030930,SUBIEN)) Q:SUBIEN=""  D
 . . K FDA S FDA(757.28,SUBIEN_","_CODIEN_",",.01)="@"
 . . D UPDATE^DIE(,"FDA",,"ZZERR")
 . K FDA S FDA(757.28,"+1,"_CODIEN_",",.01)=3031001
 . S FDA(757.28,"+1,"_CODIEN_",",1)=0
 . D UPDATE^DIE(,"FDA",,"ZZERR")
 Q
PRO ; Force Protocol
 N LEXSCHG,LEXPCALL S LEXSCHG(757,0)="",LEXSCHG(757.001,0)="",LEXSCHG(757.01,0)="",LEXSCHG(757.05,0)=""
 S LEXSCHG(757.9,0)="",LEXSCHG(757.9,1)=80,LEXSCHG(757.9,2)=80.1,LEXSCHG(757.9,3)=81
 S LEXSCHG(757.9,4)=81.3,LEXSCHG(757.9,"B",80,1)="",LEXSCHG(757.9,"B",80.1,2)=""
 S LEXSCHG(757.9,"B",81,3)="",LEXSCHG(757.9,"B",81.3,4)=""
 D:$L($T(NOTIFY^LEXXGI)) NOTIFY^LEXXGI
 Q
MSG ;   Send Installation Message
 Q:+($G(DUZ))=0!($$NOTDEF^LEX2027($G(DUZ)))
 D HOME^%ZIS N DIFROM,LEXLREV,LEXLAST,LEXBUILD,LEXIGHF,LEXSHORT
 S LEXSHORT="" D IMP^LEX2027,SEND^LEXXST Q
 ;                       
PRE ; LEX*2.0*27 Pre-Install (N/A for patch 27)
 Q
