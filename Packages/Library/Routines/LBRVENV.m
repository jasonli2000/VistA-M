LBRVENV ;SSI/ALA-Environmential Check Program ;[ 04/10/98  2:38 PM ]
 ;;2.5;Library;**2**;Mar 11, 1996
EN ;
 I '$G(DUZ)!($G(DUZ(0))'["@") W !,"USER 'DUZ' VARIABLES **NOT** CORRECTLY DEFINED.  CONFIRM THAT DUZ(0)='@'.  THEN D ^XUP." S XPDQUIT=1 Q
 I $$VERSION^XPDUTL("LBRY")<2.5 S QUIT=0 D  G ABRT:QUIT
 . I $$VERSION^XPDUTL("LBR")>2 Q
 . W !!,"VERSION 2.5 OF LIBRARY HAS NOT BEEN LOADED",! S QUIT=1
 I '$D(^XPD(9.7,"B","LBR*2.5*1")) D  G ABRT
 . W !!,"*** Patch LBR*2.5*1 must be installed prior to loading"
 . W !,"this patch.  This install will now abort.",!
 I $$PATCH^XPDUTL("LBR*2.5*2")=1,($D(^XTMP("LBRV",0))) Q
 D STA1 Q:$G(XPDQUIT)=1
 D STA2 Q:$G(XPDQUIT)=1
 W !!,">> Environment check complete and okay."
EXIT K LBRSTNN,LBRWSTN,LBRPT4,LBRVA,J,II,LSTN,GSTN,QUIT,LBRVDT
 Q
CNF ;  Ask for confirmation
 S QUIT=0
 I $G(XPDQUES("PRE1"))="" K DIR Q
 S PRE1ANS=$G(XPDQUES("PRE1"))
 I PRE1ANS="LEG" S PRE1ANS="LEGACY STATION"
 I PRE1ANS="PRI" S PRE1ANS="PRIMARY STATION"
 S DIR(0)="Y"
 S DIR("A")="Are you sure you are a "_PRE1ANS
 S DIR("A",1)="  "
 S DIR("A",2)="     Press ENTER if you wish to stop the install and"
 S DIR("A",3)="     verify which type of system you are."
 S DIR("A",4)="  "
 S DIR("A",5)="     Press 'Y' if you have answered correctly and"
 S DIR("A",6)="     wish to continue with the install."
 S DIR("A",7)="  "
 S DIR("?")="Press ENTER to abort install, 'Y' to continue"
 S DIR("B")="NO" D ^DIR
 I Y=0 S (DIRUT,DUOUT)=1
 I $D(DIRUT) S (QUIT,XPDQUIT)=1
 K DIR,Y,PRE1ANS,X
 Q
STA1 ;  Get station number and identify records to be moved
 S LBRSTNN=$$KSP^XUPARAM("INST")
 S DIC(0)="NZ",DIC="^DIC(4,",X=LBRSTNN D ^DIC
 I Y<0 D  S XPDQUIT=1 Q
 . D MES^XPDUTL("Error found in the Kernel System Parameters file and/or the Institution file.")
 . D MES^XPDUTL("Verify that the STATION NUMBER and the DEFAULT INSTITUTION fields are defined correctly.")
 K Y,DIC
 D GETS^DIQ(4,LBRSTNN,99,"I","LBRVA")
 S LBRWSTN=@($Q(LBRVA))
 Q
STA2 F II=1:1:15 S GSTN=$P($T(STN+II),";;",2) D
 . F J=1:1 S LSTN=$P(GSTN,U,J) Q:LSTN=""  D
 .. I $P(LSTN,";")=LBRWSTN S LBRWSTA=$P(LSTN,";",2)
 I $G(LBRWSTA)="" D  S XPDQUIT=1 Q
 . D MES^XPDUTL("No match found for Site Code/Station Number.")
 . D MES^XPDUTL("Please reference the 'Installation Guide/Release Notes' documentation's")
 . D MES^XPDUTL("Pre-Installation Checklist on page 6 for further instructions")
 . D MES^XPDUTL("on correctly setting up your environment.")
 . D MES^XPDUTL("  ")
 S LBRVDT=$$FMADD^XLFDT(DT,365)
 S ^XTMP("LBRV",0)=LBRVDT_"^"_DT_"^"_LBRWSTA
 Q
ABRT S XPDQUIT=1
 Q
STN ;  Identify station number and 5-letter code
 ;;101;ZZCEN^459;HIHON^463;AKANC^402;METOG^405;VTWHI^436;MTFHA^437;NDFAR^438;SDSIO^442;WYCHY^452;KSWIC^460;DEWIL^500;NYALB^501;NMALQ
 ;;502;LAALE^503;PAALT^504;TXAMA^505;WAAME^506;MIANN^508;GAATL^509;GAAUL^512;MDBAL^513;NYBAT^514;NYBTH^515;MIBAT^516;FLBAY^
 ;;517;WVBEC^518;MABED^519;TXBIG^520;MSBIL^521;ALBIR^522;TXBON^523;MABOS^525;MABRO^526;NYBRX^527;NYBRK^528;NYBUF^529;PABUT^531;IDBOI
 ;;532;NYCAN^533;NYCAS^534;SCCHA^535;ILCHL^537;ILCHW^538;OHCHI^539;OHCIN^540;WVCLA^541;OHCLE^542;PACOA^543;MOCOL^544;SCCOL^
 ;;546;FLMIA^548;FLPAL^549;TXDAL^550;ILDAN^552;OHDAY^553;MIDET^554;CODEN^555;IADEM^556;ILNCH^557;GADUB^558;NCQMI^561;NJEAS^562;PAERI
 ;;564;ARFAY^565;NCFAY^566;MDFHO^567;COFLY^568;SDFME^569;INFWA^570;CAFRE^573;FLGAI^574;NEGRI^575;COGJT^578;ILHIN^579;SDHOT^
 ;;580;TXHOU^581;WVHUN^583;ININD^584;IAIAC^585;MIIRO^586;MSJAC^589;MOKCY^590;VAHAM^671A4;TXKER^592;IAKNO^594;FLLAK^595;PALEB^596;KYLEL
 ;;597;NELIN^598;ARLIT^599;CALIV^600;CALBC^603;KYLOU^604;NJLYO^605;CALOL^607;WIMAD^608;NHMAN^609;ILMAR^610;INMAR^611;TXMAR^
 ;;612;CAMAR^613;WVMAR^614;TNMEM^617;MTMIL^618;MNMIN^619;ALMON^620;NYMON^621;TNMOH^622;TNMUR^623;OKMUS^626;TNNAS^627;CTNEW^629;LANOL
 ;;630;NYNYO^631;MANOR^632;NYNOR^635;OKOKC^636;NEOMA^637;NCASH^640;CAPAM^641;MDPER^642;PAPHI^644;AZPHO^645;PAPHD^646;PAPUN^
 ;;647;MOPOP^648;ORPOR^649;AZPRE^650;RIPRO^652;VARIC^653;ORROS^654;NVREN^655;MISAG^656;MNSCL^657;MOSLJ^658;VASAL^659;NCSAL^660;UTSLC
 ;;662;CASFR^663;WASEA^664;CASDI^665;CASEP^666;WYSHE^667;LASHR^668;WASPO^670;NYSYR^671;TXSAN^672;PRSJU^673;FLTAM^674;TXTEM^
 ;;676;WITOM^677;KSTOP^678;AZTUC^679;ALTUS^680;ALTUK^685;TXWAC^686;KSLEA^687;WAWAL^688;DCWAS^689;CTWHA^691;CALAW^692;ORWHI^693;PAWIB
 ;;695;WIMIL^750;MABOP^752;CALOC^757;OHCOL^520A0;MSGUL^525A4;MAWRX^541A0;OHCBR^596A4;KYLEC^598A0;ARNLI^646A0;PAPAS^
 ;;648A4;WAVAN^657A0;MOSLC^691A0;CALAB^593;NVLAS^
