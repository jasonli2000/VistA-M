YSPOS46 ;DALISC/WTY - CLEANUP SUBSCRIPT IN PACKAGE FILE;10/06/98
 ;;5.01;MENTAL HEALTH;**46**;Dec 30, 1994
 ;
 Q:'$D(^DIC(9.4,"C","YS"))
 N DA,DIE,DR,STR,T,YSDSC,YSIE,YSPFX,YSANS
 S YSANS=+$P(XPDQUES("POSYS46"),U)
 S YSIE="",T=0,(DIE,DIK)="^DIC(9.4,"
 F  S YSIE=$O(^DIC(9.4,"C","YS",YSIE)) Q:YSIE=""  D
 .S STR=^DIC(9.4,YSIE,0),YSDSC=$P(STR,U,3)
 .Q:YSDSC="Version 5.01 of Mental Health"
 .S DA=YSIE
 .I YSANS D ^DIK Q
 .S T=T+1,YSPFX="YS"_T
 .L +^DIC(9.4,YSIE):9999 Q:'$T
 .S DR="1////"_YSPFX
 .D ^DIE
 .L -^DIC(9.4,YSIE)
 Q
