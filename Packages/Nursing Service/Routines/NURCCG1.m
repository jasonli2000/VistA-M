NURCCG1 ;HISC/RM-DRIVER FOR DATA TRANSFER FOR TG ;2/10/92
 ;;3.0;Nursing Clinical;;Jan 24, 1996
 ;;
RESTORE S GMRGDATA=$G(^UTILITY(GMRGREF,$J,GMRGD0,0))
 I GMRGREF["124.25," Q:$P(GMRGDATA,"^",2)=""!($P(GMRGDATA,"^")="")  S DA=$O(^GMRD(124.25,"AA",$P(GMRGDATA,"^",2),$P(GMRGDATA,"^"),0))
 I GMRGREF["124.2," Q:$P(GMRGDATA,"^",5)=""!($P(GMRGDATA,"^",3)="")!($P(GMRGDATA,"^",2)="")!($P(GMRGDATA,"^")="")  S DA=$O(^GMRD(124.2,"AA",$P(GMRGDATA,"^",3),$P(GMRGDATA,"^",2),$P(GMRGDATA,"^"),$P(GMRGDATA,"^",5),0))
 I GMRGREF["124.4," Q:$P(GMRGDATA,"^")=""  S DA=$O(^GMRD(124.4,"B",$P(GMRGDATA,"^"),0))
 S GMRGZNEW=0 I DA'>0 S DA=$P(GMRGZNOD,"^",3)+1,GMRGZNEW=1 F DA=DA:1 Q:'$D(@(GMRGREF_DA_",0)"))
 I  S $P(GMRGZNOD,"^",3,4)=DA_"^"_($P(GMRGZNOD,"^",4)+1)
 S GMRGNOD="^UTILITY("""_GMRGREF_""","_$J_","_GMRGD0_")",GMRGQQ=$E(GMRGNOD,1,$L(GMRGNOD)-1)_"," F  S GMRGNOD=$Q(@GMRGNOD) Q:GMRGNOD=""!(GMRGNOD'[GMRGQQ)  D STOR Q:GMRGNOD=""!(GMRGNOD'[GMRGQQ)
 S ^UTILITY(GMRGREF,$J,GMRGD0,"GMRGIX")=DA
 Q
STOR S GMRGDATA=@GMRGNOD,GMRGX=$P($P(GMRGNOD,"^UTILITY(""",2),""","_$J)_DA_$P($P(GMRGNOD,","_$J_","_GMRGD0,2,9999),")")_")" I GMRGDATA'[("^124.21"),GMRGDATA'[("^124.41") S @GMRGX=@GMRGNOD Q
 S GMRGCNOD=$G(@GMRGX) S:GMRGCNOD="" GMRGCNOD="^"_$S(GMRGREF["124.4,":"124.41PI",1:"124.21PI")_"^^"
 K GMRGCHIL S GMRGY=(GMRGREF["124.4,") F GMRGD1=0:0 S GMRGD1=$O(@(GMRGREF_DA_",1,"_GMRGD1_")")) Q:GMRGD1'>0  S GMRGCHIL=$P($G(@(GMRGREF_DA_",1,"_GMRGD1_","_GMRGY_")")),"^",2-GMRGY,5-GMRGY) I GMRGCHIL'="" S GMRGCHIL(GMRGCHIL)=GMRGD1
 S GMRGX=$P(GMRGX,",0)")_",",GMRGQ=$P(GMRGNOD,"0)")
 F  S GMRGNOD=$Q(@GMRGNOD) Q:GMRGNOD=""!(GMRGNOD'[GMRGQ)  I GMRGNOD?.E1",0)" S GMRGD1=+$P(GMRGNOD,GMRGQ,2,999),GMRGID=$P($G(^UTILITY(GMRGREF,$J,GMRGD0,1,GMRGD1,GMRGY)),"^",2-GMRGY,5-GMRGY) D CHILD
 S @(GMRGX_"0)")=GMRGCNOD
 Q:GMRGNOD=""!(GMRGNOD'[GMRGQQ)
 G STOR
CHILD S GMRGD2=DA N DA S DA(1)=GMRGD2 S DA=$G(GMRGCHIL(GMRGID))
 I DA'>0 S DA=$P(GMRGCNOD,"^",3)+1 F DA=DA:1 Q:'$D(@(GMRGREF_DA(1)_",1,"_DA_")"))
 S GMRGDATA=@GMRGNOD I GMRGREF["124.2," S GMRGZ=$G(@(GMRGREF_DA(1)_",1,"_DA_",0)")) S:GMRGZ'="" $P(GMRGDATA,"^",6)=$P(GMRGZ,"^",6) S:GMRGZ=""&($P(GMRGDATA,"^",6)=0)&'GMRGZNEW $P(GMRGDATA,"^",6)=1
 S @(GMRGREF_DA(1)_",1,"_DA_",0)")=GMRGDATA
 S GMRGZ=0 F  S GMRGZ=$O(^UTILITY(GMRGREF,$J,GMRGD0,1,GMRGD1,GMRGZ)) Q:GMRGZ=""  S GMRGDATA=$G(^UTILITY(GMRGREF,$J,GMRGD0,1,GMRGD1,GMRGZ)) I GMRGDATA'="" S @(GMRGREF_DA(1)_",1,"_DA_","""_GMRGZ_""")")=GMRGDATA
 I '$D(GMRGCHIL(GMRGID)) S $P(GMRGCNOD,"^",3,4)=DA_"^"_($P(GMRGCNOD,"^",4)+1)
 Q
IX S DA=$G(^UTILITY(GMRGREF,$J,GMRGD0,"GMRGIX")),DIK=GMRGREF
 I GMRGREF["124.2," S GMRGDATA=$G(^GMRD(124.2,DA,0)) I $P(GMRGDATA,"^",4) S GMRGCLAS=$G(^UTILITY("^GMRD(124.25,",$J,$P(GMRGDATA,"^",4),"GMRGIX")) I GMRGCLAS S $P(^GMRD(124.2,DA,0),"^",4)=GMRGCLAS
 I GMRGREF["124.4," S GMRGDATA=$G(^GMRD(124.4,DA,2)) I '($P(GMRGDATA,"^",5)=""!($P(GMRGDATA,"^",3)="")!($P(GMRGDATA,"^",2)="")!($P(GMRGDATA,"^")=""))
 I  S GMRGD0=$O(^GMRD(124.2,"AA",$P(GMRGDATA,"^",3),$P(GMRGDATA,"^",2),$P(GMRGDATA,"^"),$P(GMRGDATA,"^",5),0)) I GMRGD0 S $P(^GMRD(124.4,DA,0),"^",2)=GMRGD0
 D IX1^DIK
 Q
RPTCHLD S DA(1)=$G(^UTILITY(GMRGREF,$J,GMRGD0,"GMRGIX"))
 F DA=0:0 S DA=$O(@(GMRGREF_DA(1)_",1,"_DA_")")) Q:DA'>0  S GMRGDATA=$G(^(DA,0)) S:GMRGREF["124.2," GMRGID=$P(GMRGDATA,"^",2,5) S:GMRGREF["124.4," GMRGID=$G(^GMRD(124.4,DA(1),1,DA,1)) D RPT
 Q
RPT Q:$P(GMRGID,"^",4)=""!($P(GMRGID,"^",3)="")!($P(GMRGID,"^",2)="")!($P(GMRGID,"^")="")  S GMRGD2=$O(^GMRD(124.2,"AA",$P(GMRGID,"^",3),$P(GMRGID,"^",2),$P(GMRGID,"^"),$P(GMRGID,"^",4),0)) Q:GMRGD2=$P(GMRGDATA,"^")!(GMRGD2'>0)
 S X=$P(GMRGDATA,"^") F GMRGD1=0:0 S GMRGD1=$O(^DD($P("124.21^124.41","^",GMRGREF["124.4,"+1),.01,1,GMRGD1)) Q:GMRGD1'>0  X:$D(^DD($P("124.21^124.41","^",GMRGREF["124.4,"+1),.01,1,GMRGD1,2)) ^(2)
 S $P(@(GMRGREF_DA(1)_",1,"_DA_",0)"),"^")=GMRGD2,X=GMRGD2,DIK=GMRGREF_DA(1)_",1," D IX1^DIK
 Q
