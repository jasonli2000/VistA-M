ONCPRE23 ;HINES CIOFO/RTK-PRE INSTALL ROUTINE FOR PATCH ONC*2.11*23
 ;;2.11;ONCOLOGY;**23**;Mar 07, 1995
1 ; Delete SEX field (#10) of ONCOLOGY PATIENT file (#160).  It will
 ; be recreated by the install as a SET OF CODES.
 S DIK="^DD(160,",DA=10,DA(1)=160 D ^DIK
 K DIK,DA
2 ;Delete file SEX CODE FOR ONCOLOGY (164.47)
 ;Replaced by SET OF CODES in field SEX (160,10)
 S DIU="^ONCO(164.47,",DIU(0)="D" D EN^DIU2
 K DIU
3 ;Create new entries MURFREESBORO VA MEDICAL CENTER and ST CLOUD VA
 ;MEDICAL CENTER in FACILITY (160.19) file
 ;Change the NAME (160.19,.02) and CITY (160.19,.03) of GEORGE E WEEMS
 ;MEMORIAL HOSPITAL to WEST PALM BEACH VA MEDICAL CENTER and WEST PALM
 ;BEACH
 I '$D(^ONCO(160.19,"B",520930)) D
 .K DD,DO
 .S DIC="^ONCO(160.19,",DIC(0)="L"
 .S DIC("DR")=".02///MURFREESBORO VA MEDICAL CENTER;.03///MURFREESBORO;.04///TN"
 .S X=520930 D FILE^DICN
 I '$D(^ONCO(160.19,"B",611550)) D
 .K DD,DO
 .S DIC="^ONCO(160.19,",DIC(0)="L"
 .S DIC("DR")=".02///ST CLOUD VA MEDICAL CENTER;.03///ST CLOUD;.04///MN"
 .S X=611550 D FILE^DICN
 S DIC="^ONCO(160.19,",X=390003 D ^DIC
 I Y'=-1 S DIE="^ONCO(160.19,",DA=+Y,DR=".02///WEST PALM BEACH VA MEDICAL CENTER;.03///WEST PALM BEACH" D ^DIE
 K DIC,X,Y,DIE,DR,DA Q
