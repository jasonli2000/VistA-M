ORQQPL4 ; ISL/JER - Lexicon Look-up w/Synonyms ;12/19/12  13:53
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**306**;Dec 17, 1997;Build 43
 ;
 ; DBIA 2950   LOOK^LEXA          ^TMP("LEXFND",$J)
 ; DBIA 1609   CONFIG^LEXSET      ^TMP("LEXSCH",$J)
 ; DBIA 1365   DSELECT^GMPLENFM   ^TMP("IB",$J)
 ; DBIA 3991   $$STATCHK^ICDAPIU
 ;
 Q
LEX(LST,X,VIEW,ORDATE,ORINCSYN) ; return list after lexicon lookup
 N LEX,ILST,I,IEN,APP
 S APP="GMPX",LST=$NA(^TMP("ORLEX",$J)) K @LST
 S:'+$G(ORDATE) ORDATE=DT
 S:'$L($G(VIEW)) VIEW="PLS"
 S ORINCSYN=+$G(ORINCSYN)
 I $S(X?.1A2.3N1".".2N:1,X?.1A2.3N1"+":1,1:0) D  Q
 . S @LST@(1)="icd^Searching by code on the Problems Tab supports SNOMED CT, and not ICD-9-CM."
 . S @LST@(2)="Please try a different search"
 D CONFIG^LEXSET(APP,VIEW,ORDATE)
 ; call LOOK^LEXA to execute the search as defined by the call to CONFIG^LEXSET
 D LOOK^LEXA(X,,1,,ORDATE)
 I '$D(LEX("LIST",1)) D  G LEXX
 . S:X?.N @LST@(1)="Code search failed"
 S ILST=0
 S @LST@(1)=$$LEXXFRM(LEX("LIST",1),ORDATE,APP),ILST=1
 D:ORINCSYN SYNONYMS(.LST,.ILST,"SCT",$P(@LST@(1),U,6),ORDATE)
 S (I,IEN)=""
 F  S I=$O(^TMP("LEXFND",$J,I)) Q:I=""  D  ;DBIA 2950
 .F  S IEN=$O(^TMP("LEXFND",$J,I,IEN)) Q:IEN=""  D
 ..N TXT,ELEMENT S TXT=^TMP("LEXFND",$J,I,IEN)
 ..S ELEMENT=IEN_U_TXT
 ..S ELEMENT=$$LEXXFRM(ELEMENT,ORDATE,APP)
 ..S ILST=ILST+1,@LST@(ILST)=ELEMENT
 ..D:ORINCSYN SYNONYMS(.LST,.ILST,"SCT",$P(ELEMENT,U,6),ORDATE)
 I '$D(@LST@(1)) S @LST@(1)="No matches found"
 E  S @LST@(ILST+1)=ILST_$S(ILST=1:" match",1:" matches")_" found"
LEXX K ^TMP("LEXFND",$J),^TMP("LEXHIT",$J),^TMP("LEXSCH",$J),^TMP("LEXLE",$J)
 Q
LEXXFRM(ORX,ORDATE,ORAPP) ; Transform text for SCT look-up
 N ORLEX,ORY,ORICD,ORSCT,ORTXT,ORCODSYS,ORCCODE,ORDCODE
 S ORLEX=$P(ORX,U),ORTXT=$P(ORX,U,2),(ORCCODE,ORCODSYS)=""
 I (ORTXT["("),(ORTXT[")") D
 . S ORCODSYS=$RE($P($P($RE(ORTXT),"("),")",2))
 . S ORCCODE=$RE($P($RE(ORCODSYS)," ")),ORCODSYS=$RE($P($RE(ORCODSYS)," ",2,99))
 . S ORTXT=$$TRIM^XLFSTR($RE($P($RE(ORTXT),"(",2,99)))
 S ORY=$$SETELEM(ORLEX,ORTXT,ORCODSYS,ORCCODE,ORDATE)
 Q ORY
SYNONYMS(LST,ILST,ORCSYS,ORCCODE,ORDT) ; Get synonyms for expression
 N ORSYN,ORI,ORDAD S ORDT=$G(ORDT,DT),ORDAD=ILST
 D GETSYN^LEXTRAN1(ORCSYS,ORCCODE,ORDT,"ORSYN",1)
 S ORI=0 F  S ORI=$O(ORSYN("S",ORI)) Q:+ORI'>0  D
 . N ELEMENT,TXT,IEN,ORDCODE
 . S IEN=$P(ORSYN("S",ORI),U,2),TXT=$P(ORSYN("S",ORI),U)
 . S ELEMENT=$$SETELEM(IEN,TXT,"SNOMED CT",ORCCODE,ORDT)_U_ORDAD
 . S ILST=ILST+1,@LST@(ILST)=ELEMENT
 Q
SETELEM(ORLEX,ORTXT,ORCODSYS,ORCCODE,ORDATE) ; Set List Element
 N ORY,ORDCODE,ORIMPDT,ORICD,ORICDID
 S ORIMPDT=$S($L($T(IMPDATE^LEXU)):$$IMPDATE^LEXU("10D"),1:0)
 S ORDCODE=$$GETDES^LEXTRAN1("SCT",ORTXT,ORDATE)
 S ORDCODE=$S(+ORDCODE=1:$P(ORDCODE,U,2),1:"")
 S ORICD=$$GETDX^ORQQPL1(ORCCODE,ORCODSYS)
 S ORICDID=+$$CODEN^ICDCODE($P(ORICD,"/"),80)
 S ORY=ORLEX_U_ORTXT_U_ORICD_U_ORICDID_U_ORCODSYS_U_ORCCODE_U_ORDCODE
 I (ORCODSYS["SNOMED")!(ORCODSYS["VHAT") D
 .S ORY=ORY_U_$S(ORIMPDT=0:"ICD-9-CM",ORDATE<ORIMPDT:"ICD-9-CM",1:"ICD-10-CM")
 Q ORY
