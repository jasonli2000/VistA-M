PRSDEAL ;HISC/GWB-EARNINGS & LEAVE SCREEN ;9/21/93  10:00
 ;;3.5;PAID;;Jan 26, 1995
 D WAIT^DICD
 S DA="",SSN=$P($G(^VA(200,DUZ,1)),"^",9)
 I SSN'="" S DA=$O(^PRSPC("SSN",SSN,0))
 I 'DA W !!,*7,"Your SSN was not found in both the New Person & Employee File!" G EXIT
 K DASHES S $P(DASHES,"-",80)="-"
 S ZERO=^PRSPC(DA,0)
 S NAME=$P(ZERO,U,1),STATION=$P(ZERO,U,7),TLU=$P(ZERO,U,8)
 S SSN=$P(ZERO,U,9),SSN=$E(SSN,1,3)_"-"_$E(SSN,4,5)_"-"_$E(SSN,6,9)
 S Y=$P(ZERO,U,49) X ^DD(450,458,2.1) S CCORG=Y
 S DS=$P($G(^PRSPC(DA,1)),U,42),LPP=$P($G(^PRSPC(DA,"MISC4")),U,16)
 S CATEGORY="EARNINGS & LEAVE SCREEN",PAGE=0
 K ^UTILITY("DIQ1",$J) S DIC="^PRSPC("
 S DR="2:5;9:11;13;15;16;19:23;26;28;30:32;37:39;42;43;47;52;53;64;89:116.2;139;226;231;458;538"
 S DIQ(0)="IE" D EN^DIQ1
 W @IOF D HDR S PRTC=""
 F FLD=32,43,4,10,47,5,53,39,26,226,231,64,538,139,3,42,30,22,15,37,19,9,16,20,13,38,28,23,458,115.17,89:1:97,97.1,98,98.1,98.2,98.3,98.4,99,99.1,100:1:114,114.1,115.01:.01:115.16,116.01:.01:116.2,21,11,2,31,52 D WRITE Q:PRTC=0
 G:PRTC=0 EXIT
EXIT K ^UTILITY("DIQ1",$J)
 N PRSTLV D KILL^XUSCLEAN W @IOF
 Q
WRITE S NODEDD=^DD(450,FLD,0),NODEUTIL=$G(^UTILITY("DIQ1",$J,450,DA,FLD,"E"))
 I (NODEUTIL="")!(NODEUTIL="NA") K NODEDD,NODEUTIL Q
 S FLDNAM=$P(NODEDD,U,1)
 S INT=^UTILITY("DIQ1",$J,450,DA,FLD,"I")
 S EXT=^UTILITY("DIQ1",$J,450,DA,FLD,"E")
 S IL=$L(INT)
 I $P(NODEDD,U,2)["NJ",+INT=0 K NODEDD,NODEUTIL Q
 I PRTC=1 D HDR S PRTC=""
 S:FLD=15 FLDNAM="NORMAL HOURS" S:FLD=458 INT=$E(INT,1,4)_":"_$E(INT,5,8)
 I $P(NODEDD,U,5)["""$""" S VAL=$FN(INT,",",2) G IOSL
 I FLD>88,FLD<116.3 S VAL=EXT I $D(^PRSP(454,1,"PUC","C",FLDNAM)) S FUIEN=$O(^PRSP(454,1,"PUC","C",FLDNAM,0)) I $P(^PRSP(454,1,"PUC",FUIEN,0),U,3)'="" S VAL=VAL_"  "_$P(^PRSP(454,1,"PUC",FUIEN,0),U,3) G IOSL
 I $P(NODEDD,U,2)["D" S VAL=EXT G IOSL
 I $P(NODEDD,U,2)["NJ" S VAL=$J(INT,IL,2) G IOSL
 S VAL=EXT
IOSL K DOTS S NOD=32-$L(FLDNAM),$P(DOTS,".",NOD)="."
 W !,FLDNAM,DOTS
 D VAL
 I ($Y>(IOSL-4))!(FLD=52) D PRTC
 Q
VAL I $L(VAL)<48 W ?32,VAL Q
 S COLUMN=32,LGTH=0
 F LOOP=1:1 Q:LGTH=$L(VAL)!(LGTH>($L(VAL)))  W:$L($P(VAL," ",LOOP))>(80-COLUMN) ! S:$L($P(VAL," ",LOOP))>(80-COLUMN) COLUMN=32 W ?COLUMN,$P(VAL," ",LOOP) S COLUMN=COLUMN+$L($P(VAL," ",LOOP))+1,LGTH=LGTH+$L($P(VAL," ",LOOP))+1
 Q
HDR W:$Y>0 @IOF S PAGE=PAGE+1
 S CLNGTH=$L(CCORG),TAB=(80-CLNGTH)\2,TAB=TAB-1
 W !,NAME,?TAB,CCORG,?62,"DUTY STATION: ",STATION_DS
 W !,SSN,?71,"T&L: ",TLU,!,DASHES
 S CLNGTH=$L(CATEGORY),TAB=(80-CLNGTH)\2,TAB=TAB-1
 W !,"PAY PERIOD: ",LPP,?TAB,CATEGORY,?73,"PAGE ",PAGE
 W !,DASHES
 K CLNGTH,TAB Q
PRTC W:$Y<22 ! K DIR,DIRUT,DIROUT,DTOUT,DUOUT
 S DIR(0)="E",DIR("A")="Press RETURN to continue" D ^DIR S PRTC=Y
 S:$D(DIRUT) PRTC=0
 Q
EMP ;S DIC="^PRSPC(",DIC(0)="AEMQZ",DIC("A")="Select EMPLOYEE: "
 ;D ^DIC I Y=-1 G EXIT
 ;S DA=+Y,ZERO=Y(0)
 ;S %ZIS="QM" D ^%ZIS G EXIT:POP
 ;I $D(IO("Q")) D  G EXIT
 ;.S ZTRTN="DISPLAY^PRSDSRD",ZTDESC="PRS SERVICE RECORD CARD"
 ;.S ZTSAVE("DA")="",ZTSAVE("ZERO")=""
 ;.D ^%ZTLOAD W:$D(ZTSK) !,"Request Queued!" D HOME^%ZIS K IO("Q") Q
DISPLAY ;U IO
