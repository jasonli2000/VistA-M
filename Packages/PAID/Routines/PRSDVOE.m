PRSDVOE ;HISC/GWB-VERIFICATION OF EMPLOYMENT ;12/09/93  10:00
 ;;3.5;PAID;;Jan 26, 1995
EMP S DIC="^PRSPC(",DIC(0)="AEMQZ",DIC("A")="Select EMPLOYEE: "
 D ^DIC I Y=-1 G EXIT
 S DA=+Y,ZERO=Y(0)
 D WAIT^DICD
 K DASHES S $P(DASHES,"-",80)="-"
 S ZERO=^PRSPC(DA,0)
 S NAME=$P(ZERO,U,1),STATION=$P(ZERO,U,7),TLU=$P(ZERO,U,8)
 S SSN=$P(ZERO,U,9),SSN=$E(SSN,1,3)_"-"_$E(SSN,4,5)_"-"_$E(SSN,6,9)
 S Y=$P(ZERO,U,49) X ^DD(450,458,2.1) S CCORG=Y
 S DS=$P($G(^PRSPC(DA,1)),U,42),LPP=$P($G(^PRSPC(DA,"MISC4")),U,16)
 S CATEGORY="VERIFICATION OF EMPLOYMENT",PAGE=0
 K ^UTILITY("DIQ1",$J) S DIC="^PRSPC("
 S DR="80;32;20;16;13;38;604;28;83;142;255;42;95;19;9;15;450;30;2;154;48;49;50;87;51;27;600;231"
 S DIQ(0)="IE" D EN^DIQ1
 W @IOF D HDR S PRTC=""
 F FLD=80,32,20,16,13,38,604,28,83,142,255,42,95,19,9,15,450,30,2,154,48,49,50,87,51,27,600,231 D WRITE Q:PRTC=0
 G:PRTC=0 EXIT
 W ! G EMP
EXIT K ^UTILITY("DIQ1",$J)
 N PRSTLV D KILL^XUSCLEAN W @IOF
 Q
WRITE S NODEDD=^DD(450,FLD,0),NODEUTIL=$G(^UTILITY("DIQ1",$J,450,DA,FLD,"E"))
 I (NODEUTIL="")!(NODEUTIL="NA") K NODEDD,NODEUTIL Q
 S FLDNAM=$P(NODEDD,U,1)
 S INT=^UTILITY("DIQ1",$J,450,DA,FLD,"I")
 S EXT=^UTILITY("DIQ1",$J,450,DA,FLD,"E")
 S IL=$L(INT)
 I $P(NODEDD,U,2)["NJ",+INT=0 K NODEDD,NODEUTIL Q
 I PRTC=1 D HDR S PRTC=""
 S:FLD=15 FLDNAM="NORMAL HOURS"
 I $P(NODEDD,U,5)["""$""" S VAL=$FN(INT,",",2) G IOSL
 I $P(NODEDD,U,2)["D" S VAL=EXT G IOSL
 I $P(NODEDD,U,2)["NJ" S VAL=$J(INT,IL,2) G IOSL
 S VAL=EXT
IOSL K DOTS S NOD=32-$L(FLDNAM),$P(DOTS,".",NOD)="."
 W !,FLDNAM,DOTS
 D VAL
 I ($Y>(IOSL-4))!(FLD=231) D PRTC
 Q
VAL I $L(VAL)<48 W ?32,VAL Q
 S COLUMN=32,LGTH=0
 F LOOP=1:1 Q:LGTH=$L(VAL)!(LGTH>($L(VAL)))  W:$L($P(VAL," ",LOOP))>(80-COLUMN) ! S:$L($P(VAL," ",LOOP))>(80-COLUMN) COLUMN=32 W ?COLUMN,$P(VAL," ",LOOP) S COLUMN=COLUMN+$L($P(VAL," ",LOOP))+1,LGTH=LGTH+$L($P(VAL," ",LOOP))+1
 Q
HDR W:$Y>0 @IOF S PAGE=PAGE+1
 S CLNGTH=$L(CCORG),TAB=(80-CLNGTH)\2,TAB=TAB-1
 W !,NAME,?TAB,CCORG,?62,"DUTY STATION: ",STATION_DS
 W !,SSN,?71,"T&L: ",TLU,!,DASHES
 S CLNGTH=$L(CATEGORY),TAB=(80-CLNGTH)\2,TAB=TAB-1
 W !,"LAST PP: ",LPP,?TAB,CATEGORY,?73,"PAGE ",PAGE
 W !,DASHES
 K CLNGTH,TAB Q
PRTC W:$Y<22 ! K DIR,DIRUT,DIROUT,DTOUT,DUOUT
 S DIR(0)="E",DIR("A")="Press RETURN to continue" D ^DIR S PRTC=Y
 S:$D(DIRUT) PRTC=0
 Q
