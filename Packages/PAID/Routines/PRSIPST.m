PRSIPST ;HISC/GWB-PAID 4.0 POST-INIT ;8/24/95  09:20
 ;;4.0;PAID;;Sep 21, 1995
 W !,"Executing PAID 4.0 post-init"
 F TLI=0:0 S TLI=$O(^PRST(455.5,TLI)) Q:TLI<1  S TLE=$P($G(^(TLI,0)),"^",1) D UPD^PRSASAL,APP^PRSASAL
 D AL S X=$G(^DD(4.3,0,"VR")) I X'="" S ^DD(200,0,"VR")=X
 S PPI=$P($G(^PRST(458,0)),"^",3)
 I PPI F DFN=0:0 S DFN=$O(^PRST(458,PPI,"E",DFN)) Q:DFN<1  D
 .S X=$P($G(^PRSPC(DFN,1)),"^",7) S:X="" X=0
 .S $P(^PRST(458,PPI,"E",DFN,0),"^",6)=X
 .I $D(^PRST(458,PPI-1,"E",DFN,0)) S:$P(^(0),"^",6)="" $P(^(0),"^",6)=X
 .Q
 D ^PRSIPST1
 K TLI,TLE,PPI,DFN,X
F454I ;Insert "I" into 2nd piece of 0 nodes for 454 sub-files
 S SUB=0 F  S SUB=$O(^PRSP(454,1,SUB)) Q:SUB=""  I $P(^PRSP(454,1,SUB,0),"^",2)'["I" S $P(^PRSP(454,1,SUB,0),"^",2)=$P(^PRSP(454,1,SUB,0),"^",2)_"I"
 K SUB
 ;Set NEW PERSON pointer in PAID EMPLOYEE file
 ;Set PAID EMPLOYEE pointer in NEW PERSON file
 ;Compute ACCESSION/SEPARATION CODE, ACCESSION/SEPARATION EFF DATE and
 ;ACCESSSION/SEPARATION NOA CODE data
 S (CNT,IEN)=0 F  S IEN=$O(^PRSPC(IEN)) S CNT=CNT+1 W:CNT#100=0 "." Q:IEN'>0  D
 .S SSN=$P(^PRSPC(IEN,0),"^",9)
 .I $D(^VA(200,"SSN",SSN)) S VAIEN=$O(^VA(200,"SSN",SSN,0)),$P(^PRSPC(IEN,200),"^",1)=VAIEN,$P(^VA(200,VAIEN,450),"^",1)=IEN
 .I $D(^PRSPC(IEN,1)),$P(^PRSPC(IEN,1),"^",33)="N" D  Q
 ..I $P(^PRSPC(IEN,1),"^",1)'="A" S $P(^PRSPC(IEN,1),"^",1)="A",$P(^PRSPC(IEN,1),"^",2)=$P(^PRSPC(IEN,0),"^",3),$P(^PRSPC(IEN,1),"^",3)="ACC" Q
 ..I $P(^PRSPC(IEN,1),"^",2)="" S $P(^PRSPC(IEN,1),"^",2)=$P(^PRSPC(IEN,0),"^",3)
 ..I $P(^PRSPC(IEN,1),"^",3)="" S $P(^PRSPC(IEN,1),"^",3)="ACC"
 .I $D(^PRSPC(IEN,1)),$P(^PRSPC(IEN,1),"^",33)'="N" D  Q
 ..I $P(^PRSPC(IEN,1),"^",1)'="S" S $P(^PRSPC(IEN,1),"^",1)="S",$P(^PRSPC(IEN,1),"^",3)="SEP" D ASEFDT Q
 ..I $P(^PRSPC(IEN,1),"^",2)="" D ASEFDT
 ..I $P(^PRSPC(IEN,1),"^",3)="" S $P(^PRSPC(IEN,1),"^",3)="SEP"
 .I '$D(^PRSPC(IEN,1)) S $P(^PRSPC(IEN,1),"^",1)="S",$P(^PRSPC(IEN,1),"^",3)="SEP"
 K DA,DIC,DLAYGO,X,D0,CNT,IEN,SSN,VAIEN,PPE,IEN459,X1,X2
CPD ;Compute PAY DATES for PAID PAYRUM DATA entries
 S IEN=0 F  S IEN=$O(^PRST(459,IEN)) Q:IEN'>0  W "." S PPE=$P(^PRST(459,IEN,0),"^",1) D NX^PRSAPPU S X1=D1,X2=23 D C^%DTC S PAYDT=X,$P(^PRST(459,IEN,0),"^",2)=PAYDT,^PRST(459,"AC",PAYDT,IEN)=""
 K IEN,PPE,X1,X2,PAYDT,X
CSB ;Clean out S.PRSD server basket
 I '$D(^XMB(3.7,.5,2)) G PST2
 S MBIEN=0,MBIEN=$O(^XMB(3.7,.5,2,"B","S.PRSD",MBIEN))
 I MBIEN="" G PST2
 S MMIEN=0 F  S MMIEN=$O(^XMB(3.7,.5,2,MBIEN,1,MMIEN)) Q:MMIEN'>0  D
 .S XMSER="S.PRSD",XMZ=MMIEN D REMSBMSG^XMA1C
PST2 K MBIEN,MMIEN,XMSER,XMZ
 D ^PRSIPST2,^PRSIKIL
ISCBUL S XMY("G.PDH@ISC-CHICAGO.VA.GOV")="",XMDUZ=.5
 S XMSUB="PAID 4.0 INSTALLATION BULLETIN"
 S PRS(1)="PAID 4.0 has been installed.",XMTEXT="PRS("
 N DIFROM S:'$D(IOM) IOM=80
 D ^XMD K PRS(1)
 W !!,"Finished",! Q
AL ; Install Alerts protocol into XU menu
 K DIC S DA(1)=$O(^DIC(19,"B","XU USER SIGN-ON",0))
 I DA(1) S DIC="^DIC(19,"_DA(1)_",10,",DIC(0)="L",DLAYGO=19,X="PRSA SUP ALERTS",D0=DA(1) D ^DIC
 Q
ASEFDT ;Compute ACCESSION/SEPARATION EFF DATE
 S PPE="" F  S PPE=$O(^PRST(459,"B",PPE),-1) Q:PPE=""  S IEN459=$O(^PRST(459,"B",PPE,0)) I $D(^PRST(459,IEN459,"P",IEN)) D NX^PRSAPPU S X1=D1,X2=13 D C^%DTC S $P(^PRSPC(IEN,1),"^",2)=X Q
 Q
