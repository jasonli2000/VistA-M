PRSTED ; HISC/REL/FPT - T&A Edits ;8/6/93  08:27
 ;;3.5;PAID;;Jan 26, 1995
PCK R !!,"Select T&L (or ALL): ",X:DTIME G:'$T!("^"[X) KIL I X="ALL" S TLIEN="" G L0
 K DIC S DIC="^PRST(455.5,",DIC(0)="EMQ" D ^DIC K DIC G:Y<1 PCK S TLIEN=+Y
L0 W ! K IOP,%ZIS S %ZIS("A")="Select LIST Printer: ",%ZIS="MQ" D ^%ZIS K %ZIS,IOP G:POP KIL
 I $D(IO("Q")) S ZTDESC="T&A EDITS",ZTRTN="Q1^PRSTED" S ZTSAVE("TLIEN")="" D ^%ZTLOAD W:$D(ZTSK) !!,"Request Queued" G KIL
 U IO D Q1 D ^%ZISC S IOP="" D ^%ZIS G KIL
Q1 ;
 S COUNT=0,PP=$P(^PRST(455,0),"^",3) G:PP<1 KIL D CODES^PRSTUTL W:$E(IOST,1)="C" @IOF S HDR=1
 S YN="" I TLIEN'="" S TL=$P(^PRST(455.5,TLIEN,0),"^") G EMP
 S TL=""
TLOOP S TL=$O(^PRST(455,"ATL",TL)) I TL="" Q
 S TLIEN="",TLIEN=$O(^PRST(455.5,"B",TL,TLIEN))
 D EMP G:YN["^" KIL
 G TLOOP
EMP S NAM=""
EMP1 S NAM=$O(^PRST(455,"ATL",TL,NAM)) Q:NAM=""
 S DFN="",YN=""
EMP2 S DFN=$O(^PRST(455,"ATL",TL,NAM,DFN)) G:DFN<1 EMP1
 I '$D(^PRST(455,PP,1,DFN,0)) G EMP2
 I $P(^PRST(455,PP,1,DFN,0),"^",2)'="P" G EMP2
 S COUNT=COUNT+1 D ^PRSTED1 I COUNT#10=1 W "."
 Q:YN["^"  G EMP2
 Q
ERR ;Edit Check Error print out
 G:'HDR!CNT E1 S Y0=$G(^PRSPC(DFN,0)) K ER
 S SSN=$P(Y0,"^",9) W !!,$E(SSN,1,3),"-",$E(SSN,4,5),"-",$E(SSN,6,9),"  ",$P(Y0,"^",1),?50,$P(Y0,"^",8),!
 S CSTR="" F A=13:1:N1 S CCODE=$P(C0,"^",A) I CCODE'="" S CSTR=CSTR_$P(T0," ",A-12)_CCODE_"  "
 F A=1:1:N2 S CCODE=$P(C1,"^",A) I CCODE'="" S CSTR=CSTR_$P(T1," ",A)_CCODE_"  "
 G:CSTR="" E1 S CSTR=$E(CSTR,1,$L(CSTR)-2)
 I $L(CSTR)<72 W !?5,CSTR,!
 E  S Y=$L(CSTR,"  ")\2 W !?5,$P(CSTR,"  ",1,Y),!?5,$P(CSTR,"  ",Y+1,999),!
E1 Q:$D(ER(ERR))  S CNT=CNT+1,ER(ERR)="" F LL=0:0 S LL=$O(^PRST(455.1,ERR,"E",LL)) Q:LL<1  W !?5,^(LL,0)
 I HDR,$Y>(IOSL-5) R:IOST?1"C".E !!,"Press RETURN to Continue. ",YN:DTIME W @IOF
 Q
KIL K %ZIS,A,C0,C1,CCODE,CNT,COUNT,CSTR,CWK,DFN,DIC,DUT,E,ER,ERR,FLSA,HDR,HMX,I,II,IOP,K,LAB,LL,LVG,MX,N1,N2,NAM,NN,NOR,PAY,POP,PB,PP,PPI,SSN,STA,T0,T1,TL,TLIEN,X,X1,Y,Y0,YN,YR,Z,ZTDESC,ZTQUEUED,ZTRTN,ZTSAVE
 Q
