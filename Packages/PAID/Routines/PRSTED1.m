PRSTED1 ; HISC/REL/FPT - T&A Edits (cont) ;7/15/92  16:02
 ;;3.5;PAID;;Jan 26, 1995
 S C0=$G(^PRST(455,PP,1,DFN,0))
 S C1=$G(^PRST(455,PP,1,DFN,1))
TK ; entry point for time keepers
 S FLSA=$P(^PRSPC(DFN,0),U,12),PB=$P(^PRSPC(DFN,0),U,20)
 S PPI=$G(^PRSPC(DFN,"PREMIUM")),PPI=$P(PPI,U,6)
 S LVG=$P(C0,"^",8),DUT=$P(C0,"^",11),CNT=0,PAY=$P(C0,"^",10),NOR=$P(C0,"^",9)
 I $E(NOR,1)'?1N S NOR=$F("+ABCDEF",$E(NOR,1))+8_$E(NOR,2)
 S CWK=$P($G(^PRSPC(DFN,1)),"^",7),HMX=$S(CWK'="C":720,1:800),(E(1),E(2))=0
 F K=13:1:23,26:1:28,48:1:53 S X=$P(C0,"^",K) I X'="" S LAB=$P(T0," ",K-12) D @LAB
 F K=1:1:5,8:1:10 S X=$P(C1,"^",K) I X'="" S LAB=$P(T1," ",K) D @LAB
 I E(1)>60!(E(2)>60) S ERR=34 D ERR^PRSTED
 G ^PRSTED2
RT ;
RL ;
AN ;
AL I X>HMX S ERR=1 D ERR^PRSTED
 I LVG=0 S ERR=10 D ERR^PRSTED
 Q
SK ;
SL I X>HMX S ERR=2 D ERR^PRSTED
 I LVG=0 S ERR=11 D ERR^PRSTED
 Q
NO ;
NP ;
WD ;
WP I X>HMX S ERR=3 D ERR^PRSTED
 I "45"[LVG,$E(X,3) S ERR=12 D ERR^PRSTED
 I DUT=3 S ERR=13 D ERR^PRSTED
 Q
AU ;
AB I X>HMX S ERR=4 D ERR^PRSTED
 Q
CE ;
CT ;
CU ;
CO I X>HMX S ERR=5 D ERR^PRSTED
 Q:PAY=+PAY&(CWK="C")
 I "ABKMNU"'[PAY S ERR=14 D ERR^PRSTED
 Q
UN ;
US I X>$S(PAY="L"&(DUT=3):500,1:400) S ERR=15 D ERR^PRSTED
 I DUT=2,$P(C1,"^",31)'="" S ERR=16 D ERR^PRSTED
 I DUT=3,$P(C1,"^",31)="" S ERR=17 D ERR^PRSTED
 I PAY="T",DUT=3,NOR="00",X>70!($P(C1,"^",31)>14) S ERR=19 D ERR^PRSTED
 I DUT=1,"ABGKMNRUY0123456789"'[PAY!($P(C1,"^",31))!(X>200) S ERR=20 D ERR^PRSTED
 Q
NA ;
NR Q:"ABGKMNU0123456789"[PAY  S ERR=21 D ERR^PRSTED Q
NB ;
NS Q:"0123456789AGKMU"[PAY  S ERR=22 D ERR^PRSTED Q
SA ;
SE S MX=$S("ABKMN"[PAY:400,1:240) I X>MX S ERR=25 D ERR^PRSTED
 I "ABGKMNU0123456789"'[PAY S ERR=26 D ERR^PRSTED
 S X1=$S("AM"[PAY:"123",1:1) I X1'[DUT S ERR=27 D ERR^PRSTED
 S X1=LAB="SE"+1,E(X1)=E(X1)+$E(X,1,2)+($E(X,3)*.25)
 Q
SB ;
SF I X>240 S ERR=28 D ERR^PRSTED
 I "BGU0123456789"'[PAY S ERR=29 D ERR^PRSTED
 I DUT'=1 S ERR=30 D ERR^PRSTED
 S X1=LAB="SF"+1,E(X1)=E(X1)+$E(X,1,2)+($E(X,3)*.25)
 Q
SC ;
SG I "0123456789GU"'[PAY S ERR=32 D ERR^PRSTED
 I DUT'=1 S ERR=33 D ERR^PRSTED
 I X>240 S ERR=31 D ERR^PRSTED
 S X1=LAB="SG"+1,E(X1)=E(X1)+$E(X,1,2)+($E(X,3)*.25)
 Q
