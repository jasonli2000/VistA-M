PRSTED2 ; HISC/REL/FPT - T&A Edits ;12/6/93  12:57
 ;;3.5;PAID;;Jan 26, 1995
 F Z=1:1:9 S E(Z)=0
 F K=29:1:32,34:1:42,46,47 S X=$P(C0,"^",K) I X'="" S LAB=$P(T0," ",K-12) D @LAB
 F K=11:1:14,16:1:24,28,29 S X=$P(C1,"^",K) I X'="" S LAB=$P(T1," ",K) D @LAB
 I E(1)+E(2)=0 G E1
 I "0123456789GHU"[PAY,E(1)>60!(E(2)>60) S ERR=41 D ERR^PRSTED
E1 I E(3)>20!(E(4)>20) S ERR=55 D ERR^PRSTED
 I E(5)>24!(E(6)>24) S ERR=61 D ERR^PRSTED
 I NOR>80,(E(5)+E(6)) S ERR=168 D ERR^PRSTED
 I E(7)+E(8)=0 G E2
 I DUT=1 S MX=NOR/2 I E(7)>MX!(E(8)>MX) S ERR=80 D ERR^PRSTED
 G:DUT=1 E2 S X1=$P(C0,"^",42)+$P(C0,"^",21),X1=X1\10+(X1#10*.25)
 I E(7)>X1 S ERR=81 D ERR^PRSTED
 S X1=$P(C1,"^",24)+$P(C1,"^",3),X1=X1\10+(X1#10*.25)
 I E(8)>X1 S ERR=81 D ERR^PRSTED
E2 I NOR=112,DUT=1,'$P(C0,"^",42)!('$P(C1,"^",24)) S ERR=67 D ERR^PRSTED
 I E(9),'$P(C1,"^",46),E(9)'=+NOR S ERR=65 D ERR^PRSTED
 I DUT=2,'(NOR="01"&("LMN"[PAY)),$P(C0,"^",42)=""!($P(C1,"^",24)="") S ERR=66 D ERR^PRSTED
 G ^PRSTED3
OA ;
OE I "ABKMN"[PAY,X>600 S ERR=35 D ERR^PRSTED
 I "ABGKMNU0123456789"'[PAY S ERR=36 D ERR^PRSTED
 S X1=LAB="OE"+1,E(X1)=E(X1)+$E(X,1,2)+($E(X,3)*.25)
 Q
OB ;
OF I "ABGU0123456789"'[PAY S ERR=37 D ERR^PRSTED
 I "AB"[PAY,X>60 S ERR=38 D ERR^PRSTED
 S X1=LAB="OF"+1,E(X1)=E(X1)+$E(X,1,2)+($E(X,3)*.25)
 Q
OC ;
OG I "0123456789GU"'[PAY S ERR=39 D ERR^PRSTED
 Q
OK ;
OS I "ABKM"'[PAY S ERR=44 D ERR^PRSTED
 I "AB"[PAY,PPI="" S ERR=45 D ERR^PRSTED
 I FLSA'="E" S ERR=46 D ERR^PRSTED
 Q
OM I X>560 S ERR=48 D ERR^PRSTED
 I ($P(C0,"^",44)'>0),NOR'>80 S ERR=50 D ERR^PRSTED
 I X>$P(C0,"^",44) S ERR=62 D ERR^PRSTED
 Q
OU I X>560 S ERR=49 D ERR^PRSTED
 I ($P(C1,"^",26)'>0),NOR'>80 S ERR=51 D ERR^PRSTED
 I X>$P(C1,"^",26) S ERR=63 D ERR^PRSTED
 Q
RA ;
RE I "ABGKMNU0123456789"'[PAY S ERR=52 D ERR^PRSTED
 S X1=LAB="RE"+3,E(X1)=E(X1)+$E(X,1,2)+($E(X,3)*.25)
 Q
RB ;
RF I "BGU0123456789"'[PAY S ERR=53 D ERR^PRSTED
 S X1=LAB="RF"+3,E(X1)=E(X1)+$E(X,1,2)+($E(X,3)*.25)
 Q
RC ;
RG I "0123456789AGKMNU"'[PAY S ERR=54 D ERR^PRSTED
 I PAY="A",X>200 S ERR=56 D ERR^PRSTED
 S X1=LAB="RG"+3,E(X1)=E(X1)+$E(X,1,2)+($E(X,3)*.25)
 Q
HA ;
HL I "ABGKMNU0123456789"'[PAY S ERR=57 D ERR^PRSTED
 S X1=LAB="HL"+5,E(X1)=E(X1)+$E(X,1,2)+($E(X,3)*.25)
 Q
HB ;
HM I "BGU0123456789"'[PAY S ERR=58 D ERR^PRSTED
 S X1=LAB="HM"+5,E(X1)=E(X1)+$E(X,1,2)+($E(X,3)*.25)
 Q
HC ;
HN I "0123456789GKMU"'[PAY S ERR=59 D ERR^PRSTED
 S X1=LAB="HN"+5,E(X1)=E(X1)+$E(X,1,2)+($E(X,3)*.25)
 Q
HD ;
HO I X>240 S ERR=60 D ERR^PRSTED
 I PAY'="U" S ERR=76 D ERR^PRSTED
 I PB'="P" S ERR=76 D ERR^PRSTED
 Q
PT ;
PH I 'X,'LVG,'(DUT=2&("BLM"[PAY)) S ERR=64 D ERR^PRSTED
 I DUT=1,NOR'>80 S ERR=67 D ERR^PRSTED
 I DUT=3 S ERR=68 D ERR^PRSTED
 S E(9)=E(9)+$E(X,1,2)+($E(X,3)*.25) Q
EA S E(7)=E(7)+$E(X,3,4)+($E(X,5)*.25)
EB I LAB="EB" S E(7)=E(7)+$E(X,3,4)+($E(X,5)*.25)
EC I LAB="EC" S E(8)=E(8)+$E(X,3,4)+($E(X,5)*.25)
ED I LAB="ED" S E(8)=E(8)+$E(X,3,4)+($E(X,5)*.25)
 I "GU1234567"'[PAY S ERR=78 D ERR^PRSTED
 I $E(X,1,2)>50 S ERR=79 D ERR^PRSTED
 Q
