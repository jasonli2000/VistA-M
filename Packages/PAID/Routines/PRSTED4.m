PRSTED4 ; HISC/REL - Edits of Miscellaneous Fields ;12/6/93  13:14
 ;;3.5;PAID;;Jan 26, 1995
 S E(1)=0
 F K=30:1:41,43,44,46 S X=$P(C1,"^",K) I X'="" S LAB=$P(T1," ",K) D @LAB
 I E(1),E(1)>$P(C1,"^",34) S ERR=111 D ERR^PRSTED
 I NOR'>80 G ^PRSTED5
 S X="" F K=1:1:3 S X=X+$P(C0,"^",K+37),X=X+$P(C1,"^",K+19)
 I X S ERR=168 D ERR^PRSTED
 G ^PRSTED5
NL Q:X'>14  S ERR=101 D ERR^PRSTED Q
DW I X>14 S ERR=102 D ERR^PRSTED
 I DUT'=3 S ERR=103 D ERR^PRSTED
 I $P(C0,"^",21)="",$P(C1,"^",3)="" S ERR=18 D ERR^PRSTED
 Q
IN I X=2,"BGU0123456789"'[PAY S ERR=104 D ERR^PRSTED
 I X=3,"0123456789AGKMU"'[PAY S ERR=105 D ERR^PRSTED
 Q
LU I "12345"'[LVG S ERR=106 D ERR^PRSTED
 I '$P(C1,"^",55) S ERR=137 D ERR^PRSTED
 Q
LN I "BGU0123456789"'[PAY S ERR=107 D ERR^PRSTED
 I '$P(C1,"^",34) S ERR=108 D ERR^PRSTED
 S E(1)=E(1)+X Q
LD I "0123456789AGKMU"'[PAY S ERR=109 D ERR^PRSTED
 I '$P(C1,"^",34) S ERR=110 D ERR^PRSTED
 S E(1)=E(1)+X Q
TO I '$P(C1,"^",34) S ERR=112 D ERR^PRSTED
 Q
LA I "355 358 359 363 672 871 899 910"'[$P(C0,"^",4) S ERR=113 D ERR^PRSTED
 I "ABJKUY"'[PAY S ERR=114 D ERR^PRSTED
 Q
ML I DUT=3 S ERR=169 D ERR^PRSTED
 Q:X'>14  S ERR=115 D ERR^PRSTED Q
CA I "45"[LVG,$E(X,4) S ERR=116 D ERR^PRSTED
 I X>$S(NOR="00":130,1:NOR*10) S ERR=117 D ERR^PRSTED
 I $E(PP,4,5)<26 S ERR=118 D ERR^PRSTED
 Q
PC I X>14 S ERR=125 D ERR^PRSTED
 I '$P(C0,"^",43),'$P(C1,"^",25) S ERR=126 D ERR^PRSTED
 I X>7,'$P(C0,"^",43)!('$P(C1,"^",25)) S ERR=127 D ERR^PRSTED
 Q
RR Q
TL Q:$D(^PRST(455.5,"B",X))  S ERR=131 D ERR^PRSTED
 I X'?3N,X'?1"VC"1U S ERR=133 D ERR^PRSTED
 Q
CY I NOR="00",PAY="L",DUT=3,$E(X,3) S ERR=119 D ERR^PRSTED
 I NOR="01","LMNQ"[PAY,DUT=2,$E(X,3) S ERR=119 D ERR^PRSTED
 I NOR<2,X>130 S ERR=120 D ERR^PRSTED
 I NOR>1,X>(NOR*10+$P(C0,"^",21)+$P(C1,"^",3)-$P(C0,"^",16)-$P(C0,"^",51)) S ERR=120 D ERR^PRSTED
 I $E(PP,4,5)<26 S ERR=121 D ERR^PRSTED
 Q
FF I NOR'>80!(DUT'=1)!(X<900)!(X>1440) S ERR=129 D ERR^PRSTED
 I '$P(C0,"^",42)!('$P(C1,"^",24)) S ERR=130 D ERR^PRSTED
 I $E(X,1,3)+($E(X,4)*.25)'=E(9) S ERR=130 D ERR^PRSTED
 Q
