PRSTRN ; HISC/CLS/WAA - Prepare 8B for Transmission ;6/2/93  11:08
 ;;3.5;PAID;;Jan 26, 1995
T0 S PP=$P(^PRST(455,0),"^",3) G:PP<1 KIL
T1 R !!,"Ready to Transmit to Austin: NO// ",%:DTIME G:'$T!(%="^") KIL S:%="" %="N"
 I $P("YES",%,1)'="",$P("NO",%,1)'="" W !,"Enter YES or NO.",*7 G T1
 I $P("NO",%,1)="" K % G KIL
 W !!,"TRANSMITTING to Austin."
 S X="N",%DT="XT" D ^%DT S NOW=+Y K %DT
 S $P(^PRST(455,PP,0),"^",3,5)="^"_DUZ_"^"_NOW
 S PYPR=$E(PP,4,5),(COUNTT,COUNT)=0
 D CODES^PRSTUTL K ^TMP($J) S (EMP,REC,NR)=0 D PROC,MAIL
 S $P(^PRST(455,PP,"S"),"^",5,6)=EMP_"^"_REC
 S $P(^PRST(455,PP,"S"),"^",7)=$P(^PRST(455,PP,"S"),"^",7)+EMP
 S $P(^PRST(455,PP,"S"),"^",8)=$P(^PRST(455,PP,"S"),"^",8)+REC
 G EX
PROC S TL="",FFLG="THPX"
PA1 S TL=$O(^PRST(455,"ATL",TL)) Q:TL=""  S TLIEN=$O(^PRST(455.5,"B",TL,0)) G:$P($G(^PRST(455.5,+TLIEN,0)),"^",9) PA1
 S NN="",(TCNT,SCNT)=0,NFLG=5 W !,"Processing T&L unit: ",TL
PA2 S NN=$O(^PRST(455,"ATL",TL,NN))
 I NN="" S FLG=$S(NFLG=2:"T",NFLG=4:"P",NFLG=5:"X",1:"") S:TLIEN $P(^PRST(455.5,TLIEN,0),"^",3)=FLG W ?30,"Total: ",$J(TCNT,4),?45,"Sent: ",$J(SCNT,4) G PA1
 S DFN=""
PA3 S DFN=$O(^PRST(455,"ATL",TL,NN,DFN)) G:DFN<1 PA2
 I '$D(^PRST(455,PP,1,DFN,0)) G PA3
 S TCNT=TCNT+1,COUNT=COUNT+1,C0=^PRST(455,PP,1,DFN,0),NEW=$F(FFLG,$P(C0,"^",2))
 I NEW=4 D CARD S NEW=5,SCNT=SCNT+1 G PA3
 I NEW=3 S NEW=4
 I NEW<NFLG S NFLG=NEW
 G PA3
CARD S STA=$P(C0,"^",4) Q:STA'?3N
 S SSN=$P(C0,"^",5) Q:SSN'?9N
 S NCODE=$P(C0,"^",6),TLB=$P(C0,"^",7) S:$L(NCODE)'=3 NCODE="   " S:$L(TLB)'=3 TLB="   "
 S LVGP=$P(C0,"^",8) S:LVGP="" LVGP=" "
 S NH=$P(C0,"^",9),NH=$S(NH="":"  ",$L(NH)=1:"0"_NH,1:NH)
 S PYPL=$P(C0,"^",10) S:PYPL="" PYPL=" "
 S DB=$P(C0,"^",11) S:DB="" DB=" "
 S DAYNO=$P(C0,"^",12) I DAYNO'?3N S DAYNO="   "
 S CD=$P(C0,"^",3) I CD'="" S CD=$E("00000"_CD,$L(CD),$L(CD)+5)
 S COUNTT=COUNTT+1 W:COUNTT#10=1 "."
 S HDR=" "_STA_SSN_NCODE_DAYNO_"8B"_TLB_LVGP_NH_PYPL_DB_PYPR,STR="" G:CD="" C0
 F A=13:1:N1 S CODE=$P(C0,"^",A) I CODE'="" S STR=STR_$P(T0," ",A-12)_CODE
 I $D(^PRST(455,PP,1,DFN,1)) S N=^(1) F A=1:1:N2 S CODE=$P(N,"^",A) I CODE'="" S STR=STR_$P(T1," ",A)_CODE
 S STR=STR_"CD"_CD
C0 I $L(STR)<49 S REC=REC+1,NR=NR+1,^TMP($J,NR,0)=HDR_STR_$J("",49-$L(STR)) G C1
 F K=49:-1:1 Q:$E(STR,K,K+1)?2U
 S REC=REC+1,NR=NR+1,^TMP($J,NR,0)=HDR_$E(STR,1,K-1)_$J("",50-K)
 S STR=$E(STR,K,999) G C0
C1 S $P(^PRST(455,PP,1,DFN,0),"^",2)="X",EMP=EMP+1 W:REC#100=1 "."
 I NR>174 D MAIL K ^TMP($J) S NR=0
 Q
MAIL Q:'NR  S XMDUZ=.5
 S XMY("G.TAB@"_^XMB("NETNAME"))=""
 S XMY("XXX@Q-TAB.VA.GOV")=""
 S SN=$P($G(^XMB(1,1,"XUS")),"^",17),SN=$S(+SN>0:$P($G(^DIC(4,SN,99)),"^",1),1:"")
 S XMSUB=^DD("SITE")_" ("_SN_") PAYROLL DATA (PAY PERIOD "_PYPR_")"
 S XMTEXT="^TMP($J," D ^XMD
 S:'$D(^PRST(455,PP,"X",0)) ^PRST(455,PP,"X",0)="^455.015P^^"
 K DIC,DD,DO S DIC="^PRST(455,PP,""X"",",DIC(0)="L",DLAYGO=455,DA(1)=PP,(X,DINUM)=XMZ D FILE^DICN K DIC,DINUM
 D NOW^%DTC S $P(^PRST(455,PP,"X",+Y,0),"^",2)=DUZ_"^"_% Q
EX W !!,REC," Records Queued for Transmission.",!
KIL K %,%H,%I,A,C0,CD,CODE,COUNT,COUNTT,DA,DAYNO,DB,DFN,DIC,DISYS,DLAYGO,EMP,FFLG,FLG,HDR,I,II,K,LVGP,N,N1,N2,NCODE,NEW,NFLG,NH,NN,NOW,NR,PP,PYPL,PYPR,REC,SCNT,SSN,STA,STR,T0,T1,TCNT,TL,TLB,TLIEN,TYP,X,X9,XCNP,XMDUZ,XMSUB,XMTEXT,XMY,XMZ,Y,Z
 K XMLOC,XMMG,SN Q
