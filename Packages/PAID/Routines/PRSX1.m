PRSX1 ; HISC/REL/FPT-MATCH 450 to 200 (INITIAL PASS) ;6/24/93  13:01
 ;;3.1;PAID;;Feb 25, 1994
 ;
 ; BN       = LOOP VALUE FOR B X-REF (#450)
 ; C0       = EMPLOYEE COUNT (#450)
 ; C1       = FILE 200 & 450 MATCH COUNT
 ; CNT      = COUNT OF NAMES THAT MIGHT MATCH 450 ENTRY
 ; DFN200   = INTERNAL NUMBER (#200)
 ; FN450    = INTERNAL NUMBER (#450)
 ; NAME200  = NEW PERSON NAME (#200)
 ; NAME450  = PAID EMPLOYEE NAME (#450)
 ; NODE     = ZERO NODE OF #450 ENTRY
 ; NX       = NAME FROM 200 THAT MIGHT MATCH 450 ENTRY
 ; SSN      = SOCIAL SECURITY NUMBER
 ; YN       = YES/NO FLAG
 ;
 D WAIT^DICD S (C0,C1)=0,(BN,YN)="" W !
 S CPP=$P(^PRST(455,0),"^",3)
 F  S BN=$O(^PRSPC("B",BN)) Q:BN=""!(YN["^")  F FN450=0:0 S FN450=$O(^PRSPC("B",BN,FN450)) Q:FN450<1!(YN["^")  S NODE=$G(^PRSPC(FN450,0)),SSN=$P(NODE,"^",9),NAME450=$P(NODE,"^",1),C0=C0+1 W:C0#100=0 "." I '$D(^PRSPC("A200",FN450)) D TRY Q:YN["^"
KILL K BN,C0,C1,CNT,DA,DIE,DIK,DIR,DIROUT,DIRUT,DFN200,FN450,DR,DTOUT,DUOUT,NAME200,NAME450,NODE,NX,SSN,X,Y,YN,CPP
 Q
TRY ; Try to match
 I SSN="" S DA=FN450,DIK="^PRSPC(" D ^DIK Q
 I $O(^PRSPC(FN450,0))="",'$D(^PRST(455,CPP,1,FN450)) S DA=FN450,DIK="^PRSPC(" D ^DIK Q
 S DFN200=$O(^VA(200,"SSN",SSN,0)) I 'DFN200 G T2
 S NAME200=$P($G(^VA(200,DFN200,0)),"^",1)
 I $TR($P(NAME450,",",1)," '-")=$TR($P(NAME200,",",1)," '-") G F1
 W !!,"SSN match found, but last name does not match."
 W !?5,"Employee: ",?15,$J(FN450,5,0),?25,NAME450,!?7,"VA 200: ",?15,$J(DFN200,5,0),?25,NAME200
T1 K DIR,DIROUT,DIRUT,DTOUT,DUOUT
 S DIR(0)="YO",DIR("A")="Are these the same person",DIR("?")="Answer 'Y' for 'Yes', 'N' for 'No' or press return if unsure." D ^DIR Q:X=""  I $D(DIRUT) S YN="^" Q
 W ! G:Y=1 F1
 S DIE="^VA(200,",DA=DFN200,DR="9///@" D ^DIE Q
T2 ; No match
 S X=NAME450 Q:$L(X)=3  D LOOK I CNT=1 D:DFN200 F0 Q
 S X=$P(NAME450,",",1)_","_$P($P(NAME450,",",2)," ",1) D LOOK I CNT=1 D:DFN200 F0 Q
 Q
F0 Q:$P($G(^VA(200,DFN200,1)),"^",9)'=""  S DIE="^VA(200,",DA=DFN200,DR="9////^S X=SSN" D ^DIE
F1 Q:$D(^PRSPC("A200",FN450))  S C1=C1+1,^PRSPC("A200",FN450)=DFN200,^PRSPC("A450",DFN200)=FN450 Q
LOOK ;
 S CNT=0 I $D(^VA(200,"B",X)) S NX=X
 E  S NX=$O(^VA(200,"B",X)) Q:(NX="")!($P(NX,X,1)'="")
 S CNT=1,DFN200=$O(^VA(200,"B",NX,0)) I $O(^VA(200,"B",NX,DFN200)) S CNT=2 Q
 S NX=$O(^VA(200,"B",NX)) Q:$P(NX,X,1)'=""  S CNT=2 Q
