PRSX2 ; HISC/REL/FPT-List Matches ;2/16/93  10:36
 ;;3.1;PAID;;Feb 25, 1994
 ;
 ; BN       = LOOP VALUE FOR B X-REF (#450)
 ; DFN200   = INTERNAL NUMBER (#200)
 ; DFN450   = INTERNAL NUMBER (#450)
 ; MCNT     = NUMBER OF 450 & 200 MATCHES
 ; NAME200  = NEW PERSON NAME (#200)
 ; NAME450  = PAID EMPLOYEE NAME (#450)
 ; NODE     = ZERO NODE OF #450 ENTRY
 ; NX       = NAME FROM 200 THAT MIGHT MATCH 450 ENTRY
 ; OCC      = OCCUPATION SERIES & TITLE CODE
 ; OFN      = INTERNAL NUMBER IN FILE 454 MULTIPLE FOR OCC VALUE
 ; PAGE     = PAGE NUMBER
 ; SSN      = SOCIAL SECURITY NUMBER
 ; TCNT     = EMPLOYEE COUNT
 ; UCNT     = NUMBER OF 450 & 200 NON-MATCHES
 ; YN       = YES/NO FLAG
 ;
 W !,"Select a printer that allows 132 characters of output per line.",!!
 K %ZIS S %ZIS="MQ" D ^%ZIS I POP D KILL Q
 I $D(IO("Q")) S ZTDESC="200 & 450 MATCHES",ZTRTN="ALL^PRSX2" D ^%ZTLOAD D KILL,^%ZISC Q
 U IO D ALL D ^%ZISC
KILL K %,BN,DFN200,DFN450,MCNT,NAME200,NAME450,NODE,OCC,OFN,PAGE,POP,SSN,TCNT,X,UCNT,Y,YN,ZTDESC,ZTREQ,ZTRTN,ZTSTOP
 Q
ALL ; list all 450 entries
 I $D(ZTQUEUED) S ZTREQ="@"
 S (BN,YN)="",(PAGE,MCNT,TCNT)=0 D HEADER,HDRALL
 F  S BN=$O(^PRSPC("B",BN)) Q:BN=""!(YN["^")  F DFN450=0:0 S DFN450=$O(^PRSPC("B",BN,DFN450)) Q:DFN450<1!(YN["^")  S NODE=$G(^PRSPC(DFN450,0)),SSN=$P(NODE,"^",9),NAME450=$P(NODE,"^",1) D
 .I $Y>(IOSL-4) D:IOST?1"C".E SCROLL D:$D(ZTQUEUED) STOPCHK Q:YN["^"  D HEADER,HDRALL
 .W !,$E(SSN,1,3),"-",$E(SSN,4,5),"-",$E(SSN,6,9),?13,NAME450
 .S TCNT=TCNT+1
 .I $D(^PRSPC("A200",DFN450)) S DFN200=^(DFN450),NAME200=$P($G(^VA(200,DFN200,0)),"^",1),MCNT=MCNT+1 W ?42,NAME200 W:NAME200="" ?75,"??? NO NAME in FILE 200" Q
 .S DFN200=$O(^VA(200,"SSN",SSN,0)) I DFN200 W ?75,"*** SSN Used by ",$P($G(^VA(200,DFN200,0)),"^",1) Q
 .S OCC=$P(NODE,"^",17) I OCC'="" S OFN=$O(^PRSP(454,1,"OCC","B",OCC,0)) I OFN W ?75,$P($G(^PRSP(454,1,"OCC",OFN,0)),"^",2)
 W !!,"NUMBER OF EMPLOYEE ENTRIES: ",TCNT,!," NUMBER OF MATCHED ENTRIES: ",MCNT
 Q
HEADER ;
 W:$Y>0 @IOF
 S PAGE=PAGE+1
 D NOW^%DTC S Y=% D DD^%DT W ?63,Y,!
 Q
HDRALL ; all entries
 W "MATCH LIST FOR FILES 200 & 450",?70,"PAGE: ",PAGE
 W !,"SSN",?13,"NAME (FILE 450)",?42,"NAME (FILE 200)",?74,"REMARK",!
 Q
HDRNO ; unmatched file 450 entries list
 W "FILE 450 ENTRIES WITH NO MATCH",?70,"PAGE: ",PAGE
 W !,"SSN",?13,"NAME (FILE 450)",?42,"REMARK",!
 Q
SCROLL ; screen hold
 K DIR,DIROUT,DIRUT,DTOUT,DUOUT
 W ! S DIR(0)="E" D ^DIR
 S:$D(DIRUT)!(Y=0) YN="^"
 K DIR,DIROUT,DIRUT,DTOUT,DUOUT
 Q
STOPCHK ; check for user request to stop background print job
 I $$S^%ZTLOAD S ZTSTOP=1,YN="^" K ZTREQ W !?10,"*** OUTPUT STOPPED AT USER'S REQUEST ***"
 Q
NOMATCH ; list 450 entries not matched or ssn discrepancies
 K %ZIS S %ZIS="MQ" D ^%ZIS I POP D KILL Q
 I $D(IO("Q")) S ZTDESC="200 & 450 NON-MATCHES",ZTRTN="NO^PRSX2" D ^%ZTLOAD D KILL,^%ZISC Q
 U IO D NO D ^%ZISC
 D KILL Q
NO ; start list
 I $D(ZTQUEUED) S ZTREQ="@"
 S (BN,YN)="",(PAGE,UCNT)=0 D HEADER,HDRNO
 F  S BN=$O(^PRSPC("B",BN)) Q:BN=""!(YN["^")  F DFN450=0:0 S DFN450=$O(^PRSPC("B",BN,DFN450)) Q:DFN450<1!(YN["^")  S NODE=$G(^PRSPC(DFN450,0)),SSN=$P(NODE,"^",9),NAME450=$P(NODE,"^",1) I '$D(^PRSPC("A200",DFN450)) D
 .I $Y>(IOSL-4) D:IOST?1"C".E SCROLL D:$D(ZTQUEUED) STOPCHK Q:YN["^"  D HEADER,HDRNO
 .W !,$E(SSN,1,3),"-",$E(SSN,4,5),"-",$E(SSN,6,9),?13,NAME450
 .S UCNT=UCNT+1
 .S DFN200=$O(^VA(200,"SSN",SSN,0)) I DFN200 W ?42,"*** SSN Used by ",$P($G(^VA(200,DFN200,0)),"^",1) Q
 .S OCC=$P(NODE,"^",17) I OCC'="" S OFN=$O(^PRSP(454,1,"OCC","B",OCC,0)) I OFN W ?42,$P($G(^PRSP(454,1,"OCC",OFN,0)),"^",2)
 W !!,"# OF FILE 450 ENTRIES UNMATCHED: ",UCNT
 Q:YN["^"
NOMATCH1 ; list File 200 entries not matched to File 450
 W @IOF
 S (BN,YN)="",(PAGE,UCNT)=0 D HEADER,HDRNO1
 F  S BN=$O(^VA(200,"B",BN)) Q:BN=""!(YN["^")  F DFN200=0:0 S DFN200=$O(^VA(200,"B",BN,DFN200)) Q:DFN200<1!(YN["^")  I '$D(^PRSPC("A450",DFN200)) D
 .I $Y>(IOSL-4) D:IOST?1"C".E SCROLL D:$D(ZTQUEUED) STOPCHK Q:YN["^"  D HEADER,HDRNO1
 .W !,DFN200,?13,BN
 .S UCNT=UCNT+1,OCC=$P($G(^VA(200,DFN200,201)),"^",1)
 .W:OCC>1 ?42,$P($G(^DIC(19,OCC,0)),"^",2)
 W !!,"# OF FILE 200 ENTRIES UNMATCHED: ",UCNT
 Q
HDRNO1 ; unmatched 200 entries list
 W "FILE 200 ENTRIES WITH NO MATCH",?70,"PAGE: ",PAGE
 W !," #",?13,"NAME",?42,"PRIMARY MENU OPTION",!
 Q
