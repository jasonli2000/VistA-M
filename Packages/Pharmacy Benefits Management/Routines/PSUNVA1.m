PSUNVA1 ;BIR/RMS - NON-VA MEDICATIONS EXTRACT ; 7/28/09 3:28pm
 ;;4.0;PHARMACY BENEFITS MANAGEMENT;**10,14**;MARCH, 2005;Build 1
 ;
 ; Reference to file #nn         supported by DBIA xxx
 ;
EN ;
 N DFN,DRUG50,GENERIC,HOSPLOC,K,LINECNT,LINEMAX,LINETOT,MSGCNT,NODE0
 N NVADOCDT,NVAEDT,NVAIEN,NVASDT,OIDRCL,OIDRUG,ORDERNBR,OREC
 N POSSCL,POSSCT,PTICN,STATION,VACLASS,VACODE,VADM,VAPROD,X
 ;
 D INITZ
 D GETRECS
 D ^PSUNVA2
 Q
 ;
INITZ ;
 ;  ** new all non-namespaced variables **
 ;
 S NVASDT=PSUSDT\1-.000001
 S NVAEDT=PSUEDT\1+.235959
 ;
 S LINEMAX=$$VAL^PSUTL(4.3,1,8.3)
 S:LINEMAX=""!(LINEMAX>10000) LINEMAX=10000
 S LINECNT=999999
 S LINETOT=0
 ;
 S PSUFAC=PSUSNDR
 ;
 ; ** get station number **
 S X=$$VALI^PSUTL(4.3,1,217)
 S STATION=+$$VAL^PSUTL(4,X,99)
 ;
 ; ** get run date **
 S %H=$H
 D YMD^%DTC
 S $P(^TMP("PSUNVA",$J),U,3)=X
 ;
 ;
 Q  ;  ** end of partition initialization **
 ;
GETRECS ;  collect records
 S NVADOCDT=NVASDT
 F  S NVADOCDT=$O(^PS(55,"ADCDT",NVADOCDT)) Q:('+NVADOCDT)!(NVADOCDT>NVAEDT)  D
 . S DFN=0
 . F  S DFN=$O(^PS(55,"ADCDT",NVADOCDT,DFN)) Q:'+DFN  D
 .. S NVAIEN=0
 .. F  S NVAIEN=$O(^PS(55,"ADCDT",NVADOCDT,DFN,NVAIEN)) Q:'+NVAIEN  D
 ... S OREC=""
 ... S $P(OREC,U,1)=""
 ... S $P(OREC,U,2)=STATION
 ... Q:$$TESTPAT^VADPT(DFN)=1
 ... S NODE0=$G(^PS(55,DFN,"NVA",NVAIEN,0))
 ... S $P(OREC,U,3)=$P(NODE0,U,3) ; DOSAGE
 ... S $P(OREC,U,4)=$P(NODE0,U,5) ; SCHEDULE
 ... S $P(OREC,U,5)=$$GET1^DIQ(55.05,NVAIEN_","_DFN_",",5) ; STATUS
 ... S $P(OREC,U,6)=DFN ; PTIEN
 ... D DEM^VADPT S $P(OREC,U,7)=$P(VADM(2),U) D KVA^VADPT ; PTSSN
 ... S PTICN=$$GETICN^MPIF001(DFN)
 ... I $E(PTICN,1,2)="-1" S PTICN=""
 ... S $P(OREC,U,8)=PTICN
 ... S $P(OREC,U,9)=$$GET1^DIQ(50.7,+NODE0,.01) ; ORDITEM
 ... S $P(OREC,U,10)=$P(NODE0,U,4) ; MEDROUTE
 ... S ORDERNBR=$P(NODE0,U,8)
 ... S $P(OREC,U,11)=ORDERNBR
 ... ;Dates in the next three lines are not in external format
 ... S $P(OREC,U,12)=$P(NODE0,U,7) ; DCDATE
 ... S $P(OREC,U,13)=$P(NODE0,U,9) ; STARDT
 ... S $P(OREC,U,14)=$P(NODE0,U,10) ; DOCDATE
 ... S HOSPLOC=+$P($G(^OR(100,ORDERNBR,0)),U,10)
 ... S $P(OREC,U,15)=HOSPLOC
 ... S $P(OREC,U,16)=$$GET1^DIQ(40.7,$P($G(^SC(HOSPLOC,0)),U,7),1) ; STOP
 ... S $P(OREC,U,17)=$$GET1^DIQ(40.7,$P($G(^SC(HOSPLOC,0)),U,7),.01) ;STOPTYPE
 ... S DRUG50=$P(NODE0,U,2)
 ... S (GENERIC,VAPROD)="" I +DRUG50 D
 .... S GENERIC=$$GET1^DIQ(50,DRUG50,.01)
 .... S $P(OREC,U,18)=GENERIC
 .... S VAPROD=$$GET1^DIQ(50.68,$P($G(^PSDRUG(DRUG50,"ND")),U,3),.01)
 .... S $P(OREC,U,19)=VAPROD
 ... S VACODE=""
 ... S VACLASS=""
 ... D
 .... I +DRUG50 S VACODE=$$GET1^DIQ(50,DRUG50,25)
 .... I VACODE'="" S VACLASS=$$GET1^DIQ(50,DRUG50,26) Q  ;->
 .... I +DRUG50 S VACODE=$$GET1^DIQ(50,DRUG50,2)
 .... I VACODE'="" S VACLASS=VACODE_"  "_$$GET1^DIQ(50.605,$O(^PS(50.605,"B",VACODE,0)),1) Q  ;->
 .... N OIDRUG,OIDRCL,POSSCL,POSSCT
 .... S (POSSCT,OIDRUG)=0
 .... F  S OIDRUG=$O(^PSDRUG("ASP",+NODE0,OIDRUG)) Q:'+OIDRUG  D
 ..... S POSSCL=$$GET1^DIQ(50,OIDRUG,25)
 ..... I POSSCL="" S POSSCL=$$GET1^DIQ(50,OIDRUG,2)
 ..... I POSSCL'="" I '$D(POSSCL(POSSCL)) S POSSCL(POSSCL)="",POSSCT=POSSCT+1
 .... I POSSCT=1 S VACODE=$O(POSSCL(""))
 ... S $P(OREC,U,20)=VACODE
 ... S $P(OREC,U,21)=VACLASS
 ... D MAKE1
 ... S:$G(MSGCNT) ^XTMP("PSU_"_PSUJOB,"PSUNVA","MSGTCNT")=MSGCNT
 ... S:LINECNT=999999 LINECNT=1
 ... S:$G(LINECNT) ^XTMP("PSU_"_PSUJOB,"PSUNVA","LINECNT")=LINECNT
 Q
 ;
MAKE1 ;   ** load one record/message **
 ;
 S LINECNT=LINECNT+1
 S LINETOT=LINETOT+1
 I LINECNT>LINEMAX S MSGCNT=$G(MSGCNT)+1,LINECNT=1
 I $L(OREC)<251 S ^XTMP("PSU_"_PSUJOB,"PSUNVA",MSGCNT,LINECNT)=OREC Q
 ;PSUZ*4*1 -- CHANGE LINE LIMIT FROM 254 TO 251
 F K=251:-1:0 Q:$E(OREC,K)="^"
 S ^XTMP("PSU_"_PSUJOB,"PSUNVA",MSGCNT,LINECNT)=$E(OREC,1,K)
 S LINECNT=LINECNT+1
 S LINETOT=LINETOT+1
 S ^XTMP("PSU_"_PSUJOB,"PSUNVA",MSGCNT,LINECNT)="*"_$E(OREC,K+1,K+250)
 Q
PRINT ; NO PRINTING FOR THIS MODULE
 Q
 ;
