PSUOP ;BHM ISC/DEB - Gather Outpatient fill date data ;29SEP93
 ;;1.0; D&PPM ;**3**;11 May 94
 K DIC,CNT,^TMP($J,"OP"),^TMP($J,"CMOP"),PSUY1,PSUY S PSUDT3=$E(PSUDT2,1,5)_"00" G CHECK
PSRX ;Loop through ^PSRX( for data
 S PSDT=$E(PSUDT1,1,5)_"00" F  S PSDT=$O(^PSRX("AL",PSDT)) G DONE:PSDT'>0!($E(PSDT,1,7)>PSUDT2) D SRCH1
 ;
SRCH1 F RXN=0:0 S RXN=$O(^PSRX("AL",PSDT,RXN)) Q:RXN'>0  F PSFILL=-1:0 S PSFILL=$O(^PSRX("AL",PSDT,RXN,PSFILL)) Q:PSFILL=""  D CHK
 Q
 ;
CHK Q:'$D(^PSRX(RXN,0))
 K DATA,PSUCMOP,RXDATE,DATA1
 I PSFILL,'$D(^PSRX(RXN,1,PSFILL,0)) Q
 I $D(^PSRX("AR",PSDT,RXN,PSFILL)) S PSUCMOP=1
 S RX0=^PSRX(RXN,0) I PSFILL S RX1=^PSRX(RXN,1,PSFILL,0),RXDATE=$P(^(0),U,1) I RXDATE<PSUDT1!(RXDATE>PSUDT2) Q
 S DRUG=+$P(RX0,"^",6) Q:'$D(^PSDRUG(DRUG,0))
 K RXDATE S COST=$S(+$P(RX0,"^",17):+$P(RX0,"^",17),$D(^PSDRUG(DRUG,660)):+$P(^(660),"^",6),1:0)
 S:'PSFILL PSUOR=1,PSURF=0,QTY=+$P(RX0,"^",7),PSUWD=$S($P(RX0,"^",11)="W":1,1:0)
 S:PSFILL PSUOR=0,PSURF=1,QTY=+$P(RX1,"^",4)
 S COST=QTY*COST,DATA="^"_PSUOR_"^"_PSURF_"^"_COST_"^"_QTY I $D(PSUCMOP) S DATA=DATA_"^"_(PSUOR+PSURF)_"^"_QTY
 I '$D(^TMP($J,"OP",DRUG)) S ^TMP($J,"OP",DRUG)=DATA Q
 S DATA1=^TMP($J,"OP",DRUG) F X=2:1:7 S $P(^TMP($J,"OP",DRUG),U,X)=$P(DATA,U,X)+$P(DATA1,U,X)
 Q
 ;
DONE ;Done sorting ^PSRX prescription data
 K PSU,PSUOR,PSUY,PSUY1
L1 ;Loop through collected data
 S PSUY1=$S('$D(PSUY1):$O(^TMP($J,"OP","")),1:$O(^TMP($J,"OP",PSUY1))) G SEND:PSUY1'>0 K PSUY
 S (PSUY(1),PSUY2)=$E($P(^PSDRUG(PSUY1,0),U,1),1,40),DATA=^TMP($J,"OP",PSUY1) F X=2:1:5 S PSUY(X)=$P(DATA,U,X)
 S PSUY(6)=(PSUY(2)+PSUY(3)),PSUY(11)=$P(DATA,U,6),PSUY(7)=(PSUY(4)/PSUY(6)),PSUY(12)=$P(DATA,U,7)
 S PSUY(8)=$S($P(^PSDRUG(PSUY1,0),U,9)="":"   ",1:"N/F"),PSUY(9)=$S('$D(^PSDRUG(PSUY1,660)):"   ",1:$S($P(^PSDRUG(PSUY1,660),U,8)="":"   ",1:$P(^PSDRUG(PSUY1,660),U,8)))
 S PSUY(10)=$S('$D(^PSDRUG(PSUY1,"ND")):" ",1:$S($P(^PSDRUG(PSUY1,"ND"),U,6)="":" ",1:$S('$D(^PS(50.605,$P(^PSDRUG(PSUY1,"ND"),U,6))):" ",1:$P(^PS(50.605,$P(^PSDRUG(PSUY1,"ND"),U,6),0),U,1))))
 ;
 S VPN=$S('$D(^PSDRUG(PSUY1,"ND")):0,1:$S($P(^PSDRUG(PSUY1,"ND"),U,2)="":0,1:$P(^PSDRUG(PSUY1,"ND"),U,2))),VPN=$S(VPN=0:" ",1:$E(VPN,1,65))
FIX ;Fix for shipping
 F YY=2,3,6,4,7,11,12 S X=PSUY(YY) D RND S PSUY(YY)=X
 S X=VPN,PSUL=65 D PAD S VPN=$E(X,1,65)
 I $D(PSURPT) S ^TMP($J,"PSUY",PSUY2,0)=PSUY(1)_U_PSUY(8)_U_PSUY(10)_U_PSUY(2)_U_PSUY(3)_U_PSUY(6)_U_PSUY(4)_U_PSUY(7)_U_PSUY(5)_U_PSUY(9)_U_PSUY(11)_U_PSUY(12)
 I $D(PSURPT),$D(PSUY(11)),PSUY(11)'="" S ^TMP($J,"CMOP",PSUY2,0)=^TMP($J,"PSUY",PSUY2,0) G L1
 I $D(PSURPT) G L1
 S PSUY(4)=$J(PSUY(4),12,2),PSUY(7)=$J(PSUY(7),8,2),PSUY(10)=$J(PSUY(10),5)
 S X=PSUY(1),PSUL=40 D PAD S PSUY(1)=$E(X,1,40) S X=PSUY(9),PSUL=10 D PAD S PSUY(9)=$E(X,1,10)
 F XX=2,3,6 S PSUY(XX)=$J(PSUY(XX),8)
 F X=4,5,11,12 S PSUY(X)=$J(PSUY(X),12)
SETUT ;Set utility global
 S ^TMP($J,"PSUY",PSUY2,0)=PSUSNDR_VPN_PSUY(10)_PSUY(1)_PSUY(8)_PSUY(2)_PSUY(3)_PSUY(6)_PSUY(4)_PSUY(7)_PSUY(5)_PSUY(9)_PSUY(11)_PSUY(12)
 I $P(^TMP($J,"OP",PSUY1),U,6)'="" S ^TMP($J,"CMOP",PSUY2,0)=^TMP($J,"PSUY",PSUY2,0)
 G L1
CNT S CNT=$S('$D(CNT):1,1:CNT+1) Q
PAD ;Add spaces
 F YY=$L(X):1:PSUL S X=X_" "
 Q
SEND ;Convert for mailman
SEND1 S X=$O(^TMP($J,"PSUY",0)) Q:X=""  D CNT S ^TMP($J,CNT,0)=^TMP($J,"PSUY",X,0) K ^TMP($J,"PSUY",X) G SEND1
 Q
CHECK ;Check for out patient version
 K X1 Q:'$D(^DIC(9.4,"B","OUTPATIENT PHARMACY"))  S X1=$O(^DIC(9.4,"B","OUTPATIENT PHARMACY",0)) Q:'$D(^DIC(9.4,X1,"VERSION"))  S X2=^DIC(9.4,X1,"VERSION") I $E(X2)<"6" G OLDV
 G PSRX
 ;
OLDV ;Don't have outpatient 6.0
 F PSDT=PSUDT1:0:PSUDT2 S PSDT=$O(^PSRX("AD",PSDT)) G DONE:PSDT'>0!(PSDT>PSUDT2) D SRCH2
 ;
SRCH2 F RXN=0:0 S RXN=$O(^PSRX("AD",PSDT,RXN)) Q:RXN'>0  F PSFILL=-1:0 S PSFILL=$O(^PSRX("AD",PSDT,RXN,PSFILL)) Q:PSFILL=""  D CHK
 Q
 ;
CMOP ;Set CMOP utility
 S ^TMP($J,"CMOP",DRUG)=PSUOR_"^"_PSURF_"^"_COST_"^"_QTY Q
RND Q:X'?.E1".".N  K XX,XXXX,XXX S XX=$P(X,".",2) Q:$L(XX)'>2  S XX=$E(XX,1,2),XXX=$E($P(X,".",2),3),XXXX=$S($P(X,".",1)="":"."_XX,1:$P(X,".",1)_"."_XX) I XXX>4 S X=XXXX+.01 Q
 Q
