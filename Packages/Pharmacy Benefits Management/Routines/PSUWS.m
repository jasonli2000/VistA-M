PSUWS ;BHM ISC/DEB - AR/WS stats gathering routine ;21SEP93
 ;;1.0; D&PPM ;;11 May 94
WS ;Gather Ward Stock Statistics
 K PSU,PSU1,PSU2,^TMP($J),CNT,PSUDT
PSUDT S PSUDT=$S('$D(PSUDT):PSUDT1,1:$O(^PSI(58.5,PSUDT))) Q:PSUDT'>0  Q:PSUDT>PSUDT2  K PSU1,PSU2,PSU3
 I '$D(^PSI(58.5,PSUDT,"S",0)) G PSUDT
WS1 S PSU1=$S('$D(PSU1):$O(^PSI(58.5,PSUDT,"S",0)),1:$O(^PSI(58.5,PSUDT,"S",PSU1))) G PSUDT:PSU1'>0 K PSU2
 I '$D(^PSI(58.5,PSUDT,"S",PSU1,"DRG")) G WS1
 K PSU2
WS2 S PSU2=$S('$D(PSU2):$O(^PSI(58.5,PSUDT,"S",PSU1,"DRG","B",0)),1:$O(^PSI(58.5,PSUDT,"S",PSU1,"DRG","B",PSU2))) G WS1:PSU2'>0
 I '$D(^PSDRUG(PSU2,0)) G WS2
 S PSU3=$O(^PSI(58.5,PSUDT,"S",PSU1,"DRG","B",PSU2,0)) K PSU31
WS3 S PSU31=$S('$D(PSU31):$O(^PSI(58.5,PSUDT,"S",PSU1,"DRG",PSU3,"CAT",0)),1:$O(^PSI(58.5,PSUDT,"S",PSU1,"DRG",PSU3,"CAT",PSU31))) G WS2:PSU31'>0
 S PSU4=$P(^PSI(58.5,PSUDT,"S",PSU1,"DRG",PSU3,"CAT",PSU31,0),U,2)
 I '$D(^PSDRUG(PSU2,"PSG")) S (PSU41,PSU42)=1 G WS4
 S PSU41=$S($P(^PSDRUG(PSU2,"PSG"),U,3)="":1,1:$P(^PSDRUG(PSU2,"PSG"),U,3)),PSU42=(PSU4*PSU41)
WS4 S PSU6=$P(^PSI(58.5,PSUDT,"S",PSU1,"DRG",PSU3,"CAT",PSU31,0),U,1)
 S PSU7=$S('$D(^PSDRUG(PSU2,0)):"Unknown",1:$E($P(^PSDRUG(PSU2,0),U,1),1,40))
 S PSU8=$S('$D(^PSDRUG(PSU2,"PSG")):"N/A",1:$P(^PSDRUG(PSU2,"PSG"),U,2)),PSU9=PSU4*($S('$D(^PSDRUG(PSU2,660)):0,1:$S($P(^PSDRUG(PSU2,660),U,6)="":0,1:($P(^PSDRUG(PSU2,660),U,6)))))
 S PSU5=$S('$D(^PSDRUG(PSU2,"PSG")):0,1:$P(^PSDRUG(PSU2,"PSG"),U,2)),PSU5=$S(PSU5=0:"03 or 04",PSU5=1:"06 or 07",PSU5=2:"17      ",PSU5=3:"22      ",1:"03 or 04")
 ;
 S VPN=$S('$D(^PSDRUG(PSU2,"ND")):0,1:$S($P(^PSDRUG(PSU2,"ND"),U,2)="":0,1:$P(^PSDRUG(PSU2,"ND"),U,2)))
 S VCL=$S('$D(^PSDRUG(PSU2,"ND")):0,1:$S($P(^PSDRUG(PSU2,"ND"),U,6)="":0,1:$S('$D(^PS(50.605,$P(^PSDRUG(PSU2,"ND"),U,6))):0,1:$P(^PS(50.605,$P(^PSDRUG(PSU2,"ND"),U,6),0),U,1))))
 ;
 S VPN=$S(VPN=0:" ",1:$E(VPN,1,65)),VCL=$S(VCL=0:" ",1:VCL)
 ;
DAVE I $E(PSU6)="R",'$D(^TMP($J,"WS",PSU7,PSU2)) S $P(^TMP($J,"WS",PSU7,PSU2),U,3)=PSU42,$P(^TMP($J,"WS",PSU7,PSU2),U,4)=PSU9 D VPNSET G WS3
 I $E(PSU6)="R",$D(^TMP($J,"WS",PSU7,PSU2)) S PSUDATA=^TMP($J,"WS",PSU7,PSU2),$P(^TMP($J,"WS",PSU7,PSU2),U,3)=$P(PSUDATA,U,3)+PSU42,$P(^TMP($J,"WS",PSU7,PSU2),U,4)=$P(PSUDATA,U,4)+PSU9 D VPNSET G WS3
 I '$D(^TMP($J,"WS",PSU7,PSU2)) S ^TMP($J,"WS",PSU7,PSU2)=PSU42_"^"_PSU9 D VPNSET G WS3
 S $P(^TMP($J,"WS",PSU7,PSU2),U,1)=$P(^TMP($J,"WS",PSU7,PSU2),U,1)+PSU42,$P(^TMP($J,"WS",PSU7,PSU2),U,2)=$P(^TMP($J,"WS",PSU7,PSU2),U,2)+PSU9 D VPNSET G WS3
 ;
VPNSET ;Set VA product name and VA class pointer nodes
 S $P(^TMP($J,"WS",PSU7,PSU2),U,5)=VPN,$P(^TMP($J,"WS",PSU7,PSU2),U,6)=VCL Q
