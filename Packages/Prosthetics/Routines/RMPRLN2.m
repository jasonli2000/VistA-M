RMPRLN2 ;PHX/RFM-RETURN ITEM FROM FILE 661 ;10/19/1993
 ;;2.0;PROSTHETICS;;10/19/1993
EN K DIC S DIC=661,DIC(0)="AEQMZ",DIC("A")="Select ITEM: " D ^DIC G:Y<0 EXIT^RMPRLN0 S RMPRITEM=+Y,PRCP("ITEM")=$P(Y,U,2)
EN1 K DIR S DIR(0)="660.1,3",DIR("A")="QTY",DIR("B")=1 D ^DIR G:$$CK EXIT^RMPRLN0 I X="" W !,"Enter `^` to exit" G EN1
 S (RMPRQTY,PRCP("QTY"))=X
 S RMPRCST=0 K DIR S DIR(0)="660.1,4",DIR("A")="UNIT COST",DIR("B")=0 D ^DIR G:$$CK EXIT^RMPRLN0 S (RMPRCOST,RMPRCST)=X
RETU K DIR S DIR(0)="660.1,13",DIR("A")="RETURNED STATUS",DIR("B")="RETURNED" D ^DIR G:$$CK EXIT^RMPRLN0 I X="" W !,"Enter the Returned Status or `^` to exit" G RETU
 S RMPRSTAT=+Y
SER K DIR S DIR(0)="660.1,5",DIR("A")="SERIAL NO." D ^DIR G:$$CK EXIT^RMPRLN0 S RMPRSER=X
 K DIR S DIR(0)="660.1,10",DIR("B")="TODAY" D ^DIR G:$$CK!(Y<0) EXIT^RMPRLN0 S RMPRDRET=+Y
POS D WAIT^DICD H 1 K DD,DO
 S DIC="^RMPR(660.1,",DIC(0)="L",X=DT D FILE^DICN G:Y<0 EXIT^RMPRLN0 S IEN=+Y,^RMPR(660.1,IEN,0)=DT_U_DFN_U_RMPRITEM_U_U_RMPRCST_U_RMPRSER_"^^^"_2_"^^"_RMPRDRET_U_$S($D(RMPRINV):+RMPRINV,1:"")_U_U_RMPRSTAT_U_RMPR("STA"),DIK=DIC,DA=IEN D IX1^DIK
 I $P(^DIC(669.9,RMPRSITE,0),U,3),$D(^PRCP(445,"AE",+PRCP("ITEM"))) G INV
 G EN^RMPRLN1
CK() Q $D(DUOUT)!($D(DTOUT))
INV S %=1 W !!,"Would you like to add this item back into inventory with this quantity" D YN^DICN G:%<0 EXIT^RMPRLN0 G:%=2 EN^RMPRLN1
 I %=0 W !!,"Enter `YES` to add item back into inventory, `NO` to not",! H 2 G INV
 K DIC S DIC="^PRCP(445,",DIC(0)="AEQM",DIC("S")="I $D(^PRCP(445,""AE"",PRCP(""ITEM""),+Y))",DIC("A")="Select INVENTORY POINT: ",PRCPPRIV=1 D ^DIC G:+Y'>0 EXIT^RMPRLN0 S (RMPRINV,PRCP("I"))=+Y
 S PRCP("TYP")="R" D ^PRCPUSA I $D(PRCP("ITEM")) W !,$C(7),$C(7),"Error encountered while trying to post this item to GIP.  Please",!,"post this item manually,",! K RMPRINV
 G EN^RMPRLN1
SETTYP ;SET TYPE OF CONTACT FOR LOAN TRANSACTION
 S RO=0 Q:'$D(^RMPR(660.1,IEN,2,0))  F  S RO=$O(^RMPR(660.1,IEN,2,RO)) Q:RO'>0  D SET21
 Q
SET21 ;SET TYPE OF CONTACT
 S Z=$P(^RMPR(660.1,IEN,2,RO,0),U,2)
 I RO=1 S:Z=1 PER1="X" S:Z=2 REC1="X" S:Z=3 TEL1="X" S:Z=4 LET1="X"
 I RO=2 S:Z=1 PER2="X" S:Z=2 REC2="X" S:Z=3 TEL2="X" S:Z=4 LET2="X"
 I RO=3 S:Z=1 PER3="X" S:Z=2 REC3="X" S:Z=3 TEL3="X" S:Z=4 LET3="X"
 Q
FOLL ;PRINT FOLLOW UP LOAN DATES
 D DIV4^RMPRSIT G:$D(X) EXIT^RMPRLN0
 D HOME^%ZIS S %ZIS="MQ" K IOP D ^%ZIS G:POP EXIT^RMPRLN0 I $D(IO("Q")) K IO("Q") S ZTRTN="PRINTF^RMPRLN2",ZTIO=ION,ZTDESC="PRINT FOLLOWUP LOAN DATES" D ^%ZTLOAD W:$D(ZTSK) !,"REQUEST QUEUED!" H 1 G EXIT^RMPRLN0
PRINTF U IO S RO=0,X1=DT,X2=30 D C^%DTC S RMPRDATE=X F  S RO=$O(^RMPR(660.1,RO)) Q:RO'>0  S RP=0 F I=1:1 S RP=$O(^RMPR(660.1,RO,2,RP)) Q:RP'>0  D DATCK
 I '$D(^TMP($J,"DAFO")) W !!,"There are no loans needing followup action at this time." G EXIT^RMPRLN0
 D W K DIR S DIR(0)="E" W ! D:$E(IOST,1)="C" ^DIR G:$D(DTOUT) EXIT^RMPRLN0 W:$Y>1 @IOF G EXIT^RMPRLN0
DATCK Q:$P(^RMPR(660.1,RO,2,RP,0),U)>RMPRDATE!($P(^(0),U,2)]"")
 S ^TMP($J,"DAFO",RO)=""
 Q
W W:$Y>1 @IOF D HDR S RO=0 F  S RO=$O(^TMP($J,"DAFO",RO)) Q:RO=""  D:$Y>(IOSL-6) HDR W !!?30,$P(^DPT($P(^RMPR(660.1,RO,0),U,2),0),U),!?8,"ITEM",?23,"QTY",?29,"SERIAL NO",?40," DATE OF LOAN",?56,"ASSET EXP",?67,"FOLLOWUP DATE" D X
 Q
X S RP=0 F  S RP=$O(^RMPR(660.1,RO,2,RP)) Q:RP'>0  D Y
 Q
Y Q:$P(^RMPR(660.1,RO,2,RP,0),U,1)>RMPRDATE
 W !,$E($P(^PRC(441,$P(^RMPR(661,$P(^RMPR(660.1,RO,0),U,3),0),U),0),U,2),1,22)
 W ?24,$P(^RMPR(660.1,RO,0),U,4),?29,$P(^(0),U,6) S Y=$P(^(0),U,10) X ^DD("DD") W ?42,Y S Y=$P(^RMPR(660.1,RO,0),U,8) X ^DD("DD") W ?55,Y S Y=$P(^RMPR(660.1,RO,2,RP,0),U) X ^DD("DD") W ?68,Y
 Q
HDR I $Y>(IOSL-6),$E(IOST,1)="C" K DIR S DIR(0)="E" D ^DIR G:$D(DTOUT) EXIT^RMPRLN0
 W:$Y>(IOSL-6) @IOF W !?18,"PROSTHETICS LOAN PROGRAM FOLLOW-UP REPORT"
 Q
EDIT ;EDIT LOAN TRANSACTION
 K RMPRDATE,DIC,X D DIV4^RMPRSIT G:$D(X) EXIT^RMPRLN0
 S DIC="^RMPR(660.1,",DIC(0)="AEMQ",X1=DT,X2=30 D C^%DTC S RMPRDATE=X
 S DIC("S")="I $P(^(0),U,9)=1,'+$P(^(0),U,11),$D(^(2)) S RO=0 F  S RO=$O(^RMPR(660.1,Y,2,RO)) Q:RO'>0  I $P(^RMPR(660.1,Y,2,RO,0),U)<RMPRDATE,$P(^RMPR(660.1,Y,2,RO,0),U,2)="""" Q"
 S DIC("W")="W ?15,$P(^DPT($P(^RMPR(660.1,Y,0),U,2),0),U)",DIC("A")="Select PATIENT: ",RMPREDT9=1
 D ^DIC G:Y<0 EXIT^RMPRLN0 S IEN=+Y,DFN=$P(^RMPR(660.1,IEN,0),U,2) D EN^RMPRLN3
 W ! S RO=0 F I=1:1:3 D EDIT1
REA R !!,"Select Date for Follow-up Action: ",RMPRDT:DTIME G:'$T EXIT^RMPRLN0 G:RMPRDT="" EDIT I RMPRDT>3 W $C(7) G REA
 L +^RMPR(660.1,IEN,0):3 I '$T W !!,$C(7),?5,"Someone else is Editing this entry" G EXIT^RMPRLN0
 S DIE="^RMPR(660.1,"_IEN_","_2_",",DA(1)=IEN,DA=+RMPRDT,DR=1 D ^DIE L -^RMPR(660.1,IEN,0)
 G EXIT^RMPRLN0
EDIT1 S RO=$O(^RMPR(660.1,IEN,2,RO)) Q:RO'>0  I $P(^RMPR(660.1,IEN,2,RO,0),U)<RMPRDATE S Y=$P(^RMPR(660.1,IEN,2,RO,0),U) X ^DD("DD") W !?20,I_".",?25,Y
 Q
