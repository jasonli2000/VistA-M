RMPSCON ;PHX/JLT-CHECK PHANTOM 1358 ;2/3/94  13:25
 ;;2.0;PROSTHETICS;**4**;10/19/1993
CHK I '$D(DUZ) W $C(7),!!,"**YOUR DUZ IS NOT DEFINED*" Q
 W $C(7) S $P(ZL,"*",80)="" W !,ZL
 W !!,?5,*7,"BE SURE TO SEND YOUR OUTPUT TO A PRINTER"
 W !!,?5,*7,"IF YOU RECEIVE ANY IFCAP ERROR MESSAGES BE SURE TO TAKE THIS"
 W !,?5,*7,"PRINTOUT TO THE RESPONSIBLE PROSTHETICS REPRESENTATIVE",!!,ZL,!!
DEV S %ZIS="MQ",%ZIS("B")="" D ^%ZIS G:POP EXIT I $E(IOST,1,2)["C-" W !!,$C(7),"CANNOT SEND OUTPUT TO YOUR TERMINAL" G DEV
 I $D(IO("Q")) S ZTIO=ION,ZTRTN="STR^RMPSCON" S ZTDESC="Prosthetic 1358 Daily Record Conversion" D ^%ZTLOAD G EXIT
STR U IO S ZDT=2931000 D NOW^%DTC S Y=% X ^DD("DD") W !!,"STARTING TIME: ",Y
 F ZT=0:0 S ZDT=$O(^RMPR(664,"B",ZDT)) Q:ZDT'>0  F RMPRA=0:0 S RMPRA=$O(^RMPR(664,"B",ZDT,RMPRA)) Q:RMPRA'>0  I $D(^RMPR(664,RMPRA,0)),'$P(^(0),U,5),'$P(^(0),U,8) D
 .S ZREF=$P(^RMPR(664,RMPRA,0),U,7),RMPRUSR=$S($P(^(0),U,9):$P(^(0),U,9),1:DUZ),DIC=424,DIC(0)="MZ",X=ZREF,DFN=$P(^(0),U,2) D ^DIC
 .I +Y>0 I $P(Y(0),U,9)="",($P(Y(0),U,5)="") S RMPREF=+Y W !!,"Converting Authorization #: ",ZREF D RCAL
 D NOW^%DTC S Y=% X ^DD("DD") W !!,"ENDING TIME: ",Y I '$D(ZCON) W !!,"NO TRANSACTIONS WERE CONVERTED"
EXIT D KILL^XUSCLEAN,^%ZISC Q
RCAL D DEM^VADPT S (R1,RMPRCT,RMPRQT,RMPRTO,RMPRI)=0,RMPRR="",RMPRSH=$S($P(^RMPR(664,RMPRA,0),U,10):$P(^(0),U,10),1:""),RMPROB=$P(^RMPR(664,RMPRA,0),U,3)
 F  S R1=$O(^RMPR(664,RMPRA,1,R1)) Q:R1'>0  S RMPRCT=$P(^RMPR(664,RMPRA,1,R1,0),U,3),RMPRQT=$P(^(0),U,4),RMPRR=$S($P(^(0),U,8)'="":RMPRR_" "_$P(^(0),U,8),1:""),RMPRTO=RMPRTO+(RMPRCT*RMPRQT)
POST S RMPRTO=$S($D(^RMPR(664,RMPRA,2)):RMPRTO-(RMPRTO*$P(^(2),U,6)/100),1:RMPRTO)
 S PRCSX=RMPROB_U_$P(^RMPR(664,RMPRA,0),U)_U_$J(RMPRTO+RMPRSH,0,2)_U_U_$E($P(VADM(1),",",1),1,6)_","_$E($P(VADM(2),U),6,9)_U_$E(RMPRR,1,60)
 S PRCS("TYPE")="FB"
 S PRCSON=$O(^PRC(442,"B",$P(PRCSX,U),0)) I PRCSON'>0 W !,?5,"*OBLIGATION NUMBER NOT FOUND" Q
 S PRCSCPAN=$P(^PRC(442,PRCSON,0),U,12) I 'PRCSCPAN W !,?5,"*CPA NUMBER INVALID" Q
 I '$D(^PRC(424,"AD",$P(PRCSX,U))) W !,?5,"*OBLIGATION HAS NOT BEEN ESTABLISHED" Q
 I $D(^PRC(442,PRCSON,7)) S PRCSS=$P(^(7),U) I $O(^PRCD(442.3,"AC",+PRCSS,0))=40 W !,?5,"*TRANSACTION IS COMPLETE" Q
 I '$D(^PRC(442,PRCSON,8)) W !,?5,"*NO SERVICE BALANCE ESTABLISHED" Q
 I $P(PRCSX,U,3)>(+^PRC(442,PRCSON,8)-$P(^PRC(442,PRCSON,8),U,3)) W !,?5,"*INSUFFICIENT REFERENCE BALANCE TO POST COMMITTED AMOUNT",!,?5,"*YOU WILL NEED TO INCREASE THE INITIAL OBLIGATION AND RE-RUN",!,?5,"*THIS CONVERSION" Q
 I $P(PRCSX,U,4)>$P(^PRC(442,PRCSON,8),U) W !,?5,"*INSUFFICIENT SERVICE BALANCE TO POST ACTUAL AMOUNT" Q
 S %DT="TX",X=$P(PRCSX,U,2) D ^%DT S:Y>0 $P(PRCSX,U,2)=Y I Y=-1 W !,?5,"*INVALID DATE/TIME" Q
 I $P(PRCSX,U,3)=""&($P(PRCSX,U,4)="") W !,?5,"*NEED AT LEAST AN ESTIMATE OR AN ACTUAL AMOUNT" Q
 I $P(PRCSX,U,5)="" W !,?5,"*YOU NEED THE REFERENCE FOR THIS OBLIGATION" Q
 L +^PRC(424,RMPREF,0):3 I $T=0 W $C(7),?5,!,"Someone else is Editing this entry!" Q
 S (PRCSDA,DA)=RMPREF,DIE="^PRC(424,",DR=".12///^S X=$S($P(PRCSX,U,3)>0:$P(PRCSX,U,3),1:$P(PRCSX,U,4));.1///^S X=$P(PRCSX,U,5);1.1///^S X=$P(PRCSX,U,6);.03///^S X=""AU"";"
 S DR(1,424,1)=".05///^S X=$S($P(PRCSX,U,3)>0:$P(PRCSX,U,3),1:$P(PRCSX,U,4));.13///^S X=$S($P(PRCSX,U,3)>0:$P(PRCSX,U,3),1:$P(PRCSX,U,4));.07////^S X=$P(PRCSX,U,2);.08////^S X=RMPRUSR;4////^S X=$P(PRCSX,U,7)" D ^DIE
 L -^PRC(424,RMPREF,0)
 S X=^PRC(424,DA,0),AUDA=DA,AUDA0=$P(X,U,1),BAL1=$P(X,U,12),TIME=$P(PRCSX,U,2),PODA=$P(X,U,2),X=$P(X,U,1)_"-0" W !,"Converting Detail Record #: ",X
 S DA=$O(^PRC(424.1,"B",X,0)) I +DA L +^PRC(424.1,DA,0):3 I $T=0 W $C(7),?5,!,"Someone else is Editing the Authorization Detail" Q
 I 'DA W !,$C(7),"Authorization Detail does not exist" Q
 S DIE="^PRC(424.1,",DR=".03///^S X=BAL1;.04////^S X=TIME;.05////^S X=RMPRUSR;08////^S X=""ORIGINAL AMOUNT"";.1///^S X=RMPRUSR" D ^DIE L -^PRC(424.1,DA,0)
 D BALUP^PRCH58(PODA,BAL1) S ZCON=1
 Q
