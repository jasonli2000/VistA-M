DGMT4 ;ALB/JDS - MEANS TEST BILLING ;01 JAN 86
 ;;MAS VERSION 5.1;
EN D LO^DGUTL W !!,"Means Test Reporting",!,"Hit return to get results from the completion of first Means Test",!
 S I=$N(^DD("FUNC","B","DGDAYSINMONTH",0)),DGDIM=^DD("FUNC",I,1)
 S DGAS=0,%DT("A")="START WITH DATE: ",%DT(0)="-DT",%DT="EAQ" D ^%DT S:Y>0 DGAS=$E(Y,1,5)_"01" G EN:X="?",Q:X="^"
 S Y=DGAS X ^DD("DD") S DHD="MEANS TEST REPORTING"_$S(DGAS:" STARTING "_Y,1:"")
 Q
EN1 ;
 D EN G Q:'$D(DGAS) S DIS(0)="S DGERR=0,I=+$E(DGAS,1,5),I=$S('I:0,1:I*100-.01) D DIS^DGMT4 I DGERR"
 S BY="@3,#.01",FR=",?",TO=FR,FLDS="[DGMTBILL]",DIC="^DG(41.3,",L=0 D EN1^DIP
Q K %DT,X1,DIC,DIS,A,A1,B,C,D,D1,D2,D3,DGAS,DGDH,DGDIM,DGE,DGED,DGFB,DGFBC,DGHEAD,DGINP,DGINPP,DGMAX3,DGMAXI,DGMAXN,DGMAXQ,DGMT,DGO,DGOUT,DGPD,DGT,E,I,I1,I2,J,K,N,S,X,DGERR,DGFBI,DGMT1,DFN,DGI,DGSD,DG1,DIV,Z1,^UTILITY(U,$J) Q
 Q
DIS F I=I:0 S I=$N(^DG(41.3,D0,4,I)) Q:I'>0!(DGERR)  S J=^(I,0) F K=8:1:10 I $L($P(J,U,K)) S DGERR=1 Q
 Q
EN2 D EN G Q:'$D(DGAS) K ^UTILITY(U,$J),DIC S (DIC,DI)="^DG(41.3,",DIC(0)="AMEQ",DIC("S")="I $P(^(0),U,3)&(DG1'[(U_(+Y)_U))",DG1=U
 S FLDS="[DGMTBILL]" G INQ2
START K A S DGT=0,DGED=DT,DGSD=0 F I=0:0 S I=$N(^DG(41.3,DFN,2,I)) Q:I'>0  S J=^(I,0),K=(U_$P(J,U,2)_U) S:"^A^B^N^"[K!($P(J,U,11)=0) DGED=+J,DGSD=0 S:"^C^P^"[K&($P(J,U,11)'=0) DGSD=$P(J,U,7) S:DGSD A(DGED)=DGSD
 I '$D(A) S $P(^DG(41.3,DFN,0),U,3)="",X=1,DA=D0 F I=0:0 S I=$N(^DD(41.3,3,1,I)) Q:I'>0  X ^(I,2)
 Q:'$D(A)  K B S I=$N(A(0)),Y=A(I)\1 W !!,"Category C as of " X ^DD("DD") W Y D HEAD^DGMT6
 S DGAS1=DGAS I DGAS S X1=DGAS,X2=-15 D C^%DTC I '$P($S($D(^DG(41.3,DFN,4,$E(X,1,5)_"00",0)):^(0),1:0),U,14) S DGAS1=0
 F I=0:0 S I=$N(A(I)) Q:I'>0  S:I>DGAS1!('DGAS1) B($S(A(I)>DGAS1:A(I),1:DGAS1))=I
 K A,DGAS1 F DGSD=0:0 S DGSD=$N(B(DGSD)) Q:DGSD'>0  S DGE=B(DGSD) D ST^DGMT6
 W !?37,"TOTAL: ",$J($J(DGT,6,2),7) Q
INQ1 S DPP=1,DPP(1,"IX")="^UTILITY(U,$J,"_DIC_"^2",DPP(1)=L_"^^^@#",Q="""",C=",",L=0 D N^DIP1 K ^UTILITY(U,$J) G Q
INQ2 K ^UTILITY(U,$J) S DI=DIC,DIC(0)="AEMQ",DG1=U,DIC("S")=$S($D(DIC("S")):DIC("S"),1:"I DG1'[(U_+Y_U)"),L=+$P(@(DIC_"0)"),U,2)
 F DGZZ=1:1 D ^DIC Q:Y'>0  S ^UTILITY(U,$J,DGZZ,+Y)="",DG1=DG1_+Y_U,DIC("A")="ANOTHER ONE: " Q:$L(DG1)>230
 K DGZZ I '$D(^UTILITY(U,$J))!(X=U) K DIC,^($J),DG1 G Q
 G INQ1
