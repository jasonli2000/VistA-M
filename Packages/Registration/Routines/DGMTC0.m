DGMTC0 ;ALBISC/TET - MEANS TEST CONVERSION RESTART ;3/16/92  12:32
 ;;5.2;REGISTRATION;;JUL 29,1992
EN ;Entry point
 S DFN=+$P(DGCK,U),DFN=$O(^DG(41.3,DFN)),DGCK=1 G RES:'$D(^DPT(DFN))&(DFN) I 'DFN D NOW^%DTC,YX^%DTC S $P(^DG(43,1,"MTC"),U,3)=DT_% D DEL^DGMTCQ G EXIT
 D NOW^%DTC,YX^%DTC W !!,">>> Correcting bad entry ",DFN," (",Y,")",!
 D ^IBYMTD,^IBYMTC
 S DGDPTZ=$G(^DPT(DFN,0)),DGEB=$P(DGDPTZ,U,3),DGED=$P($G(^DPT(DFN,.35)),U),DGED=$P(DGED,".") S:'DGEB DGEB=2860701
GET ;Get local variables
 D MT^DGMTC1 S:$D(DGINC) DGDFNV=DFN_";DPT(",DGLCT=$S('DGED:1,1:2),DGA=$S('DGED:1,1:0)
CK ;ck what was processed
 S DGER=0
ICK ;Ck income files, kill off array if filed, d inc^dgmtc2
 I '$D(DGINC)&('$D(DGMT)) G RES
 G:'$D(DGINC) MTCK
I12 S $P(DGER,U,2)=12,DGZ=^DGPR(408.12,0),DGN=$P(DGZ,U,3),DGNZ=$G(^(DGN,0)),DGDA12=+$O(^DGPR(408.12,DGN))
 I DGDA12 S DA=DGDA12,DIK="^DGPR(408.12," D ^DIK K DA,DGDA12,DIK S ^DGPR(408.12,0)=DGZ,$P(DGER,U)=1 K DGDA12,DGN,DGNZ,DGZ G ER
 S DGDA12=DGN I $P(DGNZ,U)'=DFN S $P(DGER,U)=1 K DA,DGDA12,DGN,DGNZ G ER
 ;
I2 S DGZ=^DGMT(408.21,0),DGN=+$P(DGZ,U,3),DGDA1=+$O(^(DGN)),DGNZ=$G(^DGMT(408.21,DGN,0))
 S DGZ2=^DGMT(408.22,0),DGN2=+$P(DGZ2,U,3),DGDA2=+$O(^(DGN2)),DGNZ2=$G(^DGMT(408.22,DGN2,0))
 S DGYR=0 F  S DGYR=$O(DGINC(DGYR)) Q:DGYR'>0!(DGER)  S $P(DGER,U,2)=21 D
 .I DGDA1 S DA=DGDA1,DGDAZ=$G(^DGMT(408.21,DA,0)),DIK="^DGMT(408.21," D  Q
 ..I $P(DGDAZ,U)'=DGYR K DGINC(DGYR) Q
 ..D ^DIK K DA,DGDAZ,DIK
 ..S ^DGMT(408.21,0)=DGZ,$P(DGER,U)=1
 .I $P(DGNZ,U,2)'=DGDA12 S $P(DGER,U)=1 Q
 .I DGYR<$P(DGNZ,U) K DGINC(DGYR) Q
 .I DGYR=$P(DGNZ,U) S DGDA21=DGN D
 ..S $P(DGER,U,2)=22
 ..I 'DGDA2,$P(DGNZ2,U,2)'=DGDA21 S $P(DGER,U)=1 Q
 ..I DGDA2 S DA=DGDA2,DGDAZ=$G(^DGMT(408.22,DA,0)),DIK="^DGMT(408.22," D ^DIK K DA,DGDAZ,DIK S ^DGMT(408.22,0)=DGZ2,$P(DGER,U)=1 Q
 ..S DGDA22=DGN2 I $P(DGNZ2,U)'=DFN!($P(DGNZ2,U,2)'=DGDA21) S $P(DGER,U)=1 Q
 .I DGER S DA=DGDA21,DIK="^DGMT(408.21," D ^DIK K DA,DIK Q
 .I DGYR>$P(DGNZ,U) S $P(DGER,U)=1 Q
 .K DGINC(DGYR)
 K DGN,DGN2,DGNZ,DGNZ2,DA,DGDA1,DGDA2,DGDA21,DGDA22,DGZ,DGZ2 I DGER G ER
MTCK ;Ck mt files, kill array if filed, d mt^dgmtc2
 S DGMTD=0,$P(DGER,U,2)=31,DGZ=^DGMT(408.31,0),DGN=+$P(DGZ,U,3),DGNZ=$G(^DGMT(408.31,DGN,0)),DGDA31=+$O(^DGMT(408.31,DGN))
 F  S DGMTD=$O(DGMT(DGMTD)) Q:DGMTD'>0!(DGER)  S DGMTCT=DGMTCT+1,DGMTZ=DGMT(DGMTD) S:DGMTCT=1 DGCAT=$P(DGMTZ,U,2) D
 .I DGDA31 S DA=DGDA31,DGDAZ=$G(^DGMT(408.31,DA,0)),DIK="^DGMT(408.31," D  Q
 ..I $P(DGMTZ,U)'=$P(DGDAZ,U) K DGMT(DGMTD) Q
 ..D ^DIK K DA,DGDAZ,DGDA31,DIK
 ..S ^DGMT(408.31,0)=DGZ,$P(DGER,U)=1
 .I $P(DGNZ,U,2)'=DFN S $P(DGER,U,1)=1 Q
 .I $P(DGMTZ,U)'<$P(DGNZ,U) D:DGMTCT=1 PCS^DGMTC2 K DGMT(DGMTD) Q
 .I $P(DGMTZ,U)<$P(DGNZ,U) S $P(DGER,U,1)=1 Q
 K DGDA31,DGCAT,DGMTCT,DGMTD,DGMTZ,DGN,DGNZ,DGZ
ER I DGER S (DGICT,DGMTCT)=0 D MT^DGMTC2:$P(DGER,U,2)=31,INC^DGMTC2:$P(DGER,U,2)=12 I $P(DGER,U,2)=21!($P(DGER,U,2)=22) S DGICT=DGICT+1 D I2^DGMTC2
RES ;Resume conversion
 K DGDPTZ,DGEB,DGED,DGINC,DGML,DGMS,DGMT,DGER,DGA,DGDFNV,DGLCT,^DG(41.3,DFN)
 D NOW^%DTC,YX^%DTC
 S DGCK=0,$P(^DG(43,1,"MTC"),U)=DFN W !,">>> Resuming conversion (",Y,")",!
 G PT^DGMTC1
EXIT ;Kill & quit
 K DA,DFN,DG,DGA,DGBEG,DGCAT,DGCK,DGCNVR,DGCON,DGCT,DGDAZ,DGDA12,DGDA21,DGDA22,DGDA31,DGDEP,DGDFN,DGDFNV,DGDPTZ,DGEB,DGED,DGEND,DGER,DGF,DGFIRST,DGI,DGIBEG,DGICT,DGIEND,DGINC,DGINCZ,DGITOT
 K DGL,DGLCT,DGLN,DGM,DGML,DGMS,DGMT,DGMTCT,DGMTD,DGMTOT,DGMTZ,DGN,DGN2,DGNN,DGNN2,DGNW,DGNZ,DGNZ2,DGOYR,DGP,DGPAT,DGPATCT,DGPATZ,DGS,DGT,DGV,DGWPZ,DGX,DGXI,DGXYR,DGX2,DGX3,DGXM,DGY,DGYR,DGZ,DGZ2,DIK,X,Y,ZTSK,%,%H,%I
 K DIR,DIRUT,DGMTQ,DUOUT,DTOUT,XMSUB,XMTEXT,XMY,XMDUZ,ZTIO,ZTRTN,ZTDESC D KILLALL^IBYMTC Q
