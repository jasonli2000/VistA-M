DGMTC2 ;ALBISC/TET - MEANS TEST CONVERSION-File data into files ;4/13/92  16:54
 ;;5.2;REGISTRATION;;JUL 29,1992
INC ;File income data
 S DGDFNV=DFN_";DPT(",DGLCT=$S('DGED:1,1:2)
I12 ;file entry & data into Patient Relation file, 408.12
 S DGF=408.12,DGZ=^DGPR(DGF,0),DGN=$P(DGZ,U,3)
L12 S DGN=DGN+1 L +^DGPR(408.12,DGN):1 G:'$T L12 I $D(^DGPR(408.12,DGN)) L -^DGPR(408.12,DGN) G L12
 S DGDA12=DGN,^DGPR(408.12,DGN,0)=DFN_U_1_U_DGDFNV I DGEB S ^DGPR(408.12,DGN,"E",0)="^408.1275D^"_DGLCT_U_DGLCT,^(1,0)=DGEB_U_1 I DGED S ^DGPR(DGF,DGN,"E",2,0)=DGED_U_0
 L -^DGPR(408.12,DGN)
 S ^DGPR(DGF,"B",$E(DFN,1,30),DGN)="",^DGPR(DGF,"C",$E(DGDFNV,1,30),DGN)=""
 I DGEB S ^DGPR(DGF,DGN,"E","AID",-DGEB,1)="",^DGPR(DGF,DGN,"E","B",DGEB,1)="" I DGED S ^DGPR(DGF,DGN,"E","AID",-DGED,2)="",^DGPR(DGF,DGN,"E","B",DGED,2)=""
 S $P(DGZ,U,3)=DGN,$P(DGZ,U,4)=$P(DGZ,U,4)+1,^DGPR(DGF,0)=DGZ D:$D(DGFLG) ERR^DGMTCQ K DGDFNV,DGLCT,DGN,DGF,DGVAR,DGZ
 ;
I2 S DGYR=0 F  S DGYR=$O(DGINC(DGYR)) Q:DGYR'>0  S DGICT=DGICT+1 D I21
 K DGYR D:$D(DGMT) MT Q
 ;
I21 ;File entry & data in Individual Annual Income file, 408.21
 S DGF=408.21,DGZ=^DGMT(DGF,0),DGN=$P(DGZ,U,3)
 S DGNN=DGYR_U_DGDA12 F DGI=9:1:18 S $P(DGNN,U,(DGI-1))=$P(DGINC(DGYR),U,DGI)
 S DGNN2="",DGP=2 F DGI=20:1:22 S DGP=DGP+1,$P(DGNN2,U,DGP)=$P(DGINC(DGYR),U,DGI)
 S DGT="",DGP=0 F DGI=2:1:5,19 S DGP=DGP+1,$P(DGT,U,DGP)=$P(DGINC(DGYR),U,DGI)
 D L S DGDA21=DGN,^DGMT(DGF,DGN,0)=DGNN,^(2)=DGNN2,^("TOT")=DGT L -^DGMT(DGF,DGN)
 S DGX=$P(DGNN,U),DGX2=$P(DGNN,U,2),^DGMT(DGF,"B",$E(DGX,1,30),DGN)="" S:DGX2 ^DGMT(DGF,"AI",+DGX2,-DGX,DGN)="",^DGMT(DGF,"C",$E(DGX2,1,30),DGN)=""
 D Z D:$D(DGFLG) ERR^DGMTCQ K DGF,DGN,DGZ,DGNN,DGNN2,DGT,DGI,DGP,DGVAR,DGX,DGX2
 ;
I22 ;File entry & data in Income Relation file, 408.22
 S DGF=408.22,DGZ=^DGMT(DGF,0),DGN=$P(DGZ,U,3)
 S DGDEP=$P(DGINC(DGYR),U)
 S DGNN=DFN_U_DGDA21,$P(DGNN,U,5)=$P(DGDEP,";",2),$P(DGNN,U,6)=$P(DGDEP,";",3),$P(DGNN,U,13)=$P(DGDEP,";")
 S $P(DGNN,U,8)=$S($P(DGDEP,";")=0:0,+(DGDEP)!($P(DGINC(DGYR),U,4)]"")!($P(DGINC(DGYR),U,5)]""):1,1:"")
 D L S DGDA22=DGN,^DGMT(DGF,DGN,0)=DGNN L -^DGMT(DGF,DGN)
 S ^DGMT(DGF,"B",$E($P(DGNN,U),1,30),DGN)="" S:DGDA21 ^DGMT(DGF,"AIND",$E($P(DGNN,U,2),1,30),DGN)=""
 D Z D:$D(DGFLG) ERR^DGMTCQ K DGDEP,DGF,DGN,DGVAR,DGZ,DGNN
 Q
 ;
MT ;File means test data
 S (DGMTOY,DGMTD)=0 F  S DGMTD=$O(DGMT(DGMTD)) Q:DGMTD'>0  S DGMTCT=DGMTCT+1,DGMTZ=DGMT(DGMTD),DGMTY=9999999-DGMTD,DGMTY=($E(DGMTY,1,3)-1)_"0000" S:DGMTCT=1 DGCAT=$P(DGMTZ,U,2) D
 .;File entry & data in Annual Means Test file, 408.31
 .S DGF=408.31,DGZ=^DGMT(DGF,0),DGN=$P(DGZ,U,3)
 .S DGS=$P(DGMTZ,U,2),DGS=$S(DGS="P":2,DGS="N":3,DGS="A":4,DGS="B":5,DGS="C":6,1:1),$P(DGMTZ,U,3)=DGS,$P(DGMTZ,U,2)=DFN S:DGS=3 $P(DGMTZ,U,17)=$P(DGMTZ,U)
 .D L S DGDA31=DGN,^DGMT(DGF,DGN,0)=DGMTZ D:$O(^DG(41.3,DFN,2,DGMTD,1,0)) WP
 .L -^DGMT(DGF,DGN)
 .S DGX=$P(DGMTZ,U),DGX2=$P(DGMTZ,U,2),DGX3=$P(DGMTZ,U,3)
 .S ^DGMT(DGF,"B",$E(DGX,1,30),DGN)="" I DGX2 S ^DGMT(DGF,"AID",+DGX2,-DGX,DGN)="",^DGMT(DGF,"AD",+DGX2,DGX,DGN)="",^DGMT(DGF,"ADFN"_+DGX2,DGX,DGN)="" S:DGX3 ^DGMT(DGF,"AS",+DGX3,-DGX,+DGX2,DGN)=""
 .S:DGX2 ^DGMT(DGF,"C",$E(DGX2,1,30),DGN)=""
 .I DGMTY'=DGMTOY S DA=0 D
 ..S DA=$O(^DGMT(408.21,"AI",+$O(^DGPR(408.12,"B",DFN,0)),-DGMTY,0)),DGMTOY=DGMTY
 ..I DA,'$P($G(^DGMT(408.21,DA,"MT")),U) S $P(^DGMT(408.21,DA,"MT"),U)=DGDA31,^DGMT(408.21,"AM",DGDA31,DA)=""
 .D Z D:$D(DGFLG) ERR^DGMTCQ K DGMTZ,DGF,DGN,DGVAR,DGZ,DGS,DGX,DGX2,DGX3
 .I DGMTCT=1 D PCS
 K DGCAT,DGMTD,DGMTOY,DGMTY,DGS Q
WP ;Move comments wp data, by means test
 S DGWPZ=$G(^DG(41.3,DFN,2,DGMTD,1,0)),DGL=0 F  S DGL=$O(^DG(41.3,DFN,2,DGMTD,1,DGL)) Q:DGL'>0  S DGLZ=$G(^(DGL,0)) S ^DGMT(408.31,DGDA31,"C",DGL,0)=DGLZ
 S ^DGMT(408.31,DGDA31,"C",0)=DGWPZ K DGWPZ,DGL,DGLZ Q
 ;
PCS ;Update Current Means Test Status field in patient file, .14
 I DGMTCT'=1!(+$P(DGDPTZ,U,14)) Q
 S DGS=DGCAT,DGS=$S(DGS="P":2,DGS="N":3,DGS="A":4,DGS="B":5,DGS="C":6,1:1)
L2 L +^DPT(DFN,0):1 G:'$T L2 S $P(^DPT(DFN,0),U,14)=DGS L -^DPT(DFN,0) S ^DPT("ACS",$E(DGS,1,30),DFN)=""
 Q
 ;
L S DGN=DGN+1 L +^DGMT(DGF,DGN):1 G:'$T L I $D(^DGMT(DGF,DGN)) L -^DGMT(DGF,DGN) G L
 Q
 ;
Z S $P(DGZ,U,3)=DGN,$P(DGZ,U,4)=$P(DGZ,U,4)+1,^DGMT(DGF,0)=DGZ Q
