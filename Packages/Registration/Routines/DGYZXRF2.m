DGYZXRF2 ;ALB/MIR - PRINT REPORT OF DUPLICATES OR NON-X-REFED ENTRIES ;AUG 2 91
 ;;MAS VERSION 5.0;
 ;
 ;DGYZ = XRF for non-cross-referenced, non-duplicates
 ;       PRG for non-cross-referenced, duplicates
 ;       DUP for cross-referenced, duplicates
 ;
EN ;generate report of findings
 S DGFL=0,DGONE=1,DGYZ="XRF" D HEAD,RPT
 ;
 I 'DGFL S DGYZ="PRG" D HEAD,RPT
 ;
 G:DGFL Q S DGYZ="DUP" D HEAD
 F DFN=0:0 S DFN=$O(^UTILITY("DGYZXRF","DUP",DFN)) Q:'DFN!DGFL  F TT=0:0 S TT=$O(^UTILITY("DGYZXRF","DUP",DFN,TT)) Q:'TT!DGFL  F DTE=0:0 S DTE=$O(^UTILITY("DGYZXRF","DUP",DFN,TT,DTE)) Q:'DTE!DGFL  D DUP2
 ;
Q W !! D CLOSE^DGUTQ
 K DFN,DGFL,DGONE,DGYZ,DGYZAUTO,DGYZX,DIR,DTE,DUP,IFN,L,TT,VA,Y
 Q
 ;
HEAD ;header columns
 I '$O(^UTILITY("DGYZXRF",DGYZ,0)) Q
 I IOST?1"C-".E,'DGONE S DIR(0)="E" D ^DIR S DGFL='Y I DGFL Q
 W @IOF,!,$S(DGYZ="XRF":"NON-CROSS-REFERENCED, NON-DUPLICATE ENTRIES IN ^DGPM",DGYZ="PRG":"NON-CROSS-REFERENCED, DUPLICATE ENTRIES IN ^DGPM",1:"CROSS-REFERENCED, DUPLICATE ENTRIES IN ^DGPM")
 W !,"NAME",?33,"PT ID",?46,"TRANSACTION",?58,"DATE/TIME",?82,"IFN" I DGYZ="DUP" W ?90,"IFN OF DUPLICATE"
 K L S $P(L,"-",120)="" W !,L
 I DGYZAUTO,(DGYZ'="DUP") W !!,"ENTRIES LISTED HAVE ",$S(DGYZ=1:"ALREADY",1:"NOT")," BEEN ",$S(DGYZ=1:"CROSS-REFERENCED",1:"PURGED"),"!!",!!
 I DGYZ="DUP"!'DGYZAUTO W !!,"YOU MUST CORRECT ANY PROBLEMS LISTED!!",!!
 S DGONE=0
 Q
 ;
RPT ;report for non-cross-referenced entries
 F IFN=0:0 S IFN=$O(^UTILITY("DGYZXRF",DGYZ,IFN)) Q:'IFN!DGFL  S DGYZX=^(IFN) D WRT
 Q
 ;
WRT ;write data for non-cross-referenced entries
 I $Y>(IOSL-8) D HEAD I DGFL Q
 S DFN=$P(DGYZX,"^",3) D PID^VADPT6
 W !,$S($D(^DPT(+DFN,0)):$P(^(0),"^",1),1:"UNKNOWN"),?33,VA("PID")
 S TT=$P(DGYZX,"^",2) W ?46,$S(TT=1:"ADMISSION",TT=2:"TRANSFER",TT=3:"DISCHARGE",1:"SPEC CHANGE")
 S Y=+DGYZX X ^DD("DD") W ?58,Y,?80,$J(IFN,6)
 Q
 ;
DUP2 ;get IFNs of duplicates
 F IFN=0:0 S IFN=$O(^UTILITY("DGYZXRF","DUP",DFN,TT,DTE,IFN)) Q:'IFN!DGFL  F DUP=0:0 S DUP=$O(^UTILITY("DGYZXRF","DUP",DFN,TT,DTE,IFN,DUP)) Q:'DUP!DGFL  D DUPRT
 Q
 ;
DUPRT ;write data for cross-referenced duplicates
 I $Y>(IOSL-8) D HEAD I DGFL Q
 D PID^VADPT6 W !,$S($D(^DPT(DFN,0)):$P(^(0),"^",1),1:"UNKNOWN"),?33,VA("PID")
 W ?46,$S(TT=1:"ADMISSION",TT=2:"TRANSFER",TT=3:"DISCHARGE",1:"SPEC CHANGE")
 S Y=DTE X ^DD("DD") W ?58,Y,?80,$J(IFN,6),?87,$J(DUP,6)
 Q
