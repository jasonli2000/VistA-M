VAMCS ;ALB/JRP - MAIN CD-ROM SERVER ROUTINE; [ 05/23/94   9:34 AM ] ;10/3/95  08:19
 ;;V1.0T.1;MINIMAL PATIENT DATASET;**2**;APR 3,1995 ;CFB MODIFIED ERRSTORE EXTRINSIC 19 SEP 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
DEBUG ;DEBUGGING ENTRY POINT (WRITES INFORMATION TO SCREEN)
 S VAMCSBUG=1 D SERVER Q
SERVER ;MAIN ENTRY POINT (USED FOR TASKING)
 ;DECLARE VARIABLES
 N ERR,X,POLLERR
 ;SET ERROR TRAP
 S X="ERRTRAP^VAMCS",@^%ZOSF("TRAP")
 ;CHECK FOR DEBUGGING VARIABLE
 S VAMCSBUG=+$G(VAMCSBUG)
 ;INITIALIZE ALL KEY VARIABLES
 S ERR=$$KILLKEY^VAMCS12("*",1) I +ERR<0 S X=$$ERRSTORE^VAMCSUT3("-1^Failed Initialise key Variable") ;CFB MOD TO RECORD ERROR
 W:(VAMCSBUG) !,"Variables used as keys have been initialized (set to NULL)"
 ;INITIALIZE ENCRYPTION BOARD VARIABLES
 S ERR=$$INITVARS^VAMCSDS0() I ERR<0 S X=$$ERRSTORE^VAMCSUT3(ERR) ;CFB MOD TO RECORD ERROR
 W:(VAMCSBUG) !,"Encryption variables have been initialized"
 ;INITIALIZE REQUEST & CONTROLLER
 S ERR=$$INIT9498^VAMCS11(1) I +ERR<0 S X=$$ERRSTORE^VAMCSUT3(ERR) ;CFB MOD TO RECORD ERROR
 W:(VAMCSBUG) !,"VAM - REQUEST and VAM - CONTROLLER files initialized"
 ;BEGIN POLLING FOR REQUESTS
 W:(VAMCSBUG) !,"Polling will now begin"
 S POLLERR=$$POLLER^VAMCS01(1,VAMCSBUG) I POLLERR<0 S X=$$ERRSTORE^VAMCSUT3(POLLERR) ;CFB MOD TO RECORD ERROR
 ;UNINITIALIZE REQUEST & CONTROLLER
 S ERR=$$INIT9498^VAMCS11(0,$P(POLLERR,"^",2)) I ERR<0 S X=$$ERRSTORE^VAMCSUT3(ERR) ;CFB MOD TO RECORD ERROR
 W:(VAMCSBUG) !!,"VAM - REQUEST and VAM - CONTROLLER files de-initialized"
CLEANUP ;TAG FOR ERROR TRAP TO GO TO FOR CLEAN UP
 ;DELETE ALL KEY VARIABLES
 S ERR=$$KILLKEY^VAMCS12("*") I ERR<0 S X=$$ERRSTORE^VAMCSUT3(ERR) ; CFB MOD TORECORD ERROR
 W:(VAMCSBUG) !,"Variables used as keys have been deleted"
 ;DELETE ENCRYPTION BOARD VARIABLES
 D KILLVARS^VAMCSDS0
 W:(VAMCSBUG) !,"Encryption variables have been deleted"
 ;DELETE DEBUGGING VARIABLE
 W:(VAMCSBUG) !,"Server has stopped"
 W:(VAMCSBUG) !,$P(POLLERR,"^",2)
 K VAMCSBUG
 S:($D(ZTQUEUED)) ZTREQ="@"
 Q
ERRTRAP ;TAG USED FOR SETTING OF ERROR TRAP
 W:(VAMCSBUG) !!,"** Main error trap has been entered **"
 W:(VAMCSBUG) !,"** Server will be stopped **"
 ;UNINITIALIZE REQUEST & CONTROLLER
 S ERR=$$INIT9498^VAMCS11(0,"Server has been stopped due to error"),X=$$ERRSTORE^VAMCSUT3("-1^Server has been stopped due to error") ;CFB MOD TO RECORD ERROR
 W:(VAMCSBUG) !!,"VAM - REQUEST and VAM - CONTROLLER files de-initialized"
 S POLLERR="^Server stopped due to error"
 G CLEANUP
 Q
