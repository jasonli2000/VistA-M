VAMCSUT2 ;ALB/JRP/EG/PB - MPD UTILITY ROUTINES; [ 05/23/94   9:34 AM ] ;9/28/95  14:55
 ;;V1.0T.1;MINIMAL PATIENT DATASET;**2**;APR 3,1995 ;MOD CFB MODULE NEWSTAT ;19 SEP 1995
 ;;Per VHA Directive 10-93-142, this routine should not be modified.
RESERR ;RESET ERRORS
 ;THIS IS NOT A FUNCTION
 N X,Y,VAM,%DT
 S %DT="AET",%DT("A")="DELETE ERRORS BEFORE: " D ^%DT Q:Y<0  S VAM("TIME")=+Y
 F VAM=0:0 S VAM=$O(^VAM(394.94,1,"ERROR",VAM)) Q:VAM>VAM("TIME")  Q:VAM=""  D
 .L +^VAM(394.94,1,"ERROR",0) S $P(^(0),"^",4)=$P(^VAM(394.94,1,"ERROR",0),"^",4)-1 L -^VAM(394.94,1,"ERROR",0) K ^(VAM,0),^VAM(394.94,1,"ERROR","B",VAM,VAM)
 Q
CVTDATE(DATE,STRTDATE) ; CONVERT DATE OFFSET FOR FILEMAN FORMAT
 ;INPUT  : DATE - DATE OFFSET RETURNED BY CD-ROM
 ;       : STRTDATE - STARTING DATE OF OFFSET
 ;OUTPUT : DATE IN FILEMAN FORMAT (SUCCESS)
 ;       : '-1^ERROR TEXT' ERROR
 Q:($G(DATE)="") "-1^No date passed"
 Q:($G(STRTDATE)="") "-1^No starting date passed"
 N X1,X2,Y,DOC
 S X1=STRTDATE
 ; IN ORDER TO GET THE RIGHT ORDERING, DATES HAVE TO BE IN REVERSE
 ;  ORDER SUCH THAT THE MOST RECENT VISIT (THE LARGEST OFFSET) 
 ;  IS FIRST (SMALLEST OFFSET).  TO DO THIS, WE MAKE ANOTHER OFFSET
 ;  DATE OF 999 AND SUBTRACT OUR CURRENT OFFSET FROM THAT.  THIS WAY,
 ;  MORE RECENT DATES APPEAR FIRST
 S X2=(999-DATE)
 D C^%DTC
 S (DOC,Y)=X
 X ^DD("DD")
 S DOC=Y
 ; PUT IN LEADING 0 IN DAY
 S X=$P(DOC,",",1)
 S Y=$P(X," ",2)
 I Y<10 D
 .S T="0"_Y
 .S $P(X," ",2)=Y
 .S $P(DOC,",",1)=X
 Q DOC
CVTCARE(CODE) ; CONVERT CARE CODE TO CARE RECEIVED
 ;INPUT  : CODE - CARE CODE RETURNED BY CD-ROM
 ;OUTPUT : CARE RECEIVED (SUCCESS)
 ;       : '-1^ERROR TEXT' ERROR
 N CARE,X
 Q:($G(CODE)="") "-1^No care code passed"
 N X,CARE
 ;SEARCH CARE CODE FILE FOR CODE
 S X=$O(^VAM(394.92,"B",CODE,""))
 S:(X="") X=-1
 S CARE=$P($G(^VAM(394.92,X,0)),"^",2)
 Q CARE
