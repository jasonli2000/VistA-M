DSIRCNV1 ;AMC - Document Storage System Inc - continuation of conversion routine
 ;;5.5;DSIR;;Apr 28, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
VER5P3 ;
 K ^DSIR(19620,"AFCLD"),^DSIR(19620,"AFOIA") D XREF20
 D:'$D(^DSIR(19620.91,"ADTST"))
 .D MES^DSICXPDU("Creating New Index on File 19620.91",1)
 .S DIK="^DSIR(19620.91,",DIK(1)=.02 D ENALL^DIK
 .D MES^DSICXPDU("New Index Created!",1)
 D:'$$PATCH^XPDUTL("DSIR*5.2*3") PTCH523
 D CLNEXMP
 Q
VER5P4 ;
 D MES^DSICXPDU("Repointing File 19620.92 DSIR REQUEST ADDRESS",1)
 N DSIR,PAT,CNT,II,FIL,STCD,STDT,CLRK S FIL=19620.92,DSIR=0,DSIRT=+$P($G(^DSIR(FIL,0)),U,4) D UPDSTAT^DSICXPDU(0,DSIRT)
 F II=1:1 S DSIR=$O(^DSIR(FIL,DSIR)) Q:'DSIR  D:'(II#100) UPDSTAT^DSICXPDU(II,DSIRT) D
 .S PAT=+$G(^DSIR(FIL,DSIR,0)) K ^DSIR(FIL,"B",PAT,DSIR)
 .S PAT=PAT_";DPT(",$P(^DSIR(FIL,DSIR,0),U)=PAT,^DSIR(FIL,"B",PAT,DSIR)=""
 D UPDSTAT^DSICXPDU(DSIRT,DSIRT)
 D MES^DSICXPDU("Repointing File 19620.92 DSIR REQUEST ADDRESS Completed!",1)
 D MES^DSICXPDU("Creating New Indexes on file 19620.91 DSIR STATUS HISTORY!",1)
 S FIL=19620.91,DSIR=0,DSIRT=+$P($G(^DSIR(FIL,0)),U,4) K ^DSIR(FIL,"ASTCLDT"),^DSIR(FIL,"ASTDTCL") D UPDSTAT^DSICXPDU(0,DSIRT)
 F II=1:1 S DSIR=$O(^DSIR(FIL,DSIR)) Q:'DSIR  D:'(II#100) UPDSTAT^DSICXPDU(II,DSIRT) D
 .S STCD=$G(^DSIR(FIL,DSIR,0)) I $TR($P(STCD,U,2,99),U)="" D  Q
 ..N DIK,DA S DA=DSIR,DIK="^DSIR("_FIL_"," D ^DIK
 .S CLRK=$P(STCD,U,4),STDT=$P(STCD,U,3),STCD=$P(STCD,U,2)
 .S ^DSIR(FIL,"ASTCLDT",STCD,CLRK,STDT,DSIR)=""
 .S ^DSIR(FIL,"ASTDTCL",STCD,STDT,CLRK,DSIR)=""
 D UPDSTAT^DSICXPDU(DSIRT,DSIRT)
 D MES^DSICXPDU("Indexes Created!",1)
 Q
XREF20 ;Check X-REF's on file 19620 for new ones, build them if they aren't there
 N DSIR,ROI0,ROI10,ROI6,ROI1,REQ,FOIA,CLOP,CLRK,FCLD,CLCL,II,X,PAT,DIVX,REQX,CLOX,CLCX,PTDX S X="Checking Cross References on File 19620!"
 K ^DSIR(19620,"ADTST")
 S DSIRT=+$P($G(^DSIR(19620,0)),U,4),REQX=$$EXST("AREQ"),CLOX=$$EXST("ACLOP"),CLCX=$$EXST("ACLCL"),DIVX=$$EXST("ADIV"),PTDX=$$EXST("APTDT"),FOIA=$$EXST("AFOIA")
 D MES^DSICXPDU(X,1)
 S DSIR=0 F II=1:1 S DSIR=$O(^DSIR(19620,DSIR)) Q:'DSIR  D  D:'(II#100) UPDSTAT^DSICXPDU(DSIRT,II)
 .S ROI0=$G(^DSIR(19620,DSIR,0)),ROI6=$G(^(6)),ROI10=$G(^(10)),ROI1=$G(^(1))
 .S CLOP=+$P(ROI10,U,6),CLCL=+$P(ROI10,U,7),CLRK=+$P(ROI0,U,3),DIV=+$P(ROI6,U,3),REQ=+$P(ROI1,U,1),PAT=$P(ROI0,U)
 .S FCLD=+$$FCLOSEDT^DSIROI6(DSIR)
 .I FCLD S ^DSIR(19620,"AFCLD",FCLD,DSIR)=""
 .I 'FOIA,'FCLD S:'FCLD ^DSIR(19620,"AFOIA",DSIR)=""
 .I 'REQX S:REQ ^DSIR(19620,"AREQ",REQ,DSIR)=""
 .I 'DIVX S:DIV ^DSIR(19620,"ADIV",DIV,DSIR)=""
 .I 'CLOX S:CLRK&CLOP ^DSIR(19620,"ACLOP",CLRK,CLOP,DSIR)=""
 .I 'CLCX S:CLRK&CLCL ^DSIR(19620,"ACLCL",CLRK,CLCL,DSIR)=""
 .I 'PTDX S:PAT]""&CLOP ^DSIR(19620,"APTDT",PAT,CLOP,DSIR)=""
 D UPDSTAT^DSICXPDU(DSIRT,DSIRT)
 Q
EXST(X) ;
 Q $D(^DSIR(19620,X))
PTCH523 ;Patch 5.2*3 make Other Fereral Agency available to both FOIA and Privacy Act
 ;
 N X,DSIRFDA,IENS,FIL S FIL=19620.71
 S X="Running Patch 5.2.3 PRE-INSTALLATION routine." D MES^DSICXPDU(X,1)
 D UPDSTAT^DSICXPDU(1,2)
 S IENS=$O(^DSIR(FIL,"B","OTHER FEDERAL AGENCY",0)) Q:'IENS  S IENS=IENS_","
 S DSIRFDA(FIL,IENS,.02)="B" D FILE^DIE(,"DSIRFDA")
 K DSIRFDA
 S IENS=$O(^DSIR(FIL,"B","SOCIAL SECURITY ADMINISTRATION",0)) Q:'IENS  S IENS=IENS_","
 S DSIRFDA(FIL,IENS,.02)="B" D FILE^DIE(,"DSIRFDA")
 D UPDSTAT^DSICXPDU(2,2)
 Q
CLNEXMP ;Clean-up exemptions from first and third party requests.
 N DSIR,ROIC  S ROIC=$P(^DSIR(19620,0),U,4)
 S X="Removing Exemptions from First and Third Party Requests." D MES^DSICXPDU(X,1)
 S DSIR=0 F II=1:1 S DSIR=$O(^DSIR(19620,DSIR)) Q:'DSIR  D  D:'(II#100) UPDSTAT^DSICXPDU(ROIC,II)
 .Q:$P(^DSIR(19620,DSIR,0),U)'[";DPT("
 .S ^DSIR(19620,DSIR,12)="",^DSIR(19620,DSIR,13)=""
 D UPDSTAT^DSICXPDU(ROIC,ROIC)
 Q
VER5P5 ;populate open date on old records
 N DSIR,HIST,OPDT,FIL,DSIRA,II,CNTR,DSIRI S FIL=19620.91,CNTR=$P(^DSIR(FIL,0),U,4)
 S X="Populating Open Dates in Status History File for old records!" D MES^DSICXPDU(X,1)
 S HIST=0 F II=1:1 S HIST=$O(^DSIR(FIL,HIST)) Q:'HIST  S DSIR=$G(^(HIST,0)) D  D:'(II#100) UPDSTAT^DSICXPDU(CNTR,II)
 .Q:$P(DSIR,U,3)&$P(DSIR,U,5)
 .S DSIRI=+DSIR,OPDT=""
 .S:'$P(DSIR,U,3) OPDT=$P($G(^DSIR(19620,DSIRI,10)),U,6)
 .I 'OPDT,'$P(DSIR,U,3) D LOG
 .S:'$P(DSIR,U,3) DSIRA(FIL,HIST_",",.03)=OPDT
 .S:'$P(DSIR,U,5) DSIRA(FIL,HIST_",",.05)=$P(DSIR,U,3)
 .D:$D(DSIRA) FILE^DIE(,"DSIRA")
 .K DSIRA
 D UPDSTAT^DSICXPDU(CNTR,CNTR)
 D VER5P5A
 Q
LOG ;
 S ^XTMP("DSIR NO OPEN DATE",$J,DSIR)=""
 Q
VER5P5A ;clear out bogus Dates of Birth from Unregistered Patient File.
 N UNPT,DOB,II,CNTR,FIL S FIL=19620.96,CNTR=+$P($G(^DSIR(FIL,0)),U,4) Q:'CNTR
 S X="Cleaning up invalid DOB's in file 19620.96!" D MES^DSICXPDU(X,1)
 S UNPT=0 F II=1:1 S HIST=$O(^DSIR(FIL,UNPT)) Q:'UNPT  S DOB=+$P($G(^DSIR(FIL,UNPT,0)),U,2) D:DOB>3000000  D:'(II#100) UPDSTAT^DSICXPDU(CNTR,II)
 .S $P(^DSIR(FIL,UNPT,0),U,2)=""
 D UPDSTAT^DSICXPDU(CNTR,CNTR)
 Q
PRE5P5 ;Clean up old file 19620.3 if it exists
 N DIU
 Q:'$O(^DSIR(19620.3,0))
 S DIU=19620.3,DIU(0)="D" D EN^DIU2
 Q
