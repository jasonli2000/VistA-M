DSIROIA2 ;EWL - Document Storage System Inc - AD HOC REPORTING ;09/22/2009 13:15
 ;;8.0;RELEASE OF INFORMATION - DSSI;;Dec 25, 2010;Build 11
 ;Copyright 1995-2011, Document Storage Systems, Inc., All Rights Reserved
 ;
 ;DBIA# Supported Reference
 ;----- --------------------------------
 ;2056      GETS^DIQ
 ;
 Q 
RPTDATA(AXY,INARRAY) ; RPC DSIR GET AD HOC DATA
 ;Input Parameter(s) - ARRAY AS FOLLOWS:
 ; "FIELD1_IEN^FIELD2_IEN^...^FIELDn_IEN 
 ;     (REQUIRED-VARIABLE NUMBER OF FIELDS)
 ; FILTER/SELECTION FIELDS (OPTIONAL)
 ; "F1"^F1_IEN^F1_VALUE
 ; "F2"^F2_IEN^F1_VALUE
 ; "F3"^F3_IEN^F1_VALUE
 ; DATE RANGE FIELDS (OPTIONAL)
 ; "R1"^R1_IEN^F1_VALUE1^F1_VALUE2
 ; "R2"^R2_IEN^R2_VALUE2^R2_VALUE2^["A"|"O"] ("A"=And "O"=Or)
 ; SORT FIELDS (OPTIONAL)
 ; "S1"^S1_IEN^["A"|"D"] ("A - ASCENDING IS THE DEFAULT, "D" - DESCENDING)
 ; "S2"^S2_IEN^["A"|"D"] ("A - ASCENDING IS THE DEFAULT, "D" - DESCENDING)
 ; "S3"^S3_IEN^["A"|"D"] ("A - ASCENDING IS THE DEFAULT, "D" - DESCENDING)
 ;Return Value
 ;ARRAY:- VARIABLE NUMBER OF RECORDS AS FOLLOWS:
 ; F1_DATA^F2_DATA^Fn_DATA (VARIABLE NUMBER OF DATA ITEMS)
 ; OR
 ; -1,NO RECORDS RETRIEVED
 ; Declarations
 M ^TMP("DSIRAHINARRAY")=INARRAY
 N RF,INX,IND,CTR,ERRMSG
 N FLDSTR,GFLDS,FLDCT,FLDINP
 N FLDARRAY,FI,GBL,GLBL,FLD,FIEN
 N FFLD,FVAL,FRANK,FINX,FCT
 N AXYT,FILTER,TESTSTR,TFLD
 N RFLD,RVAL1,RVAL2,ANDOR,RCT
 N SRT,SI,SD,SX,SCT
 ;
 ; Initializations
 S SRT(1)=0,SRT(2)=0,SRT(3)=0,SX(1)=0,SX(2)=0,SX(3)=0,SCT=0
 S FCT=0,RCT=0,RF=19620,FI=19620.87,FLDSTR=" "
 ;
 ;PROCESS FIELDS
 S FLDINP=$G(INARRAY(1)),FLDCT=$L(FLDINP,U),RF=19620
 F INX=1:1:FLDCT D
 .S FIEN=$P(FLDINP,U,INX),GLBL=$P(^DSIR(FI,FIEN,2),U,1),FLD=$P(^DSIR(FI,FIEN,2),U,2)
 .I (GLBL=19620)&(FLD>0) D
 ..I $P(^DSIR(FI,FIEN,1),U,6)="Y" S FLD=FLD_"E"
 ..S FLDSTR=FLDSTR_FLD_";"
 S FLDSTR=$E(FLDSTR,2,$L(FLDSTR)-1)
 ;
 ;PROCESS INPUT ARRAY
 F INX=2:1 Q:'$D(INARRAY(INX))  D
 .S TFLD=$P(INARRAY(INX),U)
 .;
 .;PROCESS FILTERS
 .I (TFLD="F1")!(TFLD="F2")!(TFLD="F3") S FCT=FCT+1 D
 ..S SSCRIPT=$P(^DSIR(FI,$P(INARRAY(INX),U,2),1),U,5)
 ..S FFLD(SSCRIPT)=$P(^DSIR(FI,$P(INARRAY(INX),U,2),2),U,2)
 ..S FVAL(SSCRIPT)=$P(INARRAY(INX),U,3)
 ..S FRANK(SSCRIPT)=$P(^DSIR(FI,$P(INARRAY(INX),U,2),1),U,5)
 ..S FINX(SSCRIPT)=$P(^DSIR(FI,$P(INARRAY(INX),U,2),2),U,3)
 .;
 .;PROCESS DATE RANGES
 .I (TFLD="R1")!(TFLD="R2") S RCT=RCT+1 D
 ..S RFLD(RCT)=$P(^DSIR(FI,$P(INARRAY(INX),U,2),2),U,2)
 ..S RVAL1(RCT)=$P(INARRAY(INX),U,3)
 ..S RVAL2(RCT)=$P(INARRAY(INX),U,4)
 ..S RINX(RCT)=$P(^DSIR(FI,$P(INARRAY(INX),U,2),2),U,3)
 ..S ANDOR=$S($P(INARRAY(INX),U,5)="O":"!",1:"&")
 .;
 .;PROCESS SORTS
 .I (TFLD="S1")!(TFLD="S2")!(TFLD="S3") S SCT=SCT+1 D
 ..S SI(SCT)=$P(INARRAY(INX),U,2)
 ..S SD(SCT)=$P(INARRAY(INX),U,3)
 ..F SRT(SCT)=1:1:FLDCT  Q:SI(SCT)=$P(FLDINP,U,SRT(SCT))
 ;
 ; PUT INDEX RECORDS IN ORDER BY SELECTION RANK
 I FCT>0 D
 .S IND=0 F INX=1:1:FCT D
 ..S IND=$O(FFLD(IND))
 ..S FFLD(INX)=FFLD(IND) K FFLD(IND)
 ..S FVAL(INX)=FVAL(IND) K FVAL(IND)
 ..S FINX(INX)=FINX(IND) K FINX(IND)
 ;
 ; SELECT THE RECORDS
 K ^TMP("DSIRFLTR",$J)
 ;ONE FILTER
 I $D(FFLD(1)) D GETIENS(.FLTR,1,FFLD(1),FINX(1),FVAL(1))
 ;TWO FILTERS
 I $D(FFLD(2)) D GETIENS(.FLTR,2,FFLD(2),FINX(2),FVAL(2)) D
 .S INX=0 F  S INX=$O(@FLTR@(INX)) Q:'INX  I '$D(^TMP("DSIRFLTR",$J,1,INX)) K @FLTR@(INX)
 .K ^TMP("DSIRFLTR",$J,1)
 ;ALL 3 FILTERS
 I $D(FFLD(3)) D GETIENS(.FLTR,3,FFLD(3),FINX(3),FVAL(3)) D
 .;S INX=0 F  S INX=$O(@FLTR@(INX)) Q:'INX  I '(($D(^TMP("DSIRFLTR",$J,2,INX)))&($D(^TMP("DSIRFLTR",$J,3,INX)))) K @FLTR@(INX)
 .S INX=0 F  S INX=$O(@FLTR@(INX)) Q:'INX  I '$D(^TMP("DSIRFLTR",$J,2,INX)) K @FLTR@(INX)
 .K ^TMP("DSIRFLTR",$J,2)
 ;
 I (FCT>0)&(RCT=1) D DTIENS1(.FLTR,FCT+1,RFLD(1),RINX(1),RVAL1(1),RVAL2(1))
 I (FCT>0)&(RCT=2) D DTIENS2(.FLTR,FCT+1,RFLD(1),RINX(1),RVAL1(1),RVAL2(1),ANDOR,RFLD(2),RINX(2),RVAL1(2),RVAL2(2))
 I (FCT>0)&(RCT>0) D
 . S INX=0 F  S INX=$O(@FLTR@(INX)) Q:'INX  I '(($D(^TMP("DSIRFLTR",$J,FCT,INX)))&($D(^TMP("DSIRFLTR",$J,FCT+1,INX)))) K @FLTR@(INX)
 . K ^TMP("DSIRFLTR",$J,FCT)
 I (FCT=0)&(RCT=1) D DTIENS1(.FLTR,1,RFLD(1),RINX(1),RVAL1(1),RVAL2(1))
 I (FCT=0)&(RCT=2) D DTIENS2(.FLTR,1,RFLD(1),RINX(1),RVAL1(1),RVAL2(1),ANDOR,RFLD(2),RINX(2),RVAL1(2),RVAL2(2))
 ;
 S AXYT=$NA(^TMP("DSIRADHOCTMP",$J)) K @AXYT
 F INX=1:1:$L(FLDSTR,";") D
 .S FLD=$P(FLDSTR,";",INX),$P(GFLDS,";",INX)=+FLD
 I (FCT>0)!(RCT>0) D
 .S INX=0 F  S INX=$O(^TMP("DSIRFLTR",$J,FCT+$S(RCT>0:1,1:0),INX)) Q:'INX  D
 ..D GETS^DIQ(RF,INX,GFLDS,"E","GBL","ERRMSG")
 ..D FIXGETS
 ..K GBL
 ;IF NO SELECTION CRITERIA
 I (FCT=0)&(RCT=0) D
 .S INX=0 F  S INX=$O(^DSIR(19620,INX)) Q:'INX  D
 ..D GETS^DIQ(RF,INX,GFLDS,"E","GBL","ERRMSG")
 ..D FIXGETS
 ..K GBL
 ;
 S AXY=$NA(^TMP("DSIRADHOCDATA",$J)) K @AXY
 S AXYT=$NA(^TMP("DSIRADHOCTMP",$J,"DILIST"))
 I $D(ERRMSG) S @AXY@(0)="-1^NO RECORDS RETRIEVED" D
 .K ^TMP("DSIRFLTR",$J),^TMP("DSIRADHOCTMP",$J)
 .K RF,INX,IND,CTR,ERRMSG,FLDSTR,GFLDS,FLDCT,FLDINP,FLDARRAY,FI,GBL,GLBL,FLD,FIEN
 .K FFLD,FVAL,FRANK,FINX,FCT
 .K AXYT,FILTER,TESTSTR,TFLD
 .K RFLD,RVAL1,RVAL2,ANDOR,RCT
 .K SRT,SI,SD,SX,SCT
 .K ^TMP("DSIRFLTR",$J)
 .Q
 S CTR=0,INX=.999 F  S INX=$O(@AXYT@(INX)) Q:'INX  S CTR=CTR+1 D
 .;
 .;SET SORT SUBSCRIPTS - DEFAULT VALUE IS 0
 .I SRT(1)>0 S SX(1)=$P(@AXYT@(INX,0),U,SRT(1)+1)
 .I $E($P(FLDSTR,";",SRT(1)),$L($P(FLDSTR,";",SRT(1))))="E"  S SX(1)=$$FIXDT(SX(1))
 .I SX(1)="" S SX(1)=0
 .I SRT(2)>0 S SX(2)=$P(@AXYT@(INX,0),U,SRT(2)+1)
 .I $E($P(FLDSTR,";",SRT(2)),$L($P(FLDSTR,";",SRT(2))))="E"  S SX(2)=$$FIXDT(SX(2))
 .I SX(2)="" S SX(2)=0
 .I SRT(3)>0 S SX(3)=$P(@AXYT@(INX,0),U,SRT(3)+1)
 .I $E($P(FLDSTR,";",SRT(3)),$L($P(FLDSTR,";",SRT(3))))="E"  S SX(3)=$$FIXDT(SX(3))
 .I SX(3)="" S SX(3)=0
 .I FCT>0 S RET=$NA(^TMP("DSIRFLTR",$J))
 .F IND=2:1:FLDCT+1 D
 ..S @AXY@(SX(1),SX(2),SX(3),INX)=$G(@AXY@(SX(1),SX(2),SX(3),INX))_$P(@AXYT@(INX,0),U,IND)
 ..I IND<FLDCT+$S((FCT>0)!(RCT>0):2,1:3) S @AXY@(SX(1),SX(2),SX(3),INX)=@AXY@(SX(1),SX(2),SX(3),INX)_U
 I CTR=0 S @AXY@(0)="-1^NO RECORDS RETRIEVED"
 K ^TMP("DSIRFLTR",$J),^TMP("DSIRADHOCTMP",$J)
 K RF,INX,IND,CTR,ERRMSG,FLDSTR,GFLDS,FLDCT,FLDINP,FLDARRAY,FI,GBL,GLBL,FLD,FIEN
 K FFLD,FVAL,FRANK,FINX,FCT
 K AXYT,FILTER,TESTSTR,TFLD
 K RFLD,RVAL1,RVAL2,ANDOR,RCT
 K SRT,SI,SD,SX,SCT
 K ^TMP("DSIRFLTR",$J)
 Q
FIXDT(INDT) ;
 N OUTDT,YYY,MM,DD,SM
 S SM=$E(INDT,1,3)
 S MM=$S(SM="JAN":"01",SM="FEB":"02",SM="MAR":"03",SM="APR":"04",SM="MAY":"05",SM="JUN":"06",SM="JUL":"07",SM="AUG":"08",SM="SEP":"09",SM="OCT":"10",SM="NOV":"11",1:"12")
 S DD=$E(INDT,5,6)
 S YYY=+$E(INDT,9,12)-1700
 S OUTDT=YYY_MM_DD
 K YYY,MM,DD,SM
 Q OUTDT
FIXGETS ;
 N NXT,FIL,IEN,RET,IX,NXTFLD,CT S CT=INX_","
 S NXT=$Q(GBL(RF,CT)),FIL=$QS(NXT,1),IEN=$QS(NXT,2),RET=+IEN
 F IX=1:1:$L(GFLDS,";") D
 .S NXTFLD=$P(GFLDS,";",IX),RET=RET_"^"_GBL(RF,IEN,NXTFLD,"E")
 S @AXYT@("DILIST",INX,0)=RET
 K NXT,FIL,IEN,RET,IX,NXTFLD,CT
 Q
GETIENS(RET,FNBR,FFLD,FINX,FVAL) ; GET RESULTING IENS AFTER FILTER
 N PTR S PTR=0,RET=$NA(^TMP("DSIRFLTR",$J,FNBR)) K @RET
 F  S PTR=$O(^DSIR(19620,FINX,FVAL,PTR)) Q:'PTR  S @RET@(PTR)=PTR
 K PTR
 Q
DTIENS1(RET,FNBR,FFLD,FINX,FVAL1,FVAL2) ;
 N PTR,GBL S PTR=0,RET=$NA(^TMP("DSIRFLTR",$J,FNBR)) K @RET
 S GBL="^DSIR(19620,"""_FINX_""","_FVAL1_")"
 F  S GBL=$Q(@GBL) Q:('($QS(GBL,2)=FINX))!($QS(GBL,3)>FVAL2)  D
 .S PTR=$QS(GBL,4),@RET@(PTR)=PTR
 K PTR,GBL
 Q
DTIENS2(RET1,FNBR,FFLD,FINX,FVAL1,FVAL2,ANDOR,RFLD,RINX,RVAL1,RVAL2) ;
 N RET2,INX
 D DTIENS1(.RET1,FNBR,FFLD,FINX,FVAL1,FVAL2)
 D DTIENS1(.RET2,FNBR+1,RFLD,RINX,RVAL1,RVAL2)
 I ANDOR="!" M @RET1=@RET2 K @RET2
 I ANDOR="&" D
 .S INX=0 F  S INX=$O(@RET1@(INX)) Q:'INX  I '$D(@RET2@(INX)) K @RET1@(INX)
 .K @RET2,RET2,INX
 Q
