SD106PT ;ALB/SWO POST INSTALL FOR SD*5.3*106;5.23.97
 ;;5.3;SCHEDULING;**106**;8.13.93
 ;CHECK %ZTSK FOR APPOINTMENT LIST TASKS SCHEDULED PRIOR
 ;TO PATCH 106 INSTALLATION AND ADD A "SDCONC" NODE, DEFAULTING VARIABLE
 ;"SDCONC" TO "C" FOR COUNT
EN ;ENTRY
 N A1,A2,CNT1,CNT2,INSTLD,POP,QTIME,RTIME,SPACE,USER
 S A1="",(CNT1,CNT2)="",POP=0,U="^",$P(SPACE," ",80)=""
 D NOW^%DTC S INSTLD=%H
 D BMES^XPDUTL("TASKS THAT WILL BE MODIFIED")
 D BMES^XPDUTL("TASK #   USER                               QUEUED TO RUN")
 F  S A1=$O(^%ZTSK(A1)) Q:A1=""  D
 . S A2=$G(^%ZTSK(A1,0)),POP=0 I $P(A2,U,1,2)'="START^SDAL" D
 . . S CNT1=CNT1+1 S POP=1
 . Q:POP  S QTIME=$P(A2,U,5)
 . I $TR(QTIME,",","")>$TR(INSTLD,",","") Q
 . S RTIME=$P(A2,U,6)
 . S USER=$P(A2,U,10)
 . Q:$G(^%ZTSK(A1,.3,"SDCONC"))'=""
 . S ^%ZTSK(A1,.3,"SDCONC")="C"
 . D MES^XPDUTL(A1_$E(SPACE,1,9-$L(A1))_USER_$E(SPACE,1,35-$L(USER))_$$HTE^XLFDT(RTIME))
 . S CNT2=CNT2+1
 D BMES^XPDUTL("TASKS CHECKED  : "_$J(CNT1,6))
 D MES^XPDUTL("TASKS MODIFIED : "_$S(CNT2>0:$J(CNT2,6),1:$J(0,6)))
EX ;
 Q
