SDPP1 ;ALB/MLI - PATIENT PROFILE PRINT ;3/4/92  09:28 ;
 ;;5.2;SCHEDULING;;JUL 29,1992
SORT S SDBD=SDBD+.1,SDED=SDED-.9,SDFL=0 U IO
 W @IOF,!,"PATIENT PROFILE COVERING:  ",$S(SDBD=1:"ALL DATES",1:$E(SDBD,4,5)_"/"_$E(SDBD,6,7)_"/"_$E(SDBD,2,3)_" THROUGH "_$E(SDED,4,5)_"/"_$E(SDED,6,7)_"/"_$E(SDED,2,3)),?55,"DATE RUN:  " S X="N",%DT="R" D ^%DT W $E(Y,4,5),"/"
 W $E(Y,6,7),"/",$E(Y,2,3),"@",$P(Y,".",2) S SDT=Y,DA=DFN,DIC="^DPT(" K DX(0) F DR=0,.36,.11,.13,.321,.1,.101 D EN^DIQ,CK G QUIT:SDFL D:DR=.36 OTHER I DR=.11,$D(^DPT(DFN,.121)) S X=^(.121) D TEMP G QUIT:SDFL
 K ^UTILITY("DIQ1",$J) S DR=".525;.323" D EN^DIQ1 W !,"PERIOD OF SERVICE: ",$S('$D(^UTILITY("DIQ1",$J,2,DFN,.323)):"UNSPECIFIED",^(.323)]"":^(.323),1:"UNSPECIFIED")
 W ?40,"  WERE YOU A PRISONER OF WAR?: ",$S('$D(^UTILITY("DIQ1",$J,2,DFN,.525)):"UNSPECIFIED",^(.525)]"":^(.525),1:"UNSPECIFIED")
 K ^UTILITY("DIQ1",$J) W !,"CURRENT MEANS TEST STATUS: " S:SDAR'="A" DGMT=$$LST^DGMTU(DFN) W $S(DGMT']"":"PATIENT NOT IN MEANS TEST FILE",1:$P(DGMT,U,3))
 S DIC="^DPT(DFN,.373," F I=0:0 S I=$O(^DPT(DFN,.373,I)) Q:I'>0  I $D(^(I,0)) S DA=I,DR=0 D EN^DIQ,CK G QUIT:SDFL
 S DIC="^DPT(DFN,.372," F I=0:0 S I=$O(^DPT(DFN,.372,I)) Q:I'>0  I $D(^(I,0)) S DA=I,DR=0 D EN^DIQ,CK G QUIT:SDFL
 S SDBD=SDBD-.1,SDED=SDED+.9 I $P(DGMT,U,4)="R" W !,?17,*7,"** THIS PATIENT REQUIRES A MEANS TEST **"
 D SORT^SDPP I SDEN W !!,"ENROLLMENTS:",! I '$D(^UTILITY($J,"E")) W ?5,"NO ENROLLMENTS FOUND" G PAPT
 F I=0:0 S I=$O(^UTILITY($J,"E",I)) Q:I'>0  F J=0:0 S J=$O(^UTILITY($J,"E",I,J)) Q:J'>0  F K=0:0 S K=$O(^UTILITY($J,"E",I,J,K)) Q:K'>0  S DA=J,DR=0,DIC="^DPT(DFN,""DE""," D EN^DIQ S DA=K,DIC="^DPT(DFN,""DE"",J,1," D EN^DIQ,CK G:SDFL QUIT
PAPT I SDAPT W !!,"APPOINTMENTS:",! W:'$D(^UTILITY($J,"A")) ?5,"NO APPOINTMENTS FOUND" I $D(^("A")) F I=0:0 S I=$O(^UTILITY($J,"A",I)) Q:I'>0  F J=0:0 S J=$O(^UTILITY($J,"A",I,J)) Q:J'>0  S DA=J,DIC="^DPT(DFN,""S""," K DR D EN^DIQ,CK G:SDFL QUIT
 I SDADD W !!,"ADD/EDITS:",! W:'$D(^UTILITY($J,"X")) ?5,"NO ADD/EDITS FOUND" I $D(^("X")) F I=0:0 S I=$O(^UTILITY($J,"X",I)) Q:I'>0  F J=0:0 S J=$O(^UTILITY($J,"X",I,J)) Q:J'>0  S DR="0;CS;PR",DA=J,DIC="^SDV(" D EN^DIQ,CK G:SDFL QUIT
 I SDISP W !!,"DISPOSITIONS:",! W:'$D(^UTILITY($J,"D")) ?5,"NO DISPOSITIONS FOUND" I $D(^("D")) F I=0:0 S I=$O(^UTILITY($J,"D",I)) Q:I'>0  S DR=0,DA=I,DIC="^DPT(DFN,""DIS""," D EN^DIQ,CK G:SDFL QUIT
 I SDMT W !!,"MEANS TESTS:",! W:'$D(^UTILITY($J,"M")) ?5,"NO MEANS TEST DATA FOUND" I $D(^("M")) S I=-9999999 F  S I=$O(^UTILITY($J,"M",I)) Q:'I  D:$O(^(I,0))="" STAT,CK G:SDFL QUIT S J=0 F  S J=$O(^UTILITY($J,"M",I,J)) Q:'J  D MTDEP G:SDFL QUIT
 I $E(IOST)="C",$Y>10 R !,"Hit 'Return' to continue",X:DTIME
QUIT W ! K ^UTILITY($J),%,%DT,DA,DIC,DFN,DGMT,DGPGM,DGVAR,DR,I,J,K,POP,S,SDADD,SDAPT,SDAR,SDBD,SDED,SDEN,SDFL,SDI0,SDIB,SDIE,SDISP,SDMT,SDT,SDUTL,SDYR,X,Y,Z D CLOSE^DGUTQ,KVAR^VADPT Q
CK I $D(DTOUT)!$D(DUOUT) S SDFL=1 K DTOUT,DUOUT Q  ;waiting for future fileman version
 I $Y<(IOSL-5)!($E(IOST)'="C") Q
 R !,"Enter '^' to exit",X:DTIME S:X["^"!'$T SDFL=1 W @IOF Q
TEMP I $P(X,"^",7),($P(X,"^",7)'>SDT),$S($P(X,"^",8)="":1,$P(X,"^",8)'<SDT:1,1:0) S DR=.121 D EN^DIQ,CK S DR=.11 Q
 Q
MTDEP S DR=".02:.13",DA=+^UTILITY($J,"M",I,J),DIC="^DGMT(408.22,",DIQ="SD",DIQ(0)="EN" N I,J D EN^DIQ1
 F SD1=.02:.01:.13 W:$D(SD(408.22,DA,SD1,"E")) !,$P(^DD(408.22,SD1,0),U)_": "_SD(408.22,DA,SD1,"E")
 W ! D CK Q
STAT S SDUTL=^UTILITY($J,"M",I) W !?5,"DATE OF TEST:  " S Y=$P(SDUTL,U,2) D DT^DIQ W ?40,"STATUS:  ",$P(SDUTL,U,3),!
 Q
OTHER W !?2,"OTHER ELIGIBILITIES" D ELIG^VADPT I $D(VAEL(1)) F I=0:0 S I=$O(VAEL(1,I)) Q:I'>0  W ?33,$P(VAEL(1,I),"^",2),!
 I $D(VAEL(1))=1 W ?33,"NONE SPECIFIED",!
 Q
