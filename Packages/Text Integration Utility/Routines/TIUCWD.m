TIUCWD ;SLC/TDP - TIU CWAD POSTING AUTO-DEMOTION ;01/13/15  12:19
 ;;1.0;TEXT INTEGRATION UTILITIES;**291**;Jun 20, 1997;Build 5
 ;
SILENT(TIUFROM,TIUTO) ;
 I +$G(TIUDA)&('+$G(DFN)) D  ;TO GET DFN BASED ON TIUDA IF NOT PRESENT
 . S DFN=+$P($G(^TIU(8925,TIUDA,0)),"^",2)
 Q:'$G(DFN)
 N TIUNDT,TIUNIEN,TIUCLIK
 Q:$G(^TIU(8925.1,+TIUFROM,0))']""
 Q:$G(^TIU(8925.1,+TIUTO,0))']""
 S (TIUNDT,TIUCLIK)=0 F  S TIUNDT=$O(^TIU(8925,"APT",DFN,TIUFROM,7,TIUNDT)) Q:'+TIUNDT  D
 . S TIUNIEN=0 F  S TIUNIEN=$O(^TIU(8925,"APT",DFN,TIUFROM,7,TIUNDT,TIUNIEN)) Q:'+TIUNIEN  D
 .. S TIUCLIK=TIUCLIK+1  Q:(TIUCLIK'>1)  ;LEAVE THE MOST RECENT NOTE
 .. D DEMOTE(TIUNIEN,TIUTO)
 Q
DEMOTE(TIUDA,TIUTO) ;
 N DIE,DR,CHKSUM,LOCKTM
 S LOCKTM=$S(+DILOCKTM>1:DILOCKTM,1:1)
 L +^TIU(8925,TIUDA,0):LOCKTM
 E  D  Q
 . W:$E(IOST,1,2)="C-" !,"Unable to obtain lock for entry "_TIUDA
 S DIE=8925,DA=TIUDA
 S DR=".01////^S X="_TIUTO_";.04////^S X="_$$DOCCLASS^TIULC1(TIUTO)
 D ^DIE
 S CHKSUM=+$$CHKSUM^TIULC("^TIU(8925,"_+TIUDA_",""TEXT"")")
 D AUDIT^TIUEDI1(TIUDA,CHKSUM,CHKSUM)
 L -^TIU(8925,TIUDA,0)
 W:$E(IOST,1,2)="C-" !,"Entry demoted from posting status "_TIUDA
 Q
