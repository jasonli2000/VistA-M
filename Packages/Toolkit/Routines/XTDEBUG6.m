XTDEBUG6 ;JLI/OAK_OIFO- ;06/07/08  17:21
 ;;7.3;TOOLKIT;**107**;Apr 25, 1995;Build 10
 ;;Per VHA Directive 2004-038, this routine should not be modified
 D EN^XTMUNIT("ZZUTXTD1")
 Q
 ;
GETCMND(XTDEBRES,XTDEBCMD) ;
 N XTDEBLIN,XTDEBVAL,XTDEBTYP,XTDEBARG,XTDEBCNT,XTDEBPOS,I,XTDEBLI1
 K XTDEBRES
 D DEBUG^XTMLOG("GETCMND","XTDEBRES,XTDEBCMD")
 S XTDEBCNT=0
 I $E(XTDEBCMD)=";" S XTDEBRES("CMND")=";",XTDEBRES("ARGS",1)=$E(XTDEBCMD,2,$L(XTDEBCMD)) Q ""
 S XTDEBCMD=$$GETSTR(.XTDEBTYP,XTDEBCMD," :")
 ; get one letter version of commands
 S XTDEBRES("CMND")=$E(XTDEBTYP)
 I XTDEBRES("CMND")=";" S XTDEBCNT=XTDEBCNT+1,XTDEBRES("ARGS",XTDEBCNT)=XTDEBCMD
 I XTDEBTYP("TERM")=":" S XTDEBCMD=$$GETSTR(.XTDEBTYP,XTDEBCMD," ") S XTDEBRES("PRECOND")=XTDEBTYP
 I XTDEBTYP("TERM")'=" " Q ""
 S XTDEBPOS=" ,:" I (XTDEBRES("CMND")="N")!(XTDEBRES("CMND")="K")!(XTDEBRES("CMND")="F") S XTDEBPOS=" "
 F  S XTDEBCMD=$$GETSTR(.XTDEBARG,XTDEBCMD,XTDEBPOS) D  Q:XTDEBARG("TERM")=" "  Q:XTDEBARG("TERM")=""
 . S XTDEBCNT=XTDEBCNT+1,XTDEBRES("ARGS",XTDEBCNT)=XTDEBARG
 . I XTDEBARG("TERM")=":" S XTDEBCMD=$$GETSTR(.XTDEBARG,XTDEBCMD," ,"),XTDEBRES("ARGS",XTDEBCNT,"POSTCOND")=XTDEBARG Q:XTDEBARG("TERM")=" "
 . Q
 S XTDEBLI1=""
 ; 070719 - code added to yield one command at a time
 F I=2:1 Q:'$D(XTDEBRES("ARGS",I))  D
 . S:I=2 XTDEBLI1=XTDEBRES("CMND")_$S($D(XTDEBRES("PRECOND")):":"_XTDEBRES("PRECOND"),1:"")_" "
 . S:I>2 XTDEBLI1=XTDEBLI1_"," S XTDEBLI1=XTDEBLI1_XTDEBRES("ARGS",I)_$S($D(XTDEBRES("ARGS",I,"POSTCOND")):":"_XTDEBRES("ARGS",I,"POSTCOND"),1:"")
 . K XTDEBRES("ARGS",I)
 . Q
 I XTDEBLI1'="" S XTDEBCMD=XTDEBLI1_" "_XTDEBCMD
 ; 070719 - end of insertion
 S XTDEBLIN=XTDEBCMD
 ; following line added in case an extra space is present between commands
 F  Q:XTDEBLIN=""  Q:$E(XTDEBLIN)'=" "  S XTDEBLIN=$E(XTDEBLIN,2,$L(XTDEBLIN))
 Q $G(XTDEBLIN)
 ;
GETSTR(XTDEBSTR,XTDEBINP,XTDEBTRM) ;
 N XTDEBOUT,XTDEBDON,XTDEBPRN,XTDEBQUO,XTDEBCHR
 K XTDEBSTR
 S XTDEBOUT=XTDEBINP
 S XTDEBSTR="",XTDEBDON=0,XTDEBQUO=0,XTDEBPRN=0
 F  S XTDEBCHR=$E(XTDEBOUT),XTDEBOUT=$E(XTDEBOUT,2,500) Q:XTDEBCHR=""  D  Q:XTDEBDON
 . I XTDEBTRM[XTDEBCHR,XTDEBPRN=0,XTDEBQUO=0 S XTDEBDON=1 Q
 . I XTDEBCHR="""" S XTDEBQUO='XTDEBQUO
 . I XTDEBCHR="(",'XTDEBQUO S XTDEBPRN=XTDEBPRN+1
 . I XTDEBCHR=")",'XTDEBQUO S XTDEBPRN=XTDEBPRN-1
 . S XTDEBSTR=XTDEBSTR_XTDEBCHR
 . Q
 S XTDEBSTR("TERM")=XTDEBCHR
 Q XTDEBOUT
