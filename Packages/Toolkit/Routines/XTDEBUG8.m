XTDEBUG8 ;JLI/FO_OAK - ;06/07/08  17:10
 ;;7.3;TOOLKIT;**107**;Apr 25, 1995;Build 10
 ;;Per VHA Directive 2004-038, this routine should not be modified
 Q
 ;
SETNOARG ;
 N XTDEBLOC,XTDEBLVL,XTDEBARG
 S XTDEBLOC=$$GETGLOB^XTDEBUG()
 S XTDEBLVL=@XTDEBLOC@("LASTLVL")+1
 S XTDEBARG=+$G(@XTDEBLOC@("LVL",XTDEBLVL,"NOARG"))
 D DEBUG^XTMLOG("SETNOARG","XTDEBARG")
 S @XTDEBLOC@("LASTLVL")=XTDEBLVL
 S @XTDEBLOC@("LVL",XTDEBLVL,"NOARG")=XTDEBARG+1
 S @XTDEBLOC@("LVL",XTDEBLVL,"ROUTINE")=@XTDEBLOC@("LVL",XTDEBLVL-1,"ROUTINE")
 S @XTDEBLOC@("LVL",XTDEBLVL,"LINE")=@XTDEBLOC@("LVL",XTDEBLVL-1,"LINE")
 S @XTDEBLOC@("LVL",XTDEBLVL,"ENTRY")="DONOARG^XTDEBUG8"
 S @XTDEBLOC@("LVL",XTDEBLVL,"TYPE")="DO"
 G DONOARG
 ;
DONOARG ;
 N XTDEBCHR,XTDEBCNT,XTDEBCMD,XTDEBLIN,XTDEBLOC,XTDEBLVL
 N XTDEBUGI
 ; ZEXCEPT: XTDEBARG  -- DEFINED PRIOR TO ENTRY
 S XTDEBLOC=$$GETGLOB^XTDEBUG()
 S XTDEBLVL=@XTDEBLOC@("LASTLVL")
 S XTDEBLIN=@XTDEBLOC@("LVL",XTDEBLVL,"LINE")+1
 S @XTDEBLOC@("LVL",XTDEBLVL,"LINE")=XTDEBLIN
 S XTDEBCMD=$$GETLINE^XTDEBUG(@XTDEBLOC@("LVL",XTDEBLVL,"ROUTINE"),"+"_@XTDEBLOC@("LVL",XTDEBLVL,"LINE"))
 D DEBUG^XTMLOG("DONOARG","XTDEBCMD")
 S @XTDEBLOC@("LVL",XTDEBLVL,"CODE")=XTDEBCMD
 S XTDEBCNT=0
 ; determine number of '.' chars starting line (ignore spaces)
 F XTDEBUGI=1:1 S XTDEBCHR=$E(XTDEBCMD,XTDEBUGI) Q:(XTDEBCHR'=" ")&(XTDEBCHR'=".")  I XTDEBCHR="." S XTDEBCNT=XTDEBCNT+1
 ; set active command line to rest of line
 S XTDEBLIN=$E(XTDEBCMD,XTDEBUGI,$L(XTDEBCMD))
 S @XTDEBLOC@("LVL",XTDEBLVL,"CMND")=XTDEBLIN
 ; higher level of 'no arg DO' skip this line
 I XTDEBCNT>@XTDEBLOC@("LVL",XTDEBLVL,"NOARG") G DONOARG
 ; no or lower level of 'no arg DO' - done with this process
 I XTDEBCNT<@XTDEBLOC@("LVL",XTDEBLVL,"NOARG") G POPLEVEL^XTDEBUG
 ; process line of code
 S @XTDEBLOC@("LVL",XTDEBLVL,"CMND")=$$GETCMND^XTDEBUG(.XTDEBARG,XTDEBLIN)
 K @XTDEBLOC@("LVL",XTDEBLVL,"XTDEBARG")
 M @XTDEBLOC@("LVL",XTDEBLVL,"XTDEBARG")=XTDEBARG
 D DEBUG^XTMLOG("DONOARG1",$NA(@XTDEBLOC@("LVL",XTDEBLVL,"CMND")))
 G STRTCMND^XTDEBUG
 ;
GETPERIO(XTDEBLIN) ; XTDEBLIN passed by reference
 N XTDEBCNT S XTDEBCNT=0
 F  Q:XTDEBLIN=""  Q:" ."_$C(9)'[$E(XTDEBLIN)  S:$E(XTDEBLIN)="." XTDEBCNT=XTDEBCNT+1 S XTDEBLIN=$E(XTDEBLIN,2,$L(XTDEBLIN))
 Q XTDEBCNT
