A1A1FMT0 ;ALB/CAW,PKE - Format data ;4/12/95 [ 02/20/96  12:10 AM ]
 ;;1.2;Prescription Practices Extract;;MAY 1,1996
PROVDATA ;Provider Info
 N A1
 S A1DATA="#",A1RXMM=A1MSG,(A1MSG,A1MSG1)=1,A1CNTR=0
 S A1="" F  S A1=$O(^TMP("A1PROV",$J,A1)) Q:'A1  S A1PROV=$P(^(A1),U) D
 .S A1PINFO(1)=$G(^VA(200,+A1,1))
 .S A1PINFO("QAR")=$G(^VA(200,+A1,"QAR"))
 .S A1PSVC=$$POINTER^A1A1EXT1($P(A1PINFO("QAR"),U),"^ECC(730,",1)
 .;
 .F A1CNT=1:1:3 S A1PSPEC(A1CNT)=$$POINTER^A1A1EXT1($P($G(^VA(200,+A1,"QAR7",A1CNT,0)),U),"^QA(747.9,",1)
 .S A1DATA=$$FRMT^A1A1FMT1($P(A1PROV,U),30,"T",A1DATA,"P") ;Provider Name
 .S A1DATA=$$FRMT^A1A1FMT1($P(A1PINFO(1),U,9),9,"T",A1DATA,"P") ;Provider SSN
 .S A1DATA=$$FRMT^A1A1FMT1(A1PSVC,35,"T",A1DATA,"P") ;Service
 .S A1DATA=$$FRMT^A1A1FMT1(A1PSPEC(1),35,"T",A1DATA,"P") ;Specialty 1
 .S A1DATA=$$FRMT^A1A1FMT1(A1PSPEC(2),35,"T",A1DATA,"P") ;Specialty 2
 .S A1DATA=$$FRMT^A1A1FMT1(A1PSPEC(3),35,"T",A1DATA,"P") ;Specialty 3
 .;
 .S DIV=$$PRIM^VASITE(DT),DIV=$P($G(^DG(40.8,DIV,0)),U,2)
 .I DIV']"" S DIV=+$$SITE^VASITE()
 .S A1DATA=$$FRMT^A1A1FMT1(DIV,5,"T",A1DATA,"P") ;Station Number
 .S A1DATA=$$FRMT^A1A1FMT1("$",1,"T",A1DATA,"P") ;End of record
 .;
 ;
 I $L($G(A1DATA)),A1DATA'="#" D LAST^A1A1FMT1(A1DATA,"P")
 Q
