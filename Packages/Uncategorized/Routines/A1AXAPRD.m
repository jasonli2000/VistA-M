A1AXAPRD ;SLL/ALB ISC; Action Plan RD Approved/Disapproved Triggered Message;12/30/87
 ;;VERSION 1.0
 ;
ENST ;
 D TKL1,TKL2
 S A1AXQ=$N(^XMB(3.8,"B","EXT REV FAC",0))
 S A1AXRC="" F A1AXI=0:0 S A1AXRC=$O(^XMB(3.8,A1AXQ,1,"B",A1AXRC)) Q:A1AXRC=""!(A1AXRC'?.N)  S A1AXFC=0 F A1AXJ=0:0 S A1AXFC=$O(^DIC(3,A1AXRC,2,A1AXFC)) Q:A1AXFC'?.N!(A1AXFC="")  D:A1AXFC=^DIZ(11830,D0,"F") SENT
 G EXIT
ENRD ;
 D TKL1,TKL2
 S A1AXQ=$N(^XMB(3.8,"B","EXT REV RD",0))
 S A1AXRC="" F A1AXI=0:0 S A1AXRC=$O(^XMB(3.8,A1AXQ,1,"B",A1AXRC)) Q:A1AXRC=""!(A1AXRC'?.N)  D SENT
EXIT ;
 K ^UTILITY($J,"AC"),P,S,R,Y,XMDUZ,XMY,XMSUB,XMTXT,A1AXNM,A1AXRC,A1AXQ,A1AXFC,A1AXI,A1AXJ,I,A1AXR1,A1AXY
 Q
TKL1 ;
 S P=+^DIZ(11830,D0,"P",D1,0),S=+^("S",D2,0),R=+^("R",D3,0)
MSG K ^UTILITY($J,"AC")
 S ^UTILITY($J,"AC",1.2,0)=" "
 S A1AXF=$S($D(^DIC(4,^DIZ(11830,D0,"F"),0)):$P(^(0),"^",1),1:"")
 S ^UTILITY($J,"AC",2.5,0)=""
 S Y=$P(^DIZ(11830,D0,0),"^",1) X ^DD("DD") S ^UTILITY($J,"AC",6,0)="Ext Rev Date is "_Y
 S Y=+^DIZ(11830,D0,"O"),^UTILITY($J,"AC",7,0)="Ext Rev Organization is "_$P(^DIZ(11831,Y,0),"^",1)
 S Y=$S($D(^DIZ(11832,P,0)):$P(^(0),"^",1),1:""),^UTILITY($J,"AC",8,0)="Program Surveyed is "_Y
 S Y=$S($D(^DIZ(11835,S,0)):$P(^(0),"^",2),1:""),^UTILITY($J,"AC",9,0)="Service/Section is "_Y
 S ^UTILITY($J,"AC",10,0)="Surveyer Recommend No. is "_R
 S ^UTILITY($J,"AC",11,0)=" "
 S ^UTILITY($J,"AC",12,0)="Recommendation:"
 S A1AXR1=0 F I=1:1 S A1AXR1=$O(^DIZ(11830,D0,"P",D1,"S",D2,"R",D3,1,A1AXR1)) Q:A1AXR1=""  S ^UTILITY($J,"AC",12+I,0)=^(A1AXR1,0)
 S ^UTILITY($J,"AC",100,0)=" "
 S ^UTILITY($J,"AC",101,0)="Action Plan:"
 S A1AXR1=0 F I=1:1 S A1AXR1=$O(^DIZ(11830,D0,"P",D1,"S",D2,"R",D3,10,A1AXR1)) Q:A1AXR1=""  S ^UTILITY($J,"AC",101+I,0)=^(A1AXR1,0)
 S ^UTILITY($J,"AC",200,0)=" "
 Q
TKL2 ;
 S ^UTILITY($J,"AC",201,0)="RD COMMENT:"
 S A1AXR1=0 F I=1:1 S A1AXR1=$O(^DIZ(11830,D0,"P",D1,"S",D2,"R",D3,6,A1AXR1)) Q:A1AXR1=""  S ^UTILITY($J,"AC",201+I,0)=^(A1AXR1,0)
 S ^UTILITY($J,"AC",300,0)=" "
 S (A1AXY,Y)="" I $D(^DIZ(11830,D0,"P",D1,"S",D2,"R",D3,9)) S A1AXY=$P(^(9),"^",1),Y=$P(^(9),"^",2) X ^DD("DD") S:A1AXY="A" A1AXY="APPROVED" S:A1AXY="D" A1AXY="DISAPPROVED"
 S ^UTILITY($J,"AC",301,0)="RD APPROVAL/DISAPPROVAL: "_A1AXY,^UTILITY($J,"AC",302,0)="RD APPROVAL/DISAPPROVAL DATE: "_Y
 S ^UTILITY($J,"AC",2,0)="RD has "_A1AXY_" the following ACTION PLAN at VAMC "_A1AXF
 S XMTEXT="^UTILITY($J,""AC"","
 Q
SENT ;
 K XMY S XMY(A1AXRC)="" S XMDUZ=.5,XMSUB="**ACTION PLAN RD "_A1AXY_" AT "_A1AXF_"**"
 D ^XMD K XMY
 Q
