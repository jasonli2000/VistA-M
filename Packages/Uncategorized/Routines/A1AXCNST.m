A1AXCNST ;SLL/ALB ISC; JCAHO CONTINGENCY REPORT SET DATA; 12/30/87
 ;;VERSION 1.0
 ;
 D INIT,FDJCAH,FDDATE,COUNT
 Q
INIT ;
 S (NFAC,N)=0
 S NCAT=$P(^DIZ(11834,0),"^",4)
 F I=1:1:NCAT+2 S $P(TOT1,"^",I)="",$P(TOT2,"^",I)=""
INLOC F I=0:0 S N=$O(^DIZ(11837,N)) Q:N=""!(N'?.N)  I N<1000 S ^UTILITY($J,"CN",N)=$P(^DIZ(11837,N,0),"^",2)_"^"_$P(^(0),"^",1)_"^ -" S NFAC=NFAC+1
 Q
FDJCAH ;FIND INTERNAL CODE FOR JCAH 
 S Y=$N(^DIZ(11831,"B","JCAHO",0))
 Q
FDDATE ;DRIVER TO FIND LATEST JCAH FOR EACH FACILITY 
 S N=0
 I $D(^DIZ(11830,"O",Y)) F I=0:0 S N=$O(^DIZ(11830,"O",Y,N)) Q:N=""  D SKIM
 Q
SKIM ;GOES THRU JCAH RECORDS AND STORES DATE TO FACILITY IN LOC. TABLE - LAST STORED IS LATEST
 Q:'$D(^DIZ(11830,N,0))
 
 S F=^DIZ(11830,N,"F")
 I ^DIZ(11830,N,0)>$P(^UTILITY($J,"CN",F),"^",3) S $P(^UTILITY($J,"CN",F),"^",3)=^DIZ(11830,N,0) S $P(^UTILITY($J,"CN",F),"^",4)=N
 Q
COUNT ;DRIVER TO COUNT "C" AND "RC" JCAH CONTINGENCIES & STORE IN TABLE
 S (REC,PRG,SCT,RCM)=0
 D SETREC
 Q
SETREC ;$O THROUGH LOC TO GET LAST SURVEY DATE
 F FAC=0:0 S FAC=$O(^UTILITY($J,"CN",FAC)) Q:FAC=""!(FAC'?.N)  I $L($P(^UTILITY($J,"CN",FAC),"^",4))>0 S REC=$P(^UTILITY($J,"CN",FAC),"^",4) D SETPRG
 Q
SETPRG ;$O THROUGH ^DIZ(11830) TO GET PRGS FOR REC.
 F PRG=0:0 S PRG=$O(^DIZ(11830,REC,"P",PRG)) Q:PRG=""!(PRG'?.N)  D SETSCT
 Q
SETSCT ;$O THROUGH ^DIZ(11830) TO GET SECTIONS FOR PRG FOR REC
 F SCT=0:0 S SCT=$O(^DIZ(11830,REC,"P",PRG,"S",SCT)) Q:SCT=""!(SCT'?.N)  D SETRCM
 Q
SETRCM ;$O THROUGH ^DIZ(11830) TO GET RECOMMENDATIONS PER SECT PER PRG PER RECORD
 F RCM=0:0 S RCM=$O(^DIZ(11830,REC,"P",PRG,"S",SCT,"R",RCM)) Q:RCM=""!(RCM'?.N)  D TALLEY
 Q
TALLEY ;SET UP LOC ARRAY AND TOT1 TO COUNT C, RC AND +
 I $D(^DIZ(11830,REC,"P",PRG,"S",SCT,"R",RCM,13)),$L($P(^(13),"^",2))>0 S CAT=$P(^(13),"^",2),FAC=^DIZ(11830,REC,"F")
 S A1AXCONT=$P(^DIZ(11830,REC,"P",PRG,"S",SCT,"R",RCM,0),"^",2)
 D @$S(A1AXCONT="C":"SETC",A1AXCONT="RC":"SETRC",A1AXCONT="+":"SETPLUS",1:"OTHER")
 Q
SETC ;SETS CONTINGENCY NODE
 I '$D(^UTILITY($J,"CN",FAC,"C")) F I=1:1:NCAT+1 S $P(^UTILITY($J,"CN",FAC,"C"),"^",I)=""
 S $P(^UTILITY($J,"CN",FAC,"C"),"^",CAT+1)=$P(^UTILITY($J,"CN",FAC,"C"),"^",CAT+1)+1
 S $P(TOT1,"^",CAT+4)=$P(TOT1,"^",CAT+4)+1,$P(TOT1,"^",1)=$P(TOT1,"^",1)+1,$P(TOT1,"^",2)=$P(TOT1,"^",2)+1
 Q
SETRC ;SETS REPEAT CONTINGENCY NODE
 I '$D(^UTILITY($J,"CN",FAC,"RC")) F I=1:1:NCAT+1 S $P(^UTILITY($J,"CN",FAC,"RC"),"^",I)=""
 S $P(^UTILITY($J,"CN",FAC,"RC"),"^",CAT+1)=$P(^UTILITY($J,"CN",FAC,"RC"),"^",CAT+1)+1
 S $P(TOT1,"^",CAT+4)=$P(TOT1,"^",CAT+4)+1,$P(TOT1,"^",1)=$P(TOT1,"^",1)+1,$P(TOT1,"^",3)=$P(TOT1,"^",3)+1
 Q
SETPLUS ;SETS PRIORITY NODE
 I '$D(^UTILITY($J,"CN",FAC,"PLUS")) F I=1:1:NCAT+1 S $P(^UTILITY($J,"CN",FAC,"PLUS"),"^",I)=""
 S $P(^UTILITY($J,"CN",FAC,"PLUS"),"^",CAT+1)=$P(^UTILITY($J,"CN",FAC,"PLUS"),"^",CAT+1)+1
 S $P(TOT1,"^",CAT+4)=$P(TOT1,"^",CAT+4)+1,$P(TOT1,"^",1)=$P(TOT1,"^",1)+1,$P(TOT1,"^",4)=$P(TOT1,"^",4)+1
 Q
OTHER ;
 Q
