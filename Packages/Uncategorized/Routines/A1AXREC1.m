A1AXREC1 ;SLL/ALB ISC; PRINT ROUTINE FOR GIVEN RECOMMEND; 3/10/88
 ;;VERSION 1.0
 ;
DEVICE ;
 S %IS("B")="HOME",%IS="MQF" D ^%ZIS G:POP EXIT1 I '$D(IO("Q")) U IO G DQ
 S ZTRTN="DQ^A1AXREC1",ZTIO=ION S ZTSAVE("A1AX*")="" S ZTDESC="EXT REV PRINT RECOM" D ^%ZTLOAD W !,"PRINT QUEUED!" X ^%ZIS("C")
 K IO("Q"),ZTRTN,ZTIO,ZTDESC,ZTSAVE,ZTSK
 K A1AXDA,A1AXDA1,A1AXDA2,A1AXDA3,A1AX,A1AXF,A1AXO,A1AXDT,A1AXSR,A1AXPR,A1AXUD
 Q
DQ ;
 D CLOSE^A1AXUTL
 S A1AXDT=+^DIZ(11830,A1AXDA,0),A1AXF=^("F"),A1AXO=^("O")
DATE W #,!!,"EXT REV DATE:  ",$E(A1AXDT,4,5)_"/"_$E(A1AXDT,6,7)_"/"_$E(A1AXDT,2,3)
FAC W "     FACILITY:  ",$P(^DIZ(11837,A1AXF,0),"^",1)
ORG W "     ORG:  ",$P(^DIZ(11831,A1AXO,0),"^",1)
UPDATE ;
 W !,"   DATE OF UPDATE: " I $D(^DIZ(11830,A1AXDA,"UD")) S A1AXUD=^("UD") W $E(A1AXUD,4,5)_"/"_$E(A1AXUD,6,7)_"/"_$E(A1AXUD,2,3)
PERSON W "   ENTERING PERSON: " I $D(^DIZ(11830,A1AXDA,"US")) W ^("US")
PGM W !,"   TYPE OF PGM: " S X=^DIZ(11830,A1AXDA,"P",A1AXDA1,0),A1AXPR=$P(X,"^",2),X=$P(^DIZ(11832,+X,0),"^",1) W X
PGMRT W "     PGM RATING:  "_$S(A1AXPR="A":"ACCREDITED",A1AXPR="N":"NON-ACCREDITED",1:"")
SERV W !,"   SERVICE: " S X=^DIZ(11830,A1AXDA,"P",A1AXDA1,"S",A1AXDA2,0),A1AXSR=$P(^(0),"^",2),X=$P(^DIZ(11835,+X,0),"^",2) W X
SERVRT W "     SERVICE RATING:  "_$S(A1AXSR="A":"ACCEPTABLE",A1AXSR="U":"UNACCEPTABLE",1:"")
SET S X=^DIZ(11830,A1AXDA,"P",A1AXDA1,"S",A1AXDA2,"R",A1AXDA3,0) S:$D(^(2)) X2=^(2) S:$D(^(5)) X5=^(5) S:$D(^(7)) X7=^(7) S:$D(^(9)) X9=^(9) S:$D(^(11)) X11=^(11) S:$D(^(13)) X13=^(13)
REC W !,"RECOMMEND NO:  ",$P(X,"^",1),"        FLAG:  ",$P(X,"^",2),"        STANDARD REF:  ",$P(X,"^",4)
RESP ;
 W !,"   TYPE OF RESPONSE:  " S A1AX=0 F I=0:0 S A1AX=$O(DIZ(11830,A1AXDA,"P",A1AXDA1,"S",A1AXDA2,"R",A1AXDA3,14,A1AX)) Q:A1AX=""  W $S(^(A1AX,0)="P":"  PAPER",^(A1AX,0)="S":"  SURVEY",^(A1AX,0)="O":"  OTHER",1:"")
CAT ;
 W "    CATEGORY OF RECOMMEND:  " I $D(X13) W $P(^DIZ(11834,$P(X13,"^",2),0),"^",1)
PARA W !,"   PARA RECOM: " I $D(X11) W $P(X11,"^",5)
RECOM W !,"   RECOMMENDATION:  " S A1AX=0 F I=0:0 S A1AX=$O(^DIZ(11830,A1AXDA,"P",A1AXDA1,"S",A1AXDA2,"R",A1AXDA3,1,A1AX)) Q:A1AX=""  W !,^(A1AX,0)
APPEAL W !,"APPEAL BY FACILITY: " W:$D(X2) $P(X2,"^",1)
 W "       APPEAL DATE: " I $D(X2) I $P(X2,"^",2)'="" S A1AX=$P(X2,"^",2) W $E(A1AX,4,5)_"/"_$E(A1AX,6,7)_"/"_$E(A1AX,2,3)
 W !,"   APPEAL RESULT:" I $D(X2) S A1AX=0 F I=0:0 S A1AX=$O(^DIZ(11830,A1AXDA,"P",A1AXDA1,"S",A1AXDA2,"R",A1AXDA3,4,A1AX)) Q:A1AX=""  W !,^(A1AX,0)
 G ^A1AXREC2
EXIT1 ;
 K A1AXDA,A1AXDA1,A1AXDA2,A1AXDA3,A1AX,A1AXSR,A1AXPR,A1AXUD,IO("Q")
 Q
