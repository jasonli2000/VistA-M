A1AXTAR ;SLL/ALB ISC; FACILITY TARGET DATE REPORT FOR ALL OR SELECTED FACILITIES; 1/15/88
 ;;VERSION 1.0
 ;
 I DUZ(2)'>999 S A1AXALL="N" G SEL
PROMPT ;
 I DUZ(2)>999 W !,"ALL FACILITIES?" S %=1 D YN^DICN W:%Y["?" !,"    ANSWER 'YES' OR 'NO'" G:%=0 PROMPT G:%<1 EXIT1 S A1AXALL=$S(%=1:"Y",%=2:"N") G @$S(%=1:"ALL",%=2:"SEL")
ALL ;All Facilities
 G STA
DQALL ;
 D CLOSE^A1AXUTL
 S IK="" F I=0:0 S IK=$O(^DIZ(11837,"B",IK)) Q:IK=""  S A1AXNM=IK,A1AXST=$N(^DIZ(11837,"B",IK,0)) W # D INIT,HD^A1AXTAR1,LOOP^A1AXTAR1,BOTTOM^A1AXTAR1,STAT^A1AXTAR1,BOTTOM^A1AXTAR1 D WAIT^A1AXUTL G:$D(A1AXEND) EXIT
 G EXIT
 ;
SEL ;Selected Facility
 S DIC="^DIZ(11837,",DIC(0)="AEQZMN" S:DUZ(2)<999 DIC("B")=$P(^DIZ(11837,DUZ(2),0),"^",1) S:DUZ(2)<999 DIC("S")="I +Y=DUZ(2)" D ^DIC G:X["^" EXIT1 Q:Y<0  K DIC S A1AXNM=$P(Y,"^",2),A1AXST=+Y
 G STA
DQSEL ;
 D CLOSE^A1AXUTL
 D INIT,HD^A1AXTAR1,LOOP^A1AXTAR1,BOTTOM^A1AXTAR1,STAT^A1AXTAR1,BOTTOM^A1AXTAR1
 G EXIT
 ;
INIT ;
 S (A1AXC,A1AXX,A1AXI,A1AXNN)=0
 Q
STA ; STATUS
 R !,"SELECT STATUS: ALL// ",X:DTIME D:X["?" LST G:X["^" EXIT1 S:X="" X="A" S A1AXSTU=$E(X,1) G:X'["A"&(X'["C")&(X'["I")&(X'["X")&(X'["N") STA
 S A1AXSTU=$E(X,1)
DATE ;DATE RANGE
DATE1 S %DT("A")="BEGINNING TARGET DATE: ",%DT="AEP" D ^%DT G:X["^" STA G:Y<0 DATE1
 S A1AXLDT=Y
DATE2 S %DT("A")="ENDING TARGET DATE:  ",%DT("B")="T" D ^%DT K %DT("B") G:X["^" DATE1 G:Y<0 DATE2
 S A1AXUDT=Y
DEVICE ;
 S %IS("B")="HOME",%IS="MQF" D ^%ZIS G:POP EXIT1 I '$D(IO("Q")) U IO G @$S(A1AXALL["Y":"DQALL",A1AXALL["N":"DQSEL")
 S ZTRTN=$S(A1AXALL["Y":"DQALL^A1AXTAR",A1AXALL["N":"DQSEL^A1AXTAR"),ZTIO=ION S ZTSAVE("A1AX*")="" S ZTDESC="EXT REV TARGET DATE REPORT" D ^%ZTLOAD W !,"PRINT QUEUED!" X ^%ZIS("C")
 K IO("Q"),ZTRTN,ZTIO,ZTDESC,ZTSAVE,X,A1AXNM,A1AXST,A1AXL,A1AXALL,DT("A"),ZTSK,POP,DT("B"),A1AXUDT,A1AXSTU,AU,AA,AP,AS,AR,A1AXO,A1AXLDT,A1AXC,A1AXI,A1AXX,A1AXNN,%,^UTILITY($J,"TAR")
 Q
LST ;
 W !,"SELECT FROM:",!,?10,"A FOR ALL",!,?10,"C FOR COMPLETE",!,?10,"I FOR IN PROCESS",!,?10,"X FOR CANCELLED",!,?10,"N FOR NON-CONCURRANCE"
 Q
EXIT ;
 D EXIT1,CLOSE1^A1AXUTL Q
EXIT1 K AU,AA,AP,AS,AR,A1AXNM,A1AXST,A1AXSTU,X,A1AXO,A1AXL,A1AXUDT,A1AXLDT,A1AXC,A1AXX,A1AXI,A1AXNN,A1AXALL,A1AXSTOP,A1AXEND,%DT("A"),%DT("B"),IO("Q")
 K %DAT,%DAT1,%DT,C,POP,Y,A1AXY,^UTILITY($J,"TAR")
 Q
