A1AXTAR1 ;SLL/ALB ISC; FACILITY TARGET DATE REPORT PRINT RPOCESSING; 1/15/88
 ;;VERSION 1.0
 ;
HD ;
 S X="EXT REV TARGET DATE REPORT" W #,!!,?(IOM-$L(X)\2),X S %DT=+$H D %CDS^%H S A1AXY=$P(%DAT1,"-",2)_" "_$P(%DAT1,"-",1)_",19"_$P(%DAT1,"-",3)
 S $P(A1AXL,"=",IOM+1)=""
 D TDATES^A1AXUTL
 W !,"VAMC: ",A1AXNM W ?(IOM-$L(A1AXY)),A1AXY
 W !,A1AXL
 W !,"TARGET    REVIEW    REVIEW  PROGRAM   SERVICE   RECOM    FLAG  STATUS    RESPONSIBLE          PARAPHRASED"
 W !,"DATE      DATE      ORGAN   TYPE                NUMBER                   PERSON               RECOM"
 W !,A1AXL
 Q
LOOP ;
 F AU=0:0 Q:$D(A1AXEND)  S AU=$N(^DIZ(11830,"TARGET",AU)) Q:AU<0  F AA=0:0 Q:$D(A1AXEND)  S AA=$N(^DIZ(11830,"TARGET",AU,AA)) Q:AA<0   F AP=0:0 Q:$D(A1AXEND)  S AP=$N(^DIZ(11830,"TARGET",AU,AA,AP)) Q:AP<0  D LOOP1
 Q
LOOP1 ;
 F AS=0:0 S AS=$N(^DIZ(11830,"TARGET",AU,AA,AP,AS)) Q:AS<0  F AR=0:0 S AR=$N(^DIZ(11830,"TARGET",AU,AA,AP,AS,AR)) Q:AR<0  I A1AXSTU["A"!(A1AXSTU[$P(^DIZ(11830,AA,"P",AP,"S",AS,"R",AR,11),"^",2)) D PR
 Q
PR ;
 I DUZ(2)>999&(DUZ(2)<9999) Q:'$D(^DIZ(11830,AA,"D"))  Q:$P(^("D"),"^",1)=""
 Q:AU>A1AXUDT!(AU<A1AXLDT)  Q:A1AXST'=^DIZ(11830,AA,"F")
 I $Y+3>IOSL D WAIT^A1AXUTL Q:$D(A1AXEND)  D HD
TARGET S X=$E(AU,4,5)_"/"_$E(AU,6,7)_"/"_$E(AU,2,3) W !,X
RDATE S X=^DIZ(11830,AA,0),X=$E(X,4,5)_"/"_$E(X,6,7)_"/"_$E(X,2,3) W ?10,X
REVIEW S X=^DIZ(11830,AA,"O"),A1AXO=$P(^DIZ(11831,+X,0),"^",1)
 W ?20,A1AXO
PGM I $D(^DIZ(11830,AA,"P",AP,0)) S X=^(0),X=$P(^DIZ(11832,+X,0),"^",1)
 W ?28,X
SERVICE I $D(^DIZ(11830,AA,"P",AP,"S",AS,0)) S X=^(0),X=$P(^DIZ(11835,+X,0),"^",1)
 W ?38,X
RECOM I $D(^DIZ(11830,AA,"P",AP,"S",AS,"R",AR,0)) S X=$P(^(0),"^",1),Y=$P(^(0),"^",2) W ?48,X
FLAG W ?57,Y
STATUS S X=$P(^DIZ(11830,AA,"P",AP,"S",AS,"R",AR,11),"^",2),X=$S(X["C":"COMPL",X["X":"CANCEL",X["I":"IN PROC",X["N":"NO-CON",1:"")
 S:X["COM" A1AXC=A1AXC+1 S:X["CANC" A1AXX=A1AXX+1 S:X["IN PROC" A1AXI=A1AXI+1 S:X["NO-" A1AXNN=A1AXNN+1
 W ?63,X
PERSON I $D(^DIZ(11830,AA,"P",AP,"S",AS,"R",AR,11)) S X=$P(^(11),"^",4) W ?73,$E(X,1,19) I $L(X)>19 S A1AXPS=$E(X,20,38)
PARA S X=$P(^DIZ(11830,AA,"P",AP,"S",AS,"R",AR,11),"^",5) S X="-"_X W ?94,$E(X,1,38)
 I $D(A1AXPS)!($L(X)>38) W ! W:$D(A1AXPS) ?73,A1AXPS W:$L(X)>38 ?94,$E(X,39,76) K A1AXPS
 Q
BOTTOM ;
 W !,A1AXL Q
STAT ;
 I A1AXSTU["A"!(A1AXSTU["C") W !,"TOTAL NO WITH STATUS 'COMPLETE': ",A1AXC
 I A1AXSTU["A"!(A1AXSTU["X") W !,"TOTAL NO WITH STATUS 'CANCELED': ",A1AXX
 I A1AXSTU["A"!(A1AXSTU["I") W !,"TOTAL NO WITH STATUS 'IN PROCESS': ",A1AXI
 I A1AXSTU["A"!(A1AXSTU["N") W !,"TOTAL NO WITH STATUS 'NON-CONCUR': ",A1AXNN
 Q
