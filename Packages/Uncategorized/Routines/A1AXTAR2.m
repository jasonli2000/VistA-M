A1AXTAR2 ;SLL/ALB ISC; FACILITY TARGET DATE REPORT PRINT RPOCESSING; 1/15/88
 ;;VERSION 1.0
 ;
HD ;
 S X="EXT REV TARGET DATE REPORT BY REVIEW ORGAN" W #,!!,?(IOM-$L(X)\2),X S %DT=+$H D %CDS^%H S A1AXY=$P(%DAT1,"-",2)_" "_$P(%DAT1,"-",1)_",19"_$P(%DAT1,"-",3)
 S $P(A1AXL,"=",IOM+1)=""
 D TDATES^A1AXUTL
 W !,"VAMC: ",A1AXNM W ?(IOM-$L(A1AXY)),A1AXY
 W !,A1AXL
 W !,"REVIEW    TARGET    REVIEW   PROGRAM  SERVICE   RECOM    FLAG  STATUS   RESPONSIBLE          PARAPHRASED"
 W !,"ORGAN     DATE      DATE     TYPE               NUMBER                  PERSON               RECOM"
 W !,A1AXL
 Q
LOOP ;
 S U="^",AC=1
 K ^UTILITY($J,"TAR")
 F AU=0:0 S AU=$N(^DIZ(11830,"TARGET",AU)) Q:AU<0  F AA=0:0 S AA=$N(^DIZ(11830,"TARGET",AU,AA)) Q:AA<0  F AP=0:0 S AP=$N(^DIZ(11830,"TARGET",AU,AA,AP)) Q:AP<0  D LOOP1
 Q
LOOP1 ;
 F AS=0:0 S AS=$N(^DIZ(11830,"TARGET",AU,AA,AP,AS)) Q:AS<0  F AR=0:0 S AR=$N(^DIZ(11830,"TARGET",AU,AA,AP,AS,AR)) Q:AR<0  I A1AXSTU["A"!(A1AXSTU[$P(^DIZ(11830,AA,"P",AP,"S",AS,"R",AR,11),"^",2)) D PR
 Q
PR ;
 I DUZ(2)>999&(DUZ(2)<9999) Q:'$D(^DIZ(11830,AA,"D"))  Q:$P(^("D"),"^",1)=""
 Q:AU>A1AXUDT!(AU<A1AXLDT)  Q:A1AXST'=^DIZ(11830,AA,"F")
TARGET S A1AXTAR=$E(AU,4,5)_"/"_$E(AU,6,7)_"/"_$E(AU,2,3)
RDATE S X=^DIZ(11830,AA,0),A1AXDT=$E(X,4,5)_"/"_$E(X,6,7)_"/"_$E(X,2,3)
REVIEW S X=^DIZ(11830,AA,"O"),A1AXO=$P(^DIZ(11831,+X,0),"^",1)
PGM I $D(^DIZ(11830,AA,"P",AP,0)) S X=^(0),A1AXPG=$P(^DIZ(11832,+X,0),"^",1)
SERVICE I $D(^DIZ(11830,AA,"P",AP,"S",AS,0)) S X=^(0),A1AXSR=$P(^DIZ(11835,+X,0),"^",1)
RECOM I $D(^DIZ(11830,AA,"P",AP,"S",AS,"R",AR,0)) S A1AXR=$P(^(0),"^",1),A1AXFG=$P(^(0),"^",2)
STATUS S X=$P(^DIZ(11830,AA,"P",AP,"S",AS,"R",AR,11),"^",2),A1AXSTT=$S(X["C":"COMPL",X["X":"CANCEL",X["I":"IN PROC",X["N":"NO-CON",1:"")
 S:A1AXSTT["COM" A1AXC=A1AXC+1 S:A1AXSTT["CANC" A1AXX=A1AXX+1 S:A1AXSTT["IN PROC" A1AXI=A1AXI+1 S:A1AXSTT["NO-" A1AXNN=A1AXNN+1
PERSON I $D(^DIZ(11830,AA,"P",AP,"S",AS,"R",AR,11)) S A1AXPS=$P(^(11),"^",4)
PARA S X=$P(^DIZ(11830,AA,"P",AP,"S",AS,"R",AR,11),"^",5) S A1AXPAR=X
 S ^UTILITY($J,"TAR",A1AXO,AU,AA,AC)=A1AXO_U_A1AXTAR_U_A1AXDT_U_A1AXPG_U_A1AXSR_U_A1AXR_U_A1AXFG_U_A1AXSTT_U_A1AXPS_U_A1AXPAR,AC=AC+1
 Q
PRT ;
 S A1AXO="" F I=0:0 Q:$D(A1AXEND)  S A1AXO=$O(^UTILITY($J,"TAR",A1AXO)) Q:A1AXO=""  S AU="" F J=0:0 Q:$D(A1AXEND)  S AU=$O(^UTILITY($J,"TAR",A1AXO,AU)) Q:AU=""  D PRT1
 Q
PRT1 ;
 S AA="" F K=0:0 Q:$D(A1AXEND)  S AA=$O(^UTILITY($J,"TAR",A1AXO,AU,AA)) Q:AA=""  S AC="" F II=0:0 Q:$D(A1AXEND)  S AC=$O(^UTILITY($J,"TAR",A1AXO,AU,AA,AC)) Q:AC=""  D WR
 Q
WR ;
 I $Y+3>IOSL D WAIT^A1AXUTL Q:$D(A1AXEND)  D HD
 W !,$P(^UTILITY($J,"TAR",A1AXO,AU,AA,AC),U,1),?10,$P(^(AC),U,2),?20,$P(^(AC),U,3) W:A1AXO="JCAHO" ?29,$P(^(AC),U,4) W ?38,$P(^(AC),U,5),?48,$P(^(AC),U,6) W ?57,$P(^(AC),U,7) W ?63,$P(^(AC),U,8)
 S X=$P(^(AC),U,9),Y=$P(^(AC),U,10) W ?72,$E(X,1,19) S:$L(X)>19 A1AXPS=$E(X,20,38) W ?93,"-"_$E(Y,1,38) S:$L(Y)>38 A1AXPA=$E(Y,39,76)
 W:$D(A1AXPS)!($D(A1AXPA)) ! W:$D(A1AXPS) ?72,A1AXPS W:$D(A1AXPA) ?93,A1AXPA K A1AXPA,A1AXPS
 Q
