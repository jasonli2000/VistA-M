A1BFEX1 ;ALBANY ISC ; ECF ; 14DEC92
 ;;V1.0
 ;;ROUTINE 1 OF EXCEPTION E-MAIL MESSAGE
EN ;
 D SETUP I OUT D EXIT Q
 D ^A1BFEX0
 D LOOP
 D EN^A1BFEX2
 D:$D(^UTILITY($J,"EXP")) EN^A1BFEX3
 D SEND^A1BFEX2
 D EXIT Q
SETUP ;Setup needed variables - first date, last date
 K ^UTILITY($J)
 S U="^",OUT=0 Q:'$D(^A1BF(11603))  S (A1BFP1,A1BFP)=$O(^A1BF(11603,"DR",0)) I A1BFP="" S OUT=1 Q
 I '$D(^A1BF(11604,1,0)) S OUT=1 Q
 S %DT="T",X="NOW" D ^%DT S A1BFLDT=Y D DD^%DT S A1BFLDTP=Y
 S (Y,A1BFFDT)=$S($P(^A1BF(11604,1,0),U,3)]"":$P(^(0),U,3),1:(A1BFP-.000001)) D DD^%DT S A1BFFDTP=Y
 S A1BFSPAC="                                     "
WHO ;
 I '$D(^A1BF(11604,1,2,0)) D NOBODY S OUT=1 Q
 I $O(^A1BF(11604,1,2,0))="" D NOBODY S OUT=1 Q
 Q
LOOP ;Major loop - look tru site r/t's for new submissions
 S A1BFP=A1BFFDT F  S A1BFP=$O(^A1BF(11603,"DR",A1BFP)) Q:A1BFP=""  D
 .S A1BFQ=0 F  S A1BFQ=$O(^A1BF(11603,"DR",A1BFP,A1BFQ)) Q:A1BFQ=""  D
 ..S A1BFR=0 F  S A1BFR=$O(^A1BF(11603,"DR",A1BFP,A1BFQ,A1BFR)) Q:A1BFR=""  D
 ...Q:'$D(^A1BF(11603,A1BFQ,1,A1BFR,0))
 ...K:$D(^UTILITY($J,"EXP",A1BFQ)) ^(A1BFQ)
 ...; REMOVED 1/20/92 EF I $P(^A1BF(11603,A1BFQ,1,A1BFR,0),U,2)>1 D
 ...D
 ....S A1BFSP=$S($D(^A1BF(11603,A1BFQ,0)):$P(^(0),U,1),1:"UNKNOWN")
 ....S:$D(^A1BF(11603,A1BFQ,1,A1BFR,0)) A1BFRT=$P(^(0),U,2),A1BFAU=$P(^(0),U,3),A1BFMU=$P(^(0),U,4)
 ....S:A1BFSP']""!(A1BFSP="UNKNOWN") (A1BFSP,A1BFSITE)="UNKNOWN"
 ....S:A1BFSP'="UNKNOWN" A1BFSITE=$S($D(^DIC(4,A1BFSP,0)):$P(^(0),U,1),1:"UNKNOWN")
 ....S A1BFSIT1=$E(($S(A1BFSITE']"":"UNKNOWN",1:A1BFSITE)_A1BFSPAC),1,25)
 ....S ^UTILITY($J,"EXC",A1BFR,A1BFSITE)=$S($P(^A1BF(11603,A1BFQ,1,A1BFR,0),U,2)>1:"*",1:" ")_A1BFSIT1_$J(A1BFRT,9,2)_$J(A1BFAU,21,0)_$J(A1BFMU,17,0)
 Q
EXIT ;
 K A1BFDTLP,A1BFFDT,A1BFFDTP,A1BFI,A1BFLDT,A1BFLDTP,A1BFML,A1BFP,A1BFPR,A1BFP1,A1BFQ,A1BFR,A1BFSITE,A1BFSIT1,A1BFSP,A1BFSPAC,DO,D1,DA,DI,DIC
 K ^UTILITY($J)
 Q
NOBODY ;
 S ^UTILITY($J,"NONAMES",1)="There are no recipients named in the A1BF ISC Site Parameters file"
 S ^UTILITY($J,"NONAMES",2)=" for the RT Exception Report"
 S XMY(.5)="",XMDUZ=.5,XMSUB="Missing RT Exception Report Recipients",XMTEXT="^UTILITY($J,""NONAMES"","
 D ^XMD K XMY,XMTEXT,XMSUB,XMDUZ
 Q
