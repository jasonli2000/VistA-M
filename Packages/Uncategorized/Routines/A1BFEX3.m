A1BFEX3 ;ALBANY ISC ; ECF ; 17DEC92 1235 [ 05/20/93  3:42 PM ]
 ;;V1.0
EN ;
 D SETUP I OUT D EXIT Q
 D LOOP,HEADER,MSG
 D EXIT Q
SETUP ;
 S OUT=0
 I '$D(^UTILITY($J,"EXP")) S OUT=1 QUIT
 Q
LOOP ;
 S A1BFI=0 F  S A1BFI=$O(^UTILITY($J,"EXP",A1BFI)) Q:A1BFI=""  D
 .S A1BFSITE=$S($D(^DIC(4,A1BFI,0)):$P(^(0),U,1),1:"UNKNOWN")
 .S A1BFLTDI=$S('$D(^A1BF(11603,A1BFI,1,"AC")):"NONE",1:$O(^A1BF(11603,A1BFI,1,"AC",0)))
 .I A1BFLTDI'="NONE" S A1BFLTDI=9999999-A1BFLTDI
 .S ^UTILITY($J,"NOTRAN",A1BFSITE)=A1BFLTDI
 Q
MSG ;
 Q:'$D(^UTILITY($J,"NOTRAN"))
 S A1BFS=0 F  S A1BFS=$O(^UTILITY($J,"NOTRAN",A1BFS)) Q:A1BFS=""  D
 .S A1BFI=^UTILITY($J,"NOTRAN",A1BFS) S:A1BFI="" A1BFI="0000000"
 .S:A1BFI'="NONE" A1BFI=$E(A1BFI,4,5)_"/"_$E(A1BFI,6,7)_"/"_$E(A1BFI,2,3)
 .S A1BFSITE=$E((A1BFS_A1BFSPAC),1,25)
 .S ^UTILITY($J,"EXCMSG",A1BFML)=A1BFSITE_$J(A1BFI,10)
 .S A1BFML=A1BFML+1
 Q
HEADER ;
 S ^UTILITY($J,"EXCMSG",A1BFML)=" "
 S ^UTILITY($J,"EXCMSG",A1BFML+1)=$E(A1BFSPAC,1,27)_"SITES NOT REPORTING"
 S ^UTILITY($J,"EXCMSG",A1BFML+2)=" "
 S ^UTILITY($J,"EXCMSG",A1BFML+3)="   SITE"_$E(A1BFSPAC,1,23)_"LAST"
 S ^UTILITY($J,"EXCMSG",A1BFML+4)="   NAME"_$E(A1BFSPAC,1,20)_"REPORT DATE"
 S ^UTILITY($J,"EXCMSG",A1BFML+5)=" ========"_$E(A1BFSPAC,1,16)_"==============="
 S ^UTILITY($J,"EXCMSG",A1BFML+6)=" "
 S A1BFML=A1BFML+7
 Q
EXIT ;
 K A1BFI,A1BFLTDI,A1BFLTD3,A1BFS,A1BFSITE
 Q
