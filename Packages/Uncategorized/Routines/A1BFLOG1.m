A1BFLOG1 ;ALBANY ISC;ECF;17MAR92;13:10pm [ 06/24/93  9:01 AM ]
 ;;V1.0
EN ;Logon routine
 D DBWRITE^A1BFDBWR G:'$D(A1BFDSON) AGAIN I A1BFDSON G AGAIN
 D DBREAD^A1BFDBWR G:'$D(A1BFDSON) AGAIN I A1BFDSON G AGAIN
 D USERLOG^A1BFDBWR G:'$D(A1BFDSON) AGAIN I A1BFDSON G AGAIN
 D CLEAN
 I $D(^A1BF(11601,1,5)) I $P(^A1BF(11601,1,5),U,1)=1 D DELET^A1BFLOG3 G EXIT1
 I $D(^A1BF(11601,1,5)) I $P(^A1BF(11601,1,5),U,2)=0 D DELET^A1BFLOG3 G EXIT1
 D EN^A1BFCHK1 I $O(A1BFERR(0))'="" G AGAIN
 D SETUP
 D OPEN I I>4 D NOPEN G AGAIN
 D PARR
 D READ I $P(A1BFZERR,"*",1)'["DSCON" D ERR1 S $ZTRAP="" S $ZERROR="" G AGAIN
 D EXIT
 D EN^A1BFLOG2
AGAIN ;
 HANG 240 G EN
SETUP ;
 S X=$P(^A1BF(11601,1,0),U,3),X1=$A("@"),X2=$A("\") D DE^XUSHSHP S A1BFACC=X
 S X=$P(^A1BF(11601,1,0),U,4),X1=$A("+"),X2=$A("}") D DE^XUSHSHP S A1BFVER=X
 S U="^" D ^%GUCI S A1BFUVOL=%UCI,A1BFUCI=$P(A1BFUVOL,",",1),A1BFVOL=$P(A1BFUVOL,",",2)
 S A1BFAJOB=0 D ^%ACTJOB F I=1:1:$L(%ACTIVE0) I $E(%ACTIVE0,I)=1 S A1BFAJOB=A1BFAJOB+1
 D ^A1BFDEVU S:'$D(A1BFARR) $P(A1BFARR,"X",12)="X"
 S A1BFCR=0
 Q
CLEAN ;
 S $ZERROR="" S $ZTRAP="" S A1BFZERR="" S U="^"
 Q
OPEN ;
 F I=1:1:5 S %ZIS=0,IOP=A1BFDEV D ^%ZIS Q:'POP 
 U IO X ^%ZOSF("EOFF"),^%ZOSF("TYPE-AHEAD") S X=80 X ^%ZOSF("RM")
 Q
PARR ;
 F I=1:1:10 S A1BFTX=$T(TEXT+I) Q:A1BFTX=""  S A1BFPRMT($E($P(A1BFTX,";",3),$L($P(A1BFTX,";",3))-11,$L($P(A1BFTX,";",3))))=$P(A1BFTX,";",4)_"^"_$P(A1BFTX,";",5)
 Q
READ ;
 S (%H,A1BFSTRH)=$H D YMD^%DTC D
 .I $E(%,1)'="." S %=".0001" Q
 .I $L(%)<5 F I=1:1:7 S %=%_"0" Q:$L(%)>4
 S A1BFSTRT=X_$S(+$P(%,".",2):%,1:".0001") 
 S A1BFINT1=$S($D(A1BFINT):A1BFINT,1:0) D
 .I $E(%,4)=0 I $E(%,5)>4 S A1BFINT=X_$E(%,1,3)_"05"
 .E  S A1BFINT=X_$E(%,1,3)_(($E(%,4,5)\5)*5)
 .I $P(A1BFINT,".",2)="000" S A1BFINT=A1BFINT_"1"
 S (A1BFLAG,A1BFLAG1)=0
READ1 ;
 S $ZTRAP="READERR^A1BFLOG1"
 U IO:(::1) R *X:0 IF  S:X>31 A1BFARR=$E(A1BFARR,2,12)_$C(X) I $D(A1BFPRMT(A1BFARR)) D @$P(A1BFPRMT(A1BFARR),U,1)
 G READ1
READERR ;
 S A1BFZERR=$ZE S $ZTRAP="" S $ZERR=""
 ;REMOVE AFTER TEST
 S ^ZZEF(A1BFINT)=A1BFZERR
 Q
EXIT ;
 S (%H,A1BFSTPH)=$H D YMD^%DTC D
 .I $E(%,1)'="." S %=".0000" Q
 .I $L(%)<7 F I=1:1:7 S %=%_"0" Q:$L(%)>6
 S A1BFSTOP=X_$S(+$P(%,".",2):%,1:"0001")
 X ^%ZIS("C")
 Q
EXIT1 ;
 Q
SENDACC ;
 F I=1:1:$L(A1BFACC) U IO W $E(A1BFACC,I)
 U IO W $C(13)
 Q
SENDVER ;
 F I=1:1:$L(A1BFVER) U IO W $E(A1BFVER,I)
 D SENDCR
 Q
SENDTERM ;
 F I=1:1:500 S X1=X U IO R *X:0 IF  I X_X1="4747" Q
 D SENDCR
 Q
SENDCR ;
 U IO W $C(13)
 S A1BFCR=A1BFCR+1
 Q
SENDREAD ;
 I A1BFLAG=0 F I=1:1:4 U IO W $E("READ",I) S A1BFLAG=1
 D SENDCR
 Q
SENDBASK ;
 F I=1:1:500 S X1=X U IO R *X:0 IF  I X_X1="4747" Q
 F I=1:1:$L(A1BFBASK) U IO W $E(A1BFBASK,I)
 D SENDCR
 Q
SENDMNO ;
 F I=1:1:500 S X1=X U IO R *X:0 IF  I X_X1="4747" Q
 I A1BFLAG1 U IO W "^" D SENDCR Q
 U IO F I=1:1:$L(A1BFMSNO) W $E(A1BFMSNO,I)
 S A1BFLAG1=1 D SENDCR
 Q
SENDACT ; 
 F I=1:1:500 S X1=X U IO R *X:0 IF  I X_X1="4747" Q
 F I=1:1:3 U IO W $E("IGNORE",I)
 D SENDCR
 Q
SENDLOG ;
 F I=1:1:3 U IO W $E("LOG",I)
 U IO W $C(13)
 Q
NOPEN ;
 S $ZERROR=""
 S A1BFERR(44)=A1BFDEV
 D EN^A1BFMSG1
 Q
ERR1 ;
 S A1BFZERR=$ZERROR S $ZERROR=""
 S A1BFERR(45)=A1BFDEV_" ERROR = "_$S($D(A1BFZERR):A1BFZERR,1:"?")
 D EN^A1BFMSG1
 Q
TEXT ;
 ;;ACCESS CODE:;SENDACC;ACCESS CODE
 ;;VERIFY CODE:;SENDVER;VERIFY CODE
 ;;Select TERMINAL TYPE NAME: ;SENDTERM;Terminal Type Prompt
 ;;Select MailMan Menu Option: ;SENDREAD;MailMan Prompt
 ;;Read MAIL BASKET: ;SENDBASK;Basket Select Prompt
 ;; Basket Message: ;SENDMNO;Message Number Prompt
 ;;Select MESSAGE Action:;SENDACT;Message Action Prompt
 ;;Select Mailman Option: ;SENDCR;Close MailMan Menu
 ;;Do you really want to halt? YES//;SENDCR;Halt Prompt
 ;;disconnect requestLocal>;SENDLOG;Local Prompt
