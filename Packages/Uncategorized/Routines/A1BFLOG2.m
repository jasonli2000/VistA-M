A1BFLOG2 ;ALBANY ISC ; ECF ; 2APRIL93 [ 06/11/93  2:54 PM ]
 ;;V1.0
EN ;
 D DBWRITE^A1BFDBWR I $D(A1BFDSON) Q:A1BFDSON
 D DBREAD^A1BFDBWR I $D(A1BFDSON) Q:A1BFDSON
 D USERLOG^A1BFDBWR I $D(A1BFDSON) Q:A1BFDSON
 D TMJ
 D POST
 D EXIT
 Q
TMJ ;
 S A1BFTMJ=0 F A1BFT=0:0 S A1BFT=$O(^%ZTSCH("TASK",A1BFT)) Q:'A1BFT  S A1BFTMJ=A1BFTMJ+1
 Q
POST ;
 S $ZERROR="" S $ZTRAP="FILERR^A1BFLOG2"
 K DD,DA,DO,DIC,DIE S U="^"
 S DIC="^A1BF(11602,",DIC(0)="LMQZ",DLAYGO=11602,X=A1BFINT D ^DIC I +Y<1 D ERR1 S $ZTRAP="" D EXIT Q
 S A1BFENO=+$P(Y,U,1)
 I $P(Y,U,3)=1 S ^A1BF(11602,A1BFENO,1,0)="^11602.01A^^" D AVG,SHOW
 S DIC="^A1BF(11602,A1BFENO,1,",DIC(0)="LMQZ",DLAYGO=11602,DA(1)=A1BFENO,X=""""_A1BFVOL_"""" D ^DIC I +Y<0 D ERR2 S $ZTRAP="" S $ZERROR="" Q
 K DD,DA,DO,DIC,DIE,DLAYGO
 S A1BFSENO=+$P(Y,U,1),DIE="^A1BF(11602,A1BFENO,1,",DA(1)=A1BFENO,DA=A1BFSENO,DR="1////^S X=A1BFSTRT;2////^S X=A1BFSTOP;5////^S X=A1BFAJOB;6////^S X=A1BFDEVU;7////^S X=A1BFTMJ;10////^S X=A1BFCR" D ^DIE
 K DA,DD,DO S $ZTRAP=""
 Q
AVG ;Average RT for last time period
 S (A1BFNOT,A1BFTRT)=0,A1BFRT="99999" S:'$D(A1BFINT1) A1BFINT1=A1BFINT
 F  Q:$L(A1BFINT1)<8!($E(A1BFINT1,$L(A1BFINT1))'="0")  I $E(A1BFINT1,$L(A1BFINT1))="0" S A1BFINT1=$E(A1BFINT1,1,$L(A1BFINT1)-1)
 S II=0 F  S II=$O(^A1BF(11602,A1BFINT1,1,II)) Q:II=""!(II'?.N)  D
 .Q:'$D(^A1BF(11602,A1BFINT1,1,II,0))  S A1BFZ=^(0) Q:$P(A1BFZ,U,10)'>0
 .S A1BF1=$P(A1BFZ,U,2),A1BF1P1=$P(A1BF1,".",1),A1BF2=$P(A1BFZ,U,3),A1BF2P1=$P(A1BF2,".",1)
 .S X=A1BF1 D H^%DTC S A1BFD1=%H,A1BFT1=%T
 .S X=A1BF2 D H^%DTC S A1BFD2=%H,A1BFT2=%T
 .Q:(A1BF2P1-A1BF1P1)>1
 .Q:A1BF1P1>A1BF2P1
 .;If same day, calc RT
 .I A1BFD1=A1BFD2 S A1BFRT=(A1BFT2-A1BFT1)/$S($P(A1BFZ,U,10)>0:$P(A1BFZ,U,10),1:1) Q:A1BFRT>10
 .;If next day, calc RT
 .I A1BF1P1+1=A1BF2P1 S A1BFRT=((86400-A1BFT1)+A1BFT2)/$S($P(A1BFZ,U,10)>0:$P(A1BFX,U,10),1:1) Q:A1BFRT>10
 .S A1BFNOT=A1BFNOT+1,A1BFTRT=A1BFTRT+A1BFRT
 S A1BFRT=$J($S(A1BFNOT=1:A1BFTRT,A1BFNOT>0:(A1BFTRT/A1BFNOT),1:"99999"),5,2)
 Q
SHOW ;Post login message to ^XMB
 S Y=A1BFENO D DD^%DT S A1BFTT=$P($P(Y,"@",2),":",1,2)
 S A1BFTT=$S($P(A1BFTT,":",1)="00":"12"_":"_$P(A1BFTT,":",2)_" AM",$P(A1BFTT,":",1)>12:$P(A1BFTT,":",1)-12_":"_$P(A1BFTT,":",2)_" PM",$P(A1BFTT,":",1)=12:A1BFTT_" noon",1:+$P(A1BFTT,":",1)_":"_$P(A1BFTT,":",2)_" AM")
 I $D(^A1BF(11601,1,5)) D @$S($P(^(5),U,2):"EN^A1BFLOG3",1:"DELET^A1BFLOG3")
 Q
FILERR ;
 S A1BFZERR=$ZERROR S $ZERROR="" S $ZTRAP=""
 S A1BFERR(46)=A1BFZERR
 D EN^A1BFMSG1,EXIT
 Q
ERR1 ;
 S A1BFERR(47)=$S($D(A1BFINT):A1BFINT,1:"?")_"."
 S $ZTRAP=""
 D EN^A1BFMSG1
 Q
ERR2 ;
 S $ZTRAP=""
 S A1BFERR(48)="Time: "_$S($D(A1BFINT):A1BFINT,1:"?")_" Vol. "_$S($D(A1BFVOL):A1BFVOL,1:"?")_"."
 D EN^A1BFMSG1
 Q
EXIT ;
 K DD,DA,DO
 K A1BF1,A1BF1P1,A1BF2,A1BF2P1,A1BFD1,A1BFD2,A1BFENO,A1BFERR,A1BFINT1,A1BFNOT,A1BFRT,A1BFSENO,A1BFT,A1BFT1,A1BFT2,A1BFTMJ,A1BFTRT,A1BFTT,A1BFVOL,A1BFX,A1BFZ
 Q
