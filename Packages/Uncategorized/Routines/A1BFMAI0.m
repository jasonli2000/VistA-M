A1BFMAI0 ;ISC-ALBANY ; ECF ; 12APR11:30AM [ 06/17/93  9:23 AM ]
 ;;V1.0
EN ;
 ;Set up startup variables
 S U="^",DTIME=$S($D(DTIME):DTIME,1:300) K ^UTILITY($J)
DRIVE ;
 D DAYS,INITAR,PEEK
 I A1BFI="" D NODAT,EXIT Q
 D LOOP
 D AVERG
 D EN^A1BFMAI1
 D EXIT
 Q
DAYS ;Set up start, end dates
 D NOW^%DTC S (Y,A1BFDS)=X D DD^%DT S A1BFRDT=Y,A1BFRD=$P(Y,".",1),A1BFLDT=A1BFDS_".0659" S X1=A1BFDS,X2=-1
 D C^%DTC S (A1BFSTDA,Y)=X D DD^%DT S A1BFDATE=Y,A1BFSTDT=$P(A1BFSTDA,".",1)_".0659"
 Q
INITAR ;
 S A1BFI="0^0^0^0^0^0^0^0^0^0^0^0^0^0^0^0^0"
 K A1BFARR F I=0:1:23 S A1BFARR(I)=A1BFI
 K A1BFGTOT S A1BFGTOT=A1BFI
 Q
PEEK ;Is there any data 
 S A1BFI=$O(^A1BF(11602,"B",A1BFSTDT)) Q:A1BFI=""  Q:$O(^A1BF(11602,",",A1BFSTDT))>A1BFLDT
 Q
LOOP ;Store data in A1BFARR and A1BFGTOT
 S (A1BFDT,A1BFPDT,A1BFQ)=A1BFSTDT,(A1BFMAXJ,A1BFMAXU,A1BFMAXD)=0,A1BFPDT=A1BFSTDT,A1BFPHR=+$E($P(A1BFPDT,".",2),1,2)  
 F A1BFI=0:0 S A1BFQ=A1BFDT,A1BFDT=$O(^A1BF(11602,"B",A1BFDT)) Q:A1BFDT=""  Q:A1BFDT>A1BFLDT  S A1BFEN=$O(^(A1BFDT,0)) I A1BFEN'="" I $D(^A1BF(11602,A1BFEN,0)) S A1BFZ=^(0) D
 .F A1BFII=0:0 S A1BFII=$O(^A1BF(11602,A1BFEN,1,A1BFII)) Q:A1BFII=""!(A1BFII'?.N)  I $D(^A1BF(11602,A1BFEN,1,A1BFII,0)) S A1BFZ=^(0) I '(($P(A1BFZ,U,2)="")!($P(A1BFZ,U,3)="")) D
 ..S A1BF1=$P(A1BFZ,U,2),A1BF1P1=$P(A1BF1,".",1),A1BF1P2=$P(A1BF1,".",2),A1BF2=$P(A1BFZ,U,3),A1BF2P1=$P(A1BF2,".",1),A1BF2P2=$P(A1BF2,".",2),A1BFHR=+$E(A1BF1P2,1,2)
 ..S X=A1BF1 D H^%DTC S A1BFD1=%H,A1BFT1=%T
 ..S X=A1BF2 D H^%DTC S A1BFD2=%H,A1BFT2=%T
 ..;If hour changes save prv hr max, reset max, check gaps, reset prv hr
 ..I A1BFPHR'=+$E(A1BF1P2,1,2) D
 ...I $D(A1BFARR(A1BFPHR)) S $P(A1BFARR(A1BFPHR),U,6)=A1BFMAXU,$P(A1BFARR(A1BFPHR),U,8)=A1BFMAXJ,$P(A1BFARR(A1BFPHR),U,7)=A1BFMAXD
 ...S (A1BFMAXU,A1BFMAXJ,A1BFMAXD)=0
 ...I ($E(A1BF1P2,1,2)-1)>+$E($P(A1BFPDT,".",2),1,2) S A1BF("GAP",A1BFQ)=A1BFPDT_"^"_A1BF1
 ...S A1BFPDT=A1BF1,A1BFPHR=A1BFHR
 ..;Quit if stop time is ridiculous
 ..Q:(A1BF2P1-A1BF1P1)>1
 ..;If same day, calc rt
 ..I A1BFD1=A1BFD2 S A1BFRT=(A1BFT2-A1BFT1)/$S($P(A1BFZ,U,10)>0:$P(A1BFZ,U,10),1:6) Q:A1BFRT>10
 ..;If next day, calc rt
 ..I A1BF1P1+1=A1BF2P1 S A1BFRT=((86400-A1BFT1)+A1BFT2)/$S($P(A1BFZ,U,10)>0:$P(A1BFZ,U,10),1:6) Q:A1BFRT>10
 ..I $D(A1BFARR(A1BFHR)) D
 ...S $P(A1BFARR(A1BFHR),U,2)=$J(($P(A1BFARR(A1BFHR),U,2)+A1BFRT),0,4)
 ...S $P(A1BFARR(A1BFHR),U,3)=$P(A1BFARR(A1BFHR),U,3)+$P(A1BFZ,U,6)
 ...S $P(A1BFARR(A1BFHR),U,4)=$P(A1BFARR(A1BFHR),U,4)+$P(A1BFZ,U,4)
 ...S $P(A1BFARR(A1BFHR),U,5)=$P(A1BFARR(A1BFHR),U,5)+$P(A1BFZ,U,5)
 ...S:$P(A1BFZ,U,6)>A1BFMAXJ A1BFMAXJ=$P(A1BFZ,U,6)
 ...S:$P(A1BFZ,U,4)>A1BFMAXU A1BFMAXU=$P(A1BFZ,U,4)
 ...S:$P(A1BFZ,U,5)>A1BFMAXD A1BFMAXD=$P(A1BFZ,U,5)
 ...S $P(A1BFARR(A1BFHR),U,1)=$P(A1BFARR(A1BFHR),U,1)+1
 I $D(A1BFARR(A1BFHR)) S $P(A1BFARR(A1BFHR),U,6)=A1BFMAXJ,$P(A1BFARR(A1BFHR),U,4)=A1BFMAXU,$P(A1BFARR(A1BFHR),U,5)=A1BFMAXD
 I ($E($P(A1BFPDT,".",2),1,2)<23)&($P(A1BFPDT,".",1)'=A1BFDS) S A1BF("GAP",A1BFQ)=A1BFQ_"^"_$P(A1BFQ,".",1)_".2359" 
 I $E($P(A1BFPDT,".",2),1)="0" D
 .Q:$E($P(A1BFLDT,".",2),1,2)-$E($P(A1BFQ,".",2),2,3)'>0
 .I $P(A1BFPDT,".",1)=A1BFDS S A1BF("GAP",A1BFQ)=A1BFQ_"^"_A1BFLDT
 I $P(A1BFPDT,".",1)'=A1BFDS S A1BF("GAP",A1BFDS_".0001")=A1BFDS_".0001"_"^"_A1BFLDT
 Q
NODAT ;
 S ^UTILITY($J,"NODAT",1)="There is no data for "_A1BFDATE
 S ^UTILITY($J,"NODAT",2)="Check to see if the RTM is running."
 K XMY F I=0:0 S I=$O(^A1BF(11601,1,1,"B",I)) Q:+I<1  S XMY(I)=""
 S XMSUB="RT FOR "_A1BFDATE,XMDUZ=.5,XMTEXT="^UTILITY($J,""NODAT"","
 D ^XMD
 K XMY,XMTEST,XMSUB,XMDUZ,I
 Q
AVERG ;Find needed averages
 F A1BFI=0:1:23 I $D(A1BFARR(A1BFI)) D
 .S A1BFSESS=$P(A1BFARR(A1BFI),U,1)
 .I A1BFSESS>0 D
 ..S $P(A1BFARR(A1BFI),U,10)=$J($P(A1BFARR(A1BFI),U,2)/A1BFSESS,0,1)
 ..S $P(A1BFARR(A1BFI),U,11)=$J($P(A1BFARR(A1BFI),U,3)/A1BFSESS,0,0)
 ..S $P(A1BFARR(A1BFI),U,12)=$J($P(A1BFARR(A1BFI),U,4)/A1BFSESS,0,0)
 ..S $P(A1BFARR(A1BFI),U,13)=$J($P(A1BFARR(A1BFI),U,5)/A1BFSESS,0,0)
 .I A1BFSESS'>0 F A1BFJ=6:1:8,10:1:13 S $P(A1BFARR(A1BFI),U,A1BFJ)="---"
 Q
EXIT ;    
 ;K A1BFARR,A1BF,A1BFCPU,A1BFDATE,A1BFD1,A1BFD2,A1BFDS,A1BFDT,A1BFEN,A1BFGTOT,A1BFHR,A1BFI,A1BFJ,A1BFK,A1BFKV,A1BFL,A1BFLDT,A1BFM,A1BFMAX,A1BFMAXJ,A1BFMAXD,A1BFMAXJ,A1BFMTJ,A1BFMAXU,A1BFN
 ;K A1BFPDT,A1BFPHR,A1BFQ,A1BFRD,A1BFRDT,A1BFRT,A1BFRTFT,A1BFSTDA,A1BFSTDT,A1BFSESS,A1BFT1,A1BFT2,A1BFTMGT,A1BFUSGT,A1BFX,A1BFZ,A1BFII
 ;K A1BF1,A1BF1P2,A1BF2,A1BF1P1,A1BF2P1,A1BF2P2,%H,%T,%Y,X,Y,I,A1BF("GAP"),A1BFARR
 Q
