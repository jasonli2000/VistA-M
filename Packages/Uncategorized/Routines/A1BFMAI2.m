A1BFMAI2 ;ALBANY ISC ; ECF 14APRIL92 10:30AM [ 05/20/93  2:19 PM ]
 ;;V1.0
EN ;
SENDLOC ;
 ;Get names of recipients
 K XMY F I=0:0 S I=$O(^A1BF(11601,1,1,"B",I)) Q:I=""  S XMY(I)=""
 ;Set up subject and text
 S XMSUB="RT FOR "_A1BFDATE,XMDUZ=.5,XMTEXT="^UTILITY($J,""A1BFMSG"","
 D ^XMD
 K XMY,XMTEXT,XMSUB,XMDUZ,I
 Q
SENDFAR ;
 S U="^",DTIME=$S($D(DTIME):DTIME,1:300) I '$D(^DD("SITE",1)) D ERRMSG G EXIT
 S A1BFSINO=^DD("SITE",1)
 I '$D(^DIC(4,"D",A1BFSINO)) D ERRMSG G EXIT
 ;Be sure to get the site number not the IFN
 S A1BFSITE=$O(^DIC(4,"D",A1BFSINO,0)) I A1BFSITE="" D ERRMSG G EXIT
 S A1BFSINA=$S($D(^DD("SITE"))#2:^DD("SITE"),1:"UNKNOWN")
 K A1BFMSG S A1BFMSG(1)=A1BFSITE_U_A1BFSTDA_U_A1BFRTGT_U_A1BFUSGT_U_A1BFMU
 S DOMEIN=^A1BF(11601,1,2),DOMAIN=$P(^DIC(4.2,DOMEIN,0),U),XMY("S.A1BF ISC RT SERVER@"_DOMAIN)=DOMEIN
 S XMTEXT="A1BFMSG(",XMSUB="RT DATA FROM SITE "_A1BFSINA,XMDUZ=.5 D ^XMD
EXIT ;
 K DOMAIN,DOMEIN,DUM,ERR,XMY,XMSUB,XMTEXT,XMDUZ,A1BFSINA,A1BFSINO,A1BFSITE,A1BFMSG
 Q
ERRMSG ;
 S ERR(1)="RT data could not be sent to the ISC.  Site number could not be identified."
 S ERR(2)="The date of this attempt was "_A1BFDATE
 S ERR(3)="Global ^DD(""SITE"",1) is "_$S($D(A1BFSINO):A1BFSINO,1:"not set")
 K XMY F I=0:0 S I=$O(^A1BF(11601,1,1,"B",I)) Q:+I<1  S XMY(I)=""
 S XMSUB="Failure to send RT data to ISC ",XMDUZ=.5,XMTEXT="ERR("
 D ^XMD
 Q
