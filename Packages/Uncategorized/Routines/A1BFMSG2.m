A1BFMSG2 ;ALBANY ISC ; ECF ; 31MAR93@1630PM [ 06/24/93  2:33 PM ]
 ;;V1.0
EN ;
 ;S $ZT="EXIT1^A1BFMSG2"
 D RECIP,LINEUP,SEND,POST,EXIT
 Q
RECIP ;
 ;If no recipients, at least postmaster
 S:'$D(A1BFEMR) A1BFEMR(.5)=""
 K A1BFTXT S A1BFTXT(0)=" ",$P(A1BFSP," ",25)=" "
 Q
LINEUP ;If errors, create message array
 S A1BFLN=1 F I=0:0 S I=$O(A1BFERR(I)) Q:I=""  D 
 .I I=5 S A1BFTXT(A1BFLN)=$P($T(TEXT+I^A1BFEM9),";",3),A1BFLN=A1BFLN+1 F J=0:0 S J=$O(A1BFERR(I,J)) Q:J=""  S A1BFTXT(A1BFLN)=$E(A1BFSP,1,25)_J,A1BFLN=A1BFLN+1 
 .Q:I=5
 .I $P($T(TEXT+I^A1BFEM9),";",3)]"" S A1BFTXT(A1BFLN)=$P($P($T(TEXT+I^A1BFEM9),";",3),",",1)_$S($L(A1BFERR(I)):A1BFERR(I),1:"")_$P($P($T(TEXT+I^A1BFEM9),";",3),",",2),A1BFLN=A1BFLN+1 
 Q
SEND ;
 Q:A1BFLN'>0!('$D(A1BFEMR))
 F I=0:0 S I=$O(A1BFEMR(I)) Q:I=""  S XMY(I)=""
 S (XMDUZ)=.5,DUZ(0)="@",XMTEXT="A1BFTXT(",XMSUB="RTM Error Msg. "_A1BFPRDT_$S($D(A1BFVOL):" ("_A1BFVOL_")",1:"")
 D ^XMD
 Q
POST ;
 S:'$D(XMZ) XMZ=9999999
 S %H=$H D YMD^%DTC
 K DA,DO,DD,DR,DLAYGO,DIE,DIC
 S DIE="^A1BF(11601,",DA=1,DR="11///NOW;12////^S X=XMZ" D ^DIE
 Q
EXIT ;
 K %,%H,A1BFERR,A1BFLN,A1BFSP,DA,DIE,DR,I,J,X,XMDUZ,XMSUB,XMTEXT,XMY,XMZ
EXIT1 ;     
 S $ZERROR="" S $ZTRAP=""
 Q
