A1BFTRR1 ;ALBANY ISC ; ECF ; 8 OCTOBER 1992 [ 05/20/93  4:26 PM ]
 ;;V1.0
EN ; Start - find earliest possible date
 S U="^",OUT=0  Q:'$D(^A1BF(11602))  S A1BFED=$O(^A1BF(11602,"B",0)) Q:A1BFED=""  S A1BFED1=A1BFED-1,Y=A1BFED D DD^%DT S A1BFEDP=$P(Y,"@",1)
 S %DT="T",X="NOW" D ^%DT D DD^%DT S A1BFLDP=$P(Y,"@",1)
START ;
 D DATES G:OUT>0 EXIT
 D DEVICE I OUT>0 G EXIT
RETURN ;
 D EN^A1BFTRR9 I $D(A1BFERFL) I A1BFERFL G EXIT
 D LOOP
 D DAILY^A1BFTRR2
 D WEEKLY^A1BFTRR3
 D EN^A1BFTRR6
 D EXIT
 Q
DATES ; Ask start date
 K DIR
 S DIR(0)="DA^"_A1BFED1_":"_"NOW:EXPT"
 S DIR("A")="Report start date ("_A1BFEDP_" - "_A1BFLDP_"): ",DIR("?")="Enter first date to report data ("_A1BFEDP_"-"_A1BFLDP_"): " D ^DIR
 I $D(DTOUT)!$D(DIRUT)!$D(DUOUT) S OUT=1 Q
 S A1BFFIR=Y K DIR
 S A1BFFIR1=A1BFFIR-1,Y=A1BFFIR D DD^%DT S A1BFFDP=$P(Y,"@",1)
 S DIR(0)="DA^"_A1BFFIR1_":"_"NOW:EXT"
 ; Ask end date
 S DIR("A")="Report end date ("_A1BFFDP_" - "_A1BFLDP_"): " S DIR("?")="Enter last date to report data ( "_A1BFFDP_" - "_A1BFLDP_"): " D ^DIR
 I $D(DTOUT)!$D(DIRUT)!$D(DUOUT) S OUT=1 Q
 S A1BFLAS=Y K DIR
 S A1BFFIR=$P(A1BFFIR,".",1)_".0000",A1BFLAS=$P(A1BFLAS,".",1)_".2359"
 Q
DEVICE ; Output device
 S %ZIS="QM" D ^%ZIS I POP>0 S OUT=1 Q
 I IOM<132 W !!,*7,"Report is formatted for 132 columns",!,"Try again" G DEVICE
 I $E(IOST,1,1)'="P" K DIR D
 .S DIR(0)="Y",DIR("A")="This is not a printer.  Is this OK",DIR("B")="YES" D ^DIR I Y=0 G DEVICE
 I $D(IO("Q")) S ZTRTN="RETURN^A1BFTRR1",ZTIO=ION_";132",ZTDESC="Local RTM Print",ZTSAVE("A1BF*")="" D ^%ZTLOAD W !!,$S($D(ZTSK):"REQUEST QUEUED",1:"REQUEST CANCELLED") D HOME^%ZIS I $D(ZTSK) S OUT=1
 Q
LOOP ;
 ;SET WEEK TO 1, START LOOP WITH -.0001, RETAIN PREVIOUS DAY, INITIALIZE WEEK 1
 S A1BFDT=A1BFFIR-.0001,A1BFDT=$O(^A1BF(11602,"B",A1BFDT))
 S (A1BFPDAY,A1BFDAY)=$P(A1BFDT,".",1) S A1BFDT=A1BFFIR-.0001
 ;Loop on days to be included in report
 F J=0:0 S A1BFDT=$O(^A1BF(11602,"B",A1BFDT)) Q:(A1BFDT>A1BFLAS)!(A1BFDT="")  D
 .S A1BFEN=$O(^A1BF(11602,"B",A1BFDT,0)) Q:A1BFEN=""  Q:'$D(^A1BF(11602,A1BFEN,0))
 .F K=0:0 S K=$O(^A1BF(11602,A1BFEN,1,K)) Q:K=""!(K'?.N)  I $D(^A1BF(11602,A1BFEN,1,K,0)) S A1BFSTR=^(0) D LOOP1
 Q
LOOP1 ;
 ;Update previous day if necessary
 I $P(A1BFDT,".",1)'=A1BFPDAY D
 .S X1=$P(A1BFDT,".",1),X2=A1BFPDAY D ^%DTC S A1BFNMD=X
 .F A1BFO=1:1:A1BFNMD S X1=A1BFPDAY,X2=1 D C^%DTC Q:X>$P(A1BFDT,".",1)  S A1BFDAY=$P(X,".",1) D
 ..D DAILY^A1BFTRR2
 ..I $P(^UTILITY("A1BF",$J,A1BFDAY,"DAAY"),U,1)="SATURDAY" D WEEKLY^A1BFTRR3
 ..S A1BFPDAY=A1BFDAY
 Q:$P(A1BFSTR,U,2)<$P(A1BFSTR,U,1)
 S X=$P(A1BFSTR,U,2) D H^%DTC S A1BFD1=%H,A1BFT1=%T
 S X=$P(A1BFSTR,U,3) D H^%DTC S A1BFD2=%H,A1BFT2=%T
 Q:(A1BFD2-1)>A1BFD1  I A1BFD2>A1BFD1 S A1BFET=(86400-A1BFT1)+A1BFT2
 I A1BFD2=A1BFD1 S A1BFET=A1BFT2-A1BFT1
 Q:$P(A1BFSTR,U,10)'>0
 S A1BFET=$S($P(A1BFSTR,U,10)=1:A1BFET,1:A1BFET/$P(A1BFSTR,U,10)) Q:A1BFET>10  I A1BFET["." S A1BFET=$P(A1BFET,".",1)_"."_$E($P(A1BFET,".",2),1,4)
 S A1BFT11=$E($P(A1BFDT,".",2),1,2) S:$L(A1BFT11)=1 A1BFT11=A1BFT11_"0" S A1BFT11=A1BFT11+1
 ;S A1BFUSCO=0 F A1BFJ=4:1:13 S A1BFUSCO=A1BFUSCO+$P(^A1BF(11602,A1BFEN,1,J,0),U,A1BFJ) Q:$P(^(0),U,A1BFJ)=""
 S $P(^UTILITY("A1BF",$J,A1BFDAY,"DTUSR"),U,A1BFT11)=$P(^UTILITY("A1BF",$J,A1BFDAY,"DTUSR"),U,A1BFT11)+$P(A1BFSTR,U,5)
 S $P(^UTILITY("A1BF",$J,A1BFDAY,"DTDEV"),U,A1BFT11)=$P(^UTILITY("A1BF",$J,A1BFDAY,"DTDEV"),U,A1BFT11)+$P(A1BFSTR,U,4)
 S $P(^UTILITY("A1BF",$J,A1BFDAY,"DTTIM"),U,A1BFT11)=$P(^UTILITY("A1BF",$J,A1BFDAY,"DTTIM"),U,A1BFT11)+A1BFET
 S $P(^UTILITY("A1BF",$J,A1BFDAY,"DTSES"),U,A1BFT11)=$P(^UTILITY("A1BF",$J,A1BFDAY,"DTSES"),U,A1BFT11)+1
 S $P(^UTILITY("A1BF",$J,A1BFDAY,"DTTMJ"),U,A1BFT11)=$P(^UTILITY("A1BF",$J,A1BFDAY,"DTTMJ"),U,A1BFT11)+$P(A1BFSTR,U,6)
 Q
EXIT ;
 K %DT,%H,%T,%ZIS,A1BFD1,A1BFD2,A1BFDAY,A1BFDT,A1BFDT1,A1BFED,A1BFED1,A1BFEDP,A1BFEN,A1BFERFL,A1BFET,A1BFFDP,A1BFFIR,A1BFFIR1,A1BFJ,A1BFLAS,A1BFLDP,A1BFNMD,A1BFDAY,A1BFSTR
 K A1BFO,A1BFT1,A1BFT11,A1BFT2,A1BFUSCO,DIR,DIRUT,DTOUT,DUOUT,J,K,OUT,POP,X,Y
 Q
