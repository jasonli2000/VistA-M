A1BHLDRS ;ISC1/JAS;MAIN DRIVER FOR ENTERING LDRS DATA ;[ 02/23/93  8:57 AM ]
 ;;V1.0;;
 S IOP="HOME" D ^%ZIS
TOP W @IOF S U="^",CNT=0 K DD,D0
 W !,?7,"*******************************************************"
 W !,?7,"*****    Lockheed Discrepancy Reporting System    *****"
 W !,?7,"*******************************************************"
 W !! S %H=$H D YX^%DTC
 S DIC="^A1BH(11180,",X=Y,DIC(0)="AEQLMZ"
 S DIC("DR")="6;1;2///^S X=DUZ;3;4//YES"
 K DD,DO D FILE^DICN B  S A1BH=+Y K DD,D0
 I Y=-1 G CLEAN Q
 S A1BHCHK=$S("Yy"[$P(^A1BH(11180,A1BH,0),"^",4):"Y",1:"N")
CHK ;     
 W @IOF
 I $D(^A1BH(11180,A1BH,1,0))=0 W !!,?5,"YOU MUST ENTER A PROBLEM DESCRIPTION BEFORE YOU CAN CONTINUE.",!,?15,"UP ARROWING WILL DELETE THE ENTRY." S DIE="^A1BH(11180,",DR="3",(X,DA)=A1BH D ^DIE G:$E(X,1,1)["^" DELETE G CHK
 I A1BHCHK="Y" S DIE="^A1BH(11180,",DR="5",(X,DA)=A1BH D ^DIE
CHK1 ;    
 W @IOF
 I A1BHCHK="Y",$D(^A1BH(11180,A1BH,2,0))=0 W !!,?5,"YOU MUST ENTER A PROBLEM SOLUTION BEFORE YOU CAN CONTINUE.",!,?15,"UP ARROWING WILL DELETE THE ENTRY" S DIE="^A1BH(11180,",DR="5",(X,DA)=A1BH D ^DIE G:$E(X,1,1)["^" DELETE G CHK1
 I A1BHCHK="Y" S (X,DA)=A1BH,DIE="^A1BH(11180,",DR="7//NOW;8//0;9//NO" D ^DIE
 S A1BHUN=$S("Yy"[$P(^A1BH(11180,A1BH,0),"^",8):"Y",1:"N")
 I A1BHCHK="Y",A1BHUN="Y" S (X,DA)=A1BH,DIE="^A1BH(11180,",DR="10" D ^DIE
 K DIC,DR,DIE
 D PROCESS
 D MAIL
TRYAGN S DIR(0)="Y",DIR("B")="NO"
 S DIR("A")="Do you want to add another item? "
 D ^DIR Q:$D(DIRUT)
 K DD,DA,A1BHUN,DATE1,DO,A1BHCHK,DIR,DIC,DIE,A1BHNODE,A1BH,A1BHL,CNT,NP,A1BHU,A1BHYN,A1BHPT,A1BHL1,A1BHL2,A1BHFLG
 I Y=1 G TOP
CLEAN ;
 K DD,DO,A1BHCHK,DIR,DIC,DIE,A1BHNODE,A1BH,A1BHL,CNT,NP,A1BHU,A1BHYN,A1BHPT,A1BHL1,A1BHL2,DR,DIK,Y,A1BHFLG
 Q
DELETE ;
 W !!,?10,"Deleting......" H 1
 S DIK="^A1BH(11180,",DA=A1BH D ^DIK
 G TRYAGN
 Q
PROCESS ;
 S CNT=CNT+1,A1BHFLG=0
 S A1BHL(CNT)=$P(^DIC(4.2,$P(^XMB(1,1,0),"^",1),0),"^",1)
 S A1BHNODE=^A1BH(11180,A1BH,0)
 S Y=$P(A1BHNODE,"^",1)
 S CNT=CNT+1,A1BHL(CNT)=Y
 S NP=$P(A1BHNODE,"^",2) D NATURE
 S CNT=CNT+1,A1BHL(CNT)=A1BHPT
 S A1BHU=$P(^VA(200,$P(A1BHNODE,"^",3),0),"^",1)
 S CNT=CNT+1,A1BHL(CNT)=A1BHU
 S Y=$P(A1BHNODE,"^",5) X ^DD("DD")
 S CNT=CNT+1,A1BHL(CNT)=Y
 S A1BHYN=$S("Yy"[$P(A1BHNODE,"^",4):"YES",1:"NO")
 S CNT=CNT+1,A1BHL(CNT)=A1BHYN
 I $D(^A1BH(11180,A1BH,1,0))=1 S A1BHL1=$P(^(0),"^",3) F I=1:1:A1BHL1 S CNT=CNT+1,A1BHL(CNT)=^A1BH(11180,A1BH,1,I,0)
 I A1BHYN="YES" D SEP S Y=$P(A1BHNODE,"^",6) X ^DD("DD") S CNT=CNT+1,A1BHL(CNT)=Y,CNT=CNT+1,A1BHL(CNT)=$P(A1BHNODE,"^",7)
 I A1BHYN="YES" I $D(^A1BH(11180,A1BH,2,0))=1 S A1BHL2=$P(^A1BH(11180,A1BH,2,0),"^",3) F J=1:1:A1BHL2 S CNT=CNT+1,A1BHL(CNT)=^A1BH(11180,A1BH,2,J,0)
 S A1BHSAT=$S($P(A1BHNODE,"^",8)="Y":"YES",1:"NO")
 I A1BHYN="YES",A1BHSAT="NO" D SEP1 Q
 I A1BHSAT="YES" D SEP1
 I A1BHSAT="YES" I $D(^A1BH(11180,A1BH,3,0))=1 S A1BHL3=$P(^A1BH(11180,A1BH,3,0),"^",3) F K=1:1:A1BHL3 S CNT=CNT+1,A1BHL(CNT)=^A1BH(11180,A1BH,3,K,0)
 Q
SEP ;
 S CNT=CNT+1,A1BHL(CNT)="--------------------"
 Q
SEP1 ;
 S CNT=CNT+1,A1BHL(CNT)="===================="
 S CNT=CNT+1,A1BHL(CNT)=A1BHSAT
 Q
NATURE ; 
 S A1BHPT=$S(NP=1:"MSM SOFTWARE",NP=2:"486 PROCESSOR",NP=3:"525 MG DRV",NP=4:"1.2 GB DRV",NP=5:"TAPE DRV",NP=6:"DISK CONTROLLER",NP=7:"TAPE CONTROLLER",NP=8:"SYPLYS",NP=9:"MONITOR",NP=10:"VIDEO CARD",NP=11:"BACKUP",NP=12:"XSYS JRNL",NP="":1)
 Q
MAIL ;
 S XMTEXT="A1BHL(",XMSUB="A1BH LDRS TRANSACTION ENTRY"
 S XMY("G.A1BH LDRS RECEIPT")=""
 S XMY("S.A1BH SERVER ENTRY@ISC-ALBANY.VA.GOV")=""
 D ^XMD
 K XMTEXT,XMSUB,XMY
 Q
