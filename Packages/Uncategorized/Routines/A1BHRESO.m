A1BHRESO ;ISC1/JAS;ENTER/EDIT RESOLUTION [ 02/23/93  8:57 AM ]
 ;;V1.0
 S A1BHL="",CNT=0
 S DIC="^A1BH(11180,",DIC(0)="AEQZ",DIC("S")="I ""Yy""'[$P(^A1BH(11180,+Y,0),""^"",4)"
 D ^DIC S A1BHDA=+Y I Y=-1 K DIC,A1BHL,CNT Q
 S A1BHD=$P(Y,"^",2)
 S A1BHN=$P(^DIC(4.2,$P(^XMB(1,1,0),"^",1),0),"^",1)
 S DIE="^A1BH(11180,",DA=+Y,DA(1)=+Y,DR="4//YES" D ^DIE
 S A1BHCHK=$S("Yy"[$P(^A1BH(11180,DA,0),"^",4):"Y",1:"N")
CHK ;     
 G:A1BHCHK="N" CLEAN
 I A1BHCHK="Y",$D(^A1BH(11180,DA,2,0))=0 W !!,?5,"YOU MUST ENTER A PROBLEM SOLUTION BEFORE CONTINUING.",!,?5,"UP ARROWING EXITS THE ENTRY, AND CHANGES PROBLEM RESOLVED TO 'NO'." S DIE="^A1BH(11180,",DR="5",X=DA D ^DIE G:$E(X,1,1)["^" EXIT G CHK
 S DIE="^A1BH(11180,",DR="7//NOW;8//0;9//NO" D ^DIE
 S A1BHNU=$S("Yy"[$P(^A1BH(11180,DA,0),"^",8):"Y",1:"N")
 I A1BHCHK="Y",A1BHNU="Y" S DIE="^A1BH(11180,",DA=A1BHDA,DA(1)=DA,DR="10" D ^DIE
 D COUNT S A1BHL(CNT)=$P(^VA(200,DUZ,0),"^",1) ;USER EDITING ENTRY
 D COUNT S A1BHL(CNT)=A1BHN ;DOMAIN NAME
 D COUNT S A1BHL(CNT)=A1BHD ;DATE OF ENTRY
 D COUNT S A1BHL(CNT)="YES"
 D COUNT S A1BHL(CNT)="----------"
 D COUNT S Y=$P(^A1BH(11180,A1BHDA,0),"^",6) X ^DD("DD") S A1BHL(CNT)=Y
 D COUNT S A1BHL(CNT)=$P(^A1BH(11180,A1BHDA,0),"^",7)
 I $D(^A1BH(11180,A1BHDA,2,0))=1 S A1BHL2=$P(^(0),"^",3) F J=1:1:A1BHL2 D COUNT S A1BHL(CNT)=^A1BH(11180,A1BHDA,2,J,0)
 D COUNT S A1BHL(CNT)="=========="
 D COUNT S A1BHL(CNT)=$S($P(^A1BH(11180,A1BHDA,0),"^",8)="Y":"YES",1:"NO")
 I $D(^A1BH(11180,A1BHDA,3,0))=1 S A1BHL3=$P(^(0),"^",3) F K=1:1:A1BHL3 D COUNT S A1BHL(CNT)=^A1BH(11180,A1BHDA,3,K,0)
 S XMTEXT="A1BHL(",XMSUB="A1BH LDRS TRANSACTION UPDATE"
 S XMY("G.A1BH LDRS RECEIPT")=""
 S XMY("S.A1BH LDRS UPDATE@ISC-ALBANY.VA.GOV")=""
 D ^XMD
CLEAN K CNT,XMTEXT,XMSUB,XMY,A1BHL,DIC,DIE,DA,DR,I,A1BHD,A1BHN,A1BHCNT,A1BHCHK,A1BHDA
 Q
COUNT ;
 S CNT=CNT+1
 Q
EXIT ;
 S $P(^A1BH(11180,DA,0),"^",4)="N"
 D CLEAN
 Q
