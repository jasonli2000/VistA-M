A1BHSERV ;ISC1/JAS;PROCESS THE A1BH LDRS ENTRY VIA SERVER [ 01/15/93  9:22 AM ]
 ;;V1.0;;
 S U="^"
 S A1BHCNT=0
PROCESS ; PROCESS THE INCOMING MESSAGE INTO THE A1BH ARRAY
 X XMREC
 I $D(XMER) G QU:XMER<0
 S A1BHCNT=A1BHCNT+1
 S A1BH(A1BHCNT)=XMRG
 ;S ^JAS(A1BHCNT)=XMRG ; USED FOR DEBUGGING
 G PROCESS
QU ; BEGIN PROCESSING THE A1BH ARRAY AND FILING THE DATA
 S X=$P(^A1BH(11181,0),"^",3)+1
 K DD,DO
 S DIC="^A1BH(11181,",DIC(0)="NL",DLAYGO=11181,DINUM=X
 S DIC("DR")="1///^S X=A1BH(1);2///^S X=A1BH(2);3///^S X=A1BH(3);4///^S X=A1BH(4);9///^S X=A1BH(5);5///^S X=A1BH(6)"
 D FILE^DICN
 I Y=-1 Q
 D NOW^%DTC S (CNT1,CNT2,CNT3,A1BHFLG)=0,(NUM,CNT2FLG,CNT3FLG)=1
 F I=7:1:A1BHCNT D EVAL
 I CNT1>0 S ^A1BH(11181,+Y,1,0)="^^"_CNT1_"^"_CNT1_"^"_X
 I CNT2>0 S ^A1BH(11181,+Y,2,0)="^^"_CNT2_"^"_CNT2_"^"_X
 I CNT3>0 S ^A1BH(11181,+Y,3,0)="^^"_CNT3_"^"_CNT3_"^"_X
 Q
EVAL ; DETERMINE WHAT SECTION NEEDS FILING
 I A1BH(6)="NO" S CNT1=CNT1+1,^A1BH(11181,+Y,1,CNT1,0)=A1BH(I) Q
 I A1BH(6)="YES" D:A1BHFLG=2 LOWER D:A1BHFLG=1 NEXT D:A1BHFLG=0 NODE
 Q
NODE ; PROCESS NODE 1 DATA
 I A1BH(I)["-----" S A1BHFLG=1 Q
 S CNT1=CNT1+1,^A1BH(11181,+Y,1,CNT1,0)=A1BH(I)
 Q
NEXT ; PROCESS NODE 2 DATA
 G:CNT2FLG=2 LINE2 G:CNT2FLG=1 LINE1
 I A1BH(I)["=====" S A1BHFLG=2 Q 
 S CNT2=CNT2+1,^A1BH(11181,+Y,2,CNT2,0)=A1BH(I)
 Q
LINE1 ; STUFF CLOSURE RESOLVED DATE/TIME FROM RESOLVED SECTION
 S CNT2FLG=2
 S $P(^A1BH(11181,+Y,0),"^",9)=A1BH(I)
 Q
LINE2 ; STUFF DOWNTIME FROM RESOLVED SECTION
 S CNT2FLG=3
 S $P(^A1BH(11181,+Y,0),"^",10)=A1BH(I)
 Q
LOWER ; PROCESS NODE 3 DATA
 G:CNT3FLG=1 LI1
 S CNT3=CNT3+1,^A1BH(11181,+Y,3,CNT3,0)=A1BH(I)
 Q
LI1 ; STUFF IF LOCKHEED SERVICE WAS UNSATISFACTORY
 S CNT3FLG=2
 S $P(^A1BH(11181,+Y,0),"^",11)=A1BH(I)
 Q
