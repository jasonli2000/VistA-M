A1CHIP1 ;ALB/ - OUTPUT TOT DISCH BY MEANS TEST CAT ; 23 DEC 88@0957
 ;;V 1.3
 S U="^",ZRT=0,%DT="T",X="N" D ^%DT S (DGGE,T2)=Y X ^DD("DD") S T2=Y
BG W !,"INPATIENT DISCHARGES BY MEANS TEST CATEGORY",! S U="^",POP=0,%DT="APE",%DT(0)="-0",%DT("A")="From DATE: " D ^%DT G:Y'>0 END
 S DGBD=Y,%DT(0)="-TODAY",%DT("A")="To DATE: " D ^%DT G:Y'>0 END S DGND=Y W ! I DGND<DGBD W *7,"TO DATE IS LESS THAN FROM DATE, TRY AGAIN" G BG
 W !,"REPORT REQUIRES 132 COLUMN OUTPUT",!
 S DGJB=2,DGPGM="EN^A1CHIP1",DGVAR="DGJB^DGBD^DGND^DGGE" D ZIS^DGUTL G:POP END
EN K ^UTILITY("A1CH",DGJB) S ^UTILITY("A1CH","I")=0,A2=0,(DGTN,K1)=1,H1=$H,B1=(DGBD-1)+.9999 D LO^DGUTL,0 F I=1:1:A2 S DGDV=$P(A(I),U,2) D T1^A1CHUTL
 D TOTW S DGDV=0,H2=$H D ET^A1CHUTL F I=0:0 S DGDV=$O(Z(DGDV)) Q:DGDV=""  S ^UTILITY("A1CH",DGJB,DGTN,DGDV)=$C(35)_U_DGGE_U_DGDV_U_DGJB_U_DGBD_U_DGND_U_Z(DGDV)_U_DGTOUT
 D ^A1CHIP2
END X:'POP ^%ZIS("C") I IO'=IO(0) U IO(0)
 S ^UTILITY("A1CH","I")=1 K B1,B2,DFN,DGBD,DGDV,DGDVN,DGEL,DGJB,DGMT,DGND,DGPGM,DGV,DGVAR,DGWADM,DGWADMT,DGWARD,DGWH,H1,I,J,K,POP,X,Y,Z
 Q
0 F I=1:1 S B1=$O(^DGPT("ADS",B1)) Q:(B1="")!(B1>(DGND+.9999))  D 1
 Q
1 S B2="" F J=1:1 S B2=$O(^DGPT("ADS",B1,B2)) Q:B2=""  D DIV Q:$L(DGDV)<3  D:$D(^UTILITY("A1CH",DGJB,DGTN,DGDV))=0 ZRO D:$D(^DGPT(B2,0))>0 2
 Q
2 S DFN=$P(^DGPT(B2,0),U,1) Q:$D(^DPT(DFN,.36))=0
 Q:$P(^DPT(DFN,.36),U,1)=""  S DGEL=$P(^(.36),U,1),DGEL=$P(^DIC(8,DGEL,0),U,4),DGWH=$P(^(0),U,5),DGV=$S(DGWH="Y":"V",DGWH="N":"N",1:0) Q:DGV=0
 S DGMT=^DGPT(B2,0) I B1<2860701 S DGMT=$S($P(DGMT,U,10)="*":"U",$P(DGMT,U,10)'="":$P(DGMT,U,10),1:"X"),^UTILITY("A1CH",DGJB,DGTN,DGDV,DGV,DGMT,DGEL)=^UTILITY("A1CH",DGJB,DGTN,DGDV,DGV,DGMT,DGEL)+1 Q
 S DGMT=$S($P(DGMT,U,10)'="":$P(DGMT,U,10),1:"U"),^UTILITY("A1CH",DGJB,DGTN,DGDV,DGV,DGMT,DGEL)=^UTILITY("A1CH",DGJB,DGTN,DGDV,DGV,DGMT,DGEL)+1 Q
 ;
ZRO ;zero facility+suffix
 S A2=A2+1 S A(A2)=U_DGDV D G1^A1CHUTL S ^UTILITY("A1CH","AI",A2)=U_DGDV Q
 ;
DIV ;get facility for cases where PTF has div as ""
 S DGDV=$P(^DGPT(B2,0),U,3)_$P(^(0),U,5) Q:DGDV'=""
 S DFN=$P(^DGPT(B2,0),U,1),DGWADM=$O(^DPT("AD",B1,DFN,0)) Q:DGWADM=""
 S DGWARD=$P(^DPT(DFN,"DA",DGWADM,0),U,4) I DGWARD="" S DGDV="" Q
 S DGDV=$P(^DIC(42,DGWARD,0),U,11) Q:DGDV=""  S DGDV=$P(^DG(40.8,DGDV,0),U,2)
 Q
 ;
TOTW ;get inpatients remaining in hospital
 S (%X,X)=0 F I=0:0 S X=$O(^DPT("CN",X)) Q:X=""  I $O(^DIC(42,"B",X,0))'="",$P(^DIC(42,$O(^DIC(42,"B",X,0)),0),U,11)'="" D TOTW0
 S X="" F I=A2:1 S X=$O(Z(X)) Q:X=""  S DGDVN=$S($D(^DG(40.8,"C",X))>0:^DG(40.8,$O(^DG(40.8,"C",X,0)),0),1:"^"),DGDV=$P(DGDVN,U,1),DGDVN=$P(DGDVN,U,2),^UTILITY("A1CH","AI",I+1)=DGDV_U_DGDVN_U_Z(X),^(0)=A2
 Q
TOTW0 I $D(Z($P(^DG(40.8,$P(^DIC(42,$O(^DIC(42,"B",X,0)),0),U,11),0),U,2)))=0 S Z($P(^DG(40.8,$P(^DIC(42,$O(^DIC(42,"B",X,0)),0),U,11),0),U,2))=0,%X=%X+1
 S:$D(A(%X+A2))=0 A(%X+A2)=^DG(40.8,$P(^DIC(42,$O(^DIC(42,"B",X,0)),0),U,11),0) D TOTW1
 Q
TOTW1 S Y=0,DGDV=$P(^DG(40.8,$P(^DIC(42,$O(^DIC(42,"B",X,0)),0),U,11),0),U,2)
 F I=0:0 S Y=$O(^DPT("CN",X,Y)) Q:Y=""  S Z($P(^DG(40.8,$P(^DIC(42,$O(^DIC(42,"B",X,0)),0),U,11),0),U,2))=Z($P(^DG(40.8,$P(^DIC(42,$O(^DIC(42,"B",X,0)),0),U,11),0),U,2))+1
 Q
 ;
