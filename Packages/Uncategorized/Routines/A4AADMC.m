A4AADMC ;HINES-CIOFO/JJM - PRINT DMC DATA FROM FILE 340 2/10/99 16:45
 ;;1.0;**** CLASS III ****;;OCT 14, 1998
V ;
 ;
START ;
 F  D DEBTOR^A4AARPT Q:'$G(DEBTOR)  D
   . D OPEN^A4AARPT1
   . D AUDIT I ('NO340D)&(BILLCNT=0) W !!,"NO DMC DATA FOUND FOR DEBTOR",!!
   . D CLOSE^A4AARPT1
EOR ;
 K DEBTOR,A4ADEB,PFLAG,POP,X,Y,NO340D,BILLCNT,DIC,SSN,U1N4,BAL,SBILLCNT
 QUIT
AUDIT ;
 D PRTRPT9
 D:'END PBILLS
EXIT ;
 D EOR^A4AARPT
 K A4ADEB,A4APAT,END
 K ADDR,ADMIN,BTOTALS
 K B0,B12,B4,B6,B7,BADADD,BILL,BN,BREQ
 K DEBTOR0,DFN,DREQ
 K INT,LTRDT3,P30DT,P91DT,PRIN
 K REFER
 K REQ1,REQ10,REQ11,REQ12,REQ13,REQ14,REQ15,REQ2,REQ3,REQ4,REQ5,REQ6,REQ7,REQ8
 K REQ8,REQ9
 K VADM,X,X1,X2
 QUIT
PRTRPT9 ;
 S END=0,NO340D=0
 K ^TMP($J,"A4ADMC")
 S ^TMP($J,"A4ADMC",0)=DT
 S (REQ1,REQ2,REQ3,REQ4,REQ5,REQ6,REQ7,REQ8,REQ9,REQ10,REQ11,REQ12,REQ13,REQ14,REQ15)=0
 IF $D(^RCD(340,DEBTOR,3)) S NO340D=1
 Q:('$G(DEBTOR))
 D CHKDREQ
 N TAMT
 F DMC=1:1:10 S DMC(DMC)=+$P($G(^RCD(340,DEBTOR,3)),U,DMC)
 D PRTHDR1
 W !,"DMC FLAG: ",DMC(1),?20,"DMC DATE:",DMC(2),?40,"DMC DISCOVERY DATE:",DMC(3)
 W !!,"DMC PRINCIPAL AMT",?20,$J(DMC(6),12,2),?40,"LESSER WITHHOLDING AMOUNT:",$J(DMC(9),12,2)
 W !,"DMC INTEREST AMT",?20,$J(DMC(7),12,2),?40,"SITE DELETION FLAG:",DMC(10)
 W !,"DMC ADMIN AMT",?20,$J(DMC(8),12,2)
 S TAMT=DMC(6)+DMC(7)+DMC(8)
 W !,?20,"------------"
 W !,"DMC AMOUNT:",?20,$J(DMC(5),12,2)," (",$J(TAMT,12,2)," )"
 W !!
 S ^TMP($J,"A4ADMC","DEBTOR",DEBTOR)=REQ1_U_REQ2_U_REQ3_U_REQ4_U_REQ5_U_REQ6_U_REQ7_U_REQ8_U_REQ9_U_REQ10_U_REQ11_U_REQ12_U_REQ13
 D BILLLST
ENDRPT9 ;
 D:IOT="VTRM" PAUSE^A4AARPT3
 W @IOF
 K DMC,DMCAMT,DMCBD,DMCBT
 QUIT
PRTHDR1 ; DMC DATA REPORT HEADER
 W !,?30,"DMC FILE 340 DATA"
 W !,?30,"DEBTOR: ",DEBTOR
 W !,?30,DT
 W !!
 QUIT
PRTHDR2 ; DMC DATA REPORT HEADER
 W !,?30,"DMC FILE 430 DATA"
 W !,?30,"DEBTOR: ",DEBTOR
 W !,?30,DT
 W !!,?19,"<---     DATA REPORTED TO DMC      --->",?60," RUNNING",?70,"   CURRENT"
 W !,"IEN",?8,"DATE",?19,"PRINCIPLE",?30,"INTEREST",?40,"   ADMIN.",?50,"   TOTAL",?60," BALANCE",?70,"   BALANCE"
 QUIT
BILLLST ; LIST OF BILLS SENT TO DMC FOR DEBTOR
 D PRTHDR2
 F DMC=1:1:4 S DMCBD(DMC)=0,DMCBT(DMC)=0
 S BN=0,DMCBT=0,BTOTALS=0,BILLCNT=0
 F  S BN=$O(^PRCA(430,"C",DEBTOR,BN)) Q:'BN  D
   . Q:'$D(^PRCA(430,BN,12))!(END)
   . S DMCAMT=0,SBILLCNT=BILLCNT+1
   . S BILL=BN D DMCAMT D CALCCBAL^A4AARPT S BTOTALS=BTOTALS+BAL
   . F DMC=2:1:4 S DMCBT=DMCBT+DMCBD(DMC),DMCBT(DMC)=DMCBT(DMC)+DMCBD(DMC)
   . W !,BN,?8,DMCBD(1),?18,$J(DMCBD(2),10,2),$J(DMCBD(3),10,2),$J(DMCBD(4),10,2),$J(DMCAMT,10,2),$J(DMCBT,10,2),?70,$J(BAL,10,2)
 S TAMT=0
 W !,?18
 F DMC=2:1:5 W "  --------"
 W !,?70,"  --------",!,?18
 F DMC=2:1:4 W $J(DMCBT(DMC),10,2) S TAMT=TAMT+DMCBT(DMC)
 W $J(TAMT,10,2),?70,$J(BTOTALS,10,2)
 QUIT
PBILLS ;
 S BILL=0
 F  S BILL=$O(^PRCA(430,"C",DEBTOR,BILL)) Q:'BILL  D
    . Q:'$D(^PRCA(430,BILL,12))
    . D PBILL1^A4AARPT1
    . S BN=BILL D CHKBREQ
 QUIT
DMCAMT ;
 S DMCAMT=0
 S DMCBD(1)="" F DMC=2:1:4 S DMCBD(DMC)=0
 Q:'$D(^PRCA(430,BILL,12))
 F DMC=1:1:4 S DMCBD(DMC)=$P(^PRCA(430,BILL,12),U,DMC)
 S DMCAMT=0
 F DMC=2:1:4 S DMCAMT=DMCAMT+DMCBD(DMC)
 QUIT
CHKADDR ;
 S BADADD=0
 S ADDR=$$DADD^RCAMADD(DEBTOR)
 I ADDR'?.ANP!(ADDR["$")!(ADDR["**")!(ADDR["///")!(ADDR["ZZZ") S BADADD=1
 QUIT
CHKBREQ ; CHECK BILL REFERRAL REQUIREMENTS
 ; SETS REFER=1 IF ALL REQUIREMENTS ARE NOT MET
 ; STORES TEST RESULTS IN BREQ
 S REFER=0
 S (REQ1,REQ2,REQ3,REQ4,REQ5,REQ6,REQ7,REQ8,REQ9,REQ10,REQ11,REQ12,REQ13,REQ14,REQ15)=0
 S B0=$G(^PRCA(430,BN,0)),B4=$G(^(4)),B6=$G(^(6)),B7=$G(^(7)),B12=$G(^(12))
 S PRIN=$P(B7,U),INT=$P(B7,U,2),ADMIN=$P(B7,U,3)+$P(B7,U,4)+$P(B7,U,5)
 S LTRDT3=$P(B6,U,3)
 S:$P(B0,U,8)'=16 REQ4=1
 S:B4 REQ5=1
 S:('LTRDT3)!(LTRDT3>P30DT) REQ7=1
 I $P(B6,U,4),($P(B6,U,5)="DC")!($P(B6,U,5)="RC") S REQ8=1
 S:$$INSURED^IBCNS1 REQ9=1
 S X=$P(B0,U,2),X=$S(X=22:1,X=23:1,(X>2)&(X<6):2,X=18:2,X=24:2,X=25:2,X=1:3,X=2:4,(X>26)&(X<30):5,X>29:6,1:"")
 S:X="" REQ10=1
 S:PRIN'>0 REQ6=1
 S:REQ1!REQ2!REQ3!REQ4!REQ5!REQ6!REQ7!REQ8!REQ9!REQ10!REQ11 REFER=1
 S:REFER ^TMP($J,"A4ADMC","BILL",BN)=$S($P(B12,U):$P(B12,U),1:DT)_U_PRIN_U_INT_U_ADMIN
 S ^TMP($J,"A4ADMC","DEBTOR",DEBTOR,"BILL",BN)=REQ1_U_REQ2_U_REQ3_U_REQ4_U_REQ5_U_REQ6_U_REQ7_U_REQ8_U_REQ9_U_REQ10_U_REQ11_U_REQ12_U_REQ13
 S BREQ=REQ1_U_REQ2_U_REQ3_U_REQ4_U_REQ5_U_REQ6_U_REQ7_U_REQ8_U_REQ9_U_REQ10_U_REQ11_U_REQ12_U_REQ13
 QUIT
CHKDREQ ; CHECKS DEBTOR'S (FILE 340) DMC REQUIREMENTS
 ; SETS REFER=1 IF ALL CONDITIONS ARE NOT MET
 ; STORES TEST RESULTS IN DREQ
 S REFER=0
 S X1=DT,X2=-91 D C^%DTC S P91DT=X
 S X1=DT,X2=-30 D C^%DTC S P30DT=X
 S DEBTOR0=$G(^RCD(340,DEBTOR,0))
 S:$P(DEBTOR0,U)'["DPT" REQ1=1
 S DFN=DEBTOR D DEM^VADPT
 S:$E(VADM(2),1,5)="00000" REQ2=1
 S:+VADM(6) REQ3=1
 S:$P($G(^RCD(340,DEBTOR,3)),U,10)=1 REQ11=1
 S:$P($G(^RCD(340,DEBTOR,1)),U,9)=1 REQ12=1
 D CHKADDR S:BADADD=1 REQ13=1
 S DREQ=REQ1_U_REQ2_U_REQ3_U_REQ4_U_REQ5_U_REQ6_U_REQ7_U_REQ8_U_REQ9_U_REQ10_U_REQ11_U_REQ12_U_REQ13
 QUIT
