A4AARPT5 ;HINES-CIOFO/JJM - AUDIT REPORT PROGRAM 5 2/10/99 15:40
V ;;1.0;**** CLASS III ****;;JUN 30, 1998
 N PBAL,TBAL,BBAL
 S (A4ATOA1,A4APSB2,A4ATNA3,A4ATL4,A4ATL5,A4ATL6,A4APSB,A4ATL7,A4ATNA,A4ATL8,A4AFRB)=0
 S:'$G(DEBTOR) DEBTOR=+$G(A4ADEB)
 Q:'$G(DEBTOR)
 D FBD
 D PCLDFCS
ERO5 ;
 K BN,DAT,DEB
 QUIT
FBD ; Find Balance Discrepancy
 S:'$G(DEBTOR) DEBTOR=$G(A4ADEB)
 Q:'$G(DEBTOR)
 S (PBAL,BBAL,TBAL)=0 K ^TMP("PRCAGT",$J)
 D BBAL^PRCAGU(DEBTOR,.BBAL)
 S A4ATOA1=BBAL
 S DAT=$$LST^RCFN01(DEBTOR,2) I DAT<1 S DAT=0
 I DAT S DAT=9999999.999999-DAT D PBAL^PRCAGU(DEBTOR,.DAT,.PBAL) S A4APSB2=PBAL
 D EN^PRCAGT(DEBTOR,DAT)
 D TBAL^PRCAGT(DEBTOR,.TBAL)
 S A4ATNA3=TBAL
 I (PBAL+TBAL'=BBAL) S Y=1,DFLAG=1
 E  S Y=0,DFLAG=0
 S A4ATL1=BBAL
 S A4ATL2=PBAL
 S A4ATL3=TBAL
 S A4ATL4=BBAL-(PBAL+TBAL)
 S A4ATL5=A4ATL2+A4ATL3+A4ATL4
 QUIT
EN(DEBTOR) ;Called to find balance discrepancy
 N PBAL,TBAL,BBAL
 D FBD
 K ^TMP("PRCAGT",$J)
 Q Y
GETLSD ; GET THE DEBTORS LAST STATEMENT DATE
 S SDT=$P($$LST^RCFN01(DEBTOR,2),U,1)
 S DATE1=$P(SDT,".",1)
 S TIME1=SDT-DATE1
 QUIT
PCLDFCS ; PRINT CHECKLIST DATA FROM CHECK STATEMENT 
 Q:END
 W !,"  1.  TOTAL OPEN & ACTIVE BILLS:",?60,$J(A4ATL1,10,2)
 W !,"  2.  PREVIOUS STATEMENT BALANCE:",?40,$J(A4ATL2,10,2)
 W !,"  3.  TOTAL OF NEW ACTIVITIES:",?40,$J(A4ATL3,10,2)
 W !,"      TOTAL OF 2,3 :",?60,$J((A4ATL2+A4ATL3),10,2)
 W !,"  4.  AMOUNT OF DISCREPANCY:",?40,$J(A4ATL4,10,2)
 W !,"  5.  TOTAL OF 2,3 & 4:",?50,$J(A4ATL5,10,2)
 QUIT
PCLDFAR ; PRINT CHECKLIST DATA FROM AUDIT REPORT
 Q:END
 S A4ATL6=A4ATOA
 S A4ATL7=A4APSB
 S A4ATL8=NATOTAL
 S A4ATL9=A4APSB+NATOTAL
 S A4ATL10=RBAL
 W !!,"  6. TOTAL OPEN & ACTIVE BILLS:",?60,$J(A4ATL6,10,2)
 W !,"  7. COMPUTED STATEMENT BALANCE:",?40,$J(A4ATL7,10,2)
 W !,"  8. TOTAL OF NEW ACTIVITIES:",?40,$J(A4ATL8,10,2)
 W !,"  9. TOTAL OF 7 & 8:",?60,$J(A4ATL9,10,2)
 W !," 10. FINAL RUNNING BALANCE:",?60,$J(A4ATL10,10,2)
 QUIT
