A4AARPT8 ;HINES-CIOFO/JJM - AUDIT REPORT PROGRAM 8 2/12/99 11:35
V ;;1.0;**** CLASS III ****;;JUN 30, 1998
 ; D BYCXREF,BYASXR
 D BYASXR,ATD433
 QUIT
BYCXREF ;;
 S:'$G(DEBTOR) DEBTOR=+$G(A4ADEB)
 Q:'DEBTOR
 N ERRCNT,OK,I
 N AB,BAL,BCNT,BILL,CC,CS,DCNT,IB,MF,PB,PRCA4307,RCNT
 N DAT,DATE1,SDT,TAOAS,TAOC,TCNT,TERRCNT,TIME1,Y
 D INIT^A4AADP1
 W #,"COMPUTING TOTAL BALANCE FOR ACTIVE & OPEN BILLS",!,"USING THE C CROSS-REFERENCE OF FILE 430"
 S DCNT=0,RCNT=0,TAOC=0
 S BN=0
 F  S BN=$O(^PRCA(430,"C",DEBTOR,BN)) Q:'BN  D
    . Q:(+($P(^PRCA(430,BN,0),U,8)'=16))&(+($P(^PRCA(430,BN,0),U,8))'=42)
    . S BILL=BN,RCNT=RCNT+1
    . D CALCCBAL^A4AARPT
    . S TAOC=TAOC+BAL
 W !,$J(RCNT,10)," BILLS CHECKED (",+$P(^PRCA(430,0),U,3),")"
 W !,"TOTAL OF ACTIVE & OPEN BILLS COMPUTED BY USING THE C XREF: ",$J(TAOC,12,2)
 W !
EOT1 ;
 QUIT
ATD433 ;;
 S:'$G(DEBTOR) DEBTOR=+$G(A4ADEB)
 Q:'DEBTOR
 N ERRCNT,OK,I,ICS,RCNT,DCNT,BCNT,TCNT,TERRCNT
 S BN=0,TN=0,RCNT=0,DCNT=0,BCNT=0,TCNT=0,TERRCNT=0,ERRCNT=0
 D INIT^A4AADP1
 W "CHECKING ""ATD"" CROSS-REFERENCE FOR FILE 433"
 S DCNT=0,RCNT=0,LERRCNT=0
 D LOOPDAT
 W !,$J(ERRCNT,10)," ERRORS FOUND"
 W !,$J(RCNT,10)," RECORDS CHECKED (",+$P(^PRCA(433,0),U,3),")"
 D EOT2
 QUIT
LOOPDAT ;; LOOP THRU DATES
 S DAT=0
 F  S DAT=$O(^PRCA(433,"ATD",DEBTOR,DAT)) Q:'DAT  D LOOPDTN
 QUIT
LOOPDTN ;; LOOP THRU TRANSACTIONS
 ; D C430XR^A4AADP3
 S TN=0
 F  S TN=$O(^PRCA(433,"ATD",DEBTOR,DAT,TN)) Q:'TN  D CHK433
 QUIT
CHK433 ;;
 S LERRCNT=ERRCNT
 K BILL,BN
 D GET433D^A4AADP3
 D GET430D^A4AADP3,C430XR^A4AADP3
 S RCNT=RCNT+1,ERRCODE=0
 D CHK1^A4AADP3,PERROR1^A4AADP1
 D CHK10^A4AADP3,PERROR1^A4AADP1
 D CHK11^A4AADP3,PERROR1^A4AADP1
 D CHK12^A4AADP3,PERROR1^A4AADP1
 D CHK13^A4AADP3,PERROR1^A4AADP1
 D C433XR^A4AADP3,PERROR1^A4AADP1
 W:LERRCNT'=ERRCNT !,"PRCA(433,""ATD"",",DEBTOR,",",DAT,",",TN,")"
 QUIT
BYASXR ;;
 S:'$G(DEBTOR) DEBTOR=+$G(A4ADEB)
 Q:'DEBTOR
 N ERRCNT,OK,I
 N AB,BAL,BCNT,BILL,CC,CS,DCNT,IB,MF,PB,PRCA4307,RCNT
 N DAT,DATE1,SDT,TAOAS,TAOC,TCNT,TERRCNT,TIME1,Y
 D INIT^A4AADP1
 W #,"COMPUTING TOTAL BALANCE FOR ACTIVE & OPEN BILLS",!,"USING THE AS CROSS-REFERENCE OF FILE 430"
 S DCNT=0,RCNT=0,TAOAS=0
 F CS=16,42 S BN=0 F  S BN=$O(^PRCA(430,"AS",DEBTOR,CS,BN)) Q:'BN  D 
   . D CHK430^A4AADP1
   . S BILL=BN,RCNT=RCNT+1
   . D CALCCBAL^A4AARPT
   . S TAOAS=TAOAS+BAL
 W !,$J(RCNT,10)," BILLS CHECKED (",+$P(^PRCA(430,0),U,3),")"
 W !,"TOTAL OF ACTIVE & OPEN BILLS COMPUTED BY USING THE AS XREF: ",$J(TAOAS,12,2)
 W !
EOT2 ;
 QUIT
KA4AV ;
 K A4ADEB,A4A,DEB,A4ADEV,A4AFRB,A4AIOSV,A4ANAME,A4ANAT,A4APAT,A4A,PSB,A4APSB2
 K A4ARDT,A4ASDAY,A4ASSN
 K A4ATL1,A4ATL2,A4ATL3,A4ATL4,A4ATL5,A4ATL6,A4ATL7,A4ATL8,A4ATL9,A4ATL10
 K A4ATNA,A4ATNA3,A4ATOA,A4ATOA1,A4AU1N4,A4APSB
EOR8 ;
 QUIT
DEBTOR ;
 D DEBTOR^A4AARPT
 D A4AARPT8
 QUIT
EXIT ;;
 D EOR^A4AARPT
CLEANUP ;;
 K ERRCNT,OK,RCNT,DCNT,TERRCNT,AT2,BCNT,BN,BN433,DAT,DCNT,DPT,ERRCNT
 K ERRCODE,NATD,RBAL,TAO,BAL,CAT,BILL,CS,DIC,FAN0P2,FLAG1
 K FAN0,FAN0P1,FAN0P8,FAN0P9,FAN6,FAN6P21
 K FBN0,FBN0P1,FBN0P10,FBN0P2,FBN0P4,FBN1,FBN1P2,FBN1P9
 K NAC,NACTDT,NAS,NAT,NAW,NB,NC,ND,OK,RCNT,TCNT,TDT,TERRCNT,TN,XTN
 K DAT,I,TN,LERRCNT
 K X,Y
 QUIT
