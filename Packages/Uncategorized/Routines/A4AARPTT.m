A4AARPT1 ;HINES-CIOFO/JJM - REPORT 1 ; 7/23/98 19:30
V ;;1.0;**** CLASS III ****;;JUN 30, 1998
 ; DEBTOR MUST BE DEFINED TO COMPUTE TOTAL FOR ACTIVE & OPEN BILLS
 ;
START ;
 S:'$G(DEBTOR) DEBTOR=+$G(A4ADEB)
 Q:'DEBTOR
 ; BFLAG = 1 TO LIST ONLY INCORRECT BILLS
 ; BFLAG = 0 TO LIST ALL BILLS
 ; FLAG1 = 0 SUMMARY FORMAT (DEFAULT)
 ; FLAG1 = 1 DETAIL  FORMAT
PRTRPT1 ;
 S:'$D(BFLAG) BFLAG=1 ; DEFAULT
 S:'$D(FLAG1) FLAG1=0 ; DEFAULT
 D SETUP
 D NOW^%DTC
 K ^XTMP("A4AARPT",$J)
 S ^XTMP("A4AARPT",$J,0)=%
 W:IOT="VTRM" @IOF
 U IO
 D PRTHDR1
 S BILL=""
 F  S BILL=$O(^PRCA(430,"C",DEBTOR,BILL)) Q:('BILL)!(END)  D
    . D:BFLAG=0 PBILL
    . D:BFLAG=1 PBILLS
 Q:END
 D HDR1
 W !,"TOTAL OF ACTIVE & OPEN BILLS: ",$J(TAO,10,2)
 S A4ATL6=TAO
ENDRPT1 ;
 D:IOT="VTRM" PAUSE^A4AARPT3
 W @IOF
 ; K AB,BAL,C,CA,CC,CS,EP,BFLAG,FLAG1,IB,ITF,LINE,MF,NAF,NATOTAL,PB,PFLAG,PRCA4307,RBAL,RFLAG,SC,STATUS,TAMT,TDATE,TIME1,TIME2,TRANS,TYPE
EOR1 ;
 QUIT
SETUP ;
 D NOW^%DTC
 D SETUP^A4AARPT
 S RBAL=0,NATOTAL=0,TAO=0,SRBAL=0
 S (LST1,LST2,LST3,LST4,LST5,LST6,SRBAL)=0
 QUIT
PBILL1 ;
 ; VALID ENTRY POINT WHEN BILL = IEN OF RECORD IN FILE 430 TO DISPLAY A BILL
  D NOW^%DTC
 K ^XTMP("A4AARPT",$J)
 S ^XTMP("A4AARPT",$J,0)=%
 S:'$D(DEBTOR) DEBTOR=+$P($G(^PRCA(430,BILL,0)),U,9)
 D SETUP
 D GETDAT^A4AARPT2
 D PBILL
 K DATE1,DATE2,DATE3,LST1,LST2,LST3,LST4,LST5,LST6,NAME,PAT,PG,SDAY,SRBAL
 K SSN,U1N4,X,DATE,F433N0,F433N1,F433N8,SFLAG,XDATE,BN
 QUIT
PBILL ;
 Q:'$G(BILL)
 S:'$D(DEBTOR) DEBTOR=+$P($G(^PRCA(430,BILL,0)),U,9)
 S FLAG1=1
 D PRTBILL
 QUIT
PBILLS1 ;
 ; VALID ENTRY POINT WHEN BILL = IEN OF RECORD IN FILE 430 TO DISPLAY AN
 ; INCORRECT BILL
  D NOW^%DTC
 K ^XTMP("A4AARPT",$J)
 S ^XTMP("A4AARPT",$J,0)=%
 D SETUP
 D PBILLS
 K DATE1,DATE2,DATE3,LST1,LST2,LST3,LST4,LST5,LST6,NAME,PAT,PG,SDAY,SRBAL
 K SSN,U1N4,X,DATE,F433N0,F433N1,F433N8,SFLAG,XDATE,BN
 QUIT
PBILLS ;
 Q:'$G(BILL)
 ; S FLAG1=1
 D ^A4AARPT7
 I ((BAL'=RBAL)&(CS'=23))!(CS=40)!((BAL'=0)&(CS=39))!((RBAL<0)&(CAT'=26))!((RBAL'=0)&(CS=23)) D
   . S FLAG1=1
   . W !,"****  INCORRECT BILL  ****"
 IF CS=44 S FLAG1=1 W !,"**** REFUND REVIEW ****"
 D PRTBILL
 S FLAG1=0
 QUIT
PRTBILL ;
 NEW X,Y
 S RBAL=0,BAL=0
 S CS=$P(^PRCA(430,BILL,0),U,8)
 S SC=$S(CS=16:" < ACTIVE >",CS=42:" < OPEN >",1:" ")
 Q:((CS=15)!(CS=18)!(CS=27)!(CS=49))
 ; I $Y+5>IOSL D PAUSE^A4AARPT3 Q:END  D PRTHDR1
 ; D:$Y>50 PRTHDR1
 D:FLAG1=1 ARPT1
 D CALCCBAL^A4AARPT W:FLAG1=1 !!," CURRENT BALANCE OF BILL: ",$J(BAL,12,2),SC," ("
 S TDATE=$P(^PRCA(430,BILL,0),U,10) S:$D(^PRCA(430,BILL,6))'=0 TDATE=$P(^PRCA(430,BILL,6),U,21)
 S Y=TDATE X ^DD("DD")
 W:FLAG1=1 Y,")"
 I ((CS=16)!(CS=42)) D 
 . S:(CS=16)!(CS=42) TAO=TAO+BAL
 S TDATE=$P(^PRCA(430,BILL,0),U,10)
 S:$D(^XTMP("A4AARPT",$J,TDATE))=0 ^XTMP("A4AARPT",$J,TDATE)=0
 S:$D(^XTMP("A4AARPT",$J,TDATE,BILL))=0 ^XTMP("A4AARPT",$J,TDATE,BILL)=1
 S:$D(^PRCA(433,"C",BILL))=0 ^TEMP($J,"Z"_BILL)=BILL
 D:$D(^PRCA(433,"C",BILL))>0 PTRANS
 Q:END
 I FLAG1=1 S LINE="",$P(LINE,"=",IOM)="" W !,LINE,!
 QUIT
PTRANS ;
 D:FLAG1=1 TRANHDR^A4AARPT2
 S RBAL=+$P(^PRCA(430,BILL,0),U,3)
 Q:(CS=15)!(CS=18)!(CS=20)!(CS=27)!(CS=40)!(CS=48)!(CS=49)
 ; D PRTTRAN2^A4AARPT6
 S TRANS=""
 F  S TRANS=$O(^PRCA(433,"C",BILL,TRANS)) Q:('TRANS)!(END)  D
 . I $Y>50 D PAUSE^A4AARPT3 Q:END  D PRTHDR1
 . D PRTTRAN3^A4AARPT6 S A4ATNA=NATOTAL
 . ; Q:CS=40
 . S ^TEMP($J,"TRANS",TRANS)=BILL
 . S XDATE=+$P($G(^PRCA(433,TRANS,1)),U,1)
 . S:$D(^XTMP("A4AARPT",$J,XDATE,BILL,TRANS))=0 ^XTMP("A4AARPT",$J,XDATE,BILL,TRANS)=0
 QUIT
PRTHDR1 ;
 Q:FLAG1=0
 W @IOF
HDR1 D HDR W !,"ACOUNTS RECEIVABLE FILE ( 430 ) DATA:",!
 QUIT
ARPT1 S LINE="",$P(LINE,"-",80)=""
 W @IOF
TRANBILL W !,"BILL NUMBER :",?16,$P(^PRCA(430,BILL,0),"^"),"(",BILL,")",?40,"       TYPE : " S TYPE=$P(^PRCA(430,BILL,0),"^",2),TYPE=$P($G(^PRCA(430.2,+TYPE,0)),"^")
 S STATUS=$P(^PRCA(430,BILL,0),"^",8),STATUS=$S($P($G(^PRCA(430.3,+STATUS,0)),"^")'="":$P(^(0),"^"),1:"")
 S DATE=$P(^PRCA(430,BILL,0),"^",10)
 W ?54,TYPE,!,"     STATUS :",?16,STATUS,?40,"       DATE :" S Y=DATE X ^DD("DD") W ?54,Y W !,?29,"AMOUNTS IN FILE 430"
 W !,LINE
 D ^A4AARPT4
 Q
 ;
HDR ;;
 D:'$D(NAME) SETUP^A4AARPT
 S LINE="",$P(LINE,"=",80)=""
 S %DT="T",X="NOW" D ^%DT X ^DD("DD")
 W @IOF,!,LINE,!,Y,?25,"AUDIT REPORT" S PG=$G(PG)+1 W ?75,PG,! W !,LINE K %DT
 W !,?13,"AR DEBTOR: ",?30,NAME," (",DEBTOR,")"
 W:SSN'="" !,?15,"    SSN: ",?30,SSN," (",U1N4,")"
 W !,?10,"STATMENT DAY: ",?30,SDAY,!,LINE
 W !
 QUIT
PRTRPT3 ;
 D PRTRPT3^A4AARPT2
 QUIT
