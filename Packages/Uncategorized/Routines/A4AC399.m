A4AC399 ;ISC4/JJM - PROGRAM TO CHECK FILE 399 ; 1/14/99 20:45
 ;;0.0;**** CLASS III ****;;JULY 14, 1995
HSK S ERRCNT=0
 S CNT=0
 S IEN=0
 W !,?30,"   REPORT 11"
 W !,?30,"AUDIT FILE 399"
 D NOW^%DTC
 W !,"JOB STARTED: ",%
START W !,"START -",^DGCR(399,0),!
 F  S IEN=$O(^DGCR(399,IEN)) Q:'IEN  S CNT=CNT+1 W:(CNT#1000)=0 "." D
 .  S F399NO=$G(^DGCR(399,IEN,0))
 .  S ARPAT=$P(F399NO,"^",2)
 .  S ARBILL=$P(F399NO,"^",1)
 .  D:ARPAT'="" CHKPAT
 .  IF (ARPAT="") D
 ..  S ERRCNT=ERRCNT+1
 ..  W !,$J(ERRCNT,8),"  *****",$J(IEN,8)," IB MISSING PATIENT *****","(",CNT,")"
 ..  QUIT
 .  D:ARBILL'="" CHKIEN
 .  IF (ARBILL="") D
 ..  S ERRCNT=ERRCNT+1
 ..  W !,$J(ERRCNT,8),"  *****",$J(IEN,8)," IB MISSING AR BILL # *****","(",CNT,")"
 ..  QUIT
EOJ ;
 D NOW^%DTC
 W !,"EOJ-",CNT,"  ",%
 K F399NO,ARBILL,ARTN,ARPAT,IEN,CNT,ERRCNT,ARIEN,ARB,TN,%
 QUIT
CHKPAT IF ($P($G(^DPT(ARPAT,0)),"^",1))="" D
 .  S ERRCNT=ERRCNT+1
 .  W !,$J(ERRCNT,8)," **** ",IEN," PATIENT ",ARPAT," NOT IN PATIENT FILE **** (",CNT,")"
 QUIT
CHKIEN S ARIEN=IEN
 IF ('+$P($G(^PRCA(430,ARIEN,0)),"^",1)) D  Q
 .  S ERRCNT=ERRCNT+1
 .  W !,$J(ERRCNT,8)," **** ",ARBILL,"(",IEN,")"," IS NOT IN AR FILE(430) **** (",CNT,")"
 S ARB=$P($P(^PRCA(430,ARIEN,0),"^",1),"-",2)
 IF ARB'=ARBILL D
 .  S ERRCNT=ERRCNT+1
 .  W !,$J(ERRCNT,8)," **** IB BILL ",ARBILL,"(",IEN,") NOT EQUAL TO AR BILL ",ARB,"(",ARIEN,")"
 QUIT
RESTART S CNT=16078
 S ERRCNT=36
 S TN=16078
 D START
EXIT ;;
 QUIT
