A4ARIGP ;HINES CIOFO/JJM-IG REPORTS ; 10/19/98 17:25
V ;;1.0;CLASS III REPORTS
QUEUE W !,"QUEUE STATS PROGRAM"
 S DIR(0)="S^B:BILLS;T:TRANSACTIONS"
 D ^DIR Q:(Y'="B")&(Y'="T")  D QUEUE1(Y) Q
QUEUE1(CODE) N IOP,ZTDESC,ZTASK,ZTIO,ZTRTN,ZTSAVE
 ;ENTRY POINT FROM NIGHTLY PROCESS
 S ZTIO="",ZTRTN=$S(CODE="B":"EN1^A4ARIG",1:"EN2^A4ARIG"),ZTDESC="AR IG REPORTS",ZTDTH=$H
 D ^%ZTLOAD,^%ZISC
 Q
EN1 ;
 K ^XTMP("A4ARIG",$J,"BILL")
 S ^XTMP("A4ARIG",$J,"BILL",0)=DT
 D BILL Q
EN2 ;
 K ^XTMP("A4ARIG",$J,"TRANS")
 S ^XTMP("A4ARIG",$J,"TRANS",0)=DT
 D TRANS Q
BILL ;PROCESS BILLS
 N CNT,XSTAT,XB7,XB0,DSTAT,XCAT,AMT,MAX,CNTR,SEQ,XFUND,XRSC,BILL,FYQ
 S (CNT,BILL,TAMT,TAMT1,TAMT2,TAMT3,TAMT4,TAMT5)=0
 D NOW^%DTC,YX^%DTC S DATE=Y
 S ^XTMP("A4ARIG",$J,"BILL","DATE/TIME",0)=Y
 F  S BILL=$O(^PRCA(430,BILL)) Q:BILL'?1N.N  D
    .N Y
    . ; Q:'$D(^PRCA(430,BILL,7))  S B7=^(7),B0=^(0)
    . S B7=$G(^PRCA(430,BILL,7)),B0=^(0)
    . ; Q:$P($G(^PRCA(430,BILL,6)),U,21)'<2980801
    .S AMT=$P(B7,U)+$P(B7,U,2)+$P(B7,U,3)+$P(B7,U,4)+$P(B7,U,5)
    .S AMT1=$P(B7,U,1)
    .S AMT2=$P(B7,U,2)
    .S AMT3=$P(B7,U,3)
    .S AMT4=$P(B7,U,4)
    .S AMT5=$P(B7,U,5)
    .Q:(AMT=0)!($P(B0,U,2)'=26)
    .S ACCRUED=$$ACCK^PRCAACC(BILL)
    .S:ACCRUED="" ACCRUED="  "
    .; Q:(STAT=18)!(STAT=20)!(STAT=27)!(STAT=31)
    .S STAT=$P(B0,U,8) S:STAT XSTAT=$E($P($G(^PRCA(430.3,STAT,0)),U),1,20),STAT=$$LJ^XLFSTR(STAT,20)
    .S XCAT=0
    .S CAT=$P(B0,U,2) S:CAT XCAT=$E($P($G(^PRCA(430.2,CAT,0)),U),1,25)
    .S XCAT=$$LJ^XLFSTR(XCAT,25)
    .S (Y,DSTAT)=$P(B0,U,14) X ^DD("DD") S DSTAT=Y
    .S:$L(DSTAT)=10 DSTAT=$E(DSTAT,1,4)_"0"_$E(DSTAT,5,10)
    .S DSTAT=$$LJ^XLFSTR(DSTAT,11)
    .S XB0=$P(B0,U),XB0=$$LJ^XLFSTR(XB0,11)
    .S FUND=$$GETFUNDB^RCXFMSUF(BILL,1)
    .S RSC=$$GETRSC
    .S XFUND=$J(FUND,6),XRSC=$J(RSC,4)
    .S TAMT=TAMT+AMT
    .S TAMT1=TAMT1+AMT1
    .S TAMT2=TAMT2+AMT2
    .S TAMT3=TAMT3+AMT3
    .S TAMT4=TAMT4+AMT4
    .S TAMT5=TAMT5+AMT5
    .S CNT=CNT+1,AMT=$$AMT(AMT)
    .S ^XTMP("A4ARIG",$J,CNT)=XB0_XCAT_XSTAT_AMT_DSTAT_FUND_RSC_"$"
    .S STAT=$P(B0,U,8)
    .D BTOTALS^A4ARIGS
    .Q
 D FYQ,BUILD("B",FYQ,CNT)
 W !,"TOTAL AMOUNT:",!,$J(TAMT,12,2),"=",$J(TAMT1,12,2),"+",$J(TAMT2,12,2),"+",$J(TAMT3,12,2),"+",$J(TAMT4,12,2),"+",$J(TAMT5,12,2),"  ",CNT,"  ",DATE
 S ^XTMP("A4ARIG",$J,"IEN",0)=TAMT_U_TAMT1_U_TAMT2_U_TAMT3_U_TAMT4_U_TAMT5_U_CNT_U_DATE
 Q
TRANS ;TRANSACTION PROCESSING
 N CNT,TRANS,BILL,TD,STDT,EDT,T0,T1,TT,FYQ,AMT,FUND,RSC
 D NOW^%DTC,YX^%DTC S DATE=Y
 S ^XTMP("A4ARIG",$J,"TRANS","DATE/TIME",0)=Y
 D FYQ
 S STDT=$S(FYQ=1:1001,FYQ=2:"0101",FYQ=3:"0401",FYQ=4:"0701")
 S STDT=$S(FYQ=1:($E(DT,1,3)-1)_STDT,1:$E(DT,1,3)_STDT)
 S EDT=$E(STDT,1,3)_$S(FYQ=1:1231.9999,FYQ=2:"0331.9999",FYQ=3:"0630.9999",1:"0930.9999")
 S (CNT,TRANS)=0
 F  S TRANS=$O(^PRCA(433,TRANS)) Q:TRANS'?1N.N  D
    .S T1=$G(^PRCA(433,TRANS,1)),T0=$G(^(0))
    .S TD=$P(T1,U,9) Q:$S(TD<STDT:1,TD>EDT:1,1:0)
    .S BILL=$P(T0,U,2) S:'BILL B0="           ",FUND="      ",RSC="    "
    .S:BILL B0=$P($G(^PRCA(430,BILL,0)),U),B0=$$LJ^XLFSTR(B0,11)
    .S AMT=$P(T1,"^",5) S AMT=$$AMT(AMT)
    .S TT=+$P(T1,"^",2) S:'TT TT="                      "
    .S:TT TT=$G(^PRCA(430.3,TT,0)),TT=$E($P(TT,"^"),1,22),TT=$$LJ^XLFSTR(TT,22)
    .D:BILL
       ..S FUND=$$GETFUNDB^RCXFMSUF(BILL,1)
       ..S RSC=$$GETRSC
       ..S FUND=$J(FUND,6),RSC=$J(RSC,4)
    .S CNT=CNT+1
    .S ^XTMP("A4ARIG",$J,"TRANS",CNT)=$J(TRANS,8)_AMT_B0_TT_FUND_RSC_"$"
    .S:$D(^XTMP("A4ARIG",$J,"TRANS","TT",TT))=0 ^XTMP("A4ARIG",$J,"TRANS","TT",TT)=0
    .S ^XTMP("A4ARIG",$J,"TRANS","TT",TT)=^XTMP("A4ARIG",$J,"TRANS","TT",TT)+AMT
           . ; FUND TOTALS
    .S:$D(^XTMP("A4ARIG",$J,"TRANS","TT",TT,"FUND",FUND))=0 ^XTMP("A4ARIG",$J,"TRANS","TT",TT,"FUND",FUND)=0
    .S ^XTMP("A4ARIG",$J,"TRANS","TT",TT,"FUND",FUND)=^XTMP("A4ARIG",$J,"TRANS","TT",TT,"FUND",FUND)+AMT
    .S:$D(^XTMP("A4ARIG",$J,"TRANS","FUND",FUND))=0 ^XTMP("A4ARIG",$J,"TRANS","FUND",FUND)=0
    .S ^XTMP("A4ARIG",$J,"TRANS","FUND",FUND)=^XTMP("A4ARIG",$J,"TRANS","FUND",FUND)+AMT
    .S:$D(^XTMP("A4ARIG",$J,"TRANS","FUND",FUND,"TT",TT))=0 ^XTMP("A4ARIG",$J,"TRANS","FUND",FUND,"TT",TT)=0
    .S ^XTMP("A4ARIG",$J,"TRANS","FUND",FUND,"TT",TT)=^XTMP("A4ARIG",$J,"TRANS","FUND",FUND,"TT",TT)+AMT
    . ; RSC TOTALS
    .S:$D(^XTMP("A4ARIG",$J,"TRANS","TT",TT,"RSC",RSC))=0 ^XTMP("A4ARIG",$J,"TRANS","TT",TT,"RSC",RSC)=0
    .S ^XTMP("A4ARIG",$J,"TRANS","TT",TT,"RSC",RSC)=^XTMP("A4ARIG",$J,"TRANS","TT",TT,"RSC",RSC)+AMT
    .S:$D(^XTMP("A4ARIG",$J,"TRANS","RSC",RSC))=0 ^XTMP("A4ARIG",$J,"TRANS","RSC",RSC)=0
    .S ^XTMP("A4ARIG",$J,"TRANS","RSC",RSC)=^XTMP("A4ARIG",$J,"TRANS","RSC",RSC)+AMT
    .S:$D(^XTMP("A4ARIG",$J,"TRANS","RSC",RSC,"TT",TT))=0 ^XTMP("A4ARIG",$J,"TRANS","RSC",RSC,"TT",TT)=0
    .S ^XTMP("A4ARIG",$J,"TRANS","RSC",RSC,"TT",TT)=^XTMP("A4ARIG",$J,"TRANS","RSC",RSC,"TT",TT)+AMT
    .Q
 D BUILD("T",FYQ,CNT)
 Q
FYQ ;CALCULATE PREVIOUS FY QUARTER
 S FYQ=$E(DT,4,5),FYQ=$S(FYQ<4:1,FYQ<7:2,FYQ<10:3,1:4)
 Q
NOW() N X,Y,%,%H
 S %H=$H D YX^%DTC
 Q Y
BUILD(CODE,FYQ,CNT) ;BUILDS MESSAGE ARRAY
 N MAX,CNTR,SEQ,REC,SITE
 S SITE=$$SITE^RCMSITE()
 S MAX=$S(CODE="B":350,1:500),(SEQ,CNTR)=0
 F CNTR=1:1:CNT D
    .D:CNTR#MAX=1
       ..K ^XTMP("A4ARIG",$J,"BUILD") S SEQ=SEQ+1
       ..S REC=0
       ..Q
    .S REC=REC+1,^XTMP("A4ARIG",$J,"BUILD",REC)=^XTMP("A4ARIG",$J,CNTR)
    .S:CNTR=CNT ^XTMP("A4ARIG",$J,"BUILD",REC+1)="END OF TRANSMISSION FOR SITE# "_SITE_":  TOTAL RECORDS: "_CNT
    .I $S(CNTR=CNT:1,CNTR#MAX=0:1,1:0) D
       ..N XMY,XMSUB
       ..S XMY("MURRAY,JOHN")="",XMDUZ="AR PACKAGE"
       ..S XMSUB=SITE_"/"_$S(CODE="B":"BILL",1:"TRANSACTION")_"/"_FYQ_"/SEQ#: "_SEQ_"/"_$$NOW()
       ..S XMTEXT="^XTMP(""A4ARIG"","_$J_",""BUILD"","
       ..D ^XMD
       ..Q
    .Q
 Q
AMT(X) ;CONVERTS AMOUNT TO RIGHT JUSTIFIED, 0 FILLED
 S X=$J(X,0,2),X=$P(X,".")_$P(X,".",2)
 S X=$E("000000000",1,9-$L(X))_X
 Q X
 ;
 ;
GETRSC() ;  return the rsc for a bill
 I FUND'=5287 Q $P($G(^PRCA(430,BILL,11)),U,6)
 ;  check missing patient for reimbursable health insurance
 I $P(^PRCA(430,BILL,0),"^",2)=9,'$P(^PRCA(430,BILL,0),"^",7) Q "    "
 Q $$CALCRSC^RCXFMSUR(BILL)
