%AAHGLP2 ;402,DJB,3/31/92**Print PIECE Data Dictionary
 ;;GEM III;;
T ;;David Bolduc - Togus, ME
XREF ;
 S J=0 F  S J=$O(^DD(FILE(LEV),FNUM,1,J)) Q:J=""!(J'>0)!FLAGQ  S XREFNAM=$P(^DD(FILE(LEV),FNUM,1,J,0),U,2) S:XREFNAM="" XREFNAM="-----" S XREFTYPE=$P(^(0),U,3) S:XREFTYPE="" XREFTYPE="REGULAR" D XREF1
 Q
XREF1 ;
 W ! D:$Y>GEMSIZE PAGE^%AAHGLP3 Q:FLAGQ
 W !?M1,"CROSS REF NAME:",?M3,XREFNAM D:$Y>GEMSIZE PAGE^%AAHGLP3 Q:FLAGQ  W !?12,"TYPE:",?M3,XREFTYPE D:$Y>GEMSIZE PAGE^%AAHGLP3 Q:FLAGQ
 S K=0 F  S K=$O(^DD(FILE(LEV),FNUM,1,J,K)) Q:K=""!FLAGQ  D:K="%D" DESCRIP I $D(^(K))#2 W ! W:K'>0 ?M1,K W:K>0 ?6,"Node: ",K S STRING=^(K) D STRING^%AAHGLP3 Q:FLAGQ
 Q
DESCRIP ;
 W ! S L=0 F  S L=$O(^DD(FILE(LEV),FNUM,1,J,K,L)) Q:L=""!FLAGQ  W !,?M1 S STRING=^(L,0) D WORD^%AAHGLP3 Q:FLAGQ
 W ! Q
HELP ;Print HELP FRAME text (^DIC(9.2,)
 W ! D:$Y>GEMSIZE PAGE^%AAHGLP3 Q:FLAGQ
 W !?M1,"This field has a HELP FRAME." D:$Y>GEMSIZE PAGE^%AAHGLP3 Q:FLAGQ
 R !?M1,"Do you wish to see the HELP FRAME text?: [YES/NO] NO//",ANS:GEMTIME S:'$T ANS="N" S:ANS="" ANS="N" Q:"Yy"'[$E(ANS)
 S XQH=^DD(FILE(LEV),FNUM,22) D EN^XQH
 K ANS,XQH Q
