%AAHGLS ;402,DJB,3/24/92**Main Screen Option Menu
 ;;GEM III;;
 ;;David Bolduc - Togus,ME
TOP ;
 I $G(FLAGLIST)="BASIC" D BASIC^%AAHGLS1 Q
 NEW PAGETEMP,TOTAL,TOTAL1
 S PAGETEMP=PAGE D ASK^%AAHGLSA
 I Z1?1.N D @$S(FLAGZERO:"ZERO^%AAHGLPS",FLAGWP:"WP^%AAHGLPS",FLAGXREF:"XREF^%AAHGLPS",$D(Z2):"PIECE^%AAHGLP",1:"^%AAHGLP") D MOVE
 I Z1["?" D ^%AAHGLH1,MOVE
 I Z1="J" S PAGETEMP=Z2 D MOVE
 I Z1="A" N FLAGHOT S FLAGHOT=PAGE D START^%AAHGL,MOVE
 I Z1="B" S PAGETEMP=PAGE-1 D MOVE
 I Z1="C" D CODE^%AAHGLS2,MOVE
 I Z1="E" D EDD^%AAHGLZ,MOVE
 I Z1="EV" D EDITV^%AAHGLE,MOVE
 I Z1="ES" D EDITS^%AAHGLE,MOVE
 I Z1="R" S FLAGREV=FLAGREV=0 D MOVE
 I Z1="S" D SKIP^%AAHGLS1
 I Z1="H" D HOT^%AAHGLS2,MOVE
 Q:FLAGQ  I FLAGL,"S"[Z1 Q
 S PAGE=PAGE+1 D HD^%AAHGLS2
 Q
MOVE ;Redraw same screen
 S PAGETEMP=$S(+PAGETEMP'=PAGETEMP:PAGE,PAGETEMP<1:1,PAGETEMP>PAGE:PAGE,1:PAGETEMP) D HD1^%AAHGLS2
 S CNTY="" F  S CNTY=$O(^TMP("A#GL",$J,GLS,PAGETEMP,CNTY)) Q:CNTY=""!FLAGQ  D
 .S GLNAM=^TMP("A#GL",$J,GLS,PAGETEMP,CNTY) I '$D(@GLNAM) W !,$J(CNTY,2),")   *** This node recently deleted ***" Q
 .S GLVAL=@GLNAM,GLSUB=$P($E(GLNAM,1,$L(GLNAM)-1),"(",2,99)
 .S GL=$P(GLNAM,"(")
 .I CODE'=0 X CODE E  R GEMXX#1:0 S:$T CODE=0 Q
 .W !,$J(CNTY,2),") ",$S('FLAGREV:GLNAM,1:$$GLOBNAME^%AAHGLA1(GLNAM))
 .S COL=$X,TOTAL=(GEMIOM-4-COL) W " = ",$E(GLVAL,1,TOTAL)
 .F  Q:$L(GLVAL)'>TOTAL  S TOTAL1=(TOTAL+GEMIOM-4-COL) W !?COL," = ",$E(GLVAL,TOTAL+1,TOTAL1) S TOTAL=TOTAL1
 D ASK^%AAHGLSA Q:FLAGQ
 I Z1?1.N D @$S(FLAGZERO:"ZERO^%AAHGLPS",FLAGWP:"WP^%AAHGLPS",FLAGXREF:"XREF^%AAHGLPS",$D(Z2):"PIECE^%AAHGLP",1:"^%AAHGLP") G MOVE
 I Z1["?" D ^%AAHGLH1 G MOVE
 I Z1="J" S PAGETEMP=Z2 G MOVE
 I Z1="A" N FLAGHOT S FLAGHOT=PAGETEMP D START^%AAHGL G MOVE
 I Z1="B" S PAGETEMP=PAGETEMP-1 G MOVE
 I Z1="C" D CODE^%AAHGLS2 G MOVE
 I Z1="E" D EDD^%AAHGLZ G MOVE
 I Z1="EV" D EDITV^%AAHGLE G MOVE
 I Z1="ES" D EDITS^%AAHGLE G MOVE
 I Z1="R" S FLAGREV=FLAGREV=0 G MOVE
 I Z1="S" W *7,?15,"You can't use Skip in a screen you've ""Backed-Up"" to.",!?15,"Hit <RETURN> to continue.." R GEMXX:GEMTIME G MOVE
 I Z1="H" D HOT^%AAHGLS2 G MOVE
 S PAGETEMP=PAGETEMP+1 Q:PAGETEMP>PAGE
 G MOVE
