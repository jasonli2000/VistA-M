%AAHRR ;402,DJB,6/25/92**Acme Routine Reader
 ;;GEM III;;
 ;;David Bolduc - Augusta,ME
EN ;Entry Point
 I $G(FLAGEDIT)'="EDIT",$G(DUZ)'>0 D ID Q:$G(DUZ)=""
 S $ZT="ERROR^%AAHRRZ"
START ;
 I '$D(GEMIOF) K FLAGARR
 NEW ARRACT,ARRHIGH,ARRLN,ARRMARK,ARRPGM,CODE,CODELN,CODETG,END,END1,FLAGL,FLAGQ,FLAGT,FLAGTAG,I,LNUM,PGTOP,START,STNG,TAG,ZTSAV
 I $G(FLAGARR)'="ARR" NEW ARRLINE,ARRS,ARRSP,FLAGARR,FLAGCOL,U
 I $G(FLAGARR)'="ARR" NEW GEMIOF,GEMIOM,GEMIOSL,GEMIOST,GEMLINE,GEMLINE1,GEMLINE2,GEMRON,GEMROFF,GEMSIZE,GEMTIME,GEMXX,GEMXY
 I $G(FLAGEDIT)'="EDIT",$G(FLAGARR)'="ARR" NEW GEMSYS ;Mumps system
 I $G(FLAGEDIT)="EDIT" NEW DX,DY
 I $G(FLAGARR)'="ARR" S FLAGQ=0 D ^%AAHRRZ1 Q:FLAGQ  ;Identify Mumps System
 D INIT^%AAHRRZ,EN^%AAHRRS G:"^"[ARRPGM EX ;^%AAHRRS selects program and loads ^TMP("PGM",$J) global.
 S FLAGARR="ARR" ;To mark the fact that AE is running
 S FLAGCOL=0 ;Used by Collapse option
TOP ;
 S (FLAGL,FLAGT)=0 D FF^%AAHRRU
 F LNUM=START:1 S FLAGQ=0 Q:'$D(^TMP("PGM",$J,ARRS,"TXT",LNUM))  S CODE=^(LNUM) D PRINT Q:FLAGQ=1
 S FLAGL=1 I FLAGQ'=1 D PAGE^%AAHRRP I FLAGQ'=1 S START=LNUM+1 G TOP
EX ;
 I $G(FLAGEDIT)'="EDIT"!(ARRS>1) K ^TMP("PGM",$J,ARRS)
 S ARRS=ARRS-1
 Q
PRINT ;Print routine lines
 S CODETG=$P(CODE,$C(9)),CODELN=$P(CODE,$C(9),2,999)
 I $G(FLAGTAG)="FT" Q:$E(CODETG,1,$L(TAG))'=TAG  S FLAGTAG=0,PGTOP=LNUM
 I $G(FLAGTAG)="FS" Q:CODE'[STNG  S FLAGTAG=0,PGTOP=LNUM
 I $G(ARRSP)=2 I LNUM'=PGTOP!(LNUM=PGTOP&($Y>1)) W ! D:$Y>GEMSIZE PAGE^%AAHRRP Q:FLAGQ  ;Single or Double spaced display
 S (END,END1)=GEMIOM-10 I $L(CODETG)>8 S END1=END1-($L(CODETG)-9)
 W !,@$S(CODETG]"":"$J(CODETG,8)",1:"LNUM"),?9,$E(CODELN,1,END1) D:$Y>GEMSIZE PAGE^%AAHRRP Q:FLAGQ  S CODELN=$E(CODELN,(END1+1),999)
PRINT1 ;Use GOTO to conserve stack levels
 Q:CODELN=""
 W !?9,$E(CODELN,1,END) D:$Y>GEMSIZE PAGE^%AAHRRP Q:FLAGQ  S CODELN=$E(CODELN,(END+1),999)
 G PRINT1
P ;Call here if KERNEL isn't running. This call no longer needed
 I $G(DUZ)'>0 D ID Q:$G(DUZ)=""
 G EN
ID ;Get DUZ
 I $D(^XUSEC(0)) D ^XUP Q  ;KERNEL loaded
 S DUZ=0,DUZ(0)=$S($G(DUZ(0))]"":DUZ(0),1:"@")
 Q
