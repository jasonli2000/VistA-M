%AAHRRE ;402,DJB,6/25/92**INSERT,DELETE,CHANGE
 ;;GEM III;;
 ;;David Bolduc - Augusta,ME
DELETE ;Delete a line
 NEW ARRLN,I,II,X1,X2 D SCREEN^%AAHRRU
DELETEA S ARRLN=$$GETLINE^%AAHRRU("DELETE WHICH LINE NUMBER(S): ") Q:ARRLN="^"
 I ARRLN="?"!(ARRLN="INVALID") D MSG1 G DELETEA
 I ARRLN="INVALID RNG" D MSG8 G DELETEA
 S (X1,X2)=ARRLN I ARRLN["-" S X1=$P(ARRLN,"-"),X2=$P(ARRLN,"-",2),ARRLN=X1
 F I=X1:1:X2 D
 .F II=ARRLN:1:(ARRHIGH-1) S ^TMP("PGM",$J,ARRS,"TXT",II)=^TMP("PGM",$J,ARRS,"TXT",(II+1))
 .K ^TMP("PGM",$J,ARRS,"TXT",ARRHIGH) S ARRHIGH=ARRHIGH-1,FLAGSAVE=1
 Q
INSERT ;Insert line of code
 NEW ARRLN,CD,VALID D SCREEN^%AAHRRU
INSERTA S ARRLN=$$GETLINE^%AAHRRU("INSERT AFTER LINE NUMBER: ") Q:ARRLN="^"
 I ARRLN'?1.N D MSG3 G INSERTA
 D SCREEN^%AAHRRU W !?1,"ENTER CODE:"
INSERT1 R !?1,"*",CD:GEMTIME S:'$T CD="^" I "^"[CD Q
 I "??"[CD D MSG4 G INSERT1
 S VALID=0 D CHKCODE G:VALID INSERT1
 S ARRHIGH=ARRHIGH+1 F I=ARRHIGH:-1:(ARRLN+2) S ^TMP("PGM",$J,ARRS,"TXT",I)=^TMP("PGM",$J,ARRS,"TXT",(I-1))
 S ARRLN=ARRLN+1 D SAVE^%AAHRREU,CHECK^%AAHRREU
 G INSERT1
CHANGE ;Change a line
 NEW ARRLN,CHK,NEW,OLD,TAB,TEMP,TEMP1,VALID D SCREEN^%AAHRRU
CHANGEA S ARRLN=$$GETLINE^%AAHRRU("CHANGE LINE NUMBER: ") G:ARRLN="^" CHANGEX
 I ARRLN'?1.N D MSG3 G CHANGEA
CHANGEB NEW CD,CDHLD S (CD,CDHLD)=$G(^TMP("PGM",$J,ARRS,"TXT",ARRLN)) G:CD']"" CHANGEX
 I $G(^%AAHE("EDIT"))="CURSOR"!($G(^("EDIT"))="")  D ^%AAHRREA G CHANGEX ;Screen oriented editor.
 D SCREEN^%AAHRRU
CHANGE1 ;
 W ! D PRINT
CHANGE2 ;
 R !!,"REPLACE: ",OLD:GEMTIME G:OLD="" CHANGEX S:OLD="end" OLD="END"
 I OLD="..." S OLD=CD
 I OLD?.E1"...".E S TEMP=$P(OLD,"..."),TEMP1=$P(OLD,"...",2) D  I CD'[OLD D MSG9 G CHANGE1
 .I TEMP="" S OLD=$E(CD,1,($F(CD,TEMP1)-1)) Q
 .I TEMP1="" S OLD=$E(CD,($F(CD,TEMP)-$L(TEMP)),$L(CD)) Q
 .S CHK=$F(CD,TEMP),OLD=$E(CD,(CHK-$L(TEMP)),($F(CD,TEMP1,CHK)-1))
 I OLD'="END",CD'[OLD D MSG9 G CHANGE1
 R !,"WITH: ",NEW:GEMTIME Q:'$T
 S CD=$S(OLD="END":CD_NEW,1:$P(CD,OLD)_NEW_$P(CD,OLD,2,999))
 S VALID=0 D CHKCODE I VALID S CD=CDHLD G CHANGE1
 S CDHLD=CD D SAVE^%AAHRREU
 G CHANGE1
CHANGEX ;CHANGE Exit
 D CHECK^%AAHRREU
 Q
PRINT ;Print code line
 S CODETG=$P(CD,$C(9)),CODELN=$P(CD,$C(9),2,999)
 S (END,END1)=GEMIOM-10 I $L(CODETG)>8 S END1=END1-($L(CODETG)-9)
 W ! W:CODETG]"" $J(CODETG,8) W ?9,$E(CODELN,1,END1) S CODELN=$E(CODELN,(END1+1),999)
PRINT1 ;Use GOTO to conserve stack levels
 Q:CODELN=""  
 W !?9,$E(CODELN,1,END) S CODELN=$E(CODELN,(END+1),999)
 G PRINT1
 Q
CHKCODE ;Check validity of line of code
 NEW LINE,TAG,TAG1
 S TAG1=$P(CD,$C(9)),TAG=$P(TAG1,"("),LINE=$P(CD,$C(9),2,999)
 I $L(CD,$C(9))'>1 D MSG6 S VALID=1 Q
 I $L(CD)>245 D MSG7 S VALID=1 Q
 I LINE="" D MSG6 S VALID=1 Q
 I TAG]"",$E(TAG)'?1AN,$E(TAG)'="%" D MSG2 S VALID=1 Q
 I $E(TAG)?1N,TAG'?1.N D MSG2 S VALID=1 Q
 I $L(TAG)>1,$E(TAG)'?1N,$E(TAG,2,999)'?1.AN D MSG2 S VALID=1 Q
 I TAG1?.E1"(""".E1""")" D MSG6 S VALID=1 Q
 Q
MSG ;Messages
MSG1 W !?1,"Enter line number (From 1-",ARRHIGH,"), or a range of numbers (Ex. 2-6)." Q
MSG2 W *7,"   Invalid line label." Q
MSG3 W "   Enter line number from 1-",ARRHIGH,"." Q
MSG4 W !?1,"Enter code you wish inserted. Use <TAB> to start or after TAG." Q
MSG6 W *7,"   Illegal line" Q
MSG7 W *7,"   Code length can't exceed 255" Q
MSG8 W *7,"   A range may not exceed 2-",ARRHIGH Q
MSG9 W *7,"   No match" Q
MSG10 W "   The line will be broken AFTER the code you enter" Q
