%AAHRRE1 ;402,DJB,6/25/92**SAVE,UNSAVE,LC,SPLIT,JOIN
 ;;GEM III;;
 ;;David Bolduc - Augusta,ME
SAVE ;Save a line
 NEW ARRLN,CNT,X1,X2 D SCREEN^%AAHRRU
SAVEA S ARRLN=$$GETLINE^%AAHRRU("SAVE WHICH LINE NUMBER(S): ") Q:ARRLN="^"
 I ARRLN="?"!(ARRLN="INVALID") D MSG1^%AAHRRE G SAVEA
 I ARRLN="INVALID RNG" D MSG8^%AAHRRE G SAVEA
 S CNT=1,(X1,X2)=ARRLN I ARRLN["-" S X1=$P(ARRLN,"-"),X2=$P(ARRLN,"-",2)
 F ARRLN=X1:1:X2 S ^%AAHE("SAVE",$J,CNT)=^TMP("PGM",$J,ARRS,"TXT",ARRLN),CNT=CNT+1
 S ^%AAHE("SAVE",$J,CNT)=""
 Q
UNSAVE ;Unsave code previously saved
 I '$D(^%AAHE("SAVE",$J)) W *7,"   No code has been saved. Select SA option." R !?2,"<RETURN> to continue..",GEMXX:GEMTIME Q
 NEW ARRLN,CD,I,II D SCREEN^%AAHRRU
UNSAVEA S ARRLN=$$GETLINE^%AAHRRU("UNSAVE AFTER LINE NUMBER: ") Q:ARRLN="^"
 I ARRLN="?"!(ARRLN["INVALID")!(ARRLN["-") D MSG3^%AAHRRE G UNSAVEA
 F I=1:1 Q:'$D(^%AAHE("SAVE",$J,I))  Q:^(I)=""  S CD=^(I) D  S FLAGSAVE=1
 .S ARRHIGH=ARRHIGH+1 F II=ARRHIGH:-1:(ARRLN+2) S ^TMP("PGM",$J,ARRS,"TXT",II)=^TMP("PGM",$J,ARRS,"TXT",(II-1))
 .S ^TMP("PGM",$J,ARRS,"TXT",(ARRLN+1))=CD,ARRLN=ARRLN+1
 Q
LC ;Locate and Change
 NEW ADJ,ARRLN,CD,FD,I,NEW,OLD,START,X1,X2 D SCREEN^%AAHRRU
LCA S ARRLN=$$GETLINE^%AAHRRU("CHANGE WHICH LINE NUMBER(S): ") Q:ARRLN="^"
 I ARRLN="?"!(ARRLN="INVALID") D MSG1^%AAHRRE G LCA
 I ARRLN="INVALID RNG" D MSG8^%AAHRRE G LCA
 R !!,"REPLACE: ",OLD:GEMTIME Q:OLD=""
 R !,"WITH: ",NEW:GEMTIME
 S (X1,X2)=ARRLN I ARRLN["-" S X1=$P(ARRLN,"-"),X2=$P(ARRLN,"-",2)
 F ARRLN=X1:1:X2 D LCRPLC
 Q
LCRPLC ;Replace OLD with NEW
 S CD=$G(^TMP("PGM",$J,ARRS,"TXT",ARRLN)) I CD'[OLD Q
 S START=0,ADJ=$L(NEW)-$L(OLD) ;ADJ will adjust START if NEW is different length than OLD
 F I=1:1:($L(CD,OLD)-1) S FD=$F(CD,OLD,START),START=FD+ADJ,CD=$E(CD,1,(FD-$L(OLD)-1))_NEW_$E(CD,FD,999)
 I $L(CD,$C(9))'=2!($L(CD)>245) W *7 Q  ;No TAB or line too long
 I $L($P($P(CD,$C(9)),"("))>8 W *7 Q  ;Line Tag too long
 I CD']"" Q
 S ^TMP("PGM",$J,ARRS,"TXT",ARRLN)=CD,FLAGSAVE=1
 Q
BREAK ;Break a line
 NEW ARRLN,CD,BRK,X1,X2 D SCREEN^%AAHRRU
BREAKA S ARRLN=$$GETLINE^%AAHRRU("BREAK WHICH LINE NUMBER: ") Q:ARRLN="^"
 I ARRLN'?1.N D MSG3^%AAHRRE G BREAKA
 S CD=^TMP("PGM",$J,ARRS,"TXT",ARRLN)
BREAK1 ;
 W !,"*",CD
BREAK2 ;
 R !?1,"BREAK AFTER WHAT CODE: ",BRK:GEMTIME S:'$T BRK="^" I "^"[BRK Q
 I "??"[BRK D MSG10^%AAHRRE G BREAK2
 I CD'[BRK D MSG9^%AAHRRE G BREAK1
 S X1=$E(CD,1,($F(CD,BRK)-1)),X2=$E(CD,$F(CD,BRK),999)
 I $L(X1,$C(9))'=2 W *7,"   You may not break at a line tag" G BREAK2
 I X2']"" W *7,"   Invalid entry" G BREAK2
 F  Q:$E(X1,$L(X1))'=" "  S X1=$E(X1,1,($L(X1)-1))
 F  Q:$E(X2)=""!($E(X2)'=" ")  S X2=$E(X2,2,999)
 S X2=$C(9)_X2
 S ^TMP("PGM",$J,ARRS,"TXT",ARRLN)=X1
 S ARRHIGH=ARRHIGH+1 F I=ARRHIGH:-1:(ARRLN+2) S ^TMP("PGM",$J,ARRS,"TXT",I)=^TMP("PGM",$J,ARRS,"TXT",(I-1))
 S ^TMP("PGM",$J,ARRS,"TXT",(ARRLN+1))=X2,ARRLN=ARRLN+1,FLAGSAVE=1
 Q
JOIN ;Join 2 lines
 NEW I,LN1,LN1CD,LN2,LN2CD D SCREEN^%AAHRRU
JOINA S LN1=$$GETLINE^%AAHRRU("1ST LINE NUMBER: ") Q:LN1="^"
 I LN1'?1.N D MSG3^%AAHRRE G JOINA
JOIN1 S LN2=$$GETLINE^%AAHRRU("2ND LINE NUMBER: ") Q:LN2="^"
 I LN2'?1.N D MSG3^%AAHRRE G JOIN1
 I LN1=LN2 W *7,"   Line numbers can't match" G JOIN1
 S LN1CD=^TMP("PGM",$J,ARRS,"TXT",LN1)
 S LN2CD=$P(^TMP("PGM",$J,ARRS,"TXT",LN2),$C(9),2,999) ;Strip off any line tag
 I ($L(LN1CD)+$L(LN2CD))>245 W *7,"   Joined line would be too long" G JOIN1
 S ^TMP("PGM",$J,ARRS,"TXT",LN1)=LN1CD_" "_LN2CD ;Insert a space
 ;Next delete 2nd line
 F I=LN2:1:(ARRHIGH-1) S ^TMP("PGM",$J,ARRS,"TXT",I)=^TMP("PGM",$J,ARRS,"TXT",(I+1))
 K ^TMP("PGM",$J,ARRS,"TXT",ARRHIGH) S ARRHIGH=ARRHIGH-1,FLAGSAVE=1
 Q
