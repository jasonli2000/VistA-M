%AAHRREB ;402,DJB,6/25/92**CHANGE - Scrn Mode - INSERT,DELETE,ARROW,OTHER
 ;;GEM III;;
 ;;David Bolduc - Augusta,ME
INSERT ;Insert a character
 I $L(CD)>244 W *7,!?15,@GEMRON," Line length may not exceed 245. ",@GEMROFF,!?15,@GEMRON,"     <RETURN> to continue..      ",@GEMROFF R GEMXX:GEMTIME S FLAGQ=1 Q
 ;Next do Tag
 I YCUR=0 D  Q:FLAGQ  S XCHAR=XCHAR+1,XCUR=XCUR+1 D SAVE^%AAHRREU,PRINTT^%AAHRREP Q
 .I XCHAR=1 S CD=X_CODETG_$C(9)_CODELN
 .I XCHAR>1 S CD=$E(CODETG,1,XCHAR-1)_X_$E(CODETG,XCHAR,999)_$C(9)_CODELN
 .D CHKTAG^%AAHRREU
 ;Next do Line
 I XCHAR>1 S CD=CODETG_$C(9)_$E(CODELN,1,XCHAR-1)_X_$E(CODELN,XCHAR,999)
 I XCHAR=1 S CD=CODETG_$C(9)_X_CODELN
 I XCHAR#69=0 S XCUR=9,YCUR=YCUR+1
 E  S XCUR=XCUR+1
 S XCHAR=XCHAR+1 D SAVE^%AAHRREU,PRINTL^%AAHRREP
 Q
DELETE ;Delete a character
 Q:XCHAR'>1
 I YCUR=0 D  D SAVE^%AAHRREU,PRINTT^%AAHRREP Q  ;Edit line tag
 .S XCUR=XCUR-1,XCHAR=XCHAR-1,CD=$E(CODETG,1,XCHAR-1)_$E(CODETG,XCHAR+1,999)_$C(9)_CODELN
 I XCHAR=2 S XCUR=XCUR-1,XCHAR=XCHAR-1,CD=CODETG_$C(9)_$E(CODELN,2,999) D SAVE^%AAHRREU,PRINTL^%AAHRREP Q
 I XCUR=9,YCUR>1 S XCUR=77,YCUR=YCUR-1
 E  S XCUR=XCUR-1
 S XCHAR=XCHAR-1,CD=CODETG_$C(9)_$E(CODELN,1,XCHAR-1)_$E(CODELN,XCHAR+1,999)
 D SAVE^%AAHRREU,PRINTL^%AAHRREP
 Q
BULKDEL ;Delete a character
 NEW TEMP
 I TAB1>0,TAB2>0 S (TAB1,TAB2)=0 Q
 S TAB2=$S(TAB1>0:XCHAR_","_YCUR,1:0)
 S TAB1=$S(TAB1=0:XCHAR_","_YCUR,1:TAB1)
 I TAB2=0 D  Q
 .S DX=73,DY=18 W @GEMSYS("CRSR"),@GEMRON,"DEL",@GEMROFF
 .S DX=XCUR,DY=TOP+YCUR+1 W @GEMSYS("CRSR") ;Use +1 because TAG takes up a line
 I $P(TAB1,",",2)=0&($P(TAB2,",",2)>0) D RESET^%AAHRREU Q  ;Tag & Line treated separate
 I $P(TAB2,",",2)=0&($P(TAB1,",",2)>0) D RESET^%AAHRREU Q
 I +TAB1>+TAB2 S TEMP=TAB1,TAB1=TAB2,TAB2=TEMP
 I YCUR=0 S CD=$E(CODETG,1,+TAB1-1)_$E(CODETG,+TAB2+1,999)_$C(9)_CODELN
 E  S CD=CODETG_$C(9)_$E(CODELN,1,+TAB1-1)_$E(CODELN,+TAB2+1,999)
 S XCHAR=+TAB1,XCUR=XCHAR#69+8,YCUR=$P(TAB1,",",2),TAB1=0,TAB2=0
 D SAVE^%AAHRREU,PRINTL^%AAHRREP,RESET^%AAHRREU
 Q
ARROW ;Process arrow keys
 I X="<AR>" D  Q
 .I YCUR=0,XCHAR'<($L(CODETG)+1) Q
 .I YCUR>0,XCHAR'<($L(CODELN)+1) Q
 .I XCHAR#69=0,YCNT>YCUR S XCUR=9,YCUR=YCUR+1,XCHAR=XCHAR+1 Q
 .S XCUR=XCUR+1,XCHAR=XCHAR+1 Q
 I X="<AL>" D  Q
 .I XCUR>9 S XCUR=XCUR-1,XCHAR=XCHAR-1 Q
 .I XCUR=9,YCUR>1 S XCUR=77,YCUR=YCUR-1,XCHAR=XCHAR-1 Q
 I X="<AU>" D
 .I YCUR=1 S YCUR=0 D  Q
 ..I XCHAR>($L(CODETG)+1) F  Q:XCHAR=1!(XCHAR=($L(CODETG)+1))  S XCHAR=XCHAR-1,XCUR=XCUR-1
 .I YCUR>1 S YCUR=YCUR-1,XCHAR=XCHAR-69
 I X="<AD>" D
 .I YCUR=0 S YCUR=1 Q
 .I YCUR<YCNT S YCUR=YCUR+1,XCHAR=XCHAR+69
 .I XCHAR>($L(CODELN)+1) F  Q:XCHAR=1!(XCHAR=($L(CODELN)+1))  S XCHAR=XCHAR-1,XCUR=XCUR-1
 Q
OTHER ;<F1>,<F2>,<F3>,<F4>
 I X="<F1>",XCUR>23 S XCUR=XCUR-15,XCHAR=XCHAR-15
 I X="<F2>",XCUR<63,($L(CODELN)-XCHAR)>14 S XCUR=XCUR+15,XCHAR=XCHAR+15
 I X="<F3>" S XCUR=9,XCHAR=1,YCUR=1
 I X="<F4>" S XCHAR=$L(CODELN)+1,XCUR=$S(XCHAR#69=0:77,1:XCHAR#69+8),YCUR=$S((XCHAR-1)#69=0:YCNT+1,1:YCNT)
 Q
