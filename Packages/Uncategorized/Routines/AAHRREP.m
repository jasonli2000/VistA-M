%AAHRREP ;402,DJB,6/25/92**CHANGE - Scrn Mode - Print
 ;;GEM III;;
 ;;David Bolduc - Augusta,ME
PRINTA ;Print All - Heading,Tag,Line
 S CODETG=$P(CD,$C(9)),(CODELN,TEMP)=$P(CD,$C(9),2,999)
 S YCNT=$S($L(CODELN)<70:1,$L(CODELN)<139:2,$L(CODELN)<208:3,$L(CODELN)<276:4,1:5)
 S TOP=18 D CLRSCRN^%AAHRREU
 S DX=14 W @GEMSYS("CRSR"),@GEMRON,"LINE:",$J(ARRLN,3),@GEMROFF
 S DX=27 W @GEMSYS("CRSR"),@GEMRON,"LENGTH:",$J($L(CD),4),@GEMROFF
 S DX=43 W @GEMSYS("CRSR"),@GEMRON,"<TAB>=Help",@GEMROFF
 S DX=58 W @GEMSYS("CRSR"),@GEMRON,"<RET>=Quit",@GEMROFF
 I TAB1>0 S DX=73 W @GEMSYS("CRSR"),@GEMRON,"DEL",@GEMROFF
 W !,@GEMRON,"  TAG-->",@GEMROFF," ",CODETG,!,@GEMRON," LINE-->",@GEMROFF
 W " ",$E(CODELN,1,69) S TEMP=$E(CODELN,70,999)
PRINTA1 ;Print remainder of line
 I TEMP="" D CLRSCRN1^%AAHRREU Q
 W !?9,$E(TEMP,1,69) S TEMP=$E(TEMP,70,999)
 G PRINTA1
PRINTL ;Print Line
 S CODETG=$P(CD,$C(9)),CODELN=$P(CD,$C(9),2,999),TEMP=$E(CODELN,XCHAR,999)
 S YCNT=$S($L(CODELN)<70:1,$L(CODELN)<139:2,$L(CODELN)<208:3,$L(CODELN)<276:4,1:5)
 S DX=34,DY=TOP W @GEMSYS("CRSR"),@GEMRON,$J($L(CD),4),@GEMROFF
 I XCUR>9 S DX=XCUR,DY=TOP+1+YCUR W @GEMSYS("CRSR"),@GEMSYS("BLANK")
 I XCUR=9,YCUR=1 S DX=XCUR,DY=TOP+1+YCUR W @GEMSYS("CRSR"),@GEMSYS("BLANK")
 I XCUR=9,YCUR>1 S DX=78,DY=TOP+1+YCUR-1 W @GEMSYS("CRSR"),@GEMSYS("BLANK") S DX=XCUR,DY=TOP+1+YCUR
 W @GEMSYS("CRSR")
 W $E(TEMP,1,78-XCUR) S TEMP=$E(TEMP,79-XCUR,999)
PRINTL1 ;Print remainder of line
 I TEMP="" W @GEMSYS("CRSR") Q
 W !?9,$E(TEMP,1,69) S TEMP=$E(TEMP,70,999)
 G PRINTL1
 Q
PRINTT ;Print Tag
 S CODETG=$P(CD,$C(9)),CODELN=$P(CD,$C(9),2,999)
 S DX=34,DY=TOP W @GEMSYS("CRSR"),@GEMRON,$J($L(CD),4),@GEMROFF
 S DX=XCUR,DY=TOP+1 W @GEMSYS("CRSR"),@GEMSYS("BLANK2")
 W $E(CODETG,XCHAR,999)
 S DX=XCUR,DY=TOP+1 W @GEMSYS("CRSR")
 Q
