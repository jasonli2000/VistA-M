%AAHRRP ;402,DJB,6/25/92**Page
 ;;GEM III;;
 ;;David Bolduc - Augusta,ME
PAGE ;Prompt that appears at the bottom of each screen
 S FLAGTAG=0 I FLAGCOL,ARRS>FLAGCOL S FLAGQ=1 Q
 F I=$Y:1:GEMSIZE W !
 W !,$E(ARRLINE,1,GEMIOM),!?1,"^,"" ""=Quit  N=Navigate  B=Branch  O=Other  ?=Help"
 I $G(FLAGEDIT)="EDIT" W $S(ARRS<2:"  ED=Edit",1:"  SA=Save")
PAGE1 ;
 W ! S ARRACT=$$READ^%AAHRRRD(" Select: ") S:ARRACT="<TO>"!(ARRACT=" ") ARRACT="^" I ARRACT="^" S FLAGQ=1 Q
 I +ARRACT=ARRACT,ARRACT>0,ARRACT'>ARRHIGH,ARRACT\1=ARRACT NEW OK S OK=0 D  G:OK PAGE1 Q
 .I $G(FLAGEDIT)'="EDIT"!(ARRS>1) D MSG5^%AAHRRU S OK=1 Q
 .S ARRLN=ARRACT D CHANGEB^%AAHRRE S FLAGQ=2,LNUM=PGTOP-1 D FF^%AAHRRU Q
 I ARRACT="<AU>" W:PGTOP=1 *7 S FLAGQ=2,PGTOP=$S(PGTOP'=1:PGTOP-1,1:PGTOP),LNUM=PGTOP-1 D FF^%AAHRRU Q
 I ARRACT="<AD>" W:LNUM>ARRHIGH *7 S FLAGQ=2,PGTOP=$S(LNUM>ARRHIGH:PGTOP,1:PGTOP+1),LNUM=PGTOP-1 D FF^%AAHRRU Q
 I ARRACT="<AL>" W:PGTOP=1 *7 S FLAGQ=2,PGTOP=$S(PGTOP>$S(ARRSP=1:12,1:6):PGTOP-$S(ARRSP=1:12,1:6),1:1),LNUM=PGTOP-1 D FF^%AAHRRU Q
 I ARRACT="<AR>" W:LNUM>ARRHIGH *7 S FLAGQ=2,PGTOP=$S(PGTOP<$S(ARRSP=1:(ARRHIGH-11),1:(ARRHIGH-5)):ARRHIGH-$S(ARRSP=1:11,1:5),1:PGTOP),LNUM=PGTOP-1 D FF^%AAHRRU Q
 I FLAGL,ARRACT="<RET>" S FLAGQ=1 Q
 I ARRACT="<RET>" S PGTOP=LNUM+1 D FF^%AAHRRU Q
 I $E(ARRACT,1,3)="COL"!($E(ARRACT,1,3)="col") S FLAGCOL=$E(ARRACT,4,99) S:FLAGCOL'?1.N!(FLAGCOL<1)!(FLAGCOL>ARRS) FLAGCOL=0 S FLAGQ=2,LNUM=PGTOP-1 D FF^%AAHRRU Q
 I $E(ARRACT)="?" D ^%AAHRRH S FLAGQ=2,LNUM=PGTOP-1 D FF^%AAHRRU Q
 S ARRACT=$TR(ARRACT,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ")
 I ",A,B,BR,C,D,E,ED,F,G,I,J,L,LE,LC,M,N,O,P,PUR,R,RS,SA,SP,T,UN,"'[(","_ARRACT_",") D MSG3^%AAHRRU G PAGE1
 I ",BR,C,D,ED,I,J,LC,LE,PUR,UN,"[(","_ARRACT_",") I $G(FLAGEDIT)'="EDIT"!(ARRS>1) D MSG5^%AAHRRU G PAGE1
 I ",SA,"[(","_ARRACT_","),$G(FLAGEDIT)'="EDIT" D MSG5^%AAHRRU G PAGE1
 I ARRACT="ED" W ?15,"I=Insrt  D=Dlt  C,'n'=Chng  LC=LctChng  SA=Save  UN=Unsave",!?15,"BR=Break  J=Join  LE=LnEdit  PUR=Purge" G PAGE1
 I ARRACT="N" W ?15,"T=Top  G=Goto  F=FndT  L=LctS",!?15,"<AU>=LnUp  <AD>=LnDn  <AL>=PgUp  <AR>=Bottom" G PAGE1
 I ARRACT="B" W ?15,"P=Prgm  A=AGL  E=EDD  RS=RtnSrch" G PAGE1
 I ARRACT="O" W ?15,"SP=Spc  M=Mark  COL'n'=Collapse  R=RdrwScrn" G PAGE1
 I ARRACT="M" S ARRMARK=PGTOP W "   Line ",PGTOP," marked" G PAGE1
 I ARRACT="T" S FLAGQ=2,LNUM=0,PGTOP=1 D FF^%AAHRRU Q
 I ARRACT="F" R !,"TAG: ",TAG:GEMTIME S:'$T TAG="^" D  S FLAGQ=2 D FF^%AAHRRU Q
 .I "^"[TAG S LNUM=PGTOP-1 Q
 .S LNUM=PGTOP,FLAGTAG="FT"
 I ARRACT="L" R !,"STRING: ",STNG:GEMTIME S:'$T STNG="^" D  S FLAGQ=2 D FF^%AAHRRU Q
 .I "^"[STNG S LNUM=PGTOP-1 Q
 .S LNUM=PGTOP,FLAGTAG="FS"
 I $E(ARRACT)="?" S ARRACT="?" D ^%AAHRRH
 I ARRACT="LE" S ^%AAHE("EDIT")=$S($G(^%AAHE("EDIT"))="CURSOR":"LINE",$G(^%AAHE("EDIT"))="LINE":"CURSOR",1:"LINE") W "   ",$S(^("EDIT")="LINE":"LINE",1:"SCREEN")," EDITOR NOW ACTIVE" G PAGE1
 I ARRACT="SP" S ARRSP=$S(ARRSP=2:1,1:2)
 I ARRACT="D" D DELETE^%AAHRRE
 I ARRACT="I" D INSERT^%AAHRRE
 I ARRACT="C" D CHANGE^%AAHRRE
 I ARRACT="SA" D SAVE^%AAHRRE1 W "   SAVE Complete" G PAGE1
 I ARRACT="UN" D UNSAVE^%AAHRRE1
 I ARRACT="LC" D LC^%AAHRRE1
 I ARRACT="BR" D BREAK^%AAHRRE1
 I ARRACT="J" D JOIN^%AAHRRE1
 I ARRACT="E" D EDD^%AAHRRZ
 I ARRACT="A" D AGL^%AAHRRZ
 I ARRACT="P" D:ARRS>3 MSG7^%AAHRRU G:ARRS>3 PAGE1 D START^%AAHRR
 I ARRACT="PUR" K ^%AAHE("SAVE") W "   PURGE Complete" G PAGE1
 I ARRACT="RS" D RSE^%AAHRRZ
 I ",?,A,BR,C,D,E,I,J,LC,LE,P,PUR,R,RS,SA,SP,UN,"[(","_ARRACT_",") S FLAGQ=2,LNUM=PGTOP-1 D FF^%AAHRRU Q
PAGE2 ;
 I ARRACT="G" D  G:ARRLN="NO" PAGE2 Q:ARRLN="^"  S ARRLN=+ARRLN,LNUM=$S(ARRLN>1:(ARRLN-1),1:0),PGTOP=LNUM+1 S FLAGQ=2 D FF^%AAHRRU Q
 .S ARRLN=$$GETLINE^%AAHRRU("LINE NUMBER: ") I ARRLN="^" S FLAGQ=2,LNUM=PGTOP-1 D FF^%AAHRRU Q
 .I ARRLN="?" S ARRLN="NO" D MSG4^%AAHRRU Q
 .I ARRLN="M" S ARRLN=$G(ARRMARK) I ARRLN="" S ARRLN="NO" D MSG6^%AAHRRU Q
 .I ARRLN?1.N Q
 .S ARRLN="NO" D MSG3^%AAHRRU
 D MSG3^%AAHRRU G PAGE1
 Q
