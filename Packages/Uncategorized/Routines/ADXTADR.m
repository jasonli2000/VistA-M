ADXTADR ;523/RES ;ADD TO FILE 165 FROM DOCFILE      ;SEP 17, 1993
 ;;1.1;;
 ; MRS                      ; ONCOLOGY CONTACT FILE
 ;MIDN=DOCTOR ID #(1,6)     ;SAVED AS "CONVERSION ID" IN CONTACT FILE
 ;MLNM=LAST NAME(7,21)      ;SAVED AS PART OF "CONTACT" FIELD
 ;MFNM=FIRST NAME(22,35)    ;SAVED AS PART OF "CONTACT" FIELD
 ;MMI=MIDDLE INIT'L(36)     ;SAVED AS PART OF "CONTACT" FIELD
 ;MADL=ADD'L ADDRESS(37,66) ;SAVED TO "STREET ADDRESS 1"
 ;MST=STREET(67,88)         ;SAVED TO "STREET ADDRESS 2"
 ;MCTY=CITY(89,103)         :SAVED AS "MRS CITY"
 ;MSTT=STATE(104,105)       ;SAVED AS "MRS STATE"
 ;MZP1(106,110)             ;SAVED AS "ZIP CODE" OR "MRS ZIP CODE"
 ;MTEL=PHONE(116,128)       :SAVE TO "PHONE" IN ONCO CONTACT FILE
 ;
 N X,ADXTX1,ADXTREC,CONTACT K ^TMP("ADXT","DR"),^TMP("ADXT","DRERR")
 S U="^",ADXTX3=0
 W !,"Adding records to the Oncology Contact file (165): "
 F ADXTX1=-1:0 S ADXTX1=$O(^TMP("ADXT","DOC",ADXTX1)) Q:ADXTX1=""  S ADXTREC=^TMP("ADXT","DOC",ADXTX1) D CONTACT
 G EXIT
CONTACT ;
 S ADXTRN=ADXTX1,ADXTERR=0,ADXTLBL="DOC" D DOC^ADXTCHK I ADXTERR>0 D VERERR^ADXTCERR Q
 S TEXT=$E(ADXTREC,7,21) D CLEAN^ADXTAP4 S MLNM=TEXT
 S TEXT=$E(ADXTREC,22,35) D CLEAN^ADXTAP4 S MFNM=TEXT
 S MMI=$E(ADXTREC,36) I MMI=" " S MMI=""
 I MMI="*" S CONTACT=MLNM_" "_MFNM,REL="4"
 E  S CONTACT=MLNM_","_MFNM_" "_MMI,REL="2"
 S TEXT=$E(ADXTREC,1,6) D CLEAN^ADXTAP4 S (MIDN,ADXTID)=TEXT
 S TEXT=$E(ADXTREC,89,103) D CLEAN^ADXTAP4 S MCTY=TEXT,CITY=MCTY
 S MZP1=$E(ADXTREC,106,110) S ZIP="",X=MZP1 D ZIP^ADXTAS1 S MZP1=X
 S MSTT=$E(ADXTREC,104,105),X=CONTACT D LOOKC Q
LOOKC ;
 K DIC S DIC="^ONCO(165,",DIC(0)="CMX" D ^DIC I Y'>0 D CONADD Q
 S ADXTDA=$P(Y,U,1),ADXTG=$G(^ONCO(165,ADXTDA,523000)),ADXTCST="M"
 I ADXTG=""&(ZIP'="") S MZP1="",MCTY="",MSTT="" D PID S ADXTX3=ADXTX3-1 Q
 I ADXTG=""&(ZIP="") D PID S ADXTX3=ADXTX3-1 Q
 I $P(ADXTG,U,1)=ADXTID Q  S MZP1="",MCTY="",MSTT="" D PID S ADXTX3=ADXTX3-1 Q
 ;I $P(ADXTG,U,1)'=ADXTID S CONTACT=CONTACT_"*",X=CONTACT G LOOKC
 S %=$$XREFDUP^ADXTUT2(165,ADXTDA,ADXTID) Q
 S ADXTFNUM="165",ADXTLBL="DOC",ADXTRN=ADXTX1,X=CONTACT,Y=".01" D TRAP^ADXTAS1 Q
CONADD ;
 S ADXTFNUM="165",ADXTLBL="DOC",ADXTRN=ADXTX1,ADXTCST="N"
 S TEXT=$E(ADXTREC,37,66) D CLEAN^ADXTAP4 S MADL=TEXT
 S TEXT=$E(ADXTREC,67,88) D CLEAN^ADXTAP4 S MST=TEXT
 S MTEL=$E(ADXTREC,116,118)_"-"_$E(ADXTREC,121,123)_"-"_$E(ADXTREC,125,128) S ADXTEL=MTEL D TEL^ADXTAS1
 D STUFF
 Q
EXIT ;
 K ADXTDA,ADXTEL,ADXTERR,ADXTFNUM,ADXTG,ADXTLBL,ADXTRN,ADXTX1,ADXTREC,ADXTID,ADXTCST,CITY,CONTACT,DA,DATE,DIE,DIC,DR,MLNM,MFNM,MMI,MADL,MST,MCTY,MSTT,MZP1,MTEL,MIDN,REL,STEL,TEXT,X,X1,ZIP
 Q
STUFF ;
 S DIC("DR")="1////^S X=REL"
 I MADL'="" S DIC("DR")=DIC("DR")_";.111///^S X=MADL"
 I MST'="" S DIC("DR")=DIC("DR")_";.112///^S X=MST"
 I ZIP'="" S DIC("DR")=DIC("DR")_";.119////^S X=ZIP"
 I ADXTEL'="" S DIC("DR")=DIC("DR")_";.131///^S X=ADXTEL"
 K DD,DO S DIC="^ONCO(165,",X=CONTACT,DIC(0)="" D FILE^DICN I Y'>0 D ERROR^ADXTAS1 Q
 S ADXTDA=$P(Y,U,1),DA=ADXTDA,ADXTG=$G(^ONCO(165,DA,.11))
 I MADL'=""&($P(ADXTG,U,1)="") S X=MADL,Y=".111" D TRAP^ADXTAS1
 I MST'=""&($P(ADXTG,U,2)="") S X=MST,Y=".112" D TRAP^ADXTAS1
 I MZP1'=""&($P(ADXTG,U,9)'="") S MZP1="",MCTY="",MSTT=""
 I MZP1'=""&($P(ADXTG,U,9)="") S X=MZP1,Y=".119" D TRAP^ADXTAS1
 S ADXTG=$G(^ONCO(165,DA,.13))
 I ADXTEL'=""&($P(ADXTG,U,1)="") S X=ADXTEL,Y=".131" D TRAP^ADXTAS1
PID S DR="523000////^S X=ADXTID;523000.1////^S X=ADXTCST"
 I MCTY'=""&(ZIP="") S DR=DR_";523001////^S X=MCTY"
 I MSTT'=""&(ZIP="") S DR=DR_";523002////^S X=MSTT"
 I MZP1'=""&(ZIP="") S DR=DR_";523003////^S X=MZP1"
 S DIE="^ONCO(165,",DA=ADXTDA D ^DIE
 S ADXTX3=ADXTX3+1 I $E(ADXTX3,$L(ADXTX3))="0" W "."
 S DIK=DIE D IX1^DIK
 Q
