ADXTAFL2 ;523/KC stuff follow-up fields into 165.51 ; 10-aug-1992
 ;;1.1;;
 ; (SUBSEQUENT TREATMENTS MULTIPLE)
EN ;
 ; INPUT VARIABLES:
 ; ADXT(""): WHERE DIFFERENT MRS FIELD VALUES ARE STORED BY SUBSCRIPT
 ; ADXTDA: DA OF THE 165.5 ENTRY TO ADD TO
 ; ADXTFILE: test description of file adding entries to
 ; OUTPUT VARIABLES: NONE
 ;
 N ADXTPC,ADXTDATE,ADXTFNUM,ADXTFLD,ADXTI,ADXTLINE,ADXTSBDA,ADXTSBF,ADXTI,X
 ;
 U IO
 S ADXTFNUM=165.5
 S ADXTFLD=60
 ;
 ; quit if already converted subs treat for this follow-up
 Q:$D(^ONCO(165.5,ADXTDA,4,"A523000",ADXT("PID")_ADXT("DTOP")_ADXT("DSQ")_ADXT("FDT")))
 ;
 S (ADXTST,ADXTST1)=0
 F ADXTI="FSRD","FRDD","FCHD","FHMD","FBRD","FOTD" D
 .S X=+$$DTCVT^ADXTUT(ADXT(ADXTI)) I +X S ADXTST=1 D
 ..I (X<ADXTST1)!(ADXTST1=0) S ADXTST1=X
 ;
 I +ADXTST D
 .S X=ADXTST1,ADXTSBDA=0 D ^ADXTMULT Q:ADXTSBDA<1
 .F ADXTI=1:1 S ADXTLINE=$P($TEXT(FIELDS+ADXTI),";;",2) Q:'+$L(ADXTLINE)  D
 ..S ADXTSBF=$P(ADXTLINE,"^",2)
 ..S ADXTFNUM=165.5,ADXTFLD=60
 ..D ^ADXTEDIT
 .;W !,"Added Sub.T.# ",ADXTSBDA," for primary# ",ADXTDA,": ",$G(^ONCO(165.5,ADXTDA,4,ADXTSBDA,0))
 ;
EXIT ;
 K ADXTPC,ADXTDATE,ADXTFNUM,ADXTFLD,ADXTI,ADXTLINE,ADXTSBDA,ADXTSBF,ADXTI,X
 Q
 ;
 ; PIECE LISTING:
 ; ==============
 ; 1: VA file MRS var to be added into
 ; 2: VA field MRS var to be added into
 ; 3: MRS field to add
 ; 4: 3 or 4 for number of slashes to stuff, W for Word processing
 ; 5-50: input transform
 ;
FIELDS ;
 ;;165.51^.04^FSR^3^
 ;;165.51^.041^FSRD^3^S X=$$DTCVT^ADXTUT(X) K:'+X X
 ;;165.51^.05^FRD^3^
 ;;165.51^.051^FRDD^3^S X=$$DTCVT^ADXTUT(X) K:'+X X
 ;;165.51^.06^FCH^3^
 ;;165.51^.061^FCHD^3^S X=$$DTCVT^ADXTUT(X) K:'+X X
 ;;165.51^.07^FHM^3^
 ;;165.51^.071^FHMD^3^S X=$$DTCVT^ADXTUT(X) K:'+X X
 ;;165.51^.08^FBR^3^S X=$$TRANSET^ADXTUT1("FBR",X) K:(+X<0) X
 ;;165.51^.081^FBRD^3^S X=$$DTCVT^ADXTUT(X) K:'+X X
 ;;165.51^.09^FOT^3^
 ;;165.51^.091^FOTD^3^S X=$$DTCVT^ADXTUT(X) K:'+X X
 ;;165.51^.03^FBCN^3^
 ;;165.51^1^FSQN^3^
 ;;165.51^3^FCMM^W^S:$$TRIM^ADXTUT(X)]"" X="COMMENTS FROM MRS FOLLOW-UP:  "_X
 ;;165.51^523000^FDT^3^S X=ADXT("PID")_ADXT("DTOP")_ADXT("DSQ")_ADXT("FDT")
