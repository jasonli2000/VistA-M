ADXTAFL3 ;523/KC stuff follow-up fields into 160.04 ; 10-aug-1992
 ;;1.1;;
 ; (FOLLOW-UP MULTIPLE)
EN ;
 ; INPUT VARIABLES: ADXT(""), MRS FIELD VALUES ARE STORED BY SUBSCRIPT
 ; ADXTDA (ENTRY IN 160 TO ADD TO)
 ; ADXTFILE: FILE ADDING TO
 ; OUTPUT VARIABLES: NONE
 ;
 N ADXTSTAT,ADXTSBF,ADXTDTOP,ADXTFILE,ADXTFNUM,ADXTFLD,ADXTLBL,ADXTLINE,ADXTSB,ADXTSBDA,DIC,ADXTI,X,Y
 ;
 U IO
 S ADXTFNUM=160
 S ADXTFLD=400
 S ADXTLBL="FL"
 ;
 ; MUST ADD CHECK SO AS NOT TO ADD ADDITIONAL IDENTICAL DATES!!!
 ;
 Q:'+$$DTCVT^ADXTUT(ADXT("FDT"))  ; KC 9/3
 ;
 S ADXTSBDA=0 I $D(^ONCO(160,ADXTDA,"F","A523000",ADXT("FDT"))) D
 .S ADXTSBDA=$O(^ONCO(160,ADXTDA,"F","A523000",ADXT("FDT"),0))
 I +ADXTSBDA G EDIT
 ;
 I '+ADXTSBDA S X=$$DTCVT^ADXTUT(ADXT("FDT")) I +X D
 .K DIC S DIC="^ONCO(160,"_ADXTDA_",""F"",",DIC(0)="MX" D ^DIC
 .I (+Y>0) S ADXTSBDA=$P(Y,"^")
 ;
 I '+ADXTSBDA S X=$$DTCVT^ADXTUT(ADXT("FDT")) D ^ADXTMULT
 ;
 I +ADXTSBDA D  ;add conversion id since created or matched
 .S ADXTLINE=$P($TEXT(ID+1),";;",2)
 .S ADXTSBF=$P(ADXTLINE,"^",2)
 .D ^ADXTEDIT
 ;
 I '+ADXTSBDA D ERROR Q  ; quit if couldn't add or match to an fup entry
 ;
EDIT ;
 I +ADXTSBDA>0 D
 .S ADXTLINE=""
 .F ADXTI=1:1 S ADXTLINE=$P($TEXT(FIELDS+ADXTI),";;",2) Q:ADXTLINE=""  D
 ..S ADXTSBF=$P(ADXTLINE,"^",2)
 ..D ^ADXTEDIT
EXIT ;
 K ADXTSTAT,ADXTSBF,ADXTDTOP,ADXTFILE,ADXTFNUM,ADXTFLD,ADXTLBL,ADXTLINE,ADXTSB,ADXTSBDA,DIC,ADXTI,X,Y
 Q
 ;
GTSTXT(ADXTDTOP) ;
 ; add which topography as per LVW@ALBANY to follow-up comments
 I $D(^ONCO(169,ADXTDTOP,0)) Q " MRS "_$P(^ONCO(169,ADXTDTOP,0),"^")_" ICDO-1 topography "
 Q " MRS Unknown ICDO-1 topography "
ERROR ;
 U IO(0)
 W !!,"Addition of an entry from the MRS Follow-up file to the ",ADXTFILE
 W " failed. PID was ",ADXT("PID"),", DTOP was ",ADXT("DTOP"),", DSQ was "
 W ADXT("DSQ"),", Follow-up date was ",ADXT("FDT"),"."
 Q
 ; PIECE LISTING:
 ; ==============
 ; 1: VA file MRS var to be added into
 ; 2: VA field MRS var to be added into
 ; 3: MRS field to add
 ; 4: 3 OR 4 FOR SLASHES, OR W IF WORD PROCESSING
 ; 5:50: input transform
 ;
ID ;
 ;;160.04^523000^FDT^3^S X=ADXT("FDT")
FIELDS ;
 ;;160.04^1^FDT^3^S X=$$ALIVE^ADXTUT($$DTCVT^ADXTUT(ADXT("FDT")),ADXTDA)
 ;;160.04^3^FMTH^3^S X=$$TRANSET^ADXTUT1("FMTH",X) K:(+X<0) X
 ;;160.04^4^FSRVQ^3^
 ;;160.04^5^FCMM^W^K:'$L($$TRIM^ADXTUT(X)) X S:$D(X) X=$$GTSTXT^ADXTAFL3(ADXT("DTOP"))_"comment: "_$$TRIM^ADXTUT(X)
 ;;160.04^5^FCRG^W^K:'+$$DTCVT^ADXTUT(X) X S:$D(X) X=$$GTSTXT^ADXTAFL3(ADXT("DTOP"))_"=> MRS FCRG (DATE OF REPORTING TO CENTRAL REGISTRY): "_X
 ;;160.04^6^FDT^3^S X=0
 ;;160.04^8^FDT^3^S X=1
