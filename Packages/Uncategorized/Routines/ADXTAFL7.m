ADXTAFL7 ;523/KC stuff follow-up fields into 160.06 ; 7-MAY-1993
 ;;1.1;;
EN ;
 ; INPUT VARIABLES: ADXT(""), MRS FIELD VALUES ARE STORED BY SUBSCRIPT
 ; ADXTDA (ENTRY IN 160 TO ADD TO)
 ; ADXTFILE: FILE ADDING TO
 ; OUTPUT VARIABLES: NONE
 ;
 N ADXTSTAT,ADXTSBF,ADXTDTOP,ADXTFILE,ADXTFNUM,ADXTFLD,ADXTLBL,ADXTLINE,ADXTSB,ADXTSBDA,DIC,ADXTI,X,Y
 ;
 U IO
 S ADXTFNUM=160
 S ADXTFLD=410
 S ADXTLBL="FL"
 ;
 ; quit if already processed attempted fup from this fup
 Q:$D(^ONCO(160,ADXTDA,"A","A523000",ADXT("PID")_ADXT("DTOP")_ADXT("DSQ")_ADXT("FDT")))
 ;
 S X=$$DTCVT^ADXTUT(ADXT("FDT")) I +X D
 .; quit if contact already entered on date of follow-up
 .Q:$D(^ONCO(160,ADXTDA,"A","AC",(9999999-X)))
 .D ^ADXTMULT Q:'+ADXTSBDA
 .S ADXTLINE=""
 .F ADXTI=1:1 S ADXTLINE=$P($TEXT(FIELDS+ADXTI),";;",2) Q:ADXTLINE=""  D
 ..S ADXTSBF=$P(ADXTLINE,"^",2)
 ..D ^ADXTEDIT
 ;
 K ADXTSBF,ADXTSDA
EXIT ;
 K ADXTSTAT,ADXTSBF,ADXTDTOP,ADXTFILE,ADXTFNUM,ADXTFLD,ADXTLBL,ADXTLINE,ADXTSB,ADXTSBDA,DIC,ADXTI,X,Y
 Q
 ;
 ; PIECE LISTING:
 ; ==============
 ; 1: VA file MRS var to be added into
 ; 2: VA field MRS var to be added into
 ; 3: MRS field to add
 ; 4: 3 OR 4 FOR SLASHES, OR W IF WORD PROCESSING
 ; 5:50: input transform
 ;
 ;;160.06^.01^FDT^4^S X=$$DTCVT^ADXTUT(X) K:'+X X
FIELDS ;
 ;;160.06^2^FCTMD^4^S X=$$GTCNTP^ADXTUT(X) K:'+X X
 ;;160.06^3^FDT^4^S X=1
 ;;160.06^5^FDT^4^S X=1
 ;;160.06^523000^FDT^3^S X=ADXT("PID")_ADXT("DTOP")_ADXT("DSQ")_ADXT("FDT")
