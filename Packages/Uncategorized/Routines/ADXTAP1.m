ADXTAP1 ;523/RES ; PATIENT/REFERRAL FILE LOOKUP    ;MAY 25, 1993
 ;;1.1;;
 ; MRS              ; ONCO FILE
 ;PID(1,7)          ;SAVED AS "CONVERSION ID" IN ONCOLOGY FILE
 ;PEXP(211,218)     ;SAVED AS "DATE OF DEATH" IN REF & ONCO FILES
 ;PCIND(275,290)    ;SAVED AS "MRS CURRENT INDUSTRY" IN ONCO FILE
 ;PLIND(291,306)    ;SAVED AS "MRS LONGEST INDUSTRY" IN ONCO FILE
 ;PCMM(339,388)     ;SAVED AS "MRS COMMENTS" IN ONCO FILE
 ;
 N X,XX,XF,ADXTX1,ADXTREC,PNAME,SSN,ADXTDA
 S U="^",ADXTX3=0
 W !,"Adding records to the Oncology Patient file (160): "
 F ADXTX1=0:1 S ADXTX1=$O(^TMP("ADXT","PAT",ADXTX1)) Q:ADXTX1=""  S ADXTREC=^TMP("ADXT","PAT",ADXTX1),ADXTRC2=^TMP("ADXT","PAT",ADXTX1+1) D NAME
 W !,ADXTX3," records were added to the Oncology Patient file"
 G EXIT
NAME ;
 S ADXTRN=ADXTX1,ADXTERR=0,ADXTLBL="PAT" D PAT^ADXTCHK I ADXTERR>0 D VERERR^ADXTCERR Q
 S TEXT=$E(ADXTREC,46,60) D CLEAN^ADXTAP4 S PLNM=TEXT
 S TEXT=$E(ADXTREC,61,74) D CLEAN^ADXTAP4 S PFNM=TEXT
 S PMI=$E(ADXTREC,75) I PMI=" "!(PMI?1P) S PMI="",PNAME=PLNM_","_PFNM
 E  S PNAME=PLNM_","_PFNM_" "_PMI
 S PSS=$E(ADXTREC,8,18),SSN=$E(PSS,1,3)_$E(PSS,5,6)_$E(PSS,8,11)
 S DATE=$E(ADXTREC,211,218),YY="523002" D DATE^ADXTAP4 S PEXP=DATE
 S PETEL=$E(ADXTRC2,34,36)_"-"_$E(ADXTRC2,39,46) S ADXTEL=PETEL D TEL^ADXTAS1 S PETEL=ADXTEL
 S TEXT=$E(ADXTRC2,47,62) D CLEAN^ADXTAP4 S PCIND=TEXT
 S TEXT=$E(ADXTRC2,63,78) D CLEAN^ADXTAP4 S PLIND=TEXT
 S TEXT=$E(ADXTRC2,79,94) D CLEAN^ADXTAP4 S PCOCC=TEXT
 S TEXT=$E(ADXTRC2,95,110) D CLEAN^ADXTAP4 S PLOCC=TEXT
 S TEXT=$E(ADXTRC2,111,160) D CLEAN^ADXTAP4 S PCMM=TEXT
 I PCMM="" S PCMM="NO COMMENTS"
 S X=$E(ADXTREC,1,7),%=$$TRIM^ADXTUT(X) S PID=X
 S X=SSN D LOOKP Q
LOOKP ;
 K DIC S DIC="^DPT(",DIC(0)="CMX" D ^DIC I Y'>0 D LOOKR Q
 S XX=$P(Y,U,1),XF="DPT(" D LOOKO Q
LOOKR ;
 K DIC S DIC="^LRT(67,",DIC(0)="CMX" D ^DIC I Y'>0 D ^ADXTAP2,LOOKO Q
 S XX=$P(Y,U,1),XF="LRT(67," D LOOKO Q
LOOKO ;
 S X=SSN I XF="" S ADXTDA=PNAME,Y=".01" D TRAP^ADXTAS1 Q
 K DIC S DIC="^ONCO(160,",DIC(0)="CMX" D ^DIC I Y'>0 D ^ADXTAP3 Q
 S ADXTDA=$P(Y,U,1),ADXTG=$G(^ONCO(160,ADXTDA,523000)) I ADXTG="" D OCK^ADXTAP4 S ADXTCST="M" D PID^ADXTAP3 S DIK="^ONCO(160,",DA=ADXTDA D IX1^DIK Q
 S %=$$XREFDUP^ADXTUT2(160,ADXTDA,PID)
 S ADXTFNUM="160",ADXTLBL="PAT",ADXTRN=ADXTX1,X=SSN,Y=".01" D TRAP^ADXTAS1
 Q
EXIT ;
 K ADXTCST,ADXTEL,ADXTFNUM,ADXTLBL,ADXTRN,ADXTX1,ADXTREC,ADXTX3,ADXTDA,ADXTOCC,ADXTRC2,ADXTG,DA,DATE,DIC,DIE,DIK,NAME,PCMM,PCIND,PLIND,PCOCC,PLOCC,PID,PSS,PNAME,PLNM,PFNM,PMI,SRC,SSN,TEXT,X,X1,XX,XF,Y,YY
 Q
