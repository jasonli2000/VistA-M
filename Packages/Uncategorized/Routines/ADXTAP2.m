ADXTAP2 ;523/RES  ; ADD ENTRY TO REFERRAL FILE  ;MAY 19, 1993
 ;;1.1;
 ; MRS                 ; REFERRAL FILE
 ;PSS(8,18)            ;SAVED AS "SSN" 
 ;PLNM,PFNM,PMI(46,75) ;SAVED AS "NAME" 
 ;PSEX(220)            ;SAVED AS "SEX" 
 ;PBRTH(198,207)       ;SAVED AS "DOB" 
 ;PMRTL(224)           ;SAVED AS "MARITAL STATUS" 
 ;PST1,PST2(76,113)    ;SAVED AS "STREET ADDRESS 1&2"
 ;PCTY(114,128)        ;SAVED AS "CITY" 
 ;PSTT(129,130)        ;SAVED AS "STATE" 
 ;PZP1(131,135)        ;SAVED AS "ZIP CODE" 
 ;PTEL(140,153)        ;SAVED AS "PHONE" 
 K DIC,DR,DIE,DA ;KC
REFADD ;
 S ADXTFNUM="67",ADXTLBL="PAT",ADXTRN=ADXTX1
 S SRC="MRS",PSEX=$E(ADXTREC,220),Y=".02" D SEX^ADXTAP4
 S PBRTH=$E(ADXTREC,198,207) S DATE=PBRTH,YY=".03" D DATE^ADXTAP4 S DOB=DATE
 S PMRTL=$E(ADXTREC,224) D MRTL^ADXTAP4
 S TEXT=$E(ADXTREC,76,105) D CLEAN^ADXTAP4 S PST1=TEXT S TEXT=$E(ADXTREC,106,113) D CLEAN^ADXTAP4 S PST2=TEXT I $L(PST2)=1 S PST2=PST2_" "
 S TEXT=$E(ADXTREC,114,128) D CLEAN^ADXTAP4 S PCTY=TEXT
 S PSTT=$E(ADXTREC,129,130) S X=PSTT D STATE^ADXTAP4
 S PZP1=$E(ADXTREC,131,135) I PZP1="00000"!(PZP1="     ") S PZP1=""
 S PTEL=$E(ADXTREC,141,143)_"-"_$E(ADXTREC,146,153) S ADXTEL=PTEL D TEL^ADXTAS1 S PTEL=ADXTEL
 S DIC("DR")=".01///^S X=PNAME"
 I PSEX'="" S DIC("DR")=DIC("DR")_";.02////^S X=PSEX"
 S DIC("DR")=DIC("DR")_";.1////^S X=SRC" I DOB'="" S DIC("DR")=DIC("DR")_";.03////^S X=DOB"
 I MRTL'="" S DIC("DR")=DIC("DR")_";.05////^S X=MRTL"
 S DIC("DR")=DIC("DR")_";.09///^S X=SSN"
 I PST1'="" S DIC("DR")=DIC("DR")_";.111///^S X=PST1"
 I PST2'="" S DIC("DR")=DIC("DR")_";.112///^S X=PST2"
 K DD,DO S DIC="^LRT(67,",X=SSN,DIC(0)="" D FILE^DICN I Y'>0 S XF="" Q
 S ADXTDA=$P(Y,U,1)
 ;
 S DR=".1////^S X=SRC" I PCTY'="" S DR=DR_";.114///^S X=PCTY"
 I STATE'="" S DR=DR_";.115////^S X=STATE"
 I PZP1'="" S DR=DR_";.116///^S X=PZP1"
 I PTEL'="" S DR=DR_";.131///^S X=PTEL"
 I PETEL'="" S DR=DR_";.132///^S X=PETEL"
 I PEXP'="" S DR=DR_";.351////^S X=PEXP"
 S DA=ADXTDA,DIE=DIC D ^DIE
 S ADXTG=$G(^LRT(67,DA,.11))
 I PST1'=""&($P(ADXTG,U,1)="") S X=PST1,Y=".111" D TRAP^ADXTAS1
 I PST2'=""&($P(ADXTG,U,2)="") S X=PST2,Y=".112" D TRAP^ADXTAS1
 I PCTY'=""&($P(ADXTG,U,4)="") S X=PCTY,Y=".114" D TRAP^ADXTAS1
 I PZP1'=""&($P(ADXTG,U,6)="") S X=PZP1,Y=".116" D TRAP^ADXTAS1
 S ADXTG=$G(^LRT(67,DA,.13))
 I PTEL'=""&($P(ADXTG,U,1)="") S X=PTEL,Y=".131" D TRAP^ADXTAS1
 I PETEL'=""&($P(ADXTG,U,2)="") S X=PETEL,Y=".132" D TRAP^ADXTAS1
 S DIK="^LRT(67,",DA=ADXTDA D IX1^DIK
 S XX=ADXTDA,XF="LRT(67,"
 ;
 K ADXTDA,ADXTEL,ADXTFNUM,ADXTG,ADXTRN,DATE,DIC,DOB,MRTL,PBRTH,PSTT,PSEX,PMRTL,PST1,PST2,PCTY,PZP1,PTEL,SRC,STATE,TEXT,X,Y
 Q
