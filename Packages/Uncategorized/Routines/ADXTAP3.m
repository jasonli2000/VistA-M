ADXTAP3 ;523/RES ; ADD ENTRY TO FILE 160              ;AUG 19, 1993
 ;;1.1;;
 ;
 ; MRS(FIELD)         ; ONCOLOGY PATIENT FILE
 ;PID(1,7)            ;SAVED AS "CONVERSION ID" IN ONCOLOGY FILE
 ;PANV(30,37)         ;SAVED AS "SUSPENSE DATE" IN ONCOLOGY FILE
 ;PLST(38,45)         ;SAVED AS "DATE ENTERED" IN ONCOLOGY FILE
 ;PLOC(208,210)       ;SAVED AS "PLACE OF BIRTH" IN ONCOLOGY FILE
 ;PRACE(221,222)      ;SAVED AS "RACE" IN ONCOLOGY FILE
 ;PETH(223)           ;SAVED AS "ETHNICITY" IN ONCOLOGY FILE
 ;PSEX(220)           ;SAVED AS "SEX" IN ONCOLOGY FILE
 ;PAUT(219)           ;SAVED AS "AUTOPSY" IN ONCOLOGY FILE
 ;PFAMH(225)          ;SAVED AS "CA IN FAMILY" IN ONCOLOGY FILE
 ;PSMK(226)           ;SAVED AS "ALCHOL HISTORY" IN ONCOLOGY FILE
 ;PALC(227)           ;SAVED AS "TOBACCO HISTORY" IN ONCOLOGY FILE
 ;PEMP(229,258)       ;SAVED AS "INDUSTRY/ADDRESS" IN ONCO. FILE
 ;PCOCC(307,322)      ;SAVED AS "OCCUPATION" IN ONCOLOGY FILE
 ;PLOCC(323,338)      ;SAVED AS "OCCUPATION" IN ONCOLOGY FILE
 ;PCMM(339,388)       ;SAVED AS "MRS COMMENTS" IN ONCOLOGY FILE
 ;
 S NAME=XX_";"_XF,ADXTFNUM="160",ADXTLBL="PAT",ADXTRN=ADXTX1
 S TEXT=$E(ADXTREC,208,210) D CLEAN^ADXTAP4 S PLOC=TEXT D PLOC^ADXTAP4
 S PRACE=$E(ADXTREC,221,222),PRACE=+PRACE,PETH=$E(ADXTREC,223)
 S PSEX=$E(ADXTREC,220),Y="10" D SEX^ADXTAP4
 S STATUS=1,PAUT="" I PEXP'="" S STATUS="0",PAUT=$E(ADXTREC,219) I PAUT'="Y"&(PAUT'="N") S PAUT="U"
 S DATE=$E(ADXTREC,38,45) S YY="26" D DATE^ADXTAP4 S PLST=DATE
 S SOURCE="SE",PFAMH=$E(ADXTREC,225)
 S PSMK=$E(ADXTREC,226) I PSMK="C"!(PSMK="F") S PSMK="Y"
 S PALC=$E(ADXTREC,227) I PALC="C"!(PALC="F") S PALC="Y"
 S TEXT=$E(ADXTRC2,1,30) D CLEAN^ADXTAP4 S PEMP=TEXT
 ;
 S DIC("DR")="10////^S X=SEX"
 I PLOC'="" S DIC("DR")=DIC("DR")_";7////^S X=PLOC"
 I PRACE'="" S DIC("DR")=DIC("DR")_";8///^S X=PRACE"
 I PETH'="" S DIC("DR")=DIC("DR")_";9////^S X=PETH"
 K DD,DO S DIC="^ONCO(160,",X=NAME,DIC(0)="" D FILE^DICN I Y'>0 D ERROR^ADXTAP4 Q
 S ADXTDA=$P(Y,U,1),ADXTCST="N"
 ;
 S DR="15///^S X=STATUS" I PAUT'="" S DR=DR_";22.9///^S X=PAUT"
 I PLST'="" S DR=DR_";25////^S X=PLST;26////^S X=SOURCE"
 I PFAMH'="" S DR=DR_";43///^S X=PFAMH"
 I PSMK'="" S DR=DR_";38///^S X=PSMK"
 I PALC'="" S DR=DR_";39///^S X=PALC"
 S DIE=DIC,DA=ADXTDA D ^DIE
 I PCOCC'="" S OCC=PCOCC D OCC^ADXTAP4 S PCOCC=OCC
 I PLOCC'="" S OCC=PLOCC S PEMP="" D OCC^ADXTAP4 S PLOCC=OCC
 D PID,ERR^ADXTAP4
 S DIK="^ONCO(160,",DA=ADXTDA D IX1^DIK
 S ADXTX3=ADXTX3+1 I $E(ADXTX3,$L(ADXTX3))="0" W "."
 G EXIT
PID ;
 S DR="523000////^S X=PID;523000.1////^S X=ADXTCST"
 I PEXP'="" S DR=DR_";523002////^S X=PEXP"
 I PCOCC'="" S DR=DR_";523003////^S X=PCOCC"
 I PLOCC'="" S DR=DR_";523004////^S X=PLOCC"
 S DIE="^ONCO(160,",DA=ADXTDA D ^DIE
 I PCMM'="" S %=$$WP^ADXTWP(160,523001,ADXTDA,"MRS COMMENT: "_PCMM)
 I PCIND'="" S %=$$WP^ADXTWP(160,523001,ADXTDA,"MRS CURRENT IND: "_PCIND)
 I PLIND'="" S %=$$WP^ADXTWP(160,523001,ADXTDA,"MRS LONGEST IND: "_PLIND)
 I PETEL'="" S %=$$WP^ADXTWP(160,523001,ADXTDA,"MRS EMPLOYER TEL: "_PETEL)
 Q
EXIT ;
 K ADXTFNUM,ADXTLBL,ADXTRN,ADXTCST,CITY,DATE,DIC,DIE,DIK,DMRS,DOD,MRTL,NAME,OCC,PANV,PRACE,PETH,PFAMH,PSEX,PEMP,PLOC,PCIND,PLIND,PCMM,PAUT,PALC,PSMK,PEXP,PLNM,PLST,PETEL,PFNM,PMI,POB,PSTT,REL,SEX,STATUS,SOURCE,TEXT,X,Y
 Q
