ADXTAP4 ;523/RES ; UTILITIES FOR ONCOLOGY/REFERRAL FILES ;AUG 19, 1993
 ;;1.1;;
 ;
CLEAN ;
 F IX=$L(TEXT):-1:0 I $E(TEXT,IX)'=" " S TEXT=$E(TEXT,1,IX) Q
 Q
DATE ;
 I DATE="00/00/00"!(DATE="0000/00/00") S DATE="" Q
 I $L(DATE)>8 S X=$E(DATE,6,7)_" "_$E(DATE,9,10)_" "_$E(DATE,1,4)
 I $L(DATE)=8 S X=$E(DATE,4,5)_" "_$E(DATE,7,8)_" "_$E(DATE,1,2)
 D ^%DT I Y>0 S DATE=Y Q
 S X=DATE,ADXTDA=PNAME,DATE="" I YY="" S YY="DATE"
 S Y=YY D TRAP^ADXTAS1 Q
STATE ;
 K DIC S DIC="^DIC(5,",DIC(0)="CMX" D ^DIC
 I Y'>0 S X=PSTT,Y=".115",ADXTDA=PNAME,STATE="90" D TRAP^ADXTAS1 Q
 S STATE=$P(Y,U,1) Q
MRTL ;
 I PMRTL="S" S MRTL="6" Q
 I PMRTL="M" S MRTL="2" Q
 I PMRTL="P" S MRTL="5" Q
 I PMRTL="D" S MRTL="1" Q
 I PMRTL="W" S MRTL="4" Q
 I PMRTL="U" S MRTL="3" Q
 S MRTL="",X=PMRTL,Y=".05",ADXTDA=PNAME D TRAP^ADXTAS1 Q
SEX ;
 I PSEX="M" S SEX="1" Q
 I PSEX="F" S SEX="2" Q
 I PSEX="O" S SEX="3" Q
 I PSEX="T" S SEX="4" Q
 S SEX="9" Q
PLOC ;
 I PLOC?3N S X=PLOC K DIC S DIC="^ONCO(165.2,",DIC(0)="X",D="A523001" D IX^DIC D PLOC2 Q
 S X=PLOC K DIC S DIC="^ONCO(165.2,",DIC(0)="X",D="C" D IX^DIC D PLOC2 Q
 S Y=-1
PLOC2 I Y'>0 S X=PLOC,PLOC="999",Y="7",ADXTDA=PNAME D TRAP^ADXTAS1 Q
 S PLOC=$P(Y,U,1) Q
OCC ;
 K DIC,ADXTOCC S DIC="^LAB(61.6,",DIC(0)="MX",X=OCC D ^DIC S:(+Y>0) ADXTOCC=$P(Y,U,1) I Y<0 S Y="42",ADXTSBF=".01" D TRAP^ADXTAS1 S ADXTOCC="1505"
 ;
 I '$D(^ONCO(160,ADXTDA,7,0)) S ^ONCO(160,ADXTDA,7,0)="^160.042PA^0^0"
 K DA,DD,D0 S DA(1)=ADXTDA
 I $D(ADXTOCC) K DIC S DIC(0)="L",DIC="^ONCO(160,"_ADXTDA_",7,",X=ADXTOCC D FILE^DICN I (+Y>0) S ADXTSBDA=$P(Y,U,1) D
 .K DIE S DIE="^ONCO(160,"_ADXTDA_",7,",DA=ADXTSBDA,DR="3///"_$TR(PEMP,";:",",,") D ^DIE
 .S ADXTG=$G(^ONCO(160,ADXTDA,7,ADXTSBDA,0))
 .I OCC'=""&($P(ADXTG,U,1)="") S X=OCC,Y="42",ADXTSBF=".01" D TRAP^ADXTAS1
 .I ADXTOCC'="1505" S OCC=""
 .I PEMP'=""&($P(ADXTG,U,4)="") S X=PEMP,Y="42",ADXTSBF="3" D TRAP^ADXTAS1
 Q
ERROR ;
 S ADXTDA=PNAME,X=NAME,Y=".01" D TRAP^ADXTAS1
 S ^TMP("ADXT","PERR",ADXTX1)=ADXTREC
 Q
ERR ;
 S ADXTG=$G(^ONCO(160,ADXTDA,1))
 I PAUT'=""&($P(ADXTG,U,13)="") S X=PAUT,Y="22.9" D TRAP^ADXTAS1
 S ADXTG=$G(^ONCO(160,ADXTDA,8))
 I PFAMH'=""&($P(ADXTG,U,1)="") S X=PFAMH,Y="43" D TRAP^ADXTAS1
 I PSMK'=""&($P(ADXTG,U,2)="") S X=PSMK,Y="38" D TRAP^ADXTAS1
 I PALC'=""&($P(ADXTG,U,3)="") S X=PALC,Y="39" D TRAP^ADXTAS1
 Q
OCK ;
 S ADXTG=$G(^ONCO(160,ADXTDA,7,1,0)) Q:ADXTG=""  I $P(ADXTG,U,1)'="1505" S PCOCC=""
 S ADXTG=$G(^ONCO(160,ADXTDA,7,2,0)) Q:ADXTG=""  I $P(ADXTG,U,1)'="1505" S PLOCC=""
 Q
