ADXTAS1 ;523/RES ; LINK FILE 165 ENTRIES TO FILE 160 ENTRIES  ;MAY 21, 1993
 ;;1.1;;
 ;
 F ADXTX4=0:0 S ADXTX4=$O(^ONCO(160,"A523000",ADXTID,ADXTX4)) Q:ADXTX4=""  S ADXTDA=ADXTX4 D LINK
 K X,SPID,CON,TYPE
 Q
LINK ;
 I '$D(^ONCO(160,ADXTDA,"C",0)) S ^ONCO(160,ADXTDA,"C",0)="^160.03S^0^0"
 K DD,D0,DA,DIC S DA(1)=ADXTDA,DIC(0)="L",DIC="^ONCO(160,"_ADXTDA_",""C"",",X=TYPE D FILE^DICN I (+Y>0) S ADXTSBDA=$P(Y,U,1) D
 .K DIE S DIE="^ONCO(160,"_ADXTDA_",""C"",",DA=ADXTSBDA,DR="1////^S X=CON" D ^DIE
 .S ADXTG=$G(^ONCO(160,ADXTDA,"C",ADXTSBDA,0))
 .I TYPE'=""&($P(ADXTG,U,1)="") S X=TYPE,Y="420",ADXTSBF=".01" D TRAP
 .I CON'=""&($P(ADXTG,U,2)="") S X=CON,Y="420",ADXTSBF="1" D TRAP
 Q
ERROR ;
 S ADXTDA=$P(Y,U,1),X=CONTACT,Y=".01" D TRAP
 S ^TMP("ADXT","CERR",ADXTX1)=ADXTREC
 Q
SREL ;
 I SREL<5!(SREL=8) S REL="3",TYPE="NOK" Q
 I SREL=5 S REL="3",TYPE="FR" Q
 I SREL=6 S REL="4",TYPE="INST" Q
 I SREL=7 S REL="2",TYPE="MD" Q
 I SREL=9 S REL="5",TYPE="OTH" Q
 S X=SREL,Y="1" D TRAP Q
TEL ;
 I ADXTEL["-000"!(ADXTEL["-   ")!(ADXTEL[" ") S ADXTEL="" Q
 Q
ZIP ;
 I X="00000"!(X="     ")!(X="") S X="" Q
 F ADXTX2=0:0 S ADXTX2=$O(^VIC(5.11,"B",X_" ",ADXTX2)) Q:ADXTX2=""  I $P(^VIC(5.11,ADXTX2,0),U,2)=CITY S ZIP=ADXTX2
 Q
TRAP ;
 W !," Field: ",Y," (",X,") Record: (",ADXTDA,") was rejected by File: (",ADXTFNUM,")!"
 S ADXTSTFF=X,ADXTFLD=Y,ADXTMRS="NA",ADXT(ADXTMRS)="NA",ADXTDD="NA"
 D STORE^ADXTCERR
 K ADXTSB,ADXTSBF,X,Y Q
