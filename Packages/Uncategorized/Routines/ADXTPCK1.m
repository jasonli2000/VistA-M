ADXTPCK1 ;523/KC continuation of ADXTPCK;11-AUG-93
 ;;1.1;;
CHKPR(ADXUERRN) ;
 ;input variables: various
 ;
 N ADXUE,ADXUI,ADXUX ;,ADXUMRS,ADXUVA,ADXUTXT
 ;
 S ADXUE=0
 N ADXUI,ADXUX F ADXUI=1:1 S ADXUX=$T(PRTEXT+ADXUI) Q:ADXUX']""  D
 .S ADXUX=$P(ADXUX,";;",2)
 .S ADXUMRS1=$P(ADXUX,"^",2),ADXUVA1=$P(ADXUX,"^"),ADXUTXT=$P(ADXUX,"^",3)
 .S ADXUMRS1=$TR(ADXUMRS1,"~","^") X "S ADXUMRS="_ADXUMRS1
 .X "S ADXUVA="_ADXUVA1
 .I ADXUMRS'=ADXUVA S ADXUE=ADXUE+1
 ;
 I +(ADXUE*100/16)>ADXUERRN D
 .W !,$J($E(+(ADXUE*100/16),1,5),6),"% mismatch for VA # ",ADXUDA
 .W " vs. MRS ",ADXT("PID"),ADXT("DTOP"),ADXT("DSQ"),": "
 .N ADXUI,ADXUX F ADXUI=1:1 S ADXUX=$T(PRTEXT+ADXUI) Q:ADXUX']""  D
 ..S ADXUX=$P(ADXUX,";;",2)
 ..S ADXUMRS1=$P(ADXUX,"^",2),ADXUVA1=$P(ADXUX,"^"),ADXUTXT=$P(ADXUX,"^",3)
 ..S ADXUMRS1=$TR(ADXUMRS1,"~","^") X "S ADXUMRS="_ADXUMRS1
 ..X "S ADXUVA="_ADXUVA1
 ..I ADXUMRS'=ADXUVA W !?5,ADXUTXT," for MRS: ",ADXUMRS,", for matched VA entry: ",ADXUVA
 .W !
 ;
 Q (ADXUE*100)/16
 ;
CHKPAT() ;
 ; input variable: ADXUDA
 N ADXUPTR,ADXUSTR,ADXU0,ADXUVNAM,ADXUE
 S ADXUE=0
 S ADXUPTR=$P($G(^ONCO(160,ADXUDA,0)),"^",1)
 S ADXUSTR="^"_$P(ADXUPTR,";",2)_$P(ADXUPTR,";")_",0)"
 S ADXU0=$G(@ADXUSTR)
 I '$L(ADXU0) W !,"BAD ONCO PAT PTR: ",ADXUPTR
 S ADXUVNAM=$$TRIM^ADXTUT($P(ADXU0,"^",1)),ADXUVSSN=$P(ADXU0,"^",9)
 S ADXUVNM2=$$TRIM^ADXTUT($P(ADXUVNAM,",",1))
 S ADXUVNM1=$$TRIM^ADXTUT($P(ADXUVNAM,",",2))
 ; get rid of M.I. from VA first name
 I $L(ADXUVNM1>2),$E(ADXUVNM1,$L(ADXUVNM1)-1,$L(ADXUVNM1))?1" "1A S ADXUVNM1=$E(ADXUVNM1,1,$L(ADXUVNM1)-2)
 I ADXUVNAM'=ADXUNAME D
 .;if VA name MI ends with ".", MRS name MI doesn't
 .I $E(ADXUVNAM,$L(ADXUVNAM))=".",$E(ADXUVNAM,1,$L(ADXUVNAM)-1)=ADXUNAME Q
 .;if first, last names same, but MI different...
 .;I ADXUVSSN=ADXUSSN,$$SOUNDEX(ADXUVNM1)=$$SOUNDEX(ADXUNM1),$$SOUNDEX(ADXUVNM2)=$$SOUNDEX(ADXUNM2) Q
 .S ADXUE=ADXUE+1
 .W !,"Name Mismatch: VA (`",ADXUDA,"): ",$$TRIM^ADXTUT($P(ADXU0,"^",1))," MRS: ",ADXUNAME Q
 I ADXUVSSN'=ADXUSSN D  S ADXUE=ADXUE+1
 .W !,"SSN Mismatch: VA (`",ADXUDA,"): ",$P(ADXU0,"^",9)," MRS: ",ADXUSSN
 K ADXUPTR,ADXUSTR,ADXU0,ADXUVNAM
 Q ADXUE
VAR1PR ;
 ; input variable: ADXUDA; output variables: many
 N ADXU0,ADXU2
 S ADXU0=$G(^ONCO(165.5,ADXUDA,0)),ADXU2=^(2)
 S ADXUPID=$P(ADXU0,"^",5)
 S ADXUDSQ=$P(ADXU0,"^",6)
 S ADXUDCLS=$P(ADXU0,"^",4)
 S ADXUDAMN=$P(ADXU0,"^",8)
 S ADXUDDIS=$P(ADXU0,"^",9)
 S ADXUDID=$P(ADXU0,"^",16)
 S ADXUDTOP=$P(ADXU2,"^",1)
 S ADXUDMOR=$P(ADXU2,"^",3)
 S ADXUDGR=$P(ADXU2,"^",5)
 S ADXUDPR=$P(ADXU2,"^",8)
 S ADXUDSZT=$P(ADXU2,"^",9)
 S ADXUDEXT=$P(ADXU2,"^",10)
 S ADXUDLYM=$P(ADXU2,"^",11)
 S ADXUT=$P(ADXU2,"^",25)
 S ADXUN=$P(ADXU2,"^",26)
 S ADXUM=$P(ADXU2,"^",27)
 K ADXU0,ADXU2
 Q
VARPAT(ADXUREC) ; copied from ^ADXTAP1...
 ; input:  ADXUREC
 ; output: ADXUPID,ADXUNAME,ADXUSSN,ADXULNM
 ;
 N TEXT,PLNM,PFNM,PMI,PSS
 S ADXUPID=$E(ADXUREC,1,7)
 S TEXT=$E(ADXUREC,46,60) D CLEAN S PLNM=TEXT
 S TEXT=$E(ADXUREC,61,74) D CLEAN S PFNM=TEXT
 S ADXUNM2=$$TRIM^ADXTUT(PLNM),ADXUNM1=$$TRIM^ADXTUT(PFNM)
 S PMI=$E(ADXUREC,75) I PMI=" "!(PMI?1P) S PMI="",ADXUNAME=PLNM_","_PFNM
 E  S ADXUNAME=PLNM_","_PFNM_" "_PMI
 S PSS=$E(ADXUREC,8,18),ADXUSSN=$E(PSS,1,3)_$E(PSS,5,6)_$E(PSS,8,11)
 S ADXUDA=$$GTPP^ADXTUT(ADXUPID) I '+ADXUDA Q
 Q
CLEAN ; copied from CLEAN^ADXTAP4...
 ; input variable: TEXT
 N ADXUIX
 F ADXUIX=$L(TEXT):-1:0 I $E(TEXT,ADXUIX)'=" " S TEXT=$E(TEXT,1,ADXUIX) Q
 Q
SOUNDEX(X) ; return soundex version of X
 ; copied from SOU^DICM1
 Q X ; disable soundex filter!
 N Y
 S ADXUSOU="01230129022455012623019202",ADXUSOV=X,X=$C($A(X)-(X?1L.E*32)),ADXUDIX=$E(ADXUSOU,$A(X)-64) F ADXUY=2:1 S Y=$E(ADXUSOV,ADXUY) Q:","[Y  I Y?1A S %=$E(ADXUSOU,$A(Y)-$S(Y?1U:64,1:96)) I %-ADXUDIX,%-9 S ADXUDIX=% I % S X=X_% Q:$L(X)=4
 S X=$E(X_"000",1,4) K ADXUSOU,ADXUSOV,Y Q X
 ;
PRTEXT ;
 ;;ADXUPID^($P(ADXT("PID"),"/")_$P(ADXT("PID"),"/",2))^Accession No.
 ;;ADXUDTOP^$$GTTP~ADXTUT(ADXT("DTOP"))^Topography
 ;;ADXUDSQ^$$TRIM~ADXTUT(ADXT("DSQ"))^Sequence No.
 ;;ADXUDCLS^$$TRIM~ADXTUT(ADXT("DCLS"))^Class of Case
 ;;ADXUDAMN^$$DTCVT~ADXTUT(ADXT("DAMN"))^First Admit Date
 ;;ADXUDDIS^$$DTCVT~ADXTUT(ADXT("DDIS"))^Discharge Date
 ;;ADXUDID^$$DTCVT~ADXTUT(ADXT("DID"))^Date DX
 ;;ADXUDMOR^$$GTMP~ADXTUT(ADXT("DMOR"))^Morphology
 ;;ADXUDGR^$$TRIM~ADXTUT(ADXT("DGR"))^Grade
 ;;ADXUDPR^$$TRIM~ADXTUT(ADXT("DPRORG"))^Paired Organ
 ;;+ADXUDSZT^+$$SIZE~ADXTUT2($$TRIM~ADXTUT(ADXT("DSZT")),ADXT("DMOR"))^Sizing
 ;;ADXUDEXT^+$$TRIM~ADXTUT(ADXT("DEXT"))^Extension
 ;;ADXUDLYM^$$TRIM~ADXTUT(ADXT("DLYM"))^Lymph Nodes
 ;;ADXUT^$$TRIM~ADXTUT($TR(ADXT("DAJCT"),"Mm",""))^T-code
 ;;ADXUN^$$TRIM~ADXTUT(ADXT("DAJCN"))^N-code
 ;;ADXUM^$$TRIM~ADXTUT(ADXT("DAJCM"))^M-code
