ADXTPCV2 ;523/KC fill in cause of death field ;16-SEP-93
 ;;1.1;;
 ;
EN ;
 N ADXURN,ADXUPP,ADXUDC,DIE,DIC,DR,DA,ADXUSRN,ADXULIN,ADXUDC,ADXUX,ADXUCNT,ADXUCNT1,ADXUID,ADXUSTR
 D LPPRIM ; put CAUSES OF DEATH, by patient, into ^TMP
 D STUFF ; stuff CAUSES OF DEATH from ^TMP into ONCO PATIENT file
EXIT ;
 K ADXURN,ADXUPP,ADXUDC,DIE,DIC,DR,DA,ADXUSRN,ADXULIN,ADXUDC,ADXUX,ADXUCNT,ADXUCNT1,ADXUID,ADXUSTR
 K ^TMP($J)
 Q 
LPPRIM ; loop through 165.5 by A523000 index
 W !,"Reading DCAUS values into ^TMP from ONCOLOGY PRIMARY file"
 S (ADXUCNT,ADXUID)=0
 K ^TMP($J) ; clear out TMP global
 F  S ADXUID=$O(^ONCO(165.5,"A523000",ADXUID)) Q:ADXUID']""  D
 .S ADXURN=$O(^ONCO(165.5,"A523000",ADXUID,"")) Q:'+ADXURN
 .S ADXUCNT=ADXUCNT+1 W:'+(ADXUCNT#10) "."
 .S ADXUPP=$P($G(^ONCO(165.5,ADXURN,0)),"^",2) Q:'+ADXUPP
 .S ADXUDC=$$DCAUS(ADXURN) S:$L(ADXUDC) ^TMP($J,ADXUPP,ADXUDC)=""
 Q
 ;
STUFF ; loop through ^TMP($J, patient pointer, value) and stuff into 160
 S (ADXUCNT,ADXUCNT1)=0
 W !,"Stuffing DCAUS values into field 18.9 in ONCOLOGY PATIENT file"
 S ADXURN=0 F  S ADXURN=$O(^TMP($J,ADXURN)) Q:'+ADXURN  D
 .S ADXUCNT=ADXUCNT+1 W:'+(ADXUCNT#10) "."
 .S ADXUDC=$O(^TMP($J,ADXURN,""))
 .I $O(^TMP($J,ADXURN,ADXUDC))]"" D  Q
 ..W !,"Conflicting CAUSE OF DEATH values for patient # ",ADXURN,": "
 ..W $$MULTCAUS(ADXURN),"."
 .K DIE,DIC,DR,DA
 .S DIE="^ONCO(160,",DR="18.9///"_ADXUDC,DA=ADXURN D ^DIE
 .I $P($G(^ONCO(160,ADXURN,1)),"^",12)'=ADXUDC D  Q
 ..W !,"Unable to stuff DCAUS value ",ADXUDC," in ONCOLOGY PATIENT # "
 ..W ADXURN,"."
 .S ADXUCNT1=ADXUCNT1+1
 W !,ADXUCNT1," DCAUS values stuffed into ONCOLOGY PATIENT entries."
 Q
DCAUS(ADXURN) ; quit DCAUS value from comments, "" if none found
 N ADXUSRN,ADXULIN,ADXUDC
 S ADXUSRN=0,ADXUDC=""
 F  S ADXUSRN=$O(^ONCO(165.5,ADXURN,19,ADXUSRN)) Q:'+ADXUSRN  D
 .S ADXULIN=$G(^ONCO(165.5,ADXURN,19,ADXUSRN,0))
 .I ADXULIN["MRS VALUE (DCAUS): " D  Q
 ..S ADXUDC=$P(ADXULIN,"MRS VALUE (DCAUS): ",2)
 Q ADXUDC
MULTCAUS(ADXURN) ; quit string of multiple CAUSE OF DEATH values.
 N ADXUSTR,ADXUDC S (ADXUDC,ADXUSTR)=""
 F  S ADXUDC=$O(^TMP($J,ADXURN,ADXUDC)) Q:ADXUDC']""  D
 .S ADXUSTR=ADXUSTR_ADXUDC_","
 Q ADXUSTR
