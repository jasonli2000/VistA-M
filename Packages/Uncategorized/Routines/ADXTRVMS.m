ADXTRVMS ;523/RES  ; PC TO MUMPS GLOBAL TRANSFER          ;OCT 7, 92
 ;;1.1;;
 ; modified 10/5/92 to use a VMS file for input
 ;
EN S U="^"
 N ADXTDIR,ADXTFN,ADXTLL,ADXTFNAM,L,R,LINE,REC
 X ^%ZOSF("TYPE-AHEAD") ; 1-OCT-1992
 K DIR W ! S DIR("A")="PLEASE ENTER THE DISK AND DIRECTORY IN VMS CONTAINING FILES TO UPLOAD",DIR(0)="F^1:100" D ^DIR Q:+Y<0
 Q:$D(DTOUT)!$D(DUOUT)!$D(DIROUT)!$D(DIRUT)
 S X=Y,ADXTDIR=X D CHECKDIR I +Y=-1 G EN
ASK ;
 S X="ERR^ADXTRVMS",@^%ZOSF("TRAP"),$ZE=""
 K DIR W ! S DIR("A")="PLEASE ENTER THE NAME OF THE FILE TO READ FROM VMS",DIR("B")="HSPFILE.T01",DIR(0)="F^11:11",DIR("?")="ENTER THE FULL VMS NAME." D ^DIR Q:+Y<0
 Q:$D(DTOUT)!$D(DUOUT)!$D(DIROUT)!$D(DIRUT)
 D TR I Y'="PATFILE.T01"&(Y'="DAGFILE.T01")&(Y'="FLWFILE.T01")&(Y'="DOCFILE.T01")&(Y'="SCDFILE.T01")&(Y'="HSPFILE.T01") G ASK
 S ADXTFN=$E(Y,1,3) I ADXTFN="DAG" S ADXTFN="DI"
 I ADXTFN="FLW" S ADXTFN="FL"
OPEN ;
 S ADXTFNAM=ADXTDIR_Y
 O ADXTFNAM:(READONLY):5
 S X="ERR^ADXTRVMS",@^%ZOSF("TRAP"),$ZE=""
 ;
 ;W !!!,"                    >>> PLEASE UPLOAD FILE NOW! <<<",!!!
 W !!,"    Working, please wait..."
 K ^TMP("ADXT",ADXTFN)
 U ADXTFNAM
 R LINE#245:300 G:'$T EXIT
 S R=1 D LF,LINE S L=R K R
 F R=L+1:1 R LINE#245:20 G:'$T END D LF,LINE
END ;
 C ADXTFNAM
 U IO(0)
 W !!,"UPLOAD ENDED!",!,R-1," LINES OF DATA HAVE BEEN CAPTURED."
 D EXIT G ASK
LF ;STRIP LF CHARACTER FROM INPUT LINE
 S ADXTLL=$L(LINE)
 I $E(LINE,1)=$C(10) S LINE=$E(LINE,2,ADXTLL),ADXTLL=ADXTLL-1
 Q
LINE ;TRANSFER ASCII LINE INTO GLOBAL LINE(S)
 I ADXTLL<244&((ADXTFN="DI")!(ADXTFN="PAT")) W !,LINE S R=R-1 Q
 I ADXTLL>243 R LINE2#220:10 G:'$T END D SPLIT Q
 S ^TMP("ADXT",ADXTFN,R)=$E(LINE,1,ADXTLL) Q
SPLIT ; DIVIDE RECORDS LONGER THAN 243 CHARACTERS
 S ADXTL2=$L(LINE2)
 ;I $E(LINE,140)=$C(40) I ADXTFN="PAT" D PAT Q
 I $E(LINE,32)="/" I $E(LINE,35)="/" I $E(LINE,40)="/" I ADXTFN="PAT" D PAT Q
 I $E(LINE,193)=$C(46) I ADXTFN="DI" D DAG Q
 U IO(0) W !,"I CAN'T IDENTIFY THIS RECORD:" W !,LINE,!,LINE2 S R=R-1 U ADXTFNAM Q
PAT ; PATIENT RECORD
 S ^TMP("ADXT",ADXTFN,R)=$E(LINE,1,227),^TMP("ADXT",ADXTFN,R+1)=$E(LINE,228,ADXTLL)_$E(LINE2,1,ADXTL2),R=R+1 Q
DAG ; DIAGNOSIS RECORD
 S REC=(R+1)/2
 S ^TMP("ADXT",ADXTFN,REC,1)=$E(LINE,1,221),^TMP("ADXT",ADXTFN,REC,2)=$E(LINE,222,ADXTLL)_$E(LINE2,1,ADXTL2),R=R+1 Q
TR ;
 S Y=$TR(Y,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ") Q
EXIT ;
 K R,REC,L,LINE,LINE2,ADXTFN,ADXTFNAM,ADXTLL,ADXTL2 Q
ERR ;
 I $ZE["OPENERR" W !!,"Could not open ",ADXTFNAM,"." G ASK
 I $ZE'["ENDO" U IO(0) W !!,$ZE D @^%ZOSF("ERRTN")
 S $ZE="" G END^ADXTRVMS
 ;
CHECKDIR ;
 N ADVDIR,ADVCHECK,ADVPOS,ADXTI,ADVNAME
 S ADVDIR=X,ADVCHECK=$P(ADVDIR,"]",1)
 S ADVPOS=0 F ADXTI=1:1:$L(ADVCHECK) I $E(ADVCHECK,ADXTI)="." S ADVPOS=ADXTI
 I ADVPOS=0 S ADVNAME=$P(ADVCHECK,"[",2),ADVCHECK=$P(ADVCHECK,"[",1)
 I  S ADVCHECK=ADVCHECK_"[000000"
 E  S ADVNAME=$E(ADVCHECK,ADVPOS+1,$L(ADVCHECK))
 E  S ADVCHECK=$E(ADVCHECK,1,ADVPOS-1)
 S ADVCHECK=ADVCHECK_"]"_ADVNAME_".DIR"
 S X="DEVERR^ADXTRVMS",@^%ZOSF("TRAP")
 S Y=1 I '$L($ZSEARCH(ADVCHECK)) S Y=-1
CHECKDI1 ;
 K ADVDIR,ADVNAME,ADVCHECK,ADVPOS,ADXTI
 Q
DEVERR ;
 S X="",@^%ZOSF("TRAP") U IO(0) W !!,$ZE
 S $ZE="",Y=-1 G CHECKDI1
