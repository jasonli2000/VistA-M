ADXTRX ;523/RES  ; PC TO MUMPS GLOBAL TRANSFER          ;OCT 14, 92
 ;;1.1;;
 ;
EN S U="^"
 N ADXTFN,ADXTLL,L,R,LINE,REC
 X ^%ZOSF("TYPE-AHEAD") ; 1-OCT-1992
ASK ;
 K DIR W ! S DIR("A")="PLEASE ENTER THE NAME OF THE FILE TO UPLOAD",DIR("B")="HSPFILE.T01",DIR(0)="F^11:11",DIR("?")="ENTER THE FULL DOS FILE NAME." D ^DIR Q:+Y<0
 Q:$D(DTOUT)!$D(DUOUT)!$D(DIROUT)!$D(DIRUT)
 D TR I Y'="PATFILE.T01"&(Y'="DAGFILE.T01")&(Y'="FLWFILE.T01")&(Y'="DOCFILE.T01")&(Y'="SCDFILE.T01")&(Y'="HSPFILE.T01") G ASK
 S ADXTFN=$E(Y,1,3) I ADXTFN="DAG" S ADXTFN="DI"
 I ADXTFN="FLW" S ADXTFN="FL"
 K ADXTFAIL
 W !!!,"                    >>> PLEASE UPLOAD ",Y," NOW! <<<",!!!
 K ^TMP("ADXT",ADXTFN)
 W !,">" R LINE#245:300 G:'$T EXIT
 S R=1 D LF,LINE S L=R K R
 F R=L+1:1 W !,">" R LINE#245:20 G:'$T END D LF,LINE
END ;
 W !!,"UPLOAD ENDED!",!,R-1," LINES OF DATA HAVE BEEN CAPTURED."
 I $D(ADXTFAIL) W !,"Warning: ",ADXTFAIL," records from the upload could not be processed and were not loaded."
 D EXIT G ASK
LF ;STRIP LF CHARACTER FROM INPUT LINE
 S ADXTLL=$L(LINE)
 I $E(LINE,1)=$C(10) S LINE=$E(LINE,2,ADXTLL),ADXTLL=ADXTLL-1
 Q
LINE ;TRANSFER ASCII LINE INTO GLOBAL LINE(S)
 I ADXTLL<244&((ADXTFN="PAT")!(ADXTFN="DI")) W !,LINE S R=R-1 Q
 I ADXTLL>243 R LINE2#220:20 G:'$T END D SPLIT Q
 S ^TMP("ADXT",ADXTFN,R)=$E(LINE,1,ADXTLL) Q
SPLIT ; DIVIDE RECORDS LONGER THAN 243 CHARACTERS
 S ADXTL2=$L(LINE2)
 ;I $E(LINE,140)=$C(40) D PAT Q
 I $E(LINE,32)="/" I $E(LINE,35)="/" I $E(LINE,40)="/" I ADXTFN="PAT" D PAT Q
 I $E(LINE,193)=$C(46) I ADXTFN="DI" D DAG Q
 S ADXTFAIL=+$G(ADXTFAIL)+1
 W !,"I CAN'T IDENTIFY THIS RECORD:" W !,LINE,!,LINE2,! S R=R-1 Q
PAT ; PATIENT RECORD
 S ^TMP("ADXT",ADXTFN,R)=$E(LINE,1,227),^TMP("ADXT",ADXTFN,R+1)=$E(LINE,228,ADXTLL)_$E(LINE2,1,ADXTL2),R=R+1 Q
DAG ; DIAGNOSIS RECORD
 S REC=(R+1)/2
 S ^TMP("ADXT",ADXTFN,REC,1)=$E(LINE,1,221),^TMP("ADXT",ADXTFN,REC,2)=$E(LINE,222,ADXTLL)_$E(LINE2,1,ADXTL2),R=R+1 Q
TR ;
 S Y=$TR(Y,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ") Q
EXIT ;
 K R,REC,L,LINE,LINE2,ADXTFN,ADXTLL,ADXTL2 Q
