AJEYCF05         ;RMS/HINES - COMPUTED FINDING FOR NVA ACE-I'S ; 15 Oct 2004  2:47 PM
 ;;1.5
 ;FEB 17,2005 ADDED SUBROUTINE NVAINS, Rose Hunt@Milwaukee
 ;
NVAINS(DFN,TEST,DATE,VALUE,TEXT) ; Insulin in Non-VA Meds Data
 S TEST=0,DATE=""
 N AJEYCLAS
 S AJEYCLAS="HS501"
 D BOO
 Q
NVAACEI(DFN,TEST,DATE,VALUE,TEXT) ;
 S TEST=0,DATE=""
 N AJEYCLAS
 S AJEYCLAS="CV800"
 D BOO
 Q
NVABBLK(DFN,TEST,DATE,VALUE,TEXT) ;
 S TEST=0,DATE=""
 N AJEYCLAS
 S AJEYCLAS="CV100"
 D BOO
 Q
NVAARB(DFN,TEST,DATE,VALUE,TEXT) ;
 S TEST=0,DATE=""
 N AJEYCLAS
 S AJEYCLAS="CV805"
 D BOO
 Q
BOO N AJEYCLAI,AJEYDRUG,AJEYNVA,AJEYNVAN,AJEYORDI,AJEYORDA
 S AJEYCLAI=$O(^PS(50.605,"B",AJEYCLAS,0)) Q:'+AJEYCLAI
 S AJEYDRUG=0 F  S AJEYDRUG=$O(^PSDRUG("VAC",AJEYCLAI,AJEYDRUG)) Q:'+AJEYDRUG  D  ;
 . S AJEYORDI=+$G(^PSDRUG(AJEYDRUG,2)) Q:'+AJEYORDI
 . S AJEYORDA(AJEYORDI)=""
BASE S AJEYNVA=0 F  S AJEYNVA=$O(^PS(55,DFN,"NVA",AJEYNVA)) Q:'+AJEYNVA  D  ;
 . S AJEYNVAN=$G(^PS(55,DFN,"NVA",AJEYNVA,0))
 . Q:$P(AJEYNVAN,"^",6)  ;;ONLY LOOK AT ACTIVE ITEMS
 . I $D(AJEYORDA(+AJEYNVAN)) D  ;
 .. S TEST=1,DATE=$P(AJEYNVAN,"^",10)
 .. S VALUE=$P(^PS(50.7,+AJEYNVAN,0),"^")
 Q
NVAASA(DFN,TEST,DATE,VALUE,TEXT) ;
 S TEST=0,DATE=""
 N AJEYORDN
 S AJEYORDN="ASPIRIN"
 D HOO
 Q
NVAWARF(DFN,TEST,DATE,VALUE,TEXT) ;
 S TEST=0,DATE=""
 N AJEYORDN
 S AJEYORDN="WARFARIN"
 D HOO
 Q
HOO N AJEYORD,AJEYORDI,AJEYORDA,AJEYNVA,AJEYNVAN
 S AJEYORD="" F  S AJEYORD=$O(^PS(50.7,"B",AJEYORD)) Q:AJEYORD']""  D  ;
 . Q:AJEYORD'[AJEYORDN
 . S AJEYORDI=0 F  S AJEYORDI=$O(^PS(50.7,"B",AJEYORD,AJEYORDI)) Q:AJEYORDI'=+AJEYORDI  D  ;
 .. S AJEYORDA(AJEYORDI)=""
 D BASE
 Q
