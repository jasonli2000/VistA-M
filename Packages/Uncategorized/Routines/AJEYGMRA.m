AJEYGMRA ;RMS/HINES - ALLERGY/ADVERSE REACTION COMPUTED FINDING ; 6-25-04
 ;;2.0
ARTCL(DFN,NGET,BDT,EDT,NFOUND,TEST,DATE,DATA,TEXT) ;
 ;;PARAMETER IS A VA DRUG CLASS CODE E.G., XX###
 S NFOUND=0
 Q:(TEST="")!(NGET=0)
 N IEN,CLASS,CLARRAY,EXTDATE,DATEEVAL
 I TEST'["*" S CLARRAY(TEST)=""
 E  D WILDCARD
 Q:'$D(CLARRAY)
 S CLASS="" F  S CLASS=$O(CLARRAY(CLASS)) Q:(NFOUND=NGET)!(CLASS']"")  D  ;
 .Q:'$O(^PS(50.605,"B",CLASS,0))
 .S IEN=$O(^GMR(120.8,"APC",DFN,CLASS,""))
 .Q:'+IEN
 .S DATEEVAL=+$$GET1^DIQ(120.8,+IEN,4,"I")
 .Q:(DATEEVAL<BDT)!(DATEEVAL>EDT)
 .S NFOUND=NFOUND+1,TEST(NFOUND)=1
 .S DATE(NFOUND)=DATEEVAL
 .S EXTDATE=$$GET1^DIQ(120.8,+IEN,4)
 .S DATA(NFOUND,"REACTANT")=$$GET1^DIQ(120.8,+IEN,.02)
 .S TEXT(NFOUND)="Documented reaction to an agent in class "_CLASS_", reactant was: "_DATA(NFOUND,"REACTANT")_"."
 Q
 ;
WILDCARD ;EXPAND ARRAY OF DRUG CLASSES BASED ON "*" WILDCARD SYMBOL
 ; * MAY BE USED AS THE 5TH CHARACTER TO MAKE A SERIES
 Q:(TEST'?4E1"*")!(TEST?3E2"*")
 N CLBEG,CLEND,CLLOOP
 S CLBEG=$E(TEST,1,4)_" ",CLEND=$E(TEST,1,4)_":"
 S CLLOOP=CLBEG F  S CLLOOP=$O(^PS(50.605,"B",CLLOOP)) Q:CLLOOP]CLEND  D  ;
 . S CLARRAY(CLLOOP)=""
 Q
