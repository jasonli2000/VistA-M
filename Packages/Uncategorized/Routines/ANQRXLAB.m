ANQRXLAB ;MUSKOGEE OK./GLD-DRUG+LAB RESULT PRINT ;1-30-89
 ;;1.01;
 ;A ROUTINE WHICH LOOP THRU THE LAST FILL X-REF OF ^PSRX AND GETS
 ;PATIENTS WITH A SPECIFIC DRUG. THEN GETS THE LRDFN FROM THE 
 ;PATIENT FILE AND LOOPS THRU THE PATIENTS LAB DATA TO FIND
 ;RESULTS WITHIN THE DATE RANGE YOU SPECIFY FOR THE LAB TEST
 ;USED TO MINITOR THE DRUG. IT THEN PRINTS THE PATIENT'S NAME
 ;SSN, LAST FILL DATE, AND THE LAB TEST RESULTS IF ANY.
 ;THIS IS INTENDED AS A QA MINITOR AND SHOULD NOT BE RUN FOR
 ;MORE THAN A 30 DAY FILL DATE INTERVAL, OR 1 YEAR LAB TEST INTERVAL.
ANQSITE S Y=DT X ^DD("DD") S SITE=^DD("SITE")_" "_^DD("SITE",1)_"  "_Y
BDATE S %DT="EXA",%DT("A")="ENTER BEGINNING FILL DATE: " D ^%DT G CLEAN:Y<0 S ANQBD=Y X ^DD("DD") S ANQBDR=Y
EDATE S %DT("A")="ENTER ENDING LAST FILL DATE: " D ^%DT G CLEAN:Y<0 S ANQED=Y X ^DD("DD") S ANQEDR=Y
LDATE S %DT("A")="ENTER EARLIEST DATE FOR LAB RESULTS: " D ^%DT G CLEAN:Y<0 S LDATE=Y X ^DD("DD") S LDATER=Y
DRUG R !,"ENTER THE KEY WORD IN THE DRUG GENERIC NAME: ",ANQDRUG:DTIME G CLEAN:'$T I "^"[ANQDRUG G CLEAN
 I $O(^PSDRUG("B",$E(ANQDRUG,1,$L(ANQDRUG)-1)))'[ANQDRUG W !,"NO CORRESPONDING ENTRY, TRY AGAIN OR TYPE RETURN TO EXIT" G DRUG
LABT S DIC="^LAB(60,",DIC(0)="QEAM" D ^DIC G:Y<0 CLEAN S ANQLBT=$P(Y,"^",1),ANQLABTN=$P(Y,"^",2) G:ANQLBT="" CLEAN
 S ANQLABT=^LAB(60,ANQLBT,.2)
 W !,"ENTER THE SPECIMEN USED IN THE LAB FOR THIS TEST, SERUM,PLASMA,BLOOD ETC."
ANQSP S DIC="^LAB(61,",DIC(0)="QEAM" D ^DIC G:Y<0 CLEAN S ANQSP=$P(Y,"^",1) G:ANQSP="" CLEAN
ANQUNIT S ANQUNIT=$P(^LAB(60,ANQLBT,1,ANQSP,0),"^",7)
ANQANS R !,"DO YOU WANT RX INFO? N// ",ANQANS:DTIME G CLEAN:'$T S:ANQANS="" ANQANS="N" I "YN"'[ANQANS W !,"ANSWER YES OR NO" G ANQANS
DEVICE K IOP S %IS="MQ" D ^%ZIS G:POP CLEAN2
 I $D(IO("Q")) K IO("Q") S ZTSAVE("*")="",ZTRTN="DQ^ANQRXLAB",ZTDESC="LAB LIST" D ^%ZTLOAD K ZTSK G CLEAN
DQ K ^UTILITY($J) S ANQBD=ANQBD-1,PAGE=0 U IO W @IOF D HDR
LOOP1 F J=0:0 S ANQBD=$O(^PSRX("AD",ANQBD)) Q:ANQBD=""  Q:ANQBD>ANQED  S ANQRX=0 D LOOP2
 G CLEAN
LOOP2 F J2=0:0 S ANQRX=$O(^PSRX("AD",ANQBD,ANQRX)) Q:ANQRX=""  D CHECK1 
 Q
CHECK1 S ANQDGN=$P(^PSRX(ANQRX,0),"^",6),ANQDRUGN=$P(^PSDRUG(ANQDGN,0),"^",1)
 I ANQDRUGN'[ANQDRUG Q
 S ANQPROV=$P(^PSRX(ANQRX,0),"^",4),ANQPROVN=$P(^DIC(16,ANQPROV,0),"^",1),ANQPROT=$P(^DIC(6,ANQPROV,0),"^",5)
 S ANQTYPE="NONE" I ANQPROT S ANQTYPE=$P("FULL TIME^PART TIME^C & A^FEE^STAFF","^",ANQPROT)
CHECK2 S ANQPT=$P(^PSRX(ANQRX,0),"^",2) W ! D PRINT2
 I '$D(^DPT(ANQPT,"LR")) W ?49,"NO LAB DATA EXISTS",?75,$E(ANQPROVN,1,20),?100,ANQTYPE,! D:ANQANS["Y" ANQRXI Q
 S LRDFN=$P(^DPT(ANQPT,"LR"),"^",1),ANQLBENT=0,ANQINDIC=0
LOOP3 F J2=0:0 S ANQLBENT=$O(^LR(LRDFN,"CH",ANQLBENT)) Q:ANQLBENT=""  S ANQLDATE=$P(^LR(LRDFN,"CH",ANQLBENT,0),"^",1) Q:ANQLDATE<LDATE  D CHECK3
 I ANQINDIC=0 W ?49,"NO LAB DATA IN RANGE",?75,$E(ANQPROVN,1,20),?100,ANQTYPE,!
 D:ANQANS["Y" ANQRXI
 Q
CHECK3 I $D(^LR(LRDFN,"CH",ANQLBENT,ANQLABT)) D RESULT
 Q
RESULT Q:$P(^LR(LRDFN,"CH",ANQLBENT,0),"^",5)'=ANQSP  Q:'$P(^(0),"^",3)
 S Y=ANQLDATE X ^DD("DD") W ?49,$E(Y,1,11),?64,$P(^LR(LRDFN,"CH",ANQLBENT,ANQLABT),"^",1)," ",ANQUNIT,?75,$E(ANQPROVN,1,20),?100,ANQTYPE W !
 S ANQINDIC=1 Q
 Q
PRINT2 I $Y>(IOSL-7) W @IOF,SITE,! D HDR2
 W ?1,$E($P(^DPT(ANQPT,0),"^",1),1,20),?25,$P(^DPT(ANQPT,0),"^",9) S Y=ANQBD X ^DD("DD") W ?37,Y Q
HDR W SITE,!!,"PATIENTS RECEIVING ",ANQDRUG," WITH FILLS BETWEEN ",ANQBDR," AND ",ANQEDR,!," WITH DATE OF COLLECTION AND RESULTS FOR LAB TEST ",ANQLABTN," AFTER ",LDATER,!
HDR2 S PAGE=PAGE+1 W !,"NAME",?25,"SSN",?37,"FILL DATE",?49,"LAB DATE",?65,"RESULTS",?75,"RX PROVIDER",?100,"TYPE",?110,"PAGE ",PAGE,!
 F J=1:1:120 W "_"
 W ! Q
ANQRXI W "RX #: ",$P(^PSRX(ANQRX,0),"^",1),"   ","DRUG: ",$P(^PSDRUG(ANQDGN,0),"^",1),"   ","SIG: ",$P(^PSRX(ANQRX,0),"^",10),! Q
CLEAN W !! X ^%ZIS("C")
CLEAN2 K ANQINDIC,ANQPT,ANQLDATE,PAGE,ANQBD,ANQBDR,ANQLBENT,ANQLABT,ANQDGN,ANQDRUGN,ANQDRUG,J,J1,J2,ANQRX,ANQPROV,ANQPROVN,LDATE,LDATER,ANQED,ANQEDR,ANQPROT,ANQTYPE,ANQLABTN,ANQLBT,ANQSP,ANQUNIT,ANQANS,DIC,LRDFN,POP,SITE,Y,%DT
 Q
