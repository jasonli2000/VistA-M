AOVTIU2 ;VAPA/640/LYL tiu utility routine  1/12/98
 ;;1.0;TEXT INTEGRATION UTILITIES;;Jun 20, 1997
 ;
 ;
DIET(DFN) ; -- Entry point called by current diet tiu object
 ;get current diet
 N WARD,ADMIN,Y,X,FHLD,FHOR,FHORD,X1,A1,ADM,D3
 S WARD=$G(^DPT(DFN,.1))
 S:WARD'="" ADM=$G(^DPT("CN",WARD,DFN))
 I WARD=""!($G(ADM)="") Q "Not currently admitted"
 D CUR^FHORD7
 Q $S(Y'="":Y,1:"No current order")
 ;
SUPFEED(DFN,TARGET) ; -- entry point for inpt supplemental feedings for tiu
 N WARD,ADM,X,NO,L,K1,K,N,K2,Z,Y,Q,LINE
 S WARD=$G(^DPT(DFN,.1))
 S:WARD ADM=$G(^DPT("CN",WARD,DFN))
 I WARD=""!($G(ADM)="") Q "Not currently admitted"
 S X(0)=$G(^FHPT(DFN,"A",ADM,0))
 S NO=$P(X(0),"^",7),Y=$S('NO:"",1:^FHPT(DFN,"A",ADM,"SF",NO,0))
 S L=4 F K1=1:1:3 S K=0,N(K1)="" F K2=1:1:4 S Z=$P(Y,U,L+1),Q=$P(Y,U,L+2),L=L+2 I Z'="" S:'Q Q=1 S:N(K1)'="" N(K1)=N(K1)_"; " S N(K1)=N(K1)_Q_" "_$P(^FH(118,Z,0),"^",1)
 S LINE=0
 S N=0 F  S N=$O(N(N)) Q:N<1  S LINE=LINE+1,@TARGET@(LINE,0)=N(N)
 S @TARGET@(0)="^^"_LINE_"^"_LINE_"^"_DT_"^^"
 Q "~@"_$NA(@TARGET)
 ;
DIETMED(DFN,TARGET) ; -- entry for diet medicaitons only
 N WARD,PX,LINE,PSD
 S WARD=$G(^DPT(DFN,.1))
 S PX=$S(WARD="":0,1:1) D DRUG^FHASM4
 S (LINE,PX)=0
 F  S PX=$O(PSD(PX)) Q:PX<1  S LINE=LINE+1,@TARGET@(LINE,0)=PSD(PX)
 S @TARGET@(0)="^^"_LINE_"^"_LINE_"^"_DT_"^^"
 Q "~@"_$NA(@TARGET)
 ;
WGT(DFN,NULL) ;get patient "weight" from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 I $D(WGT),WGT S WGT=WGT_" lbs ("_$J((WGT/2.2),0,1)_" kg)" ;580/mod/ml/2990401
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,1:WGT)
 ;
AJK3AMP(DFN,NULL) ;get patient amputation % from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,1:AJK3AMP)
 ;
AJK3FS(DFN,NULL) ;get patient frame size from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 I $D(AJK3FS) S AJK3FS=$S(AJK3FS="S":"Small",AJK3FS="M":"Medium",AJK3FS="L":"Large",1:"")
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,1:AJK3FS)
 ;
AJK3NP(DFN,NULL) ;get nutrition problem [20P], #19 from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 I $G(AJK3NP)]"" S Y=AJK3NP,C=$P(^DD(115.011,19,0),U,2) D Y^DIQ S AJK3NP=Y
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,1:AJK3NP)
 ;
AJK3KCAL(DFN,NULL) ;get kcal required [16N], #15 from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,AJK3KCAL]"":AJK3KCAL_" KCal/day",1:"")
 ;
AJK3GM(DFN,NULL) ;get protein (gm) required [17N], #16 from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,AJK3GM]"":AJK3GM_" gm/day",1:"")
 ;
AJK3FL(DFN,NULL) ;get fluid (ml/day) required [18N], #17 from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 I $G(AJK3FL)]"" S Y=AJK3FL,C=$P(^DD(115.011,17,0),U,2) D Y^DIQ S AJK3FL=Y
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,AJK3FL]"":AJK3FL_" ml/day",1:"")
 ;
AJK3RSK(DFN,NULL) ;get risk category [19P], #18 from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 I AJK3RSK]"",$P($G(^FH(115.4,+AJK3RSK,0)),U,2)]"" S AJK3RSK=$P($G(^FH(115.4,+AJK3RSK,0)),U,2)
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,1:AJK3RSK)
 ;
AJK3ADT(DFN,NULL) ;get nutrition assessment [1D], #.01 from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 ; the following line added because undefined variable ADT encountered if patient has not assessment data/ml/2981127
 I $D(NO) Q NO
 S Y=ADT X ^DD("DD") S ADT=Y
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,1:ADT)
 ;
AJK3BM(DFN,NULL) ;get body mass index [21N], #21 from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,AJK3BM_AJK3BMP]"":AJK3BM_" ("_AJK3BMP_"%)",1:"")
 ;
DWGT(DFN,NULL) ;get patient "date weight taken" from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 I '$D(NO) S Y=DWGT X ^DD("DD") S DWGT=Y
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,1:DWGT)
 ;
UWGT(DFN,NULL) ;get patient usual body weight from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET
 I $D(UWGT),UWGT S UWGT=UWGT_" lbs ("_$J((UWGT/2.2),0,1)_" kg)" ;580/mod/ml/2990407
 Q $S($D(NO)&$D(NULL):NULL,$D(NO):NO,1:UWGT)
 ;
IDEALWT(DFN,NULL) ;get patient ideal weight from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET I $D(NO) Q $S($D(NULL):NULL,1:NO)
 S X1=IBW_" lbs",X2=+$J(IBW/2.2,0,1)_" kg"
 Q X1_" ("_X2_")"
 ;
DHEIGHT(DFN,NULL) ;get patient height from diet files
 N NO,UWGT,Y,N1,K,ADT,HGT,HGP,WGT,DWGT,IBW,XD,ASN,AJK3AMP,AJK3FS,AJK3NP,AJK3KCAL,AJK3GM,AJK3FL,AJK3RSK,AJK3BM,AJK3BMP
 D DIETSET I $D(NO) Q $S($D(NULL):NULL,1:NO)
 S X1=$S(HGT\12:HGT\12_"'",1:"")_$S(HGT#12:" "_(HGT#12)_"""",1:""),X2=+$J(HGT*2.54,0,0)_" cm"
 S:HGP'="" X2=X2_" "_HGP
 Q X1_" ("_X2_")"
 ;
DIETSET ; -- sets up stuff for diet objects
 S ASN=$O(^FHPT(DFN,"N",0)) I ASN'>0 S NO="No assessments on file." Q
 S Y=^FHPT(DFN,"N",ASN,0)
 S N1=0 F K=1,4,5,6,7,8,9,10,11,12,16,17,18,19,20,21,22 S N1=N1+1,@$P("ADT HGT HGP WGT WGP DWGT UWGT IBW AJK3FS AJK3AMP AJK3KCAL AJK3GM AJK3FL AJK3RSK AJK3NP AJK3BM AJK3BMP"," ",N1)=$P(Y,"^",K)
 Q
 ;      ==> (#.01) NUTRITION ASSESSMENT [1D] ^ (#1) SEX [2S] ^ (#2) AGE
 ;      ==>[3N] ^ (#3) HEIGHT [4N] ^ (#4) HEIGHT PARAMETERS [5S] ^
 ;      ==>(#5) WEIGHT [6N] ^ (#6) WEIGHT PARAMETERS [7S] ^ (#7) DATE
 ;      ==>WEIGHT TAKEN [8D] ^ (#8) USUAL WEIGHT [9N] ^ (#9) IDEAL
 ;      ==>BODY WEIGHT [10N] ^ (#10) FRAME SIZE [11S] ^ (#11)
 ;      ==>AMPUTATION % [12N] ^  ^  ^  ^ (#15) KCAL REQUIRED [16N] ^
 ;      ==>(#16) PROTEIN (GM) REQUIRED [17N] ^ (#17) FLUID (ML/DAY)
 ;      ==>REQUIRED [18N] ^ (#18) RISK CATEGORY [19P] ^ (#19)
 ;      ==>NUTRITION PROBLEM [20P] ^ (#21) BODY MASS INDEX [21N] ^
 ;      ==>(#22) BODY MASS INDEX % [22N] ^ (#101) ENTERING CLINICIAN
 ;      ==>[23P] ^ (#102) DATE/TIME ENTERED [24D] ^ (#23) NITROGEN
 ;      ==>BALANCE [25N] ^
