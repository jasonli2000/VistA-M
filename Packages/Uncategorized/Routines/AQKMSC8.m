AQKMSC8 ;RENO/KEITH;010897 [ 02/04/97  4:23 PM ]
 ;;1;Cleanup garbage radiology add/edits
 N DIR,ZTSAVE,X,Y,DTOUT,DUOUT,AQKACT
 S DIR(0)="S^L:LIST ONLY;D:DELETE AND LIST",DIR("A")="Select clean-up action",DIR("B")="LIST ONLY" D ^DIR Q:$D(DTOUT)!$D(DUOUT)  S AQKACT=Y,ZTSAVE("AQKACT")=""
 D EN^XUTMDEVQ("BADRAD^AQKMSC8","Cleanup bad rad. add/edits",.ZTSAVE) Q
 ;
BADRAD S AQKPAGE=1 D PARM,HDR S AQKCT=0 G:('$G(AQKSC)!'$G(AQKDT)) EXIT S AQKD=2961001 F  S AQKD=$O(^SDV(AQKD)) Q:'AQKD!(AQKD>(AQKDT+2))  S AQKS=0 F  S AQKS=$O(^SDV(AQKD,"CS","B",AQKSC,AQKS)) Q:'AQKS  D CHEK
 D:$Y>(IOSL-4) HDR W !!,AQKCT," ""garbage"" Radiology add/edits ",$S(AQKACT="L":"found.",1:"deleted.")
EXIT K AQKCT,AQKD,AQKDT,AQKOE,AQKOE0,AQKPAGE,AQKPK,AQKPKP,AQKPKV,AQKPT,AQKS,AQKS0,AQKSC,AQKSCOE,AQKSDV,AQKVS,DIR,DIK,DA,D0,DTOUT,DUOUT,I,Y Q
 ;
PARM S AQKSC=$O(^DIC(40.7,"C",105,0)) Q:'AQKSC  S AQKPK=$O(^DIC(9.4,"C","RA",0)) Q:'AQKPK  S AQKPKV=$O(^DIC(9.4,AQKPK,22,"B",4.5,0)) Q:'AQKPKV  S AQKPKP=$O(^DIC(9.4,AQKPK,22,AQKPKV,"PAH","B",8,0))
 S:'AQKPKP AQKPKP=$O(^DIC(9.4,AQKPK,22,AQKPKV,"PAH","B","8 SEQ #8",0)) Q:'AQKPKP  S AQKDT=$P(^DIC(9.4,AQKPK,22,AQKPKV,"PAH",AQKPKP,0),U,2) Q
 ;
CHEK S AQKS0=^SDV(AQKD,"CS",AQKS,0),AQKOE=$P(AQKS0,U,8),AQKOE0=$G(^SCE(+AQKOE,0)),AQKVS=$P(AQKOE0,U,5)
 I AQKACT="D" K DA,DIK S DA=AQKS,DA(1)=AQKD,DIK="^SDV("_DA(1)_",""CS""," D ^DIK S DA=0 F  S DA=$O(^SDV(DA(1),"CS",DA)) Q:'DA  D:$P(^SDV(DA(1),"CS",DA,0),U,8)=AQKOE ^DIK
 I AQKOE,AQKACT="D" D CLEAN(AQKOE,AQKVS)
 D:$Y>(IOSL-5) HDR W !,"SCH. VISITS: ",AQKD," / ",AQKS,"    OUTP. ENC.: ",AQKOE,"    VISIT: ",AQKVS S AQKCT=AQKCT+1 Q
 Q
 ;
CLEAN(AQKOE,AQKVS) ;Clean up OUTPATIENT ENCOUNTER/DIAGNOSIS/PROVIDER/CLASSIFICATION, VISIT and V file records
 ;Required input: AQKOE=OUTPATIENT ENCOUNTER record IFN
 ;Optional input: AQKVS=VISIT record IFN
 N DA,DIK,AQKFNAM,AQKGL,AQKVFILE
 I '$G(AQKVS) S AQKVS=$P(^SCE(AQKOE,0),U,5)
 K DA,DIK S DA=AQKOE,DIK="^SCE(" D ^DIK,MOE F I=2:1:4 S AQKGL="^SDD(409.4"_I_"," D KFR("""OE"",",AQKOE)
 S I=$$DELXMIT^SCDXFU03(AQKOE,1)
 I AQKVS,'$D(^SCE("AVSIT",AQKVS)) D MVSIT(AQKVS) S DA=AQKVS,DIK="^AUPNVSIT(" D ^DIK S DA=0 F  S DA=$O(^AUPNVSIT("AD",AQKVS,DA)) Q:'DA  D MVSIT(DA) S DIK="^AUPNVSIT(" D ^DIK
 Q
 ;
MOE S DA=0 F  S DA=$O(^SCE("APAR",AQKOE,DA)) Q:'DA  D CLEAN(DA)
 Q
 ;
MVSIT(AQKVS) ;Delete V file entries
 ;Required input: AQKVS=VISIT record IFN
 S AQKVFILE=9000010 F  S AQKVFILE=$O(^DIC(AQKVFILE)) Q:AQKVFILE>9000010.23  S AQKFNAM=$O(^DD(AQKVFILE,0,"NM","")),AQKGL=^DIC(AQKVFILE,0,"GL") D KFR("""AD"",",AQKVS)
 Q
 ;
KFR(XR,AQKREC) ;Delete file entries
 ;Required input: XR=cross reference name
 ;Required input: AQKREC=encounter or visit record IFN
 S DA=0 F  S DA=$O(@(AQKGL_XR_AQKREC_","_DA_")")) Q:'DA  S DIK=AQKGL D ^DIK
 Q
 ;
HDR ;Header
 W:AQKPAGE'=1 @IOF W:$X ! W "Cleanup Radiology ""garbage"" add/edits",$S(AQKACT="L":" (list only)",1:" (delete and list)"),?(80-$L(AQKPAGE)-6),"Page: ",AQKPAGE W ! F I=1:1:80 W "-"
 Q
 ;
BADADD(D0) ;Function for finding bad add/edits
 ;Required input: D0=OUTPATIENT ENCOUNTER record IFN
 N AQK0,AQKPT,AQKDT,AQKSC,X
 S AQK0=^SCE(D0,0),AQKDT=$P($P(AQK0,U),"."),AQKPT=$P(AQK0,U,2),X="No add/edits",AQKSDV=$G(^SDV("ADT",AQKPT,AQKDT)) Q:'AQKSDV X
 S X="No pointer found",AQKSC=0
LOOK S AQKSC=$O(^SDV(AQKSDV,"CS",AQKSC)) Q:'AQKSC X  S AQKSCOE=$P(^SDV(AQKSDV,"CS",AQKSC,0),U,8) G:AQKSCOE'=D0 LOOK S X="Pointer found" Q X
 ;
BADAPP(D0) ;Function for finding bad appointment encounters
 ;Required input: D0=OUTPATIENT ENCOUNTER record IFN
 N AQKPT,AQK0,AQKDT,X,AQKOE
 S AQK0=^SCE(D0,0),AQKDT=$P(AQK0,U),AQKPT=$P(AQK0,U,2) Q:'$D(^DPT(AQKPT,"S",AQKDT)) "No such appointment"
 S AQKOE=$P(^DPT(AQKPT,"S",AQKDT,0),U,20) Q:AQKOE'=D0 "No pointer found"
 Q "Pointer found"
