ASFRXAVG ;PROGRAM TO CALCULATE AVERAGE DAILY DISPENSING OF PRESELECTED DRUG AND CALCULATE AVERAGE COST PER DAY
 ;;V1.0 GDM/SYR 9/93
LOOKUP S DIC="^PSDRUG(",DIC(0)="AEMNQZ" D ^DIC Q:Y=-1  S PDRUG=Y(0,0)
 W !!,"Do you want to Print each entry for ",PDRUG_" ?" S %=2 D YN^DICN S PFLAG=%  ;PRINT IF PFLAG=1
 S PDRUG1=PDRUG F I=1:1 S PDRUG1=$O(^PSDRUG("B",PDRUG1)) Q:PDRUG1'[PDRUG  S PIFN(I)=$O(^PSDRUG("B",PDRUG1,""))
 S HOLDI=I-1 K I
 S %IS="Q" D ^%ZIS Q:IO=""
 S ZTRTN="PRT^ASFRXAVG" F G="PDRUG","PFLAG" S ZTSAVE(G)=""
 I IO'=IO(0) D ^%ZTLOAD X ^%ZIS("C") W !!,"Request Queued !! ",! Q
 ;
PRT W !,"PROCESSING.......",!
 S (TQPD,TQTY,TDSUP,TPRES,TMGM)=0 D:PFLAG=1 HD D PRT1 G END
PRT1 S ENTNO=0 F I=0:0 S ENTNO=$O(^PSRX(ENTNO)) Q:ENTNO="B"  I $D(^PSRX(ENTNO,0)) S ENTRY=^PSRX(ENTNO,0),RXNUM=$P(ENTRY,"^",1) D:$Y>(IOSL-2)&(PFLAG=1) HD D
 .Q:'$P(ENTRY,"^",6)  S PSRXDRG=$P(ENTRY,"^",6),PSRXDRG=$P(^PSDRUG(PSRXDRG,0),"^",1) Q:PSRXDRG'=PDRUG  D
 ..S QTY=$P(ENTRY,"^",7),DSUP=$P(ENTRY,"^",8),UPRICE=$P(ENTRY,"^",17),QPD=(QTY/DSUP),TQPD=(TQPD+QPD),TQTY=(TQTY+QTY),TDSUP=(TDSUP+DSUP),TPRES=(TPRES+1) D GMGM S MGM1=(MGM1*QPD),TMGM=(TMGM+MGM1) D:PFLAG=1 WRT 
 K PSRXDRG,QTY,DSUP,QPD,MGM1,%,%IS,DIC,DIC(0),G,HOLDI,PDRUG1,PIFN,RXNUM,TQPD,Y,Y(0) Q
 ;
WRT ;THIS SECTION PRINTS THE INDIVIDUAL ENTRY IF USER SPECIFIED TO DO SO
 W !?1,$E(PSRXDRG,1,30),?32,RXNUM,?43,QTY,?49,DSUP,?57,$J(QPD,4,2),?65,$J(MGM1,5,3)
 Q
HD ;THIS SECTION PRINTS THE HEADING
 W #,!?15,"Average Daily Mgm Dispensed of ",PDRUG,!!?1,"GENERIC NAME",?32,"RX#",?42,"QTY",?47,"DAY SUP",?55,"QTY/DAY",?65,"MGM/DAY",!?1,"------------",?32,"---",?42,"---",?47,"------",?55,"-------",?65,"----------",!
 Q
END W !!?25,"Summary of ",PDRUG,!?30,"------------------------",!,"TOTAL PRESCRIPTIONS: ",TPRES I TPRES>0 W !,"AVG QTY DISPENSED: ",$J((TQTY/TPRES),5,2),!,"AVG SUPPLY PER PRES: ",$J((TDSUP/TPRES),5,2),!,"AVG MG PER DAY: ",$J((TMGM/TPRES),5,2),!
 K QTY,MGM,MGM1,TMGM,DSUP,UPRICE,PFLAG,TPRES,TQTY,TDSUP,TDCOST,DCOST,PDRUG,PSRXDRG,ENTNO,ENTRY,RXNUMTQPD,QPD,ZTRTN,ZTSAVE
 Q
GMGM ;THIS SECTION DETERMINES THE STRENGTH OF THE DRUG IN MGM'S
 N I
 Q:PSRXDRG'["MG"
 F I=1:1:$L(PSRXDRG," ") S MGM=$P(PSRXDRG," ",I) S:MGM["MG" MGM1=+MGM
 Q
