CUAAUTO ;RJ WILMINGTON DE & LM COLUMBIA SC-MICOM LOADER; 6-9-85
 ;Version 3.6
 S S="*",DX=0,DY=0,CUAAS="S R=$S((C?1""LIM T"".N1"" ""1""I"".E)!(C?1""LIM T"".N1"" ""1""E"".E):""ENTER CLASSES"",1:""TASK COMPLETE"")"
 S DIC="^%ZIS(""Z"",105,",DIC(0)="AENQZ" W ! D ^DIC G:Y=-1 E S DA=+Y,CUAA=DIC_DA_",1," D 1^CUAAVR I X=U W !,"No connection made." G E
 I '$D(@(CUAA_"0)")) U IO(0) W !,"WARNING: NO COMMANDS FOUND." G M1
 S N=0,CUAAG=CUAA_"N)",CUAAF=CUAA_"N,0)" F M=1:1 S N=$N(@CUAAG) Q:N<0  I $D(@CUAAF) S C=$P(@CUAAF,";") D DIM I $D(C) X CUAAS D T,R D:X=S X
 U IO(0) W !!,M-1," COMMANDS LOADED.  NOW LOADING MESSAGES.",!
M1 S CUAA=DIC_DA_",2,",M=0 S:$D(@(CUAA_"0)")) M=$P(@(CUAA_"0)"),U,3) I 'M W !,"NO CLASS 0 MESSAGE FOUND." G CM
 S C="MSG 0",R=">" D T F J=1:1:M S CUAAF=CUAA_J_",0)" D R I $D(@CUAAF) Q:X=S  S C=@CUAAF D T
 D R G:X=S M S C="",R="TASK COMPLETE" D T,R G:X=S M S C="MSG 0 C0" D T,R
M I X=S U IO(0) W !,"** ERROR LOADING CLASS 0 MESSAGE **",! G E
CM S CUAA=DIC_DA_",3," G:'$D(@(CUAA_"0)")) E S N=$P(@(CUAA_"0)"),U,3),K=0 F J=1:1:N S CUAAF=CUAA_J_",0)" I $D(@CUAAF) S K=K+1,C="MSG "_K_" C"_+@CUAAF,R="ENTER CLASS MESSAGE" D T,R Q:X=S  S C=$P(@CUAAF,U,2),R="TASK COMPLETE" D T,R Q:X=S
 U IO(0) I '(X=S) W !!,"AUTO LOAD COMPLETE.",!
 E  W !,"** ERROR LOADING MESSAGE **",!
E D:Y'=-1 CP^CUAADIS X ^%ZIS("C") K C,CUAA,CUAAF,CUAAG,CUAAS,DA,DIC,I,IO,J,M,N,S,X,Y Q
T U IO X ^%ZOSF("XY") U IO(0) F I=1:1 D P S X=$E(C,I) Q:X=""  U IO W X
 U IO W *13 U IO(0) W ! Q
P F P=1:1:400
 Q
R F I=1:1:4 U IO R X:1 U IO(0) W X Q:X[R
 I X[R R X:1 Q
 S X=S Q
DIM S:$E(C,1)=" "!($E(C,1)="&") C=$E(C,2,$L(C)) S X=C D ^DIM Q:'$D(X)  Q:X=""
 S X="" I $D(DUZ(0))#2,(DUZ(0)="@") X C
 K C Q
X I IO(0) W !,"** COMMAND NUMBER ",M,": ",C,"  REJECTED BY MICOM **",! H 1 Q
