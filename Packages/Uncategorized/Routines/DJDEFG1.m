DJDEFG1 ;JA/WVDC COMP. ENTRY LENGTH, DEFINITION OF SECOND SCREEN ; 07 Aug 86  7:54 PM
 ;VERSION 1.36
 ;RECOMPUTATION OF THE FIRST FIELD FOR MULTIPLE FIELD SCREEN
 D EN1 S V(12)=DJSS
 S DJAA=0,DJ8=V(21) F DJK=1:1 Q:'$D(^DD(+DJ8,0,"UP"))  S DJAA=DJAA+1,DJ8=^("UP")
 S DJAA=DJAA+1
 S X=V(12),DIC="^DJR(",DIC(0)="LZ",DLAYGO=2000 D ^DIC K DLAYGO S (DA,DJSDA)=+Y
T X DJCP W !,@DJHIN,"A new screen name has been created for this multiple",!,"The screen name is **",V(12),"**",@DJLIN,*7
 W !,"Enter the Title for this Screen: "
T1 S DX=33,DY=19 X XY R DJT:DTIME I $L(DJT)<2!($L(DJT)>64)!(DJT'?1ANP.ANP)!(DJT?1"?".E) W !!!,*7,"Required entry.  You must enter a title (2-64 characters) for this screen. " S $P(DJDB," ",$L(DJT))=" " X XY W DJDB K DJDB G T1
 S ^DJR(DJSDA,0)=V(12)_U_15_U_U_$P(^DJR(DJDN,0),U,4)_U_U_+V(21)_U_DJT_U_DJAA_U_1
 S ^DJR(DJSDA,1,0)="^2000.07A^1^1"
 S DJS5=V(5),DJS8=V(8),DJS21=V(21),DJS20=V(20),V(5)=+V(21),V(20)=.01 D EN2
 S DJS6=$P(^DD(V(5),.01,0),U,2) S DJS6=$P(DJS6,"M",1)_$P(DJS6,"M",2)
 S DJNAME=$P(^DD(V(5),.01,0),U,1),DJAA=DJAA+1,DJNAMEL=$L(DJNAME)+4 S ^DJR(DJSDA,1,1,0)=DJNAME_U_"DY="_DJAA_",DX=0"_U_V(8)_U_"DY="_DJAA_",DX="_DJNAMEL_U_.01_U_DJS6_U_U_0_U_DJAA_U_DJNAMEL_U_DJAA_U_U_1
 S ^DJR(DJSDA,1,"A",1,1)=""
 S ^DJR(DJSDA,1,"B",DJNAME,1)=""
 S V(8)=DJS8,V(21)=DJS21,V(20)=DJS20,V(5)=DJS5 K DJS8,DJS5,DJS21,DJS20,DJNAME,DJAA,DJSDA Q
EN ;GENERATES NEW SCREEN FOR MULTIPLE PAGE SCREEN
 X DJCP W !,@DJHIN,"YOU HAVE GONE BEYOND THE 15TH ROW",@DJLIN,*7
 I V(4)]"" X DJCP W !,"NEW SCREEN CANNOT BE CREATED, THERE IS ALREADY A RIGHTLINKED SCREEN",*7 H 2 S DJS="" Q
 W !,@DJHIN,"A NEW SCREEN IS BEING FORMATTED",@DJLIN,*7
 D EN1
 S X=DJSS,DIC="^DJR(",DIC(0)="LZ",DLAYGO=2000 D ^DIC K DLAYGO S DJDAA=+Y
 ;R !,"Enter Title for New Screen:",DJX:DTIME S DJT=$T S:'DJT DJX=V(2)
 S DIE=DIC,DR=.08,DA=+Y D ^DIE S:$D(X) DJX=X S:'$D(X) DJX=V(2) I $D(Y) S DR=".01///@" D ^DIE X DJCL W "SCREEN DELETED",*7 H 1 K Y S DJS="" Q
 S $P(^DJR(DJDN,0),U,5)=DJSS
 S ^DJR(DJDAA,0)=DJSS_U_15_U_$P(^DJR(DJDN,0),U,1)_U_$P(^DJR(DJDN,0),U,4)_U_U_V(5)_U_DJX_U_$P(^DJR(DJDN,0),U,8)_U_DJLSTN
 S V(1)=DJSS,V(3)=$P(^DJR(DJDN,0),U,1),V(2)=DJX,V(4)=""
 S ^DJR(DJDAA,1,0)="^2000.07A^"_0_U_0
 S DJX1=$P(^DJR(DJDAA,0),U,8)+2,V(22)=DJX1,V(23)=0,V(25)=$L(V(6))+4,DJXL=V(25)+V(8)
 S (DA(1),W(1),DJDN)=DJDAA,DJDNM=DJSS K DJSS,DJX Q
EN1 ;GENERATION OF NEW SCREEN ALGORITHM
 S DJSS=$E(V(1),1,16)
1 S DJSSN=$L(DJSS),DJSSLL=$E(DJSS,DJSSN-2,DJSSN)
N1 I DJSSLL?3N S DJSSK=DJSSLL+10 S DJSS=$E(DJSS,1,DJSSN-3)_$S($L(DJSSK)=2:0_DJSSK,1:DJSSK) S DJSS=$E(DJSS,1,16) G:'$D(^DJR("B",DJSS))&('$D(^DJR(DJSS,0))) KILL S DJSSLL=DJSSLL+10 G 1
 S DJSS=$E(DJSS,1,13)_"010" G:'$D(^DJR("B",DJSS))&('$D(^DJR(DJSS,0))) KILL G 1
KILL K DJSSK,DJSSN,DJSSL,DJSSLL,DJDAA Q
EN2 ;FIND LENGTH OF ELEMENT
 S DJ22=^DD(+V(5),V(20),0),DJT=$P(^(0),U,2)
 I DJT>0,$P(^DD(+DJT,.01,0),U,2)'["W" S DJI="M",V(6)=V(6)_"(M)",V(8)=30
 I DJT["P" S DJ22=^DD(+$P(DJT,"P",2),.01,0) S DJT=$P(^(0),U,2)
 I DJT["F" S V(8)=+$P(DJ22,"$L(X)>",2)
 I DJT["C" S DJX3=$P(DJ22,U,2),V(8)=+$P(DJX3,"J",2)+$P(DJX3,",",2) S:V(8)<4 V(8)=4
 S:DJT["D" V(8)=17
 I DJT["S" S V(8)=4 S DJS=$P(^DD(+V(5),V(20),0),U,3) F DJK=1:1 S Y=$P(DJS,";",DJK) Q:Y=""  S DJS1=$P(Y,":",2) I $L(DJS1)>V(8) S V(8)=$L(DJS1)
 I DJT>0,$P(^DD(+DJT,.01,0),U,2)["W" S DJI="W",V(6)=V(6)_"(W)",V(8)=5
 S:DJ22["$L(X)>" V(8)=+$P(DJ22,"$L(X)>",2)
 S:'$D(V(8)) V(8)=4
 S:V(8)<2 V(8)=4
 K DJX3,DJ22,DJT,DJS,DJS1 Q
EN3 ;CREATE AND PROMPT FOR ENTRY NUMBER
 S:V(20)>0 DJLNM=(DJLNM+1)\1 S:V(20)<0 DJLNM=DJLNM+.1 I $D(^DJR(+W(1),1,"A",DJLNM)) G EN3
N X DJCP
2 S DY=17,DX=0 X XY W "Entry Number:",DJLNM,"//" R DJX:DTIME G U:DJX="^" S:DJX="" DJX=DJLNM I DJX=+DJX,(DJX<100),(DJX>0) G M
 X DJCP X DJCL W "CHOOSE A NUMBER BETWEEN 1 AND 99",*7 G 2
M I '$D(^DJR(+W(1),1,"A",DJX)) S DJLNM=DJLSTN S:DJLSTN<DJX (DJLNM,DJLSTN)=DJX S V(7)=DJX Q
 X DJCP X DJCL W "SORRY THIS NUMBER HAS ALREADY BEEN USED!!",*7 G 2
U S DJLNM=DJLNM-1
 W !,"ENTRY DELETED!",*7 D EN1^DJDPLS Q
