EVETR6 ;SLC/GPM - Auto download report ; 2/24/04 12:37pm
 ;;1.0;HEALTH EVET;**2**;Nov 05, 2002
 ;
 Q
 ;
AUTO ; Entry Point for automated running
 N H
 S H=+$H
 D RPT($$HTFM^XLFDT(H-7),$$HTFM^XLFDT(H))
 Q
 ;
MAN ; Entry point for manual running
 N DIR,X,Y,DUOUT,STDATE,ENDATE
 F  D  Q:Y'=""!$D(DUOUT)
 . S DIR(0)="D",DIR("A")="ENTER STARTING DATE: ",DIR("B")="TODAY"
 . D ^DIR
 Q:$D(DUOUT)
 S STDATE=Y
 F  D  Q:Y'=""!$D(DUOUT)
 . S DIR(0)="D",DIR("A")="ENTER ENDING DATE: ",DIR("B")="TODAY"
 . D ^DIR
 Q:$D(DUOUT)
 S ENDATE=Y
 D RPT(STDATE,ENDATE)
 Q
 ;
RPT(STDATE,ENDATE) ;
 N STRPT,ENRPT,TXTR,TXMT,TCHR,TCNT,TREQ,XTRACT,XMIT,CHAR,CNT,REQ,IEN,DDATE,NODE,DFN,USERNM,DTM,DAY,TIME,SITE,LINE
 N TEXT,XMDUN,XMDUZ,XMTEXT,XMROU,XMSTRIP,XMSUB,XMY,XMZ,XMDF
 ;
 S STRPT=$$FMTE^XLFDT(STDATE,5)
 S ENRPT=$$FMTE^XLFDT(ENDATE\1,5)
 S (TXTR,TXMT,TCHR,TCNT,TREQ)=0
 S SITE=+$$SITE^VASITE
 S SITE=$$GET1^DIQ(4,SITE_",",.01)
 ;
 S XMDF="",(XMDUN,XMDUZ)="Health eVet Package"
 S XMY("G.EVET HEALTH EVET ALERTS")=""
 S XMSUB=SITE_" Health eVet Download Report "_ENRPT
 S XMTEXT="TEXT("
 S TEXT(1)=$J(SITE,$L(SITE)\2+40)
 S TEXT(2)="My HEALTHeVET DOWNLOAD ACTIVITY FOR "_STRPT_" - "_ENRPT
 S TEXT(2)=$J(TEXT(2),$L(TEXT(2))\2+40)
 S TEXT(3)=" "
 S TEXT(4)=$J("DATE",28)_$J("TIME",10)_$J("EXTRACT",11)_$J("XMIT",7)_$J("CHAR",9)
 S TEXT(5)="USERNAME"_$J("STARTED",22)_$J("STARTED",10)_$J("TIME",8)_$J("TIME",8)_$J("SENT",9)_$J("# AREAS",10)
 ;
 S DDATE=STDATE
 F  S DDATE=$O(^EVET(2276.1,"AC",DDATE)) Q:'DDATE!(DDATE>ENDATE)  D
 . S IEN=0
 . F  S IEN=$O(^EVET(2276.1,"AC",DDATE,IEN)) Q:IEN=""  D
 .. S NODE=$G(^EVET(2276.1,IEN,0))
 .. Q:NODE=""
 .. S TREQ=TREQ+1
 .. S DFN=$P(NODE,"^",2)
 .. S USERNM=$O(^EVET(2275,"B",DFN,0))
 .. I USERNM'="" S USERNM=$P($G(^EVET(2275,USERNM,0)),"^",4)
 .. I USERNM="" S USERNM="UNKNOWN: "_DFN
 .. S CNT=$P($G(^EVET(2276.1,IEN,1,0)),"^",3)
 .. I CNT=1,"monster,all"[$P($G(^EVET(2276.1,IEN,1,1,0)),"^") S CNT=18
 .. S DTM=$$FMTE^XLFDT($P(NODE,"^",3),2)
 .. S DAY=$P(DTM,"@"),TIME=$P(DTM,"@",2)
 .. S XTRACT=$P(NODE,"^",8),TXTR=TXTR+XTRACT
 .. S XMIT=$P(NODE,"^",5),TXMT=TXMT+XMIT
 .. S CHAR=$P(NODE,"^",4),TCHR=TCHR+CHAR
 .. S TCNT=TCNT+CNT
 .. S TEXT(TREQ+5)=$E(USERNM,1,20)_$J(DAY,29-$L(USERNM))_$J(TIME,11)_$J(XTRACT,8)_$J(XMIT,8)_$J(CHAR,10)_$J(CNT,8)
 .. Q
 . Q
 S LINE="-------"
 S TEXT(TREQ+6)=$J(LINE,49)_$J(LINE,8)_$J(LINE,9)_$J(LINE,9)
 S REQ="REQUESTS: "_TREQ
 S TEXT(TREQ+7)=REQ_$J(TXTR,48-$L(REQ))_$J(TXMT,8)_$J(TCHR,10)_$J(TCNT,8)
 D ^XMD
 Q
 ;
