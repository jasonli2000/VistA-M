GRTRAN ;TRANSFER DATA FROM FILEMAN - ALB ISC ; 14 AUG 84  10:20 am
 ;D. MADDOCK - VERSION 3.02
 D GCLEAR^GRPK
T R !!!,"ENTER DATASET DATA IS TO BE TRANSFERRED INTO:  ",X:60
 G:X="" QUIT S DIC=GRUSR,GRNEW=0 D ^DIC G:X="?" T
 I Y'=-1 S GRDAT=GRUSR_$P(Y,U)_",""SS""," G SS
 K:$L(X)>30!($L(X)<3)!'(X?1A.ANP) X
 I '$D(X) W *7,!!,"NAME MUST BE BETWEEN 3 AND 30 CHARACTERS LONG. " G T
 R !!,"IS THIS A NEW DATA SET?  ",YN:60
 G:$E(YN,1)'="Y" T D EN01^GRPLOAD
 S @(DIC_V_"B"_V_","_V_X_V_","_GRNXT_")")="",GRNEW=1
 S GRDAT=GRUSR_GRNXT_",""SS"",",GRFD=GRDAT_"0)",@GRFD="^11001.02^0^0"
SS ;CHOOSE THE SUBSET
 R !!!!,"ENTER SUBSET DATA IS TO BE TRANSFERRED INTO:  ",X:60 G:X="" T
 S DIC=GRDAT G:GRNEW=1 SS1 D ^DIC G:X="?" SS
 I Y'=-1 S GRSUB=GRDAT_$P(Y,U)_",""PT"",",(GRNEW,GREND)=0 G FER
SS1 K:$L(X)>30!($L(X)<3)!'(X?1A.ANP) X
 I '$D(X) W *7,!!,"NAME MUST BE BETWEEN 3 AND 30 CHARACTERS LONG. " G SS
 I GRNEW=1 S YN="Y" G SS2
 R !!,"IS THIS A NEW SUBSET?  ",YN:60
SS2 G:$E(YN,1)'="Y" SS D EN01^GRPLOAD S GRNEW=1,GREND=0
 S @(DIC_V_"B"_V_","_V_X_V_","_GRNXT_")")=""
 S GRSUB=GRDAT_GRNXT_",""PT"",",GRFD=GRSUB_"0)",@GRFD="^11001.03A^0^0"
FER ;PRINT/SORT RESULTS OR STORED VALUES?
 S DIC(0)="QXMZ" R !!!,"IS THE DATA LOCATED IN A FILEMAN FILE",!,"OR IS IT PRINT/SORT RESULTS (F OR P)?  ",X:60
 I X="" S GRNEW=0 G SS
 I "FP"'[$E(X,1) W *7,!!,"NOT A VALID OPTION.  TRY AGAIN.  ",! G FER
 I $E(X,1)="P" D ^GRXFER G:'$D(^DOSV(0,$I,"BY",1)) FER G:X="^" FER G QUIT1
 S GRNFLD1="W !!,""FIELD IS NOT NUMERIC"",!!,*7,*7"
 S GRNFLD2="W !!,""INVALID FIELD TYPE"",!!,*7,*7"
 S GRNFLD="W !!,""NOT A VALID FIELD NAME"",!!,*7,*7"
 S S="W !!,""NOT A VALID FILE NAME"",!!,*7,*7"
FER1 R !!,"ENTER FILE MANAGER FILE FROM WHICH DATA IS TO BE TRANSFERRED:  ",X:60
 G:X="" FER S DIC="^DIC(",DIC(0)="QEMZ" D ^DIC
 G:X="?" FER1 I Y=-1 X S G FER1
 ;GRIFN IS INTERNAL FILE NUMBER ;GREC IS GLOBAL REFERENCE
 S GRIFN=+Y,GREC=^DIC(GRIFN,0,"GL"),GRMEAN=1
 D ^GRTRAN1 G:GRFLD="" FER1 S DIC(0)="QXMZ"
 K GRNFLD1,GRNFLD2,GRNFLD,S,N,GRYSUB,GRYREF,GRSREF,GRIFN,GRDDN,GREC
 K M,GRXLOC,C,GRPREF,GRPSUB,GRDDP,GRTEMP1,F G QUIT
QUIT1 K GRBY,GRBYTE,F,GRFF,GRFTE,GRSOR,GRYNAM,GRNAM,GRTWO,Z,T,GRANS,GRINC,J,YN
QUIT K GRDONE,GREND,I,GRSCR,GRTYP,GRMEAN,GRFLD,GRYLOC,GRNEW,GRTEMP,GRNXT,GRFD,GRSUB,YN
 D ^%ZIS W @IOF Q
