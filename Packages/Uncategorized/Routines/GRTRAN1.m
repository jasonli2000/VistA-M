GRTRAN1 ;TRANSFER DATA FROM FILEMAN TO GRAPHPAK - ALB ISC ; 08 AUG 84  1:03 pm
 ;D. MADDOCK - VERSION 3.02
Y S GRTYP="Y VALUES",GRSREF=GREC K GRSCR D MAIN Q:GRFLD=""
 S GRYSUB=GRSCR,GRYREF=GRSREF
MEAN ;X VALUES OR MERELY LABELS?
 R !!!!,"ARE THE X VALUES MEANINGFUL OR ARE THEY JUST LABELS (M OR L)?  ",X:60
 G:X="" Y I "ML"'[$E(X,1) W *7,!!,"NOT A VALID OPTION.  TRY AGAIN.  ",! G MEAN
 S GRTYP="X VALUES",GRMEAN=1,GREND=0 I $E(X,1)="L" S GRMEAN=0,GRTYP="LABELS" D:GRNEW=0 EN01
 S GRSREF=GREC K GRSCR,GRPREF D MAIN G:GRFLD="" MEAN S GREND=GREND+1
 S DIC=GRSUB,(M,N)=0
LOOP S M=$N(@(GRSREF_V_M_V_")")),N=$N(@(GRYREF_V_N_V_")"))
 Q:M=-1!(M="B")!(N=-1)!(N="B")
 S GRXLOC=GRSREF_V_M_V_","_$P(GRSCR,";",1)_")",GRYLOC=GRYREF_V_N_V_","_$P(GRYSUB,";",1)_")"
 G:'$D(@GRXLOC)!'$D(@GRYLOC) LOOP
 I $P(GRSCR,";",2)["E" S GRTEMP="S X=$E(@GRXLOC,"_$P(GRSCR,"E",2)_")" X GRTEMP
 E  S X=$P(@GRXLOC,U,$P(GRSCR,";",2))
 S:$D(GRPREF)&(X'="") GRTEMP=GRPREF_X_","_$P(GRPSUB,";",1)_")",X=$P(@GRTEMP,U,$P(GRPSUB,";",2))
 I $P(GRYSUB,";",2)["E" S GRYSUB="S Y=$E(@GRYLOC,"_$P(GRYSUB,"E",S)_")" X GRYSUB
 E  S Y=$P(@GRYLOC,U,$P(GRYSUB,";",2))
 G:'$D(X)!'$D(Y)!(X="")!(Y="") LOOP
 I GRMEAN=1 S X=X_"^"_Y_"^"
 E  S X=GREND_"^"_Y_"^"_X
 D EN01^GRPLOAD S GREND=GREND+1 G LOOP
MAIN W !!,"ENTER FIELD NAME OF DATA TO BE USED AS ",GRTYP,":  " R GRFLD:60 Q:GRFLD=""
 S DIC="^DD("_GRIFN_",",X=GRFLD D ^DIC G:GRFLD="?" MAIN I Y=-1 X GRNFLD G MAIN
 S GRFLD=$P(^DD(GRIFN,+Y,0),U,1)
 I $P(^DD(GRIFN,+Y,0),U,2)["C"!($P(^DD(GRIFN,+Y,0),U,2)["W") X GRNFLD2 G MAIN
 G:$P(^DD(GRIFN,+Y,0),U,2)["N"!($P(^DD(GRIFN,+Y,0),U,2)["D") FOUND
 I $P(^DD(GRIFN,+Y,0),U,2)?1N.N.E S C=1,N=+Y,GRDDN=GRIFN,(GRTEMP,GRTEMP1)=GRSREF D MULT Q:$D(GRSCR)  S GRSREF=GRTEMP1 G MAIN
 I GRMEAN=1 X GRNFLD1 G MAIN
 I $P(^DD(GRIFN,+Y,0),U,2)["P" S GRPREF="^"_$P(^DD(GRIFN,+Y,0),U,3),GRDDP=+$P($P(^DD(GRIFN,+Y,0),U,2),"P",2),GRPSUB=$P(^DD(GRDDP,.01,0),U,4)
FOUND S GRSCR=$P(^DD(GRIFN,+Y,0),U,4) Q
MULT ;SPECIFIED FIELD IS MULTIPLE
 S F=$P(^DD(GRDDN,.01,0),U,1)
LOOP3 W !,GRFLD," IS A MULTIPLE FIELD.  CHOOSE A SPECIFIC ",F,": " R X:60
 Q:X=""  S DIC=GRSREF D ^DIC G:X="?" LOOP3
 I Y=-1 W !!,X," IS INVALID.  TRY AGAIN.  ",!!,*7,*7 G LOOP3
 S GRFLD=$P(^DD(GRIFN,+Y,0),U,1)
 S GRTEMP=GRSREF_+Y_","_V_$P($P(^DD(GRDDN,N,0),U,4),";",1)_V_","
 I '$D(@(GRTEMP_"0)")) W !!,GRFLD," IS AN UNDEFINED FIELD FOR ",X,".  CHOOSE ANOTHER ",F,!!,*7,*7 G LOOP3
 S GRSREF=GRTEMP,GRDDN=+$P(^DD(GRDDN,N,0),U,2)
UP D SMAIN Q:$D(GRSCR)!(X="")
 I C=4 W !!,"SORRY.  YOU CAN NOT HAVE MORE THAN THREE SUB FIELDS.  ",!!,*7,*7 Q
 S GRFLD=X G MULT
SMAIN W !!,"ENTER SUB FIELD NAME OF DATA TO BE USED AS ",GRTYP,":  " R X:60 Q:X=""
 S DIC="^DD("_GRDDN_"," D ^DIC G:X="?" SMAIN
 I Y=-1 X GRNFLD G SMAIN
 S GRFLD=$P(^DD(GRIFN,+Y,0),U,1)
 I $P(^DD(GRDDN,+Y,0),U,2)["C"!($P(^DD(GRDDN,+Y,0),U,2)["W") X GRNFLD2 G SMAIN
 G:$P(^DD(GRDDN,+Y,0),U,2)["N"!($P(^DD(GRDDN,+Y,0),U,2)["D") FND
 I $P(^DD(GRDDN,+Y,0),U,2)?1N.N.E S N=+Y,C=C+1 Q
 I GRMEAN=1 X GRNFLD1 G SMAIN
 I $P(^DD(GRDDN,+Y,0),U,2)["P" S GRPREF="^"_$P(^DD(GRDDN,+Y,0),U,3),GRDDP=+$P($P(^DD(GRDDN,+Y,0),U,2),"P",2),GRPSUB=$P(^DD(GRDDP,.01,0),U,4)
FND S GRSCR=$P(^DD(GRDDN,+Y,0),U,4) Q
EN01 ;IS DATA CONSISTENT?
 S N=1,GREND=$P(@(GRSUB_"0)"),U,4)
LOOP1 S M=0,K=99999
LOOP2 S M=$N(@(GRSUB_M_")")) G:M=-1!(M="PP") SET
 I $P(@(GRSUB_M_",0)"),U,1)<K S K=$P(@(GRSUB_M_",0)"),U,1),L=M
 G LOOP2
SET Q:'$D(L)  S GRTEMP(N)=$P(@(GRSUB_L_",0)"),U,2,3),N=N+1 K @(GRSUB_L_",0)") G:N'>GREND LOOP1
 S @(GRSUB_"0)")=$P(@(GRSUB_"0)"),U,1,2)_"^0^0",DIC=GRSUB
 F N=1:1:GREND S X=N_"^"_GRTEMP(N) D EN01^GRPLOAD
 K M,GRTEMP,K,L,N,GRNEW Q
