PRMQWB ;Chicago ISC/DDA- OPTION AND TEMPLATE CALLS ;5/29/90  14:50
 ;;1.2;Utilization Review;;
EN2 ;
 S PRMQDAT=+X,PRMQXDT=DT,(X,Y)=""
 Q
EN3 ;
 S PRMQAVG=$S(Z]"DRG":X,1:""),(X,Y,PRMQDIF,PRMQEXP)="" I PRMQDAT,PRMQAVG S X1=PRMQDAT,X2=PRMQAVG+.5\1 D C^%DTC S PRMQEXP=X S:'$D(PRMQXDT) PRMQXDT="" S X1=PRMQXDT,X2=PRMQEXP D ^%DTC S PRMQDIF=$S(X>0:"+"_X,1:X),(X,Y)=""
DD I PRMQEXP S Y=PRMQEXP X ^DD("DD") S X=Y
 K PRMQAVG,PRMQDIF,PRMQEXP,PRMQXDT,X1,X2,Y Q
EN4 ;
 S PRMQEXP=Y,(X,Y,PRMQDIF)="" I PRMQDAT,PRMQEXP S X1=PRMQDAT,X2=PRMQEXP+.5\1 D C^%DTC S PRMQEXP=X S:'$D(PRMQXDT) PRMQXDT="" S X1=PRMQXDT,X2=PRMQEXP D ^%DTC S PRMQDIF=$S(X>0:"+"_X,1:X),(X,Y)="" G DD
EN5 ;
 S PRMQXDT=+Y,(X,Y)=""
 Q
EN6 ;
 S (PRMQWRD,PRMQQUIT,X)="",PRMQDFN=$P(@(DCC_D0_",0)"),U)
 F PRMQQ(0)=0:0 S PRMQQ(0)=$O(^DGPM("APTT1",PRMQDFN,PRMQQ(0))) Q:PRMQQ(0)'>0!PRMQQUIT  F PRMQQ=0:0 S PRMQQ=$O(^DGPM("APTT1",PRMQDFN,PRMQQ(0),PRMQQ)) Q:PRMQQ'>0!PRMQQUIT  D 6
 I 'PRMQWRD,$D(^DPT(PRMQDFN,.1))#2,$P(^(.1),U)]"" S (X,Y)=$P(^(.1),U)_"*"
 I $D(^DPT(PRMQDFN,.1))#2,$P(^(.1),U)]"",'(X[$P(^(.1),U)) S (X,Y)=$P(^(.1),U)_"*"
 K PRMQDFN,PRMQWRD,PRMQQUIT,D,E Q
6 ;
 I $D(^DGPM(PRMQQ,0))#2,$P(^(0),U)\1=Y S PRMQWRD=$P(^(0),U,6),PRMQQUIT=1 S:PRMQWRD]"" (X,Y)=$S($D(^DIC(42,PRMQWRD,0))#2:$P(^(0),U),1:"")
 Q
EN10 ;
 Q:'$D(DUZ(0))  D:'$D(DT) DT^DICRW
 K L,PRMQEN S DIC="^PRMQ(513.8,",DIC(0)="AEQMZ" D ^DIC K:Y<0 DIC,BY,FLDS,FR,TO,DHD,PRMQDAT,%,%W,%Y,DIYS Q:Y<0  S PRMQEN=+Y
 Q:'$D(PRMQEN)
 S DIC=DIC_+Y_",1,",DIC("A")="Select ADMISSION OR TRANSFER-IN DATE: " D ^DIC K DIC("A") Q:Y<0  S PRMQDAT=$P(Y(0),U)
 S DIC="^PRMQ(513.8,",FLDS="[PRMQ PRINT 6]",BY="[PRMQ SORT 62]",(FR,TO)=PRMQEN_","_PRMQDAT,DHD="@" D EN1^DIP
 K PRMQDL,DIJ,DP,PRMQDAT,X,Y G EN10
 Q
EN11 ;
 Q:'$D(DUZ(0))  D:'$D(DT) DT^DICRW
 K L,PRMQEN S DIC="^PRMQ(513.8,",DIC(0)="AEQMZ" D ^DIC K:Y<0 DIC,BY,FLDS,FR,TO,DHD,PRMQDAT,%W,%Y,DIJ,DIYS,DP Q:Y<0  S PRMQEN=+Y
 Q:'$D(PRMQEN)
 S DIC=DIC_+Y_",1,",DIC("A")="Select ADMISSION OR TRANSFER-IN DATE: " D ^DIC K DIC("A") Q:Y<0  S PRMQDAT=$P(Y(0),U)
 S DIC="^PRMQ(513.8,",FLDS="[PRMQ PRINT 71]",BY="[PRMQ SORT 62]",(FR,TO)=PRMQEN_","_PRMQDAT,DHD="@" D EN1^DIP
 K PRMQDAT,X G EN11
 Q
ENSEC ; SECONDARY ENTRY INTO KILL SECONDARY REVIEW LEVEL FIELDS
 S PRMQDA(0)=DA,PRMQDA(1)=DA(1),PRMQDA("X")=X,PRMQDA("Y")=Y
 K ^PRMQ(513.8,DA(1),1,DA,14) Q:'$D(^PRMQ(513.8,DA(1),1,DA,11))#2  S PRMQDA("NODE")=^(11)
 F I=30.5:.1:30.9 S X=$P(PRMQDA("NODE"),"^",$E(I,4)-1) I X]"" F J=0:0 S J=$O(^DD(513.801,I,1,J)) Q:+J'=J  X:$D(^DD(513.801,I,1,J,2))#2 ^(2)
 S $P(^PRMQ(513.8,DA(1),1,DA,11),"^",4,8)="^^^8^4" F I=30.8:.1:30.9 S X=$S(I=30.8:8,1:4) F J=0:0 S J=$O(^DD(513.801,1,I,1,J)) Q:+J'=J  X:$D(^DD(513.801,I,1,J,1))#2 ^(1)
 S DA=PRMQDA(0),DA(1)=PRMQDA(1),X=PRMQDA("X"),Y=PRMQDA("Y") K PRMQDA,I,J
XITSEC ; EXIT ENSEC
 K J,PRMQDA
 Q
