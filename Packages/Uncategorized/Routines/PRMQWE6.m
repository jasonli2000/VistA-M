PRMQWE6 ;Chicago ISC/DDA- PRMQ EDIT 6 ;5/25/90  11:53
 ;;1.2;Utilization Review;;
EN1 ; ENTRY POINT FOR OPTION PRMQ EDIT 6, PRE-ADMISSION CERTIFICATION
 W !! S DIC="^PRMQ(513.8,",DIC(0)="AELMNQZ",DLAYGO=513.8 D ^DIC K DIC,DLAYGO G:Y=-1 EXIT
 S D0=+Y D ENLOOK^PRMQW G:PRMQDATE="^"!(PRMQDATE="") XIT
 I $P(PRMQDATE,"^",5)="" S VAIP("E")=$P(PRMQDATE,"^",6) D IN5^VADPT S $P(PRMQDATE,"^",5)=VAIP(13) K VAIP,VAERR
 I $P(PRMQDATE,"^",4)="*" S (PRMQOUT,PRMQD1)=0 F PRMQI=0:0 S PRMQD1=$O(^PRMQ(513.8,PRMQD0,1,"B",$P(PRMQDATE,"^",2),PRMQD1)) Q:PRMQD1'>0  D EN1I Q:PRMQOUT=1
 I $P(PRMQDATE,"^",4)'="*" S DIE="^PRMQ(513.8,",DA=PRMQD0,DR="2///^S X=$P(PRMQDATE,""^"")",DR(2,513.801)=".01;S PRMQD1=DA" D ^DIE K DA,DIE,DR,DIDEL
 I PRMQD1'>0 W !!,"ERROR IN UR RECORD: IEN=",D0,"  DATE: ",$P(PRMQDATE,"^"),! G XIT
 S DIE="^PRMQ(513.8,"_PRMQD0_",1,",(DA,D1)=PRMQD1,DA(1)=PRMQD0,DIDEL=513.801
 S DR=".51//^S X=PRMQ(""INS"");S:X=2 Y="""";.52;S:X=2 Y=""@1"";.53;S PRMQ("".53"")=X;S:X=1 Y=""@2"";.54///@;.55///@;H 3;@2;.57;.54;.55;@1;.551;S:X=2 Y=""@3"";.552;@3;.56//^S X=""YES"";I '$D(PRMQ("".53""))#2 S Y=""@4"""
 S DR(1,513.801,1)="I PRMQ("".53"")=1 S Y=""@4"";.58;@4;.605;S:X=2 Y="""";.61;S:X=2 Y="""";.62;S:X=2 Y="""";.63"
 D INS
 D ^DIE K DA,DIE,DR,DIDEL
XIT D EXIT G EN1
 Q
INS ; ENTRY TO DETERMINE VATERAN'S INSURANCE STATUS
 S PRMQ("INS")=0
 G:'$D(^DPT(PRMQDFN,.312,0))#2 INSX
 S PRMQ("VD0")=0
 F PRMQJ=0:0 S PRMQ("VD0")=$O(^DPT(PRMQDFN,.312,PRMQ("VD0"))) Q:PRMQ("VD0")'>0  I $P(^(PRMQ("VD0"),0),"^",6)="v" S PRMQ("INS")=1 Q
INSX S PRMQ("INS")=$S(PRMQ("INS")=1:"YES",1:"NO")
 Q
EN1I S PRMQ(.2)=$P(^PRMQ(513.8,D0,1,PRMQD1,7),"^",4),PRMQ("D.2")=$E($P(PRMQDATE,"^",3),1),PRMQOUT=$S(PRMQ(.2)=1&(PRMQ("D.2")="T"):1,PRMQ(.2)=2&(PRMQ("D.2")="A"):1,PRMQ(.2)=""&(PRMQ("D.2")="A"):1,1:0)
 Q
EXIT ; KILL VARIABLES
 K %,%DT,%T,%W,%X,%Y,C,D0,D1,DFN,DI,DA,DIE,DR,DIDEL,DQ,I,J,PRMQDATE,PRMQDFN,PRMQ,PRMQD0,PRMQD1,PRMQX,PRMQXF,PRMQOUT,PRMQI,PRMQPOST,PRMQPRE,PRMQSUR,PRMQDIS,PRMQADTT,PRMQINAP,PRMQPLOS,PRMQTOT,X,Y
 Q
