PRMQWE71 ;Chicago ISC/DDA- PRMQ EDIT 7.1 AND PRMQ EDIT 7.11 ;5/9/90  10:31
 ;;1.2;Utilization Review;;
EN1 ; ENTRY POINT FOR OPTION PRMQ EDIT 7.1
 W !! S DIC="^PRMQ(513.8,",DIC(0)="AELMNQZ",DLAYGO=513.8 D ^DIC K DIC,DLAYGO G:Y=-1 EXIT
 S D0=+Y D ENLOOK^PRMQW G:PRMQDATE="^"!(PRMQDATE="") XIT
 I $P(PRMQDATE,"^",5)="" S VAIP("E")=$P(PRMQDATE,"^",6) D IN5^VADPT S $P(PRMQDATE,"^",5)=VAIP(13) K VAIP,VAERR
 I $P(PRMQDATE,"^",4)="*" S (PRMQOUT,PRMQD1)=0 F PRMQI=0:0 S PRMQD1=$O(^PRMQ(513.8,PRMQD0,1,"B",$P(PRMQDATE,"^",2),PRMQD1)) Q:PRMQD1'>0  D EN1I Q:PRMQOUT=1
 I $P(PRMQDATE,"^",4)'="*" S DIE="^PRMQ(513.8,",DA=PRMQD0,DR="2///^S X=$P(PRMQDATE,""^"")",DR(2,513.801)=".01;S PRMQD1=DA" D ^DIE K DA,DIE,DR,DIDEL
 I PRMQD1'>0 W !!,"ERROR IN UR RECORD: IEN=",D0,"  DATE: ",$P(PRMQDATE,"^"),! G XIT
 S DIE="^PRMQ(513.8,"_PRMQD0_",1,",(DA,D1)=PRMQD1,DA(1)=PRMQD0,DIDEL=513.801
 S DR="30;S:X=""""!(X=""@"") PRMQ(""DEL"")=1,Y="""";30.1;30.2;S PRMQ(""ADMF"")=X;@2;30.8;S PRMQ(""RFA"")=X;30.9;I PRMQ(""RFA"")=8&(X'=4) D ENDR^PRMQWE71 S Y=""@2"";S:PRMQ(""ADMF"")=1 Y=""@1"" S:PRMQ(""ADMF"")=""@"" Y="""""
 S DR(1,513.801,1)="30.5;S:X="""" Y=""@1"";30.6;30.7;30.75;@1;5;S PRMQDRG=X S:X="""" Y="""";D ^PRMQDRG S:PRMQSKP=1 Y=""@3"";5.8///^S X=""NOW"";@3;9.5;30.95"
 S DR(2,513.807)="1///^S X=PRMQDRG"
 D ^DIE K DA,DIE,DR,DIDEL
 I $D(PRMQ("DEL")) S DIE="^PRMQ(513.8,"_PRMQD0_",1,",DA=PRMQD1,DA(1)=PRMQD0,DIDEL=513.801,DR="30.1///@;30.2///@;30.95///@" D ^DIE K DA,DIE,DR,DIDEL
XIT D EXIT G EN1
 Q
EN1I S PRMQ(.2)=$P(^PRMQ(513.8,D0,1,PRMQD1,7),"^",4),PRMQ("D.2")=$E($P(PRMQDATE,"^",3),1),PRMQOUT=$S(PRMQ(.2)=1&(PRMQ("D.2")="T"):1,PRMQ(.2)=2&(PRMQ("D.2")="A"):1,PRMQ(.2)=""&(PRMQ("D.2")="A"):1,1:0)
 Q
ENDR W !,"When REASON FOR ADMISSION is ""Review Criteria Met"", REVIEWER'S RECOMMENDED",!,"ACTION must be ""No Action Required"".",*7
 Q
EN2 ; ENTRY POINT FOR OPTION PRMQ EDIT 7.11
 W !! S DIC="^PRMQ(513.8,",DIC(0)="AELMNQZ",DLAYGO=513.8 D ^DIC K DIC,DLAYGO G:Y=-1 EXIT
 S D0=+Y D ENLOOK^PRMQW G:PRMQDATE="^"!(PRMQDATE="") XIT2
 I $P(PRMQDATE,"^",5)="" S VAIP("E")=$P(PRMQDATE,"^",6) D IN5^VADPT S $P(PRMQDATE,"^",5)=VAIP(13) K VAIP,VAERR
 I $P(PRMQDATE,"^",4)="*" S (PRMQOUT,PRMQD1)=0 F PRMQI=0:0 S PRMQD1=$O(^PRMQ(513.8,PRMQD0,1,"B",$P(PRMQDATE,"^",2),PRMQD1)) Q:PRMQD1'>0  D EN1I Q:PRMQOUT=1
 I $P(PRMQDATE,"^",4)'="*" S DIE="^PRMQ(513.8,",DA=PRMQD0,DR="2///^S X=$P(PRMQDATE,""^"")",DR(2,513.801)=".01;S PRMQD1=DA" D ^DIE K DA,DIE,DR,DIDEL
 I PRMQD1'>0 W !!,"ERROR IN UR RECORD: IEN=",D0,"  DATE: ",$P(PRMQDATE,"^"),! G XIT2
 S DIE="^PRMQ(513.8,"_PRMQD0_",1,",(DA,D1)=PRMQD1,DA(1)=PRMQD0,DIDEL=513.801
 S DR="30.5;S:X="""" Y=""@1"";30.6;30.7;30.75;@1;30.8;S PRMQ(""RFA"")=X;30.9;I PRMQ(""RFA"")=8&(X'=4) D ENDR^PRMQWE71 S Y=""@1"";30.95"
 S PRMQ("ARF")=$S($D(^PRMQ(513.8,PRMQD0,1,PRMQD1,11))#2:$P(^(11),"^",3),1:"") I PRMQ("ARF")="" W !,"Patient is missing 'Admission Review' data.  Please enter it first." G XIT2
 D ^DIE K DA,DIE,DR,DIDEL
XIT2 D EXIT G EN2
 Q
EXIT ; KILL VARIABLES AND EXIT
 K %,%DT,%T,%W,%X,%Y,C,D0,D1,DFN,DI,DISYS,DQ,I,PRMQDFN,PRMQ,PRMQD0,PRMQD1,PRMQX,PRMQXF,PRMQOUT,PRMQDRG,PRMQI,PRMQSKP,PRMQDATE,X,Y
 Q
