PRMRCLOS ;GLRISC/JES,GWB - NUMBER OF CLOSED INCIDENTS BY DISPOSITION AUTHORITY 88/2/29  1:41 PM ; 1/12/89  10:11 ;
 ;VERSION 1.01
 D ^PRMRDATE G:PRMRSTOP QUIT
 S %ZIS="QM" D ^%ZIS K %ZIS K:POP IO("Q") G:POP QUIP
 I $D(IO("Q")) K IO("Q") S ZTIO=ION,ZTRTN="EN^PRMRCLOS",ZTDESC="Number of Closed Incidents" D ZLOOP,^%ZTLOAD K ZTSK,ZTIO G QUIP
 G EN
ZLOOP ;
 F I="PRMR1HED","PRMR2HED","PRMRENGD","PRMRNBEG","PRMRNEND","PRMRSPOT","PRMRSTOP" S ZTSAVE(I)=""
 Q
EN ;
 U IO S (AUTH,GTCAINC,N)=0 F I=0:0 S N=$O(^PRMQ(513.72,N)) Q:N'?1.N  I $D(^PRMQ(513.72,N,"I")) D RANGE I OK S AUTH=$P(^PRMQ(513.72,N,2),"^",22) I AUTH'="" D GETDISP S CARRAY(AUTH)=Y D GETTOT
 S (PRMOUT,PRMRDEV,PRMRHEAD)=0 S:IOST["C-" PRMRDEV=1 D HEAD
 D HEAD Q:PRMOUT  W ! S N=0 F I=0:0 S N=$O(CARRAY(N)) Q:N=""  W !?10,CARRAY(N),?64,$J(TOTACT(N),4,0) D HEAD Q:PRMOUT
 D HEAD Q:PRMOUT  W !!?10,"TOTAL NUMBER OF INCIDENTS CLOSED: ",?64,$J(GTCAINC,4,0) D HEAD Q:PRMOUT
 I PRMRDEV W !!,"Press <Return> to continue: " R X:DTIME
QUIP ;
 K:$D(ZTSK) ^%ZTSK(ZTSK) W @IOF X ^%ZIS("C")
QUIT ;
 K %,%DT,AUTH,CARRAY,GTCAINC,HOLD,I,N,OK,POP,PRMOUT,PRMR1HED,PRMR2HED,PRMRDEV,PRMRENGD,PRMRHEAD,PRMRNBEG,PRMRNEND,PRMRSPOT,PRMRSTOP,RDATE,TOTACT,Y
 Q
RANGE ;
 S OK=0 Q:'$D(^PRMQ(513.72,N,2))#2  Q:$P(^PRMQ(513.72,N,2),"^",21)=""  S RDATE=$P(^PRMQ(513.72,N,2),"^",21) S:((RDATE=PRMRNBEG)!(RDATE>PRMRNBEG))&((RDATE=PRMRNEND)!(RDATE<PRMRNEND)) OK=1
 Q
GETDISP ;
 S HOLD=";"_$P(^DD(513.72,51,0),"^",3),%=$F(HOLD,";"_AUTH_":") S:%'<1 Y=$P($E(HOLD,%,999),";",1) Q
GETTOT ;
 S GTCAINC=GTCAINC+1 I $D(TOTACT(AUTH)) S TOTACT(AUTH)=TOTACT(AUTH)+1
 E  S TOTACT(AUTH)=1
 Q
HEAD ; PRINT APPROPRIATE PAGE BREAKS
 I PRMRHEAD,PRMRDEV,$Y>(IOSL-5) W !!,"""^"" TO STOP: " R X:DTIME S:X["^"!'$T PRMOUT=1 S PRMRHEAD=0
 I PRMRHEAD,'PRMRDEV,$Y>(IOSL-5) S PRMRHEAD=0
 Q:PRMOUT  I 'PRMRHEAD W @IOF X PRMR1HED W !?25,"NUMBER OF INCIDENTS CLOSED",!,@PRMRSPOT,PRMR2HED,!!?10,"DISPOSITION AUTHORITY",?64,"TOTAL",! F I=1:1:80 W "-"
 S PRMRHEAD=1
 Q
