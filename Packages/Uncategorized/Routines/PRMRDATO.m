PRMRDATO ;GLRISC/JES,DDA - LIST OF UNCLOSED INCIDENTS BY DATE ; 1/29/89  09:24 ;
 ;VERSION 1.01
 D ^PRMRDATE G:PRMRSTOP QUIT
 S %ZIS="QM" D ^%ZIS K %ZIS K:POP IO("Q") G:POP QUIP
 I $D(IO("Q")) K IO("Q") S ZTIO=ION,ZTRTN="EN^PRMRDATO",ZTDESC="List of Unclosed Incidents by Date" D ZLOOP,^%ZTLOAD K ZTSK,ZTIO G QUIP
 G EN
ZLOOP ;
 F I="PRMR1HED","PRMR2HED","PRMRENGD","PRMRNBEG","PRMRNEND","PRMRSPOT","PRMRSTOP" S ZTSAVE(I)=""
 Q
EN ;
 S (PRMROUT,N,SKIP,TODATE,GTODINC,TYPE)=0 F I=0:0 S N=$O(^PRMQ(513.72,N)) Q:N'?1.N  I $D(^PRMQ(513.72,N,"I")) D RANGE I OK D GETSUBT S DARRAY(DATE,TYPE,NAME)=^PRMQ(513.941,TYPE,0)_"^"_ENGLDATE
 U IO S (PRMRHEAD,PRMRDEV)=0 S:$E(IOST)="C" PRMRDEV=1 D HEAD ;X PRMR1HED D HEAD Q:PRMROUT  W !?25,"UNCLOSED INCIDENTS BY DATE",!,@PRMRSPOT,PRMR2HED D ^PRMRCONF W ?2,"DATE",?10,"DESCRIPTION",!?2,"TYPE",?15,"PATIENT",! F I=1:1:80 W "-"
 S (DATE,LDATE,NAME,TYPE)="" F I=0:0 S DATE=$O(DARRAY(DATE)) Q:DATE=""  F II=0:0 S TYPE=$O(DARRAY(DATE,TYPE)) Q:TYPE=""  F III=0:0 S NAME=$O(DARRAY(DATE,TYPE,NAME)) Q:NAME=""  D WRITE G:PRMROUT QUIP
 D WRITOT D HEAD Q:PRMROUT  W !!?10,"TOTAL UNCLOSED INCIDENTS: ",GTODINC
 I PRMRDEV W !!,"Press <Return> to continue: " R X:DTIME
QUIP ;
 K:$D(ZTSK) ^%ZTSK(ZTSK) W @IOF X ^%ZIS("C")
QUIT ;
 K DARRAY,DATE,ENGLDATE,GTODINC,I,II,III,LDATE,N,NAME,OK,POP,PRMR1HED,PRMR2HED,PRMRENGD,PRMRNBEG,PRMRNEND,PRMRSPOT,PRMRSTOP,RDATE,SKIP,TODATE,TYPE,Y,%DT
 Q
RANGE ;
 S OK=0,RDATE=$P(^PRMQ(513.72,N,0),"^",1) S:((RDATE=PRMRNBEG)!(RDATE>PRMRNBEG))&((RDATE=PRMRNEND)!(RDATE<PRMRNEND)) OK=1
 Q:'$D(^PRMQ(513.72,N,2))
 S:$P(^PRMQ(513.72,N,2),"^",21) OK=0
 Q
GETSUBT ;
 S TYPE=^PRMQ(513.72,N,"I"),DATE=$E($P(^PRMQ(513.72,N,0),"^",1),1,7),NAME=$P(^DPT($P(^PRMQ(513.72,N,0),"^",2),0),"^",1) S Y=DATE X ^DD("DD") S ENGLDATE=Y
 S GTODINC=GTODINC+1 I $D(TODATE(DATE)) S TODATE(DATE)=TODATE(DATE)+1
 E  S TODATE(DATE)=1
 Q
WRITE ;
 G:LDATE=DATE NODATE D:SKIP&(DATE>LDATE) WRITOT S LDATE=DATE,SKIP=1 D HEAD Q:PRMROUT  W !!?2,$P(DARRAY(DATE,TYPE,NAME),"^",3),!
NODATE ;
 D HEAD Q:PRMROUT  W !?2,$P(DARRAY(DATE,TYPE,NAME),"^",1),?10,$P(DARRAY(DATE,TYPE,NAME),"^",2),!?15,NAME Q
WRITOT ;
 Q:GTODINC<1  D HEAD Q:PRMROUT  W !!?10,"TOTAL INCIDENTS THIS DATE: ",TODATE(LDATE),! Q
HEAD ; INSERT PROPER PAGE BREAKS
 Q:PRMROUT  I PRMRHEAD,PRMRDEV,$Y>(IOSL-5) W !!,"""^"" TO STOP: " R X:DTIME S:X["^"!'$T PRMROUT=1 S PRMRHEAD=0
 Q:PRMROUT  I 'PRMRHEAD&PRMRDEV D HDR S PRMRHEAD=1
 I ('PRMRHEAD&'PRMRDEV)!('PRMRDEV&($Y>(IOSL-5))) D HDR S PRMRHEAD=1
 Q
HDR ; WRITE HEADER
 W @IOF X PRMR1HED W !?25,"UNCLOSED INCIDENTS BY DATE",!,@PRMRSPOT,PRMR2HED D ^PRMRCONF W ?2,"DATE",?10,"DESCRIPTION",!?2,"TYPE",?15,"PATIENT",! F I=1:1:80 W "-"
 Q
