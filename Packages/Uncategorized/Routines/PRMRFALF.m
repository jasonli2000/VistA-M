PRMRFALF ;GLRISC/JES,GWB - NUMBER OF FALLS BY FALL FACTOR 88/2/29  1:41 PM ; 1/13/89  10:44 ;
 ;VERSION 1.01
 D ^PRMRDATE G:PRMRSTOP QUIT
 S %ZIS="QM" D ^%ZIS K %ZIS K:POP IO("Q") G:POP QUIP
 I $D(IO("Q")) K IO("Q") S ZTIO=ION,ZTRTN="EN^PRMRFALF",ZTDESC="Number of Falls by Fall Factor" D PLOOP,^%ZTLOAD K ZTSK,ZTIO G QUIP
 G EN
PLOOP ;
 F I="PRMR1HED","PRMR2HED","PRMRENGD","PRMRNBEG","PRMRNEND","PRMRSPOT","PRMRSTOP" S ZTSAVE(I)=""
 Q
EN ;
 S FFACTORS=$P(^DD(513.72,20,0),"^",3)_"STOP" F FF=1:1 S TEMPRRAY=$P(FFACTORS,";",FF) Q:TEMPRRAY="STOP"  S FFRRAY($P(TEMPRRAY,":",1))=$P(TEMPRRAY,":",2)
 U IO S (GTFALF,N)=0,SKIP=0 F I=0:0 S N=$O(^PRMQ(513.72,N)) Q:N'?1.N  I $D(^PRMQ(513.72,N,"FF")),$P(^PRMQ(513.72,N,"FF"),"^",1)]"" D RANGE I OK S NAME=$P(^DPT($P(^PRMQ(513.72,N,0),"^",2),0),"^",1) D BUILD
 S (PRMOUT,PRMRDEV,PRMRHEAD)=0 S:IOST["C-" PRMRDEV=1 D HEAD
 D HEAD Q:PRMOUT  S (LFALF,FALF)="" F I=0:0 S FALF=$O(FARRAY(FALF)) Q:FALF=""  S NAME="" F II=0:0 S NAME=$O(FARRAY(FALF,NAME)) Q:NAME=""  D WRITE G:PRMOUT QUIP
 D HEAD Q:PRMOUT  D PERCENT W !!!?10,"TOTAL FALL FACTORS: ",GTFALF,!
 I PRMRDEV W !!,"Press <Return> to continue: " R X:DTIME
QUIP ;
 K:$D(ZTSK) ^%ZTSK(ZTSK) W @IOF X ^%ZIS("C")
QUIT ;
 K FALF,FARRAY,FF,FFACTORS,FFRRAY,GTFALF,I,II,LFALF,N,NAME,NN,OK,PERCENT,POP,PRMOUT,PRMR1HED,PRMR2HED,PRMRDEV,PRMRENGD,PRMRHEAD,PRMRNBEG,PRMRNEND,PRMROPEN,PRMRSPOT,PRMRSTOP,RDATE,SKIP,TEMPRRAY,TOFALF,%DT
 Q
RANGE ;
 S OK=0,RDATE=$P(^PRMQ(513.72,N,0),"^",1) I ((RDATE=PRMRNBEG)!(RDATE>PRMRNBEG))&((RDATE=PRMRNEND)!(RDATE<PRMRNEND)) S OK=1
 Q
BUILD ;
 S GTFALF=GTFALF+1,FALF=$P(^PRMQ(513.72,N,"FF"),"^",1) I '$D(FARRAY(FALF,NAME)) S FARRAY(FALF,NAME)=1
 E  S FARRAY(FALF,NAME)=FARRAY(FALF,NAME)+1
 Q
WRITE ;
 S:'$D(TOFALF(FALF)) TOFALF(FALF)=0 S TOFALF(FALF)=FARRAY(FALF,NAME)+TOFALF(FALF)
 G:LFALF=FALF NOFALF D:SKIP&(FALF]LFALF) PERCENT S LFALF=FALF,SKIP=1 D HEAD Q:PRMOUT  W !!!?2,FFRRAY(FALF)
NOFALF ;
 D HEAD Q:PRMOUT  W !?40,NAME Q
PERCENT ;
 Q:GTFALF<1  S PERCENT=$J(TOFALF(LFALF)/GTFALF,2,2) I PERCENT="1.00" S PERCENT=100 G WRLINE
 I $E(PERCENT,3)=0 S PERCENT=" "_$E(PERCENT,4)
 E  S PERCENT=$E(PERCENT,3,4)
WRLINE ;
 D HEAD Q:PRMOUT  W !!?10,"TOTAL THIS FACTOR: ",TOFALF(LFALF),?40,"PERCENT OF TOTAL FALL FACTORS:  ",PERCENT," %" Q
HEAD ; PRINT APPROPRIATE PAGE BREAKS
 Q:PRMOUT  I PRMRHEAD,PRMRDEV,$Y>(IOSL-5) W !!,"""^"" TO STOP: " R X:DTIME S:X["^"!'$T PRMOUT=1 S PRMRHEAD=0
 I PRMRHEAD,'PRMRDEV,$Y>(IOSL-5) S PRMRHEAD=0
 Q:PRMOUT  I 'PRMRHEAD W @IOF X PRMR1HED W !?23,"NUMBER OF FALLS BY FALL FACTOR",!,@PRMRSPOT,PRMR2HED D ^PRMRCONF W ?2,"FALL FACTOR",?40,"PATIENT",! F I=1:1:80 W "-"
 S PRMRHEAD=1
 Q
