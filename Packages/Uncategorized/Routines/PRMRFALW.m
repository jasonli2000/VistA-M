PRMRFALW ;GLRISC/JES,GWB - NUMBER OF FALLS BY WARD 88/2/29  1:41 PM ; 1/29/89  12:51 ;
 ;;1.01;;**10,11**;
 D ^PRMRDATE G:PRMRSTOP QUIT
 S %ZIS="QM" D ^%ZIS K %ZIS K:POP IO("Q") G:POP QUIP
 I $D(IO("Q")) K IO("Q") S ZTIO=ION,ZTRTN="EN^PRMRFALW",ZTDESC="Number of Falls by Ward" D PLOOP,^%ZTLOAD K ZTSK,ZTIO G QUIP
 G EN
PLOOP ;
 F I="PRMR1HED","PRMR2HED","PRMRENGD","PRMRNBEG","PRMRNEND","PRMRSPOT","PRMRSTOP" S ZTSAVE(I)=""
 Q
EN ;
 S (GTFALL,N,SKIP)=0 F I=0:0 S N=$O(^PRMQ(513.72,N)) Q:N'?1.N  S PRMRIND=$S($D(^PRMQ(513.72,N,"I"))#2:^("I"),1:""),PRMRFAL=$P(PRMRIND,U) I PRMRFAL]"",($P(^PRMQ(513.72,N,0),"^",9)'=""),(+$P(^PRMQ(513.941,PRMRFAL,0),U)=106) D RANGE D:OK BUILD
 U IO S (PRMOUT,PRMRDEV,PRMRHEAD)=0 S:$E(IOST)="C" PRMRDEV=1 D HEAD
 D HEAD Q:PRMOUT  W ! S (LWARD,WARD)="" F I=0:0 S WARD=$O(FARRAY(WARD)) Q:WARD=""  D WRITE D HEAD G:PRMOUT QUIP
 D WRITOT D HEAD Q:PRMOUT  W !?13,"GRAND TOTAL:",?60,$J(GTFALL,3),!
 I PRMRDEV R !,"Press return to continue ",X:DTIME
QUIP ;
 K:$D(ZTSK) ^%ZTSK(ZTSK) W @IOF X ^%ZIS("C")
QUIT ;
 K FARRAY,GTFALL,I,II,LWARD,N,NN,OK,POP,PRMOUT,PRMR1HED,PRMR2HED,PRMRDEV,PRMRENGD,PRMRHEAD,PRMRNBEG,PRMRNEND,PRMRSPOT,PRMRSTOP,RDATE,SKIP,WARD,%DT,PRMRFAL,PRMRIND
 Q
RANGE ;
 S OK=0,RDATE=$P(^PRMQ(513.72,N,0),"^",1) S:((RDATE=PRMRNBEG)!(RDATE>PRMRNBEG))&((RDATE=PRMRNEND)!(RDATE<PRMRNEND)) OK=1
 Q
BUILD ;
 S WARD=$P(^DIC(42,$P(^PRMQ(513.72,N,0),"^",9),0),"^",1),GTFALL=GTFALL+1
 I '$D(FARRAY(WARD)) S FARRAY(WARD)=1
 E  S FARRAY(WARD)=FARRAY(WARD)+1
 Q
WRITE ;
 D:SKIP&(WARD]LWARD) WRITOT S LWARD=WARD,SKIP=1 Q
WRITOT ;
 Q:GTFALL<1  D HEAD Q:PRMOUT  W !?13,LWARD,?60,$J(FARRAY(LWARD),3),! Q
HEAD ; PRINT APPROPRIATE PAGE BREAKS
 I PRMRHEAD,PRMRDEV,$Y>(IOSL-5) W !!,"""^"" TO STOP: " R X:DTIME S:X["^"!'$T PRMOUT=1 S PRMRHEAD=0
 I PRMRHEAD,'PRMRDEV,$Y>(IOSL-5) S PRMRHEAD=0
 Q:PRMOUT
 I 'PRMRHEAD W @IOF X PRMR1HED W !?27,"NUMBER OF FALLS BY WARD",!,@PRMRSPOT,PRMR2HED,!!?13,"WARD",?60,"TOTAL",! F I=1:1:80 W "-"
 S PRMRHEAD=1
 Q
