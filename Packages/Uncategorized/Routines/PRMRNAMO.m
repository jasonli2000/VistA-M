PRMRNAMO ;GLRISC/JES,DDA - LIST OF UNCLOSED INCIDENTS BY PATIENT NAME ; 1/29/89  08:57 ;
 ;VERSION 1.01
 D ^PRMRDATE G:PRMRSTOP QUIT
 S %ZIS="QM" D ^%ZIS K %ZIS K:POP IO("Q") G:POP QUIP
 I $D(IO("Q")) K IO("Q") S ZTIO=ION,ZTRTN="EN^PRMRNAMO",ZTDESC="List of Unclosed Incidents by Patient Name" D PLOOP,^%ZTLOAD K ZTSK,ZTIO G QUIP
 G EN
PLOOP ;
 F I="PRMR1HED","PRMR2HED","PRMRENGD","PRMRNBEG","PRMRNEND","PRMRSPOT","PRMRSTOP" S ZTSAVE(I)=""
 Q
EN ;
 S (PRMROUT,N,GTONINC,TYPE)=0 F I=0:0 S N=$O(^PRMQ(513.72,N)) Q:N'?1.N  I $D(^PRMQ(513.72,N,"I")) D RANGE I OK D GETSUBT S NARRAY(NAME,DATE,TYPE)=^PRMQ(513.941,TYPE,0)_"^"_ENGLDATE
 U IO S (PRMRDEV,PRMRHEAD)=0 S:$E(IOST)="C" PRMRDEV=1 D HEAD
 D HEAD G:PRMROUT QUIP W ! S (DATE,NAME,TYPE)="" F I=0:0 S NAME=$O(NARRAY(NAME)) Q:NAME=""  F II=0:0 S DATE=$O(NARRAY(NAME,DATE)) Q:DATE=""  F III=0:0 S TYPE=$O(NARRAY(NAME,DATE,TYPE)) Q:TYPE=""  D WRITE G:PRMROUT QUIP
 D HEAD G:PRMROUT QUIP W !!?10,"TOTAL UNCLOSED INCIDENTS: ",GTONINC
 I PRMRDEV W !!,"Press <Return> to continue: " R X:DTIME
QUIP ;
 K:$D(ZTSK) ^%ZTSK(ZTSK) W @IOF X ^%ZIS("C")
QUIT ;
 K PRMROUT,PRMRDEV,PRMRHEAD,DATE,ENGLDATE,I,II,III,N,NAME,NARRAY,OK,POP,PRMR1HED,PRMR2HED,PRMRENGD,PRMRNBEG,PRMRNEND,PRMRSPOT,PRMRSTOP,RDATE,GTONINC,TYPE,Y,%DT
 Q
RANGE ;
 S OK=0,RDATE=$P(^PRMQ(513.72,N,0),"^",1) S:((RDATE=PRMRNBEG)!(RDATE>PRMRNBEG))&((RDATE=PRMRNEND)!(RDATE<PRMRNEND)) OK=1
 Q:'$D(^PRMQ(513.72,N,2))
 S:$P(^PRMQ(513.72,N,2),"^",21) OK=0
 Q
GETSUBT ;
 S TYPE=^PRMQ(513.72,N,"I"),DATE=$E($P(^PRMQ(513.72,N,0),"^",1),1,7),NAME=$P(^DPT($P(^PRMQ(513.72,N,0),"^",2),0),"^",1) S Y=DATE X ^DD("DD") S ENGLDATE=Y,GTONINC=GTONINC+1 Q
WRITE ;
 D HEAD Q:PRMROUT  W !?2,NAME,?30,$P(NARRAY(NAME,DATE,TYPE),"^",3),!?2,$P(NARRAY(NAME,DATE,TYPE),"^",1),?10,$P(NARRAY(NAME,DATE,TYPE),"^",2),! Q
HEAD ; INSERT PROPER PAGE BREAKS
 Q:PRMROUT  I PRMRHEAD,PRMRDEV,$Y>(IOSL-5) W !!,"""^"" TO STOP: " R X:DTIME S:X["^"!'$T PRMROUT=1 S PRMRHEAD=0
 Q:PRMROUT  I 'PRMRHEAD&PRMRDEV D HDR S PRMRHEAD=1
 I ('PRMRDEV&($Y>(IOSL-5)))!('PRMRDEV&'PRMRHEAD) D HDR S PRMRHEAD=1
 Q
HDR ; WRITE HEADER
 W @IOF X PRMR1HED W !?21,"UNCLOSED INCEDENTS BY PATIENT NAME",!,@PRMRSPOT,PRMR2HED D ^PRMRCONF W ?2,"PATIENT",?30,"DATE",!?2,"CODE",?30,"DESCRIPTION",! F I=1:1:80 W "-"
 Q
