PRMRTYPE ;GLRISC/JES,GWB - NUMBER OF INCIDENTS BY TYPE 88/2/29  1:41 PM ; 1/27/89  14:11 ;
 ;VERSION 1.01
 D ^PRMRDATE G:PRMRSTOP QUIT
 S %ZIS="QM" D ^%ZIS K %ZIS K:POP IO("Q") G:POP QUIP
 I $D(IO("Q")) K IO("Q") S ZTIO=ION,ZTRTN="EN^PRMRTYPE",ZTDESC="Number of Incidents by Type" D PLOOP,^%ZTLOAD K ZTSK,ZTIO G QUIP
 G EN
PLOOP ;
 F I="PRMR1HED","PRMR2HED","PRMRENGD","PRMRNBEG","PRMRNEND","PRMRSPOT","PRMRSTOP" S ZTSAVE(I)=""
 Q
EN ;
 U IO S (GTATINC,GTATYPE,N,TYPE)=0 F I=0:0 S N=$O(^PRMQ(513.72,N)) Q:N'?1.N  I $D(^PRMQ(513.72,N,"I")) D RANGE I OK S TYPE=^PRMQ(513.72,N,"I"),TARRAY(TYPE)=^PRMQ(513.941,TYPE,0) D GETTOT
 S (PRMOUT,PRMRDEV,PRMRHEAD)=0 S:IOST["C-" PRMRDEV=1 D HEAD
 D HEAD Q:PRMOUT  W ! S N=0 F I=0:0 S N=$O(TARRAY(N)) Q:N'?1.N  W !?2,$P(TARRAY(N),"^",1),?10,$P(TARRAY(N),"^",2),?74,$J(TOTINC(N),4,0) D HEAD G:PRMOUT QUIP
 D HEAD Q:PRMOUT  W !!?10,"TOTAL TYPES OF INCIDENTS: ",GTATYPE,?43,"  TOTAL NUMBER OF INCIDENTS: ",?74,$J(GTATINC,4,0),!
 I PRMRDEV W !!,"Press <Return> to continue: " R X:DTIME
QUIP ;
 K:$D(ZTSK) ^%ZTSK(ZTSK) W @IOF X ^%ZIS("C")
QUIT ;
 K GTATINC,GTATYPE,I,N,OK,POP,PRMOUT,PRMR1HED,PRMR2HED,PRMRDEV,PRMRHEAD,PRMRENGD,PRMRNBEG,PRMRNEND,PRMRSPOT,PRMRSTOP,RDATE,TARRAY,TOTINC,TYPE,%DT
 Q
RANGE ;
 S OK=0,RDATE=$P(^PRMQ(513.72,N,0),"^",1) S:((RDATE=PRMRNBEG)!(RDATE>PRMRNBEG))&((RDATE=PRMRNEND)!(RDATE<PRMRNEND)) OK=1
 Q
GETTOT ;
 S GTATINC=GTATINC+1 I $D(TOTINC(TYPE)) S TOTINC(TYPE)=TOTINC(TYPE)+1
 E  S TOTINC(TYPE)=1,GTATYPE=GTATYPE+1
 Q
HEAD ; PRINT APPROPRIATE PAGE BREAKS
 I PRMRHEAD,PRMRDEV,$Y>(IOSL-5) W !!,"""^"" TO STOP: " R X:DTIME S:X["^"!'$T PRMOUT=1 S PRMRHEAD=0
 I PRMRHEAD,'PRMRDEV,$Y>(IOSL-5) S PRMRHEAD=0
 Q:PRMOUT  I 'PRMRHEAD W @IOF X PRMR1HED W !?20,"NUMBER OF INCIDENTS BY INCIDENT TYPE",!,@PRMRSPOT,PRMR2HED,!!?2,"CODE",?10,"DESCRIPTION",?74,"TOTAL",! F I=1:1:80 W "-"
 S PRMRHEAD=1
 Q
