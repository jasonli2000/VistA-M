PSREF2 ; XAK,MJK/ALBANY ; 21 AUG 84  11:22 PM
 ;4.02 ; 12/13/84
CHK I '$D(SPND) S %DT="TX" D ^%DT S (XS,X)=Y I Y<1 K X G Q
 S RX0=^PSRX(DA(1),0),RX2=$S($D(^(2)):^(2),1:""),J=DA(1),LSTFIL=$P(RX2,"^",2),DYS=$P(RX0,"^",8) S:'LSTFIL LSTFIL=$P(RX0,"^",13) S FL(0)=LSTFIL
 S:$D(XS) X=XS S (RCT,LSTFILX,JJ)=0
 F J=0:0 S J=$N(^PSRX(DA(1),1,J)) Q:J'>0  S RCT=RCT+1,JJ=J,FL(RCT)=+^(J,0) I +^(0)>LSTFIL S LSTFILX=LSTFIL,LSTFIL=+^(0)
 S:'($D(DA)#2) DA=JJ+1 S:JJ=DA LSTFIL=LSTFILX K LSTFILX
OLD I JJ>DA K X W !?5,*7,"NOT LAST REFILL OR A NEW REFILL...DATE EDITING NOT ALLOWED!" G Q
REF I DA>JJ,RCT'<$P(RX0,"^",9) K X W !?5,*7,"NO REFILLS LEFT!" G Q
 I $D(SPND) S MW="M",XS=DT F J=0:1:RCT S X1=FL(RCT-J),X2=DYS*(J+1)-10 D C^%DTC S (X,XS)=$S(X<XS:XS,1:X)
EXDT I X>$P(RX2,U,6) K X W !,?5,*7,"REFILL DATE BEYOND EXPIRATION DATE!" G Q
DUP G:$D(SPND) Q I X=LSTFIL K X W !?5,*7,"REFILL ALREADY ENTERED FOR THAT DATE!" G Q
LATE I X<LSTFIL K X W !?5,*7,"LATER FILL DATE ALREADY EXISTS!" G Q
TWTY F J=0:1:RCT S X1=XS,X2=FL(RCT-J) D ^%DTC S Y=(J+1)*DYS-10 I X<Y W !?5,*7,"LESS THAN ",Y," DAYS FOR ",J+2," FILLS" R "    OK//",ANS Q:"Y"[$E(ANS,1)  K X G Q
 S X=XS
Q K ANS,FL,RX2,LSTFIL,X1,X2,XS,%DT,J,JJ Q
