RJPTFD ; DRGs REG-DAK/AVAMC 2/27/83 ; 18 SEP 84  12:54 pm
 ;ADT VERSION 3.4 ;AUG 10 1986
 ;RJ MODIFIED
EN1 S B(70)=$S($D(^DGPT(PTF,70)):^(70),1:""),DAM=$S($P(B(70),U,3)=4:1,1:0),TAC=$P(B(70),U,13),TAC=$S($L(TAC):1,1:0),SEX=$P(^DPT(DFN,0),U,2),TRS=$S($P(B(70),U,3)=5:1,1:0),EXP=$S($P(B(70),U,3)>5:1,1:0)
 S AGE="" S:'$D(DOB) DOB=$P(^(0),"^",3) I DOB S AGE=$S(+B(70):+B(70),1:DT)-DOB\10000
 G Q:'$L(AGE)
CD G Q:AGE'?1N.N K NOR,NSD,NDR,RG,CC,ORG S L=$P(B(70),U,10) S:L="" L=$P(B(70),U,11) G Q:L="" S L1=$S($D(^ICD9(L,0)):^(0),1:"") G Q:L1="" S DXLS=L,MDC=$P(L1,U,5),PD=$P(L1,U,2),RG=0 I MDC=12 S MDC=$S(SEX="F":13,1:12)
 I 'MDC D 469 G Q
 F NDR=1:1 S RG=$N(^ICD9(+L,"DR",RG)) Q:RG<1  S RG(RG)=""
 S (OR,SD)="",SD1=1,NOR=0 F NSD=0:0 S NSD=$N(^DGPT(PTF,"M","AC",NSD)) Q:NSD'>0  I NSD'="",$D(^ICD9(+NSD,0)),NSD'=DXLS S SD=SD_$P(^(0),U,2) S:$P(^(0),U,2)'["g" SD1=0
OP F NO=0:0 S NO=$N(^DGPT(PTF,"S",NO)) Q:NO'>0  S L=^(NO,0) F I=1:1:5 S L1=$P(L,U,I+7) I L1'="",$D(^ICD0(+L1,0)) S DA=+L1,L2=^(0),OR=OR_$P(L2,U,2),NOR=NOR+1 F ORG=0:0 S ORG=$N(^ICD0(DA,"DR",ORG)) Q:ORG<1  S ORG($P(^ICD(ORG,0),"^",5),ORG)=""
 S L=$S($D(^DGPT(PTF,"401P")):^("401P"),1:"") I L'="" F I=1:1:5 S L1=$P(L,U,I) I L1'="",$D(^ICD0(+L1,0)) S DA=+L1,L2=^(0),OR=OR_$P(L2,U,2),NOR=NOR+1 F ORG=0:0 S ORG=$N(^ICD0(DA,"DR",ORG)) Q:ORG<1  S ORG($P(^ICD(ORG,0),"^",5),ORG)=""
 ;I PD["H"!(SD["H") D DRGANY G:RG<1 MORE D WRT Q
MORE I MDC=5,'NOR!(OR'["O") D MI,WRT:RG>0 G Q
 I MDC=18,OR["O" S RG=415 D WRT G Q
 I MDC=19,OR["O" S RG=424 D WRT G Q
 I MDC=23,OR["O" S RG=461 D WRT G Q
 I MDC=14 D ^DGDRG14,WRT G Q
 I MDC=20,DAM S RG=433 D WRT G Q
 I MDC=22 S RG=$S(TAC:456,PD["*"!(SD["*"):457,OR'["O":460,1:0) I RG D WRT G Q
 I MDC=15,TRS!EXP S RG=385 D WRT G Q
 I MDC=15,SD1 S RG=391 D WRT G Q
 I 'NOR,NDR<3 S RG=$N(RG(0)) D:RG<1 469 D WRT G Q
 I 'NOR S RG=$N(RG(0)) X:$D(^ICD(RG,"MC")) ^ICD(RG,"MC") D WRT G Q
 D ^DGDRG6:MDC=6,^DGDRG8:MDC=8,^DGDRG2:MDC=2,^DGDRG3:MDC=3 S RG=$N(ORG(MDC,0)) G:RG<1 NOP X:$D(^ICD(RG,"MC")) ^ICD(RG,"MC") D WRT G Q
NOP I OR["O",MDC'=20 D 468,WRT G Q ;to avoid DRG468 put ';' in front of If statment
D S RG=$N(RG(0)) X:$D(^ICD(RG,"MC")) ^ICD(RG,"MC") D:RG<1 469 D WRT G Q
WRT D:'$D(RG) 469 Q:RG<0  S DRGCAL=^ICD(RG,0),DRG=+RG Q
 W !!,"Diagnosis Related Group: ",$J($P(DRGCAL,"^",1),6),?40,"Avg len of stay: ",$J($P(DRGCAL,"^",8),6)
 W !?14," Affil wt: ",$J($P(DRGCAL,"^",2),6),?43,"Non-Affil wt: ",$J($P(DRGCAL,"^",7),6)
 W !?12," Low day(s): ",$J($P(DRGCAL,"^",3),6),?39,"Local low day(s): ",$J($P(DRGCAL,"^",9),6)
 W !?13," High days: ",$J($P(DRGCAL,"^",4),6),?40,"Local High days: ",$J($P(DRGCAL,"^",10),6)
 K AGE,B(70),CC,DA,DAM,DOB,DXLS,EXP,I,L,L1,L2,MDC,NDR,NO,NOR,NSD,OR,ORG,PD,RG,SD,SEX,TAC,TRS Q
469 S RG=469 Q  ;W *7,!!,"DRG= 469 PDX INVALID AS DISCHARGE DIAGNOSIS" Q
468 S RG=468 Q  ;W *7,!!,"DRG= 468 PDX NOT MATCHED TO OPERATIONS" Q
MI I PD["I"!(SD["I") S RG=$S($S($D(EXP):EXP,1:0):123,PD["V"!(SD["V"):121,1:122) Q
CATH I OR["H" S RG=$S(PD["X"!(SD["X"):124,1:125) Q
 S RG=$N(RG(0)) X:$D(^ICD(RG,"MC")) ^ICD(RG,"MC") D:RG<1 469 Q
 Q
DRGANY ;ANY DX PRIMARY OR SEC FOR DRG;REG/AVAMC 1/8/84 ; 31 MAR 84  2:28 pm
 I PD["E"!(SD["E") S RG=386 Q
 I PD["p"!(SD["p") S RG=$S(PD["J"!(SD["J"):387,1:388) Q
 I PD["R"!(SD["R") S RG=$S(PD["J"!(SD["J"):389,NSD=1!(NSD=2&(SD["O")):391,1:390) Q
 Q
Q K ORG,DAM,NDR,NOR,NSD,SD1,%PTF,AGE,B,DOB,DXLS,EXP,ICDCAL,L1,L2,MDC,NO,OR,PD,SD,DS1,SEX,TAC,TRS Q
