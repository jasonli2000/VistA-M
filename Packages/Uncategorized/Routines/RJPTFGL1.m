RJPTFGL1 ;RJ WILM DE -OUTPUT RECORD STATISTICS; 12-12-85
 ;;4.0
 K ^UTILITY("PTFGL",$J) S:'$D(DTIME) DTIME=300 S %DT="APE",%DT("A")="Start with Discharge Date: " D ^%DT G:Y=-1 Q S Z1=Y,%DT("A")="End with Discharge Date: " D ^%DT G:Y=-1 Q S Z2=Y
 W !!,"This report can be Queued by typing a ""Q"" at the DEVICE: prompt."
 K IOP S %IS="QM",%IS("B")="" D ^%ZIS G:POP Q I $D(IO("Q")) S PGM="QQ^RJPTFGL1" D ^RJPTFQ S ^%ZTSK(ZTSK,"Z1")=Z1,^%ZTSK(ZTSK,"Z2")=Z2,^%ZTSK(ZTSK,"ZTSK")=ZTSK K PGM,ZTSK G Q
 W !,"This will take a while."
S S (RJCOD,RJREL,RJEAL,RJACC,RJTOT,DX,DY)=0,I=0 F RJCT=1:1 S I=$O(^DGPT(I)) Q:I'?.N  D LU
 D H,O,T
Q X ^%ZIS("C") K %IS,IO("Q"),IOP,RJCOD,RJREL,RJEAL,RJACC,RJTOT,RJCT,I,X1,X2,Z1,Z2,RJACCD,RJCODD,RJEALD,RJNAM,RJPAT,RJRELD,RJSTAT,%DT,RJWAR,^UTILITY("PTFGL",$J) Q
LU Q:'$D(^DGPT(I,70))  Q:$P(^DGPT(I,70),"^",1)=""  S X1=Z1,(X2,D)=$P(^DGPT(I,70),"^",1) D ^%DTC Q:X>0  S X2=D,X1=Z2 D ^%DTC Q:X<0
 S RJWAR=$P(^DGPT(I,70),"^",2),RJNAM=^DGPT(I,0),RJPAT=^DPT($P(RJNAM,"^",1),0),RJTOT=RJTOT+1
 S RJRELD="",RJCODD="" I $D(^DGP(45.84,I,0)) S RJSTAT=^DGP(45.84,I,0),RJRELD=$P(RJSTAT,"^",4),RJCODD=$P(RJSTAT,"^",2) S:RJRELD RJREL=RJREL+1 S:RJCODD RJCOD=RJCOD+1
 S:'RJCODD&($D(^DGP(45.84,I,0))) RJCODD="CLOSED" S RJEALD="",RJACCD="" I $D(^DGPT(I,460)) S RJSTAT=^DGPT(I,460),RJEALD=$P(RJSTAT,"^",4),RJACCD=$P(RJSTAT,"^",3) S:RJEALD RJEAL=RJEAL+1 S:RJACCD RJACC=RJACC+1
 S ^UTILITY("PTFGL",$J,D,I)=$P(RJPAT,"^",1)_"^"_$P(RJPAT,"^",9)_"^"_RJWAR_"^"_RJCODD_"^"_RJRELD_"^"_RJEALD_"^"_RJACCD Q
O S N=0 F I=1:1 S N=$N(^UTILITY("PTFGL",$J,N)) Q:N=-1  D P
 Q
P S J=0 F I=1:1 S J=$N(^UTILITY("PTFGL",$J,N,J)) Q:J=-1  D P1
 Q
P1 U IO W !,$E(N,4,5),"-",$E(N,6,7),"-",$E(N,2,3),?20 S RJPAT=^UTILITY("PTFGL",$J,N,J) W $P(RJPAT,"^",1),?50,$P(RJPAT,"^",2) W:$P(RJPAT,"^",3)'="" ?62,$E($P(^DIC(42.4,$P(RJPAT,"^",3),0),"^",1),1,20)
 S RJCODD=$P(RJPAT,"^",4) S:RJCODD'="CLOSED" RJCODD=$E(RJCODD,4,5)_"-"_$E(RJCODD,6,7)_"-"_$E(RJCODD,2,3) W ?84,RJCODD S RJRELD=$P(RJPAT,"^",5) W ?96,$E(RJRELD,4,5),"-",$E(RJRELD,6,7),"-",$E(RJRELD,2,3) S RJEALD=$P(RJPAT,"^",6)
 W ?108,$E(RJEALD,4,5),"-",$E(RJEALD,6,7),"-",$E(RJEALD,2,3) S RJACCD=$P(RJPAT,"^",7) W ?120,$E(RJACCD,4,5),"-",$E(RJACCD,6,7),"-",$E(RJACCD,2,3) Q
T W ! F I=1:1:131 W "="
 W !,"TOTALS-->",?20,RJTOT,?84,RJCOD,?96,RJREL,?108,RJEAL,?120,RJACC W:IO'=IO(0) @IOF Q
H U IO W @IOF X ^%ZOSF("XY") W !,?50,"PTF G/L STATISTICS",!,?40,"DISCHARGES FROM ",$E(Z1,4,5),"-",$E(Z1,6,7),"-",$E(Z1,2,3),"  TO ",$E(Z2,4,5),"-",$E(Z2,6,7),"-",$E(Z2,2,3)
 W !!,"DISCHARGE DATE",?20,"PATIENT NAME",?50,"SSN",?62,"DISCHARGE BEDSECTION",?84,"CLOSED",?96,"RELEASED",?108,"EAL",?120,"ACCEPTED",! F J=1:1:131 W "-"
 Q
QQ S Z1=^%ZTSK(ZTSK,"Z1"),Z2=^%ZTSK(ZTSK,"Z2") G S
