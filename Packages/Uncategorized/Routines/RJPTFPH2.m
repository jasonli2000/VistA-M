RJPTFPH2 ;RJ WILM DE -DRG BY DISCHARGE PHYSICIAN; 3-12-87
 ;;4.0
 K ^UTILITY($J,"RJPTF") S %DT="APE",%DT("A")="From Discharge Date: " D ^%DT G:Y=-1 X S Z1=Y,%DT("A")="To Discharge Date: " D ^%DT G:Y=-1 X S Z2=Y
 K %DT R !,"Long or Short Report: Both// ",RJFORM S RJFORM=$S(RJFORM["L"!(RJFORM["l"):1,RJFORM["S"!(RJFORM["s"):0,1:2) W:RJFORM "   Long" W:RJFORM=2 " and Short" W:'RJFORM "   Short"
 W !!,"This report can be queued by typing a ""Q"" at the DEVICE: prompt." S %IS="QM",%IS("B")="" K IOP D ^%ZIS G:POP X
 I $D(IO("Q")) S PGM="QQ^RJPTFPH2" D ^RJPTFQ S ^%ZTSK(ZTSK,"Z1")=Z1,^%ZTSK(ZTSK,"Z2")=Z2,^%ZTSK(ZTSK,"RJFORM")=RJFORM G X
 W !,"This may take a while..."
S S (RJIN,RJNC,RJCNC,RJCNR,RJR,RJTOTAL)=0 K ^UTILITY($J,"RJPTF") S U="^",PTF=0 F P=1:1 S PTF=$O(^DGPT(PTF)) Q:PTF'?.N  D 1
 D ^RJPTFPO2,^RJPTFGR1,^RJPTFGR2,^RJPTFGR3,^RJPTFGR4
X K ^UTILITY($J,"RJPTF"),PTF,D,I,RJCAS,RJFORM,RJFUN,RJNM,RJTCAS,RJTFUN,RJTTCAS,RJTTFUN,RJTTWWU,RJTWWU,RJWWU,X,Y,%DT,%H,%I,P,PTF,RJCAS,RJDAY,RJDRG,RJFORM,RJFUN,RJLOS,RJNM,RJPHY,RJTCAS,RJTFUN,RJTDAY,RJTLOS,RJTTCAS,RJTTDAY,RJTTFUN,RJTTLOS,RJWWU,Z1,Z1
 K RJPERIOD,Z2 X ^%ZIS("C") Q
1 D ^RJPTFPL2 Q:RJFLAG
 S X2=$P(^DGPT(PTF,0),"^",2),X1=D D ^%DTC S RJDAY=$S(X>0:X,1:0)
 I '$D(^DGPT(PTF,460)) D NOPHY Q
 I $P(^DGPT(PTF,460),"^",5)="" D NOPHY Q
 I '$D(^DIC(16,$P(^DGPT(PTF,460),"^",5),0)) D NOPHY Q
 S RJPHY=$P(^DIC(16,$P(^DGPT(PTF,460),"^",5),0),"^",1)
DRG I '$D(^DGPT(PTF,"RJ",0)) S ^UTILITY($J,"RJPTF",RJPHY,"NO-DRG")=$S('$D(^UTILITY($J,"RJPTF",RJPHY,"NO-DRG")):1,1:^("NO-DRG")+1) S RJDRG="NO-DRG" G LOS
 S RJDRG=^DGPT(PTF,"RJ",0),^UTILITY($J,"RJPTF",RJPHY,RJDRG)=$S('$D(^UTILITY($J,"RJPTF",RJPHY,RJDRG)):1,1:^(RJDRG)+1)
LOS S ^UTILITY($J,"RJPTF",RJPHY,RJDRG,"ZLOS")=$S('$D(^UTILITY($J,"RJPTF",RJPHY,RJDRG,"ZLOS")):RJDAY,1:^("ZLOS")+RJDAY)
 Q
NOPHY S ^UTILITY($J,"RJPTF","ZZZZNOPHY")=$S('$D(^UTILITY($J,"RJPTF","ZZZZNOPHY")):1,1:^("ZZZZNOPHY")+1)
 S RJPHY="ZZZZNOPHY" G DRG
QQ S Z1=^%ZTSK(ZTSK,"Z1"),Z2=^%ZTSK(ZTSK,"Z2"),RJFORM=^%ZTSK(ZTSK,"RJFORM") G S
