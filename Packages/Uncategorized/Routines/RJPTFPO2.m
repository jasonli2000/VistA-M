RJPTFPO2 ;RJ WILM DE; DRG BY DISCHARGE PHYSICIAN; 3-12-87
 ;;4.0
 U IO D H S:'$D(RJFORM) RJFORM=0 S (RJTTCAS,RJTTFUN,RJTTDAY,RJTTLOS,X)=0  F PTF=0:0 S X=$O(^UTILITY($J,"RJPTF",X)) Q:X=""  D 1
 W ! F I=1:1:90 W "="
 W !,"TOTALS",?45,$J(RJTTCAS,5,0),?52,$J(RJTTFUN,15,2),?68,$J(RJTTLOS,10,0),?80,$J(RJTTDAY,10,0)
 S:RJTTDAY=0 RJTTDAY=1 S:RJTTLOS=0 RJTTLOS=1 W !,"AVERAGE FUNDING PER AVG LOS: ",$J(RJTTFUN/RJTTLOS,0,2),?40,"AVERAGE FUNDING PER PAT DAY: ",$J(RJTTFUN/RJTTDAY,0,2)
 W ! F I=1:1:90 W "="
 I $D(^UTILITY($J,"RJPTF","ZZZZNOPHY")) W !,"NO PHYSICIANS ASSOCIATED WITH A PATIENT: ",^("ZZZZNOPHY"),! S ^UTILITY($J,"RJPTF","ZZZZNOPHY","TOTAL")=^UTILITY($J,"RJPTF","ZZZZNOPHY")
 I RJFORM=2 S RJFORM=0 G RJPTFPO2
 D ^RJPTFPD2,H3 S (H1,H2,H3,H4,Y)=0 F PTF=0:0 S Y=$O(^UTILITY($J,"RJPTF",Y)) Q:Y=""  D 3
 Q
1 W !,"PHSICIAN: ",X D H1 S (RJTCAS,RJTFUN,RJTDAY,RJTLOS)=0
 S Y=0 F PTF=0:0 S Y=$O(^UTILITY($J,"RJPTF",X,Y)) Q:Y<1  D 2
 I Y="NO-DRG" D NODRG
 I RJFORM W ! F I=1:1:90 W "."
 W !,"TOTALS BY PHYSICIAN",?45,$J(RJTCAS,5,0),?52,$J(RJTFUN,15,2),?68,$J(RJTLOS,10,0),?80,$J(RJTDAY,10,0)
 S:RJTDAY=0 RJTDAY=1 S:RJTLOS=0 RJTLOS=1 W !,"AVERAGE FUNDING PER AVG LOS: ",$J(RJTFUN/RJTLOS,0,2),?40,"AVERAGE FUNDING PER PAT DAY: ",$J(RJTFUN/RJTDAY,0,2)
 S ^UTILITY($J,"RJPTF",X,"TOTAL")=RJTCAS_"^"_$J(RJTFUN,0,2)_"^"_RJTLOS_"^"_RJTDAY
 W ! F I=1:1:90 W "-"
 S RJTTCAS=RJTTCAS+RJTCAS,RJTTFUN=RJTTFUN+RJTFUN,RJTTLOS=RJTTLOS+RJTLOS,RJTTDAY=RJTTDAY+RJTDAY
 Q
2 S D=^UTILITY($J,"RJPTF",X,Y),RJNM=$S('$D(^ICD(Y,2)):"NO NAME",1:^(2)),RJWWU=$S($D(^ICD(Y,0)):$P(^(0),"^",11),1:0),RJFUN=RJWWU*29.91*D,RJLOS=$S($D(^ICD(Y,0)):$P(^(0),"^",8),1:0)*D,RJDAY=$S($D(^UTILITY($J,"RJPTF",X,Y,"ZLOS")):^("ZLOS"),1:0)
 I RJFORM W !?10,Y,?15,$E(RJNM,1,19),?35,$J(RJWWU,9,2),?45,$J(D,5,0),?52,$J(RJFUN,15,2),?68,$J(RJLOS,10,0),?80,$J(RJDAY,10,0)
 S RJTCAS=RJTCAS+D,RJTFUN=RJTFUN+RJFUN,RJTLOS=RJTLOS+RJLOS,RJTDAY=RJTDAY+RJDAY
 Q
3 S D=^UTILITY($J,"RJPTF",Y,"TOTAL"),RJCAS=$P(D,"^",1),RJFUN=$P(D,"^",2),RJLOS=$P(D,"^",3),RJDAY=$P(D,"^",4)
 S RJCAS=$J(RJCAS/RJTTCAS*100,0,2),RJFUN=$J(RJFUN/RJTTFUN*100,0,2),RJLOS=$J(RJLOS/RJTTLOS*100,0,2),RJDAY=$J(RJDAY/RJTTDAY*100,0,2)
 S:Y="ZZZZNOPHY" Y="NO PHYSICIAN" W !,$E(Y,1,14),?15,$J(RJCAS,10,2),?25,$J(RJFUN,10,2),?35,$J(RJLOS,10,2),?45,$J(RJDAY,10,2),!
 S:Y="NO PHYSICIAN" Y="ZZZZNOPHY" S ^UTILITY($J,"RJPTF",Y,"TOTAL")=RJCAS_"^"_RJFUN_"^"_RJLOS_"^"_RJDAY S:RJCAS>H1 H1=RJCAS S:RJFUN>H2 H2=RJFUN S:RJLOS>H3 H3=RJLOS S:RJDAY>H4 H4=RJDAY
 Q
NODRG S D=^UTILITY($J,"RJPTF",X,Y),RJDAY=$S($D(^UTILITY($J,"RJPTF",X,Y,"ZLOS")):^("ZLOS"),1:0),RJTCAS=RJTCAS+D,RJTDAY=RJTDAY+RJDAY
 W:RJFORM !,?10,"NODRG",?35,$J(0,9,2),?45,$J(D,5,0),?52,$J(0,15,2),?68,$J(0,10,0),?80,$J(RJDAY,10,0)
 Q
H W @IOF,!?26,"DRG BY DISCHARGE PHYSICIAN" S X="T" D ^%DT W "  ",$E(Y,4,5),"-",$E(Y,6,7),"-",$E(Y,2,3),!?26,"PERIOD: "
 S RJPERIOD=$E(Z1,4,5)_"-"_$E(Z1,6,7)_"-"_$E(Z1,2,3)_" To "_$E(Z2,4,5)_"-"_$E(Z2,6,7)_"-"_$E(Z2,2,3) W RJPERIOD
 W ! F I=1:1:90 W "="
 Q
H1 W:RJFORM !?10,"DRG",?19,"NAME",?40,"WWU" W ?47,"CASES",?60,"FUNDING",?71,"AVG LOS",?82,"PAT DAYS",!?10 F I=1:1:80 W "."
 Q
H3 W @IOF,!!!,"PHYSICIAN",?17,"% CASES",?27,"% FUNDING",?37,"% AVG LOS",?47,"% PAT DAYS"
 W ! F I=1:1:79 W "-"
 Q
