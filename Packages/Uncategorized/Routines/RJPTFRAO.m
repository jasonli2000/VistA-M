RJPTFRAO ;RJ WILM DE -RAM REPORT OUTPUT; 12-12-85
 ;;4.0
 S X="T",(RJGC,RJGW,RJGS)=0 K %DT D ^%DT D ^%ZIS U IO D H S X=0,U="^" F I=1:1 S X=$N(^UTILITY($J,"RJPTF",X)),(RJTC,RJTW,RJTS)=0 Q:X=-1!(X="D")  W:$D(^DIC(42.4,X,0)) !,$E($P(^DIC(42.4,X,0),U,1),1,28) W:'$D(^DIC(42.4,X,0)) !,"**",X D 1
 W ! F I=1:1:131 W "#"
 W !,"TOTALS-->",?62,RJGC,?69,$J(RJGW,0,2),?79,$J(RJGS,0,2),?89,$J(RJGW+RJGS,0,2)
 S X=0,X=$N(^UTILITY($J,"RJPTF","D",X)) W:X'=-1 @IOF W:X'=-1 !,"PAIENTS NOT RUN THROUGH DRG CALCULATOR OR NOT CHECKED",!,"PATIENT NAME",?30,"DISCHARGE DATE",?50,"NOT CHECKED"
 S X=0 F I=1:1 S X=$N(^UTILITY($J,"RJPTF","D",X)) Q:X=-1  W !,$P(^DPT($P(^DGPT(X,0),U,1),0),U,1),"(",X,")",?30 D D W:'$D(^DGP(45.84,X,0)) ?55,"*"
 K ^UTILITY($J,"RJPTF"),Z1,Z2,RJGC,RJGS,RJGW,RJSI,RJTC,RJTS,RJTW U IO(0) X ^%ZIS("C") Q
D Q:'$D(^DGPT(X,70))  S D=$P(^DGPT(X,70),U,1),D=$E(D,4,5)_"-"_$E(D,6,7)_"-"_$E(D,2,3) W D Q
1 S Y=0,RJSI=$S((X>51&(X<61))!(X=50)!(X=62)!(X=63):.027,1:.042) F J=1:1 S Y=$N(^UTILITY($J,"RJPTF",X,Y)) Q:Y=-1  D 2
 W !,?20 F K=1:1:111 W "="
 W !,?20,"TOTALS:",?62,RJTC,?69,$J(RJTW,0,2),?79,$J(RJTS,0,2),?89,$J(RJTS+RJTW,0,2),! S RJGC=RJGC+RJTC,RJGS=RJGS+RJTS,RJGW=RJGW+RJTW Q
2 S W=$P(^ICD(Y,0),U,7)*1.00 S:Y=468!(Y=469)!(Y=470) W=85.25 S S=RJSI*W,C=^UTILITY($J,"RJPTF",X,Y),RJTC=RJTC+C,RJTS=RJTS+(S*C),RJTW=RJTW+(C*W)
 W ?25,$P($P(^ICD(Y,0),U,1),"G",2),?30,$J(W,0,2),?39,$J(S,0,2),?49,$J(W+S,0,2),?62,C,?69,$J(C*W,0,2),?79,$J(S*C,0,2),?89,$J(C*(W+S),0,2),!
 Q
H W @IOF W !,?39,"RAM REPORT AS OF ",$E(Y,4,5),"-",$E(Y,6,7),"-",$E(Y,2,3),!,?30,"DISCHARGES FOR PERIOD: ",$E(Z1,4,5),"-",$E(Z1,6,7),"-",$E(Z1,2,3)," TO ",$E(Z2,4,5),"-",$E(Z2,6,7),"-",$E(Z2,2,3)
 W !!,"WARD LOCATION",?25,"DRG",?32,"WWU",?39,"SAL ADJ",?49,"TOT WWU",?61,"CASES",?69,"TOT WWU",?79,"TOT SAL",?89,"TOT WWU"
 W ! F I=1:1:131 W "-"
 Q
