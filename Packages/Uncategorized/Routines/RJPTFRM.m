RJPTFRM ;RJ WILM DE -87 RAM REPORT; 1-26-87
 ;;4.0
 S %DT="APE",%DT("A")="From Discharge Date: " D ^%DT G:Y=-1 X S Z1=Y,%DT("A")="To Discharge Date: " D ^%DT G:Y=-1 X S Z2=Y
 W !!,"This report can be queued by typing a ""Q"" at the DEVICE: prompt." S %IS="QM",%IS("B")="" K IOP D ^%ZIS G:POP X
 I $D(IO("Q")) S PGM="QQ^RJPTFRM" D ^RJPTFQ S ^%ZTSK(ZTSK,"Z1")=Z1,^%ZTSK(ZTSK,"Z2")=Z2 G X
 W !,"This may take a while..."
S S RJNEURO=10,RJMED="12^15",RJINT="40^80",RJSURG="50^53^54^55^59^60^63"
 K ^UTILITY($J,"RJPTF") S U="^",PTF=0 F P=1:1 S PTF=$O(^DGPT(PTF)) Q:PTF'?.N  D:$D(^DGPT(PTF,70)) 1
 D ^RJPTFRM1
X K %DT,Z1,Z2,X1,X2 Q
1 Q:$P(^DGPT(PTF,70),U,1)=""  S X1=Z1,(X2,D)=$P(^DGPT(PTF,70),U,1) D ^%DTC Q:X>0  S X1=Z2,X2=D D ^%DTC Q:X<0
 I '$D(^DGPT(PTF,"RJ",0)) S:$D(^DGP(45.84,PTF,0)) ^UTILITY($J,"RJPTF","D",PTF)="" Q
 I ^DGPT(PTF,"RJ",0)="" S:$D(^DGP(45.84,PTF,0)) ^UTILITY($J,"RJPTF","D",PTF)="" Q
 I '$D(^DGP(45.84,PTF,0)) S ^UTILITY($J,"RJPTF","D",PTF)="" Q
 I $P(^DGP(45.84,PTF,0),U,2)="" S ^UTILITY($J,"RJPTF","D",PTF)="" Q
 S RJDATE=$P($P(^DGPT(PTF,0),"^",2),".",1),RJCO1=0 F K=1:1 S RJCO1=$O(^DGPT(PTF,"M",RJCO1)) Q:RJCO1'?.N  I $D(^DGPT(PTF,"M",RJCO1,0)) D 2
 Q
2 S RJLBS=$P(^(0),"^",2),Z=$O(^DGPT(PTF,"M",RJCO1)) I Z<1 G SET
 S Z=$P(^(Z,0),"^",2) I RJNEURO[RJLBS,RJNEURO'[Z G SET
 I RJMED[RJLBS,RJMED'[Z G SET
 I RJINT[RJLBS,RJINT'[Z G SET
 I RJSURG[RJLBS,RJSURG'[Z G SET
 Q
SET1 S RJDAT2=$P($P(^DGPT(PTF,"M",RJCO1,0),"^",10),".",1)-RJDATE I RJDAT2<2 S DRG="LOS<2"
 S RJDATE=$P($P(^DGPT(PTF,"M",RJCO1,0),"^",10),".",1) S:'$D(^UTILITY($J,"RJPTF",RJLBS,DRG)) ^UTILITY($J,"RJPTF",RJLBS,DRG)=0 S ^UTILITY($J,"RJPTF",RJLBS,DRG)=^UTILITY($J,"RJPTF",RJLBS,DRG)+1 Q
SET I $D(^DGPT(PTF,"M",RJCO1,460001)) S DRG=^(460001) G SET1
 S B(70)=^DGPT(PTF,70),$P(B(70),"^",2)=RJLBS,$P(B(70),"^",10)=$P(^DGPT(PTF,"M",RJCO1,0),"^",5),DFN=$P(^DGPT(PTF,0),"^",1)
 S DAM=$S($P(B(70),U,3)=4:1,1:0),TAC=$P(B(70),U,13),TAC=$S($L(TAC):1,1:0),SEX=$P(^DPT(DFN,0),U,2),TRS=$S($P(B(70),U,3)=5:1,1:0),EXP=$S($P(B(70),U,3)>5:1,1:0)
 S AGE="",DOB=$P(^(0),"^",3) I DOB S AGE=$S(+B(70):+B(70),1:DT)-DOB\10000
 D CD^RJPTFD S ^DGPT(PTF,"M",RJCO1,460001)=DRG G:DRG'="" SET1
 Q
QQ S Z1=^%ZTSK(ZTSK,"Z1"),Z2=^%ZTSK(ZTSK,"Z2") G S
