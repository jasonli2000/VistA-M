RMPC8P ;DDC/MAB;ROES PATCH SET-UP; [ 10/13/93  3:45 PM ]
 ;;1.1;RMPF;**8**;Sept 21, 1993
 I '$D(DUZ) W !!,"PLEASE SET YOUR DUZ BEFORE RUNNING THIS UPDATE!" G END
 I '$D(^RMPF(791811)) W !!,"CANNOT INSTALL PATCH - YOU DO NOT HAVE THE REMOTE INVENTORY PRODUCT FILE ON YOUR SYSTEM!!" G END
 W !!,"ROES PATCH RMPF*1.1*8 - PRODUCT FILE UPDATES",!!
 S PG0=$O(^RMPF(791811.1,"B","CUSTOM HEARING AIDS",0))
 S PG1=$O(^RMPF(791811.1,"B","STOCK HEARING AIDS",0))
 W !!,"Inactivating old models, adding new models and components,",!,"and deleting obsolete components and batteries...."
 F IZ=1:1:4 S ST=$T(COMP+IZ) D  K DIC,DLAYGO
 .S X=$P(ST,";",3),CD=$P(ST,";",4),DLAYGO=791811.2
 .Q:$D(^RMPF(791811.2,"B",X))
 .S DIC="^RMPF(791811.2,",DIC(0)="L",DIC("DR")=".03////^S X=CD"
 .K DD,DO D FILE^DICN
 .I Y<0 W !!,"Component ",X," not added."
 F IY=1:1 S ST=$T(MODEL+IY) Q:ST=""  D  K DIC,DLAYGO
 .S X=$P(ST,";",3),MK=$P(ST,";",4),CS=$P(ST,";",5)
 .Q:$D(^RMPF(791811,"B",X))
 .S PG=PG0 S:IY<3 PG=PG1
 .S DIC="^RMPF(791811,",DIC(0)="L",DLAYGO=791811
 .S DIC("DR")=".02////^S X=MK;.03////^S X=PG;.04////^S X=CS" K DD,DO D FILE^DICN
 .S PG=PG0 I Y<0 W !!,"MODEL ",$P(ST,";",3)," not added."
 F IZ="UI-1","UI-3","UI-K" S DIE=791811 D  K DA
 .S DA=$O(^RMPF(791811,"B",IZ,0)) Q:'DA
 .S ^RMPF(791811,DA,"I")=1
 F IX="V-1-HS","V-1","CE 9","CE L9-LP","CE H9","SA C9","SA S9" D
 .S DA=$O(^RMPF(791811,"B",IX,0))
 .I 'DA W !!,IX," NOT IN REMOTE INVENTORY PRODUCT FILE!!" Q
 .K ^RMPF(791811,DA,102),^RMPF(791811,DA,101)
 W !!,"...done.",!!,"Starting updates...."
 G ^RMPC8D
END Q
COMP ;;NEW COMPONENTS
 ;;CLASS D COMP;CLD-C
 ;;CLASS D HIGH POWER;CLD-H
 ;;CLASS D LINEAR;CLD-L
 ;;HIGH POWER;POWER
MODEL ;;NEW MODELS
 ;;P400;OTICON;188
 ;;P420;OTICON;200
 ;;V-4;UNITRON;165.31
 ;;DCE-DA-AGCI;DANAVOX;170.50
 ;;LITE-DA-AGCI;DANAVOX;189.10
 ;;CONCHA-DA-AGCI;DANAVOX;218.24
 ;;DISCRETION-DA-AGCI;DANAVOX;231.26
