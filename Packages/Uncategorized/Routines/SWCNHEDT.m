SWCNHEDT ;DLG/FARGO;ML/BHAM;EDIT CNHC ENTRIES ; 3/4/86  11:44 AM
 ;VERSION 1.0
 K SWR S DIC="^SOWK(657,",DIC(0)="AEQM",DIC("S")="I $P(^(0),U,10)=""A""" D ^DIC G:+Y'>0 DONE S SWD1=+Y,SWD=$P(Y,U,2),SWSK=1 S %X="^SOWK(657,"_SWD1_",",%Y="SWR(" D %XY^%RCR,^SWCNHDIS
SWPT R !!,"IS THIS THE CORRECT ENTRY? Y//",SWRD:DTIME S:SWRD="" SWRD="Y" G DONE:SWRD["^",SWCNHEDT:SWRD?1"N".E I SWRD["?" W !,"ENTER YES OR NO." G SWPT
 ;CLOSE CASE IF ANSWER IS YES
AC R !,"DO YOU WANT TO CLOSE THIS CASE? N//",SWRD:DTIME S:SWRD="" SWRD="N" G DONE:SWRD["^",KRD:SWRD?1"N".E I SWRD["?" W !,"If you want to close out this case answer yes else answer no." G AC
 W !,"EFFECTIVE DATE OF CLOSE (MUST BE PRECISE)? " D ND G:SWND<0 AC
 S $P(^SOWK(657,SWD1,0),U,10)="C",$P(^(0),U,13)=SWND I $D(^SOWK(657,SWD1,4)) S N=$P(^SOWK(657,SWD1,4,0),U,3) S:$P(^SOWK(657,SWD1,4,N,0),U,2)="" $P(^SOWK(657,SWD1,4,N,0),U,2)=SWND
 G DONE
 ;ALLOWS CHANGING CONTRACT END DATE WITHOUT MAKING NEW RECORD
KRD R !,"Change ENDING DATE? N//",SWRD:DTIME S:SWRD="" SWRD="N" G DONE:SWRD["^",NRC:SWRD?1"N".E I SWRD["?" W !,"If ENDING DATE of contract needs to be altered answer yes else answer no." G KRD
DRD S SWDA=$P(^SOWK(657,SWD1,0),U,13) W !,"ENDING DATE: ",$S(SWDA="":"//",1:$E(SWDA,4,5)_"/"_$E(SWDA,6,7)_"/"_$E(SWDA,2,3)_"//") D ND G:SWND'>0 DRD S $P(^SOWK(657,SWD1,0),U,13)=SWND
 ;ALLOW ADDING NON-RECURRING COST INFORMATION WITHOUT MAKING NEW RECORD
NRC R !,"Change or add to NON-RECURRING COST information? N//",SWRD:DTIME S:SWRD="" SWRD="N" G DONE:SWRD["^",ERD:SWRD?1"N".E I SWRD["?" W !,"If editing NON-RECURRING COST information answer yes else answer no." G NRC
 I '$D(^SOWK(657,SWD1,3)) S ^(3,0)="^657.03^^"
 K DIC("S") S DIE="^SOWK(657,",DA=SWD1,DA(1)=3,DR="[SWNRC]" D ^DIE
 ;ALLOW ADDING ASIH INFORMATION WITHOUT MAKING NEW RECORD
ERD R !,"Change or add to ASIH information? N//",SWRD:DTIME S:SWRD="" SWRD="N" G DONE:SWRD["^",SWED:SWRD?1"N".E I SWRD["?" W !,"IF EDITING ASIH INFORMATION ANSWER YES ELSE ANSWER NO." G ERD
 I '$D(^SOWK(657,SWD1,4)) S ^(4,0)="^657.04D^^"
 K DIC("S") S DIE="^SOWK(657,",DA=SWD1,DA(1)=4,DR="[SWASIH]" D ^DIE
 ;EDIT OF MORE DATA, OLD RECORD CLOSED, NEW OPENED W/ CASE # SUFFIXED
SWED R !,"Edit additional information? N//",SWRD:DTIME S:SWRD="" SWRD="N" G DONE:SWRD["^",DONE:SWRD?1"N".E I SWRD["?" W !,"If you need to change other fields answer yes else answer no." G SWED
 L ^SOWK(657) S NEXT=$P(^SOWK(657,0),U,3)+1,NUM=$P(^(0),U,4)+1,$P(^SOWK(657,0),U,3)=NEXT,$P(^SOWK(657,0),U,4)=NUM L
 S ^SOWK(657,NEXT,0)=^SOWK(657,SWD1,0),$P(^SOWK(657,SWD1,0),U,10)="C",^SOWK(657,"B",SWD,NEXT)=""
 S SWC=$P(^SOWK(657,NEXT,0),U,2),N=$L(SWC),SWC=+SWC_$C($A($E(SWC,N,N))+1),N=$L(SWC),SA=N S:SA=1 SA=2 S:+$A($E(SWC,N,N))<58 SWC=$E(SWC,1,(SA-1))_"A" S $P(^SOWK(657,NEXT,0),U,2)=SWC,^SOWK(657,"C",SWC,NEXT)=""
 S SWNAM=$P(^DPT($P(^SOWK(657,SWD1,0),U,1),0),U,1),^SOWK(657,"FYN",$P(^SOWK(657,SWD1,0),U,9)_"-"_SWNAM_"-"_SWC,NEXT)=""
ED W !,"EFFECTIVE DATE OF CHANGE (MUST BE PRECISE)? " D ND G:SWND<1 ED
 S $P(^SOWK(657,NEXT,0),U,12)=SWND,$P(^SOWK(657,SWD1,0),U,13)=SWND
 I $D(^SOWK(657,SWD1,4)) S N=0 F I=0:0 S N=$N(^SOWK(657,SWD1,4,N)) Q:N'>0  I $P(^(N,0),U,2)="" S $P(^(0),U,2)=SWND,^SOWK(657,NEXT,4,1,0)=SWND,^SOWK(657,NEXT,4,0)="^657.04D^1^1" Q
 S DIE=DIC,DA=NEXT,DR="[SWCNHEDT]" D ^DIE
 G SWCNHEDT
DONE K DIC,SWD1,SWD,SWSK,SWRD,NEXT,NUM,%X,%Y,SWC,DIE,DA,DR,%DT,IOP,N,SA,SSN,SWDDS,SWE,SWI,SWIT,SWN,X,Y,SWR,SWDT,%Y1,D,DD,DO,I,SWND,SWNAM,SWDA Q
ND K Y S %DT="AEX" D ^%DT S SWND=0 S:Y>0 SWND=+Y Q
