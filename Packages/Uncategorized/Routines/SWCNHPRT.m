SWCNHPRT ;DLG/FARGO;ML/BHAM;PRINT CNHC COST BREAKOUT ; 2/21/86
 ;VERSION 1.0
 W !,*7,"***WARNING*** This report requires wide (132 column) paper.",!
RD R !,"FISCAL YEAR TO PRINT  ('ALL' TO PRINT ALL YEARS)? ",X:DTIME Q:(X["^")!(X="")  S:X?2N SWFY2=X,NAME2="" G FULL:(X?2N)!(X="ALL") W !!,*7,"     ENTER 2 DIGIT YEAR YOU WANT TO PRINT OR 'ALL' TO PRINT ALL ENTRIES" G RD
FULL S SWYR=X,SWPG=0 D ^%ZIS G:POP CLOSE U IO
 S N=0 D SWZA,GET,HDR D:(SWFY1=SWYR)!(SWYR="ALL") CALC,PRTV
 F I=0:0 D GET D:N<0 PRTL,SUB,PRTL,TOT Q:N<0  D PROC
CLOSE W @IOF X ^%ZIS("C")
 K %X,%Y,EN,LIN,M,N,NAME1,NAME2,POP,SUB,SWAS,SWCN,SWEC,SWED,SWFY1,SWFY2,SWPPR,SWSD,SWSSN,T,TOT,I,SWPG,SWR,SWYR,X,SWNR,X1,X2 Q
GET S N=$N(^SOWK(657,"FYN",N)) Q:N<0  S M=0,M=$N(^SOWK(657,"FYN",N,M)),SWFY1=$E(N,1,2),NAME1=$P(N,"-",2),SWSSN=$P(^DPT($P(^SOWK(657,M,0),U,1),0),U,9),SWCN=$P(N,"-",3),SWR=^SOWK(657,M,0),SWSD=$P(SWR,U,12),SWED=$P(SWR,U,13)
 S SWPPR=$P(SWR,U,8),(SWEC,SWAS,SWNR)=0
 I $D(^SOWK(657,M,2)) S EN=$P(^(2,0),U,3),SWEC=0 F I=1:1:EN S SWEC=SWEC+$P(^SOWK(657,M,2,I,0),U,2)
 I $D(^SOWK(657,M,3)) S EN=$P(^(3,0),U,3),SWNR=0 F I=1:1:EN S SWNR=SWNR+$P(^SOWK(657,M,3,I,0),U,2)
 I $D(^SOWK(657,M,4)) S EN=$P(^(4,0),U,3),SWAS=0 F I=1:1:EN S X1=$P(^SOWK(657,M,4,I,0),U,2),X2=$P(^(0),U,1) S:X1="" X1=DT D ^%DTC S SWAS=SWAS+X+1
 Q
CALC S X1=SWED,X2=SWSD D ^%DTC S T(1)=X+1,T(2)=SWAS
 I SWFY1'=$E(DT,2,3) S T(3)=X-SWAS+1
 I DT'<SWED S T(3)=X-SWAS+1
 E  S X1=DT,X2=SWSD D ^%DTC S T(3)=X-SWAS+1
 S T(4)=T(1)-T(3),T(5)=T(1)*(SWPPR+SWEC)+SWNR,T(6)=T(3)*(SWPPR+SWEC)+SWNR,T(7)=T(5)-T(6),T(8)=SWNR,T(9)=SWEC*T(3) Q
PRTL W " " F I=43:1:48,50:1:55,57:1:62,64:1:69,79:1:87,89:1:97,99:1:108,110:1:119,121:1:130 W ?(I),"-"
 W ! D:$Y'<IOSL HDR Q
PRTV W SWFY1," ",$E(NAME1,1,20),?25,$E(SWSSN,1,3)_"-"_$E(SWSSN,4,5)_"-"_$E(SWSSN,6,9)," ",SWCN,?43,$J(T(1),6),?50,$J(T(2),6),?57,$J(T(3),6),?64,$J(T(4),6),?71,$J(SWPPR,7,2)
 W ?79,$J(T(8),9,2),?89,$J(T(9),9,2),?99,$J(T(5),10,2),?110,$J(T(6),10,2),?121,$J(T(7),10,2),! D:$Y'<IOSL HDR
 F I=1:1:9 S TOT(I)=TOT(I)+T(I),SUB(I)=SUB(I)+T(I)
 S SWFY2=SWFY1,NAME2=NAME1 Q
TOT W ?10,"TOTALS: ",?43,$J(TOT(1),6),?50,$J(TOT(2),6),?57,$J(TOT(3),6),?64,$J(TOT(4),6),?79,$J(TOT(8),9,2),?89,$J(TOT(9),9,2),?99,$J(TOT(5),10,2),?110,$J(TOT(6),10,2),?121,$J(TOT(7),10,2) D:N'<0 HDR Q
SUB W ?10,"SUB TOTALS: ",?43,$J(SUB(1),6),?50,$J(SUB(2),6),?57,$J(SUB(3),6),?64,$J(SUB(4),6),?79,$J(SUB(8),9,2),?89,$J(SUB(9),9,2),?99,$J(SUB(5),10,2),?110,$J(SUB(6),10,2),?121,$J(SUB(7),10,2),!! D:$Y'<IOSL HDR Q
SWZA F I=1:1:9 S (T(I),TOT(I),SUB(I))=0
 Q
SWZS F I=1:1:9 S SUB(I)=0
 Q
PROC I (SWFY1=SWFY2),((SWYR="ALL")!(SWYR=SWFY1)) G NAME
 I (SWFY1'=SWFY2),((SWYR="ALL")!(SWYR=SWFY1)) D PRTL,SUB,PRTL,TOT,SWZA G NAM1
 Q
NAME I NAME1'=NAME2,NAME2]"" D PRTL,SUB,SWZS
NAM1 D CALC,PRTV Q
HDR W @IOF S SWPG=SWPG+1
 W "CONTRACT NURSING HOME COST BREAKOUT",?109,$P("JAN FEB MAR APR MAY JUN JUL AUG SEP OCT NOV DEC"," ",+$E(DT,4,5))," ",$E(DT,6,7),",",(1700+$E(DT,1,3)),"  PAGE: ",SWPG,!
 W ?38,"CASE TOTAL   SICK   DAYS   DAYS  PATIENT  NON-RECUR EXCEPTION OBLIGATED  INCURRED",!
 W "FY PATIENT NAME",?25,"SOC SEC NUM  NUM  DAYS    DAYS   USED   LEFT  PAY RT   COST TOTL COST TOTL   COSTS     COSTS      BALANCE",!
 F I=1:1:132 S $P(LIN,"-",I)=""
 W LIN,!! Q
