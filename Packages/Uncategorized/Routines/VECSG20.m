 ;ATL/ACE PROGRAMMER-MAILING LIST SYSTEM VECSG20
 ;;1
 ;       VARIABLE LIST
 ;       NAME, ADDR, CITY, STATE, ZIP = SAME AS VECSG19
 ;       MISSING =       SET TO 1 IF NAME DOES NOT EXIST
 ;       RECORD  =       CONTAINS RECORD COPIED FROM ^VECS5G
 ;       INDATA  =       USER'S RESPONSE BEFORE IT IS VALIDATED
START ;
 W @IOF
 W !!,?20,"EDIT MAILING LIST ENTRY"
NAME ;
 S MISSING=0
 R !!,"ENTER NAME : ",NAME:DTIME I '$T!(NAME["^")!(NAME="") G EXIT
 I NAME'?1U.A1","1U.AP!($L(NAME)>30)!(NAME["?")
 I  W !,"ENTER THE NAME OF THE NEW ENTRY."
 I  W "  ENTER LAST NAME,FIRST NAME",! G NAME
 D LOOKUP D STORE:MISSING'=1 G NAME
EXIT ;
 K NAME,ADDR,CITY,STATE,ZIP,MISSING,RECORD,INDATA
 Q
LOOKUP ;
 I $D(^VECS5G(NAME))=0 W !,"RECORD DOES NOT EXIST"
 I  S MISSING=1 G LOOKEXIT
 S RECORD=^VECS5G(NAME)
 S ADDR=$P(RECORD,"^",1)
 S CITY=$P(RECORD,"^",2)
 S STATE=$P(RECORD,"^",3)
 S ZIP=$P(RECORD,"^",4)
ADDR ;
 W !,"ADDRESS : ",$S(ADDR="":"",1:ADDR_"//") R INDATA:DTIME
 I '$T!(INDATA["^") G LOOKEXIT
 I INDATA="" G CITY
 I INDATA'?.ANP!($L(INDATA)>25)!(INDATA["?") W !,"ADDRESS MUST NOT "
 I  W "EXCEED 25 CHARACTERS",! G ADDR
 S ADDR=INDATA
CITY ;
 W !,"CITY : ",$S(CITY="":"",1:CITY_"//") R INDATA:DTIME
 I '$T!(INDATA["^") G LOOKEXIT
 I INDATA="" G STATE
 I INDATA'?.ANP!($L(INDATA)>20)!(INDATA["?") W !,"CITY MUST NOT EXCEED"
 I  W " 20 CHARACTERS" G CITY
 S CITY=INDATA
STATE ;
 W !,"STATE : ",$S(STATE="":"",1:STATE_"//") R INDATA:DTIME
 I '$T!(INDATA["^") G LOOKEXIT
 I INDATA="" G ZIP
 I INDATA'?2U!(INDATA["?") W !,"STATE MUST BE A 2 LETTER"
 I  W " ABBREVIATION",! G STATE
 S STATE=INDATA
ZIP ;
 W !,"ZIP CODE : ",$S(ZIP="":"",1:ZIP_"//") R INDATA:DTIME
 I '$T!(INDATA["^") G LOOKEXIT
 I INDATA="" G LOOKEXIT
 I INDATA'?5N&(INDATA'?9N)!(INDATA["?") W !,"ZIP CODE MUST BE FIVE OR"
 I  W " NINE DIGITS, NO HYPHENS" G ZIP
 S ZIP=INDATA
LOOKEXIT ;
 Q
STORE ;
 S ^VECS5G(NAME)=ADDR_"^"_CITY_"^"_STATE_"^"_ZIP
 Q
