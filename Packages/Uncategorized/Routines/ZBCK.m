BACKUP ;DSM11 UTILITIES; COPYRIGHT 1980 DEC
START U 0 K  S $ZT="UNEX^BACKUP"
ST2 D GETOPT G FDONE:OP=""
 I OP="?" D ^BACKHELP G ST2
 I OP=1 D START^BACKCRE G ST2
 I OP=3 D START^ZBCK1 G ST2
 I OP=4 D START^BACKFL G ST2
 I OP'=2 G ST2
 W !,"** Note:  please do not dismount any resident disks unless "
 W "explicitly",!,"   asked to do so.",!
NM2 S QUES="NMQ",DEF="" X ^%Q("EN") G ST2:ANS=""!%A S NM=ANS
 I '$D(^SYS(0,"BACKUP",NM)) D NMQH G ST2
ST C 63 O 63::3 G EX2:$T
 I $D(UNATTN) S MSG="VIEW BUFFER BUSY.  UNATTENDED BACKUP ABORTED" G ERROR
 W !,"VIEW BUFFER IS BUSY - PLEASE TRY AGAIN LATER.",! G FDONE
EX2 I '$D(^SYS(0,"BACKUP",NM,"DISKS")) G INC
 S NUM=^("DISKS")
 G INC:'$D(^("JOURNAL OPTION")) S JOP=^("JOURNAL OPTION")
 I JOP>1 G INC:'$D(^("JOURNAL SPACE SAME"))
 D TYPES^SYSROU
 I $V($V(44)+35)#2 S ID=""
 E  S ID=^SYS(0,"RUNNING")
 S S=$V($V(44)+56)#256\4,SYU=$P(TYPES,",",S\8+1)_(S#8)
 S:'$D(UU) UU=-1
 S DIF=0
 F DK=1:1:NUM D DKVER^BACKVERI Q:%FAIL
 G INC:%FAIL=1,FDONE:%FAIL>1,MTNEX:%FAIL=-1
 I $D(OP) I OP=3 S ERFLG=0 Q
OK ;
 S:JOP=3&'DIF DIF=1
 S QU=DIF
 K (QU,NUM,TYPES,ID,SYU,NM,MF,UU,FM,JOP,UNATTN,DSKNO,MTCNT)
 G PREPARE^BACKUPPR
INC I $D(UNATTN) S MSG="BACKUP-COMMAND-FILE '"_NM_"' INCOMPLETE.  UNATTENDED BACKUP ABORTED" G ERROR
 I OP=3 W !!,"BACKUP-COMMAND-FILE '",NM,"' IS INCOMPLETE !",! G FDONE
 W !!,"** BACKUP-COMMAND-FILE """,NM,""" IS INCOMPLETE.  TO DEFINE "
 W "THE MISSING",!,"INFORMATION, PLEASE SELECT 'EDIT' (OPTION 1).",!
 G ST2
MTNEX I $D(UNATTN) S MSG="NO MAGTAPE UNIT #"_U_" IN THIS CONFIGURATION.  UNATTENDED BACKUP ABORTED" G ERROR
 W !,"NO MAGTAPE UNIT #",U," IN THIS CONFIGURATION"
CNP W !," -- CANNOT PROCEED.",! G FDONE
UNEX I $D(UNATTN) S MSG="** UNEXPECTED ERROR: "_$ZE_".  UNATTENDED BACKUP ABORTED" G ERROR
 U 0 W:$ZE'?1"<INRPT".E !,"** UNEXPECTED ERROR:"
 W !,?3,$ZE,!
FDONE S ERFLG=1 I $D(MSG) S ^SYS(0,"UNATTENDED BACKUP",NM,+$H,$P($H,",",2))=MSG
 C 63 Q
STR S UNATTN="",NM=$P(^SYS(0,"UNATTENDED BACKUP FILE"),";",1)
 S ^SYS(0,"UNATTENDED BACKUP TIME")=$P(^("UNATTENDED BACKUP TIME"),";",2,999)
 S ^("UNATTENDED BACKUP FILE")=$P(^("UNATTENDED BACKUP FILE"),";",2,999)
 G ST
GETOPT W !,"OPTIONS:",!
 W "  1. CREATE, DELETE, OR EDIT A BACKUP-COMMAND-FILE",!
 W "  2. PERFORM REGULAR BACKUP (ATTENDED BACKUP)",!
 W "  3. PERFORM SCHEDULED BACKUP (UNATTENDED BACKUP)",!
 W "  4. MAINTAIN SCHEDULED BACKUP FILES",!!
OPENT W "ENTER OPTION  >   "
 R OP,! S:OP="^" OP="" Q:OP=""
 Q:OP="?"!(OP?1N&("1234"[OP))
 W !,"PLEASE ENTER NUMBER OF OPTION DESIRED",!! G OPENT
HD ZU $V($V(44)+346)#256:(:::::32) W *7,*7,!!,"***  " D H^CTKDAT W "  UNATTENDED BACKUP MESSAGE  ***",!,MSG,!! Q
ERROR D HD G FDONE
NMQ W !,"ENTER NAME OF THE BACKUP COMMAND FILE YOU WISH TO EXECUTE" Q
NMQH W !,"YOU MUST ENTER THE NAME OF AN EXISTING BACKUP COMMAND FILE."
NAMHLP W !,"YOUR EXISTING BACKUP COMMAND FILES ARE NAMED:",! S A=""
 F I=0:1 S A=$ZS(^SYS(0,"BACKUP",A)) Q:A=""  W ?5,A,!
 I 'I W ?5,"NONE DEFINED! -- SELECT OPTION NUMBER 1 TO DEFINE ONE.",! Q
 W !,"(IF YOU WISH TO EXAMINE THE CONTENTS OF A BACKUP COMMAND FILE "
 W "WITHOUT CHANGING",!
 W "IT, SELECT OPTION NUMBER 1 AND TYPE <CR> IN RESPONSE TO "
 W "EACH QUESTION.)",! Q
