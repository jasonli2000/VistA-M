ZPCDI ;PROGRAM TO BUILD A FM TEMPLATE FROM THE VB INPUT STRINGS [ 07/24/95  6:21 PM ] ;7/8/96  09:15
 ;COPYRIGHT 1994 ASPIRE TECHNOLOGY
 ;
 I $D(ECHOOFF)=0 S ECHOOFF=^%ZOSF("EOFF")
 I $D(ECHOON)=0 S ECHOON=^%ZOSF("EON")
LP ;
 X ECHOOFF
 R A:180 I '$T G TIMOUT
 S C=C+1 S ^ZSQLINT($J,"CMDLOG","TEMPLATE",C)=A
 I A["|BEGIN-TEMPLATE-DATA|" S I=0 K ^XTEMP($J),TNAME G LP
 I A["|TEMPLATE-FLDDATA|" D  G LP ; 
 .;S SUB=$P(A,"|",3),SUB=$P(SUB,"^",5)_" "
 .;S:A["*Top Multiple*" SUB=$P(A,"^",3)_" "
 .;I SUB="" Q  ;QUIT DO DOT
 .I $D(I)=0 S I=0
 .S I=I+1
 .S ^XTEMP($J,I)=$P(A,"|",3)
 I A["|TEMPLATE-SORTDATA|" D  G LP 
 .S BY=+$P(A,"|",3),FR=$P(A,"|",4),TO=$P(A,"|",5) ;,FNUM=$P(A,"|",6)
 I A["|TEMPLATE-BUILD|" D BUILDTEM G LP
 I A["|TEMPLATE-EXEC|" H 1 D EXECDIP G LP
 I A["|TEMPLATE-FILENUM|" S FNUM=$P(A,"|",3) G LP
 I A["|TEMPLATE-QUIT|" Q  ;GOTO MAIN PCDIALOG
 I A["^ZPCDIAG" Q  ;IN WRONG SPOT
NG I A'["TEMPLATE" S X=A,NOTEMP=1 Q  ;not a template instruction
 G LP
OK ;SEND A POSITIVE REMARK TO VB PC
 X ECHOON
 W "**OK**",! Q
 ;
BUILDTEM ;BUILD TEMPLATE
 D NOW^%DTC S DTM=$J(%,0,4),DTX=X
 ;GET NEXT NUMBER FOR TEMPLATE
 S T=""
 S FNUM=FILENUM
 I $D(^DIPT("F"_FNUM,"DSTREAM"))>0 S T=$O(^DIPT("F"_FNUM,"DSTREAM",""))
 I T="" D  ;
 .S Z=$G(^DIPT(0)),T=$P(Z,"^",3)+1 ;NEXT NUMBER FOR TEMPLATE
 .S $P(^DIPT(0),"^",3)=T
 S TNUM=T ;ASSIGN IT
 K ^DIPT(TNUM),FNM,FNM2
 S TNAME="DSTREAM" I $D(DUZ)=0 S DUZ=1
 ;I T'=TNUM S $P(^DIPT(0),"^",3)=TNUM ;SET THE NEW TEMPLATE NUMBER
 S ^DIPT(TNUM,0)=TNAME_"^"_DTM_"^@^"_FNUM_"^"_DUZ_"^@^"_DTX
 ;SET HEADER-DON'T WANT ONE
 S ^DIPT(TNUM,"H")="@@"
 ;BUILD CROSS-REFS
 S ^DIPT("B",TNAME,TNUM)=""
 S ^DIPT("F"_FNUM,TNAME,TNUM)=1
XX ;SET THE PRINT FIELDS UP...
 S SNGQT=$C(34),DBNAM=""
 S DBLQT=$C(34)_$C(34)
 S ^DIPT(TNUM,"F",1)="W !,"_$C(34)_"**Record #:"_$C(34)_",D0;"_SNGQT_"RECORD NUMB:"_SNGQT
 S I=0,FLDCNT=1 ;THE ABOVE  LINE IS THE FIRST RECORD
 S SUBXX="^XTEMP("_$J_")"
 S OLDLEV=0 ;initil variable to test for chng in level
 S LEV=0,ZPATH="",OLDFLD=9999999
LOOPFLD ;LOOP THRU THE SELECTED FIELDS
 S WPIND="" ;USE A \ TO INDICATE A WP FIELD TO PC
 ;S I=I+1 I $D(^XTEMP($J,I))=0 Q
 S SUBXX=$Q(@SUBXX)
 ;W !,"SUBXXX=",SUBXX
 I SUBXX=""!(SUBXX'[$J) D ERASKEY Q  ;,CLEANTEM Q  ;EXIT POINT FOR BUILDTEM
 S X=@SUBXX
 I DBNAM="" S DBNAM=$p(X,"^",6)
 I DBNAM["." S NUMPER=$L(DBNAM,".")-1 F II=1:1:NUMPER S DBNAM=$TR(DBNAM,".","") ;remove periods
 ;S X=$G(^XTEMP($J,I)) I X="" Q
 S FLDNAME=$P(X,"^",1),FLDTYPE=$P(X,"^",2),FIELDNUM=$P(X,"^",5)
 I FLDTYPE["*Top Multiple*" S ZPATH=$P(X,"^",3)
 S MYLEV=$L(FIELDNUM,",")-1 I MYLEV>0&($P(OLDFLD,",",1)'=$P(FIELDNUM,",",1)) D ENDREC
 S OLDFLD=FIELDNUM
MUL ;I FLDTYPE["M" G LOOPFLD ;A MULTIPLE FIELD
 S FLDCNT=FLDCNT+1
 S FNAME=$P(X,"^",1),FIELDNUM=$P(X,"^",5) ;removed _"=" from FNAME
 I $L(FNAME," ")>2 S NUMSP=$L(FNAME," ")-1 F II=1:1:NUMSP S FNAME=$TR(FNAME," ","_")
 I FNAME["." S NUMPER=$L(FNAME,".")-1 F II=1:1:NUMPER S FNAME=$TR(FNAME,".","") ;remove periods
 I FNAME["/" S NUMPER=$L(FNAME,"/")-1 F II=1:1:NUMPER S FNAME=$TR(FNAME,"/","_") ;remove back slashes
 S FLDTYPE=$P(X,"^",2),TABNAME=$P(X,"^",6)
 S NUMSP=$L(TABNAME," ")-1 F II=1:1:NUMSP S TABNAME=$TR(TABNAME," ","_")
 I TABNAME["." S NUMPER=$L(TABNAME,".")-1 F II=1:1:NUMPER S TABNAME=$TR(TABNAME,".","") ;remove any periods
 I TABNAME["/" S NUMPER=$L(TABNAME,"/")-1 F II=1:1:NUMPER S TABNAME=$TR(TABNAME,"/","_") ;removes back slashes
 S FLEVEL=$P(X,"^",4)
 I TABNAME="" G LOOPFLD ;HAVE THE MULTIPLE INDICATOR
 I FLDTYPE["W" S FIELDNUM=FIELDNUM_",.01",WPIND="\"
 S LEV=0 D PROCLEV^ZPCDI1 ;CHECK MULTIPLE FIELD LEVEL
 I MYLEV>LEV D ENDREC
 I LEV>0 G LOOPFLD
 ;HERE TO DO THE REGULAR FIELDS
 S TABLFLD=TABNAME_"."_FNAME_"="
 S ^DIPT(TNUM,"F",FLDCNT)="W "_$C(34)_WPIND_TABLFLD_$C(34)_";C1;Z~"
 S FLDCNT=FLDCNT+1
 S ^DIPT(TNUM,"F",FLDCNT)=FIELDNUM_"~"
 I FLDTYPE["W" S FLDCNT=FLDCNT+1,^DIPT(TNUM,"F",FLDCNT)="W "_$C(34)_"|*ENDWP*|"_$C(34)_";C1;Z~"
 G LOOPFLD
 Q
 ;
EXECDIP ;EXECUTE THE DIP (PRINT  PROGRAM  OF THE FILEMANAGER)
 ;
 ;W "EXECUTING TEMPLATE",!
 H 1
 I $D(TNAME)=0 S TNAME="DSTREAM"
 I $D(FR)=0 S FR="" ;FROM TO SORT
 I $D(TO)=0 S TO="" ;TO FIELD FOR SORTING
 I $D(BY)=0 S BY="@.01" ;FIELDS TO SORT BY
 K DX
 S DIC=FNUM,DUZ(0)="@"
 S IOP="0;P-DEC;256"
 S FLDS="["_TNAME_"]"
 S DHD="@@" ;NO HEADER
 S DHIT="W "_"""*ENDREC*"""
 S L=0
 ;K DIS,DISX,DIOEND,DIOBEG,D0,DIS
 D EN1^DIP
 W "**ENDTX**",!
 Q          
TIMOUT ;TIMEOUT go back to main
 Q
 ;
 G ^ZPCDIAG
XTEMP ;CREATES ^XTEMP
 S FILEN=5
 S FLDN="",CNT=0 K ^XTEMP($J)
 S ^XTEMP($J)=""
 S FILEN=""
 F  S FILEN=$O(^TMP($J,FILEN)) Q:FILEN=""  F  S FLDN=$O(^TMP($J,FILEN,FLDN)) Q:FLDN=""  D COMX ;
 Q
COMX ;GET SUB
 S X=^TMP($J,FILEN,FLDN)
 S SKEY=-1 S SKEY=$P(X,"^",8)
 I $P(X,"^",2)["*Top Mul" S SKEY=$P(X,"^",6)
 I SKEY="" Q
 I SKEY'["," S SKEY=SKEY_" "
 F P=1,2,6,7,8,9 S ^XTEMP($J,SKEY)=$G(^XTEMP($J,SKEY))_$P(X,"^",P)_"^"
 ;Previous line pulls fieldname,filenum etc
 Q
ENDREC ; WRITE THE NODE TO CREATE THE *ENDREC*
 ;I $L(ZPATH)>0 S ZPATH=ZPATH_","
 S ZLEN=$L(FIELDNUM,",")-1,ZZPATH=""
 F CX=1:1:ZLEN S ZZPATH=ZZPATH_$P(FIELDNUM,",",CX)_","
 S FLDCNT=FLDCNT+1
 S ^DIPT(TNUM,"F",FLDCNT)=ZZPATH_"W "_$C(34)_"*ENDREC*"_$C(34)_";C1;Z~"
 ;.S OLDLEV=LEV
 Q
ERASKEY ;DELETES THE LAST KEY NODE IN ^DIPT
 S SCR=99999
 F  S SCR=$O(^DIPT(TNUM,"F",SCR),-1) Q:SCR=""  I $G(^DIPT(TNUM,"F",SCR))["[KEY_"&($G(^DIPT(TNUM,"F",SCR))'["\") K ^DIPT(TNUM,"F",SCR) Q  
 Q
 
CLEANTEM ;CLEAN TEMPLATE **ENDREC**
 I $D(TNUM)=0 Q
 S P=0,OLDX="FIRST PASS THRU"
 F  S P=$O(^DIPT(TNUM,"F",P)) Q:P=""  S X=$G(^DIPT(TNUM,"F",P)) D MORE
 Q
MORE ;
 I OLDX=X K ^DIPT(TNUM,"F",P)
 S OLDX=X
 Q
