PRCAREPT ;SF-ISC/YJK-AR LIST,REPORT ;8/26/93  8:43 AM
V ;;4.0;ACCOUNTS RECEIVABLE;;11/22/93
 ;MAS,Agent Cashier, 3rd party,DC/DOJ reports.
PRINT ;ask if the report will be queued.
 K PRCAQUE S PRCA("MESS")="Do you wish to queue this report " D QUE^PRCAQUE Q:'$D(PRCAQUE)  D DIP Q
DIP S L=0,FLDS=PRCATEMP,BY=PRCASORT,FR=PRCAFT,TO=PRCALAST,DHD=PRCAHDR D EN1^DIP K L,FLDS,BY,FR,TO,DHD,DIC,IOP
KILLV K ZTSK,PRCAKDT1,PRCAQUE,DQTIME,PRCAKDT2,PRCACT,PRCADCOJ,PRCATEMP,PRCADT1,PRCADT2,PRCABN,PRCABILN,PRCAHDR,PRCAFT,PRCALAST,PRCATEMP,PRCASORT Q
END D KILLV K PRCA,PRCAIOP Q
REPT ;====================== REPORT SUBROUTINES ==========================
EN1 ;report for agent cashier for reconciliation.
 NEW DIC,FR,TO,FLDS,BY,DIR,DIRUT,DUOUT,DIROUT
 S DIR(0)="SBM^D:DETAIL;S:SUMMARY",DIR("A")="Type of Payment Report",DIR("B")="SUMMARY",DIR("?",1)="A detailed report will list each payment transaction."
 S DIR("?",2)=" ",DIR("?",3)="A summary report will not list the individual transactions",DIR("?")="and only give the TOTAL payments, COUNT, and MEAN for each Category."
 D ^DIR
 I Y="D"!(Y="S") S DIC="^PRCA(433,",BY="[PRCA PAYMENT DATE]",FLDS=$S(Y="D":"[PRCAR CASH]",1:"[PRCAR CASH SUMMARY]"),(FR,TO)="?,?",L=0 D EN1^DIP
 Q
 ;
EN2 ;report for MAS reconciliation with AR.
 S PRCA("DATE")="DATE BILL PREPARED" D ASKDT^PRCAQUE G:(PRCADT1="")!(PRCADT2="") END
 S PRCAFT=PRCADT1_",T,100",PRCALAST=PRCADT2_",T,199",PRCAHDR="MAS RECONCILIATION REPORT"
 S DIC="^PRCA(430," S PRCATEMP="[PRCAR MAS REPORT]",PRCASORT="@DATE BILL PREPARED,@CATEGORY:INTERNAL(TYPE),@CURRENT STATUS:STATUS NUMBER" D PRINT,END Q
 ;
EN3 ;print 3rd party accounts receivable report.
 S PRCAHDR="3RD PARTY ACCOUNTS RECEIVABLE REPORT",PRCASORT="PATIENT,DC/DOJ REFERRAL DATE,@CURRENT STATUS:STATUS NUMBER,@CATEGORY:INTERNAL(TYPE)"
 S PRCAFT=",,102,T",PRCALAST=",,102,T",PRCATEMP="[PRCA 3RD REPORT]",DIC="^PRCA(430," D PRINT,END Q
 ;
EN4 ;Report AR referred to DC
 S PRCA("DATE")="DATE REFERRED TO DC" D ASKDT^PRCAQUE G:(PRCADT1="")!(PRCADT2="") END
 S PRCACT=0,PRCADCOJ="",PRCATEMP="[PRCAD DC DOJ]",PRCASORT="@DC/DOJ REFERRAL CODE,DC/DOJ REFERRAL DATE,DEBTOR",PRCAFT="DC,"_PRCADT1_",",PRCALAST="DC,"_PRCADT2_",",PRCAHDR="ACCOUNTS RECEIVABLE REFERRED TO DC"
 K PRCAEN4 S DIC="^PRCA(430," D PRINT,END Q
EN5 ;Report AR referred to DOJ
 S PRCA("DATE")="DATE REFERRED TO DOJ" D ASKDT^PRCAQUE G:(PRCADT1="")!(PRCADT2="") END
 S PRCATEMP="[PRCAD DC DOJ]",PRCASORT="@DC/DOJ REFERRAL CODE,DC/DOJ REFERRAL DATE,DEBTOR",PRCAFT="DOJ,"_PRCADT1_",",PRCALAST="DOJ,"_PRCADT2_","
 S PRCAHDR="ACCOUNTS RECEIVABLE REFERRED TO DOJ",DIC="^PRCA(430," D PRINT,END Q
 ;
EN6 ;print other transaction for CALM code sheet in the AT section.
 D TSK^PRCAPTR Q
 ;
EN8 ;DC debt collection report.
 S PRCA("DATE")="DATE REFERRAL TO DC" D ASKDT^PRCAQUE G:(PRCADT1="")!(PRCADT2="") END
 S PRCATEMP="[PRCAS DC]",PRCASORT="[PRCAS DC SORT]",PRCAHDR="DISTRICT COUNSEL DEBT COLLECTION REPORT FROM "_PRCAKDT1_" TO "_PRCAKDT2
 S DIC="^PRCA(433,",PRCAFT="DC,,,"_PRCADT1_",",PRCALAST="DC,,,"_PRCADT2_"," D PRINT,END Q
EN9 ;DOJ debt collection report.
 S PRCA("DATE")="DATE REFERRAL TO DOJ" D ASKDT^PRCAQUE G:(PRCADT1="")!(PRCADT2="") END
 S PRCATEMP="[PRCAS DC]",PRCASORT="[PRCAS DC SORT]",PRCAHDR="DEPARTMENT OF JUSTICE DEBT COLLECTION REPORT FROM "_PRCAKDT1_" TO "_PRCAKDT2
 S DIC="^PRCA(433,",PRCAFT="DOJ,,,"_PRCADT1_",",PRCALAST="DOJ,,,"_PRCADT2_"," D PRINT,END Q
EN10 ;print contingent 3rd party accounts receivable report.
 S PRCA("DATE")="DATE REFERRED TO DC/DOJ" D ASKDT^PRCAQUE G:(PRCADT1="")!(PRCADT2="") END
 S PRCAHDR="CONTINGENT 3RD PARTY AR REPORT",PRCASORT="PATIENT,REFERRAL DATE,@CURRENT STATUS:STATUS NUMBER,@CATEGORY:INTERNAL(CATEGORY NUMBER)"
 S PRCAFT=","_PRCADT1_",102,22",PRCALAST=","_PRCADT2_",102,22",PRCATEMP="[PRCAR CONTINGENT REPORT]",DIC="^PRCA(430," D PRINT,END Q
 ;
