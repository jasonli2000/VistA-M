ZRESP ; NATURAL LANGUAGE RESPONDER AMT ;2/8/94  14:11
 S U="^",DTIME=300,DRDA="" K DA ;,$ZT="ERROR^ZRESP"
 I $D(DT)=0 S X="T",%DT="" D ^%DT S DT=Y
 W !,"Natural Language Interpreter",!
ASK ;
 I $D(LRDFNSAV) S LRDFN=LRDFNSAV
 D EXIT S ERR=0,AMBGU=0,CNT=0,CHC=""
 R !,"Enter text: ",X
 I $D(DA)=0 S DA=DRDA
 I X="?"!(X="HELP") D HELP^ZRESUTL1 G ASK
 I X="??" D FIELD^ZRESUTL1 G ASK
 S X=$$UPPER^ZRESUTIL(X)
 S Y=$$BADWORD^ZRESUTIL(X) I BADWORD=1 G ASK
 I X["^"!(X="") G EXIT1
CH I $P(X," ",1)="SELECT"!($P(X," ",1)="CHOOSE") S:$D(FILE) OLDFILE=FILE K DIC,FILFLG S FLX="",ARG=$P(X," ",2,999) D  
 .F I=1:1:$L(ARG,"|") S WORD(I)=$S(ARG["|":$P(ARG,"|",I),1:$P(ARG," ",1))
 .S FNAME="" F I=1:1:$L(ARG,"|") S FNAME=FNAME_WORD(I)_" " D:$D(^DIC("B",$E(FNAME,1,$L(FNAME)-1)))  Q:$D(FILFLG)
 ..S FNAME=$E(FNAME,1,$L(FNAME)-1),FLX=$P(ARG,FNAME,2,999),FLX=$E(FLX,2,999),FILFLG=1,CMD="SELECT" S:$E(FLX,1)=" " FLX=$E(FLX,2,999)
 ..S FILE=$O(^DIC("B",FNAME,0)),DIC=^DIC(FILE,0,"GL")
HOP ;SKIP ARG STUFF
 S CMD=$P(X," ",1),ARG=$P(X," ",2,999)
 I $P(ARG," ",1)="LAST"!($P(ARG," ",1)="DRUG") S ARG1=" "_$P(ARG," ",2,999),ARG=$P(ARG," ",1)
CALC ;CALCULATION
CALC2 I CMD="CALCULATE"!(CMD="CALC") S $ZT="MATHERR^ZRESP" D  G ASK ;  
 .I ARG']"" W !,"Sorry nothing to calculate",*7,! Q
 .W " = ",@ARG
CONSTR ;CONSTRUCT GROUP
 I CMD="CONSTRUCT" D ^ZRESUTL2 G ASK
CONVERT I CMD="CONVERT" D  G ASK
 .W X
SELECT I CMD="SELECT"!(CMD="CHOOSE") D  G ASK
 .K CHOICES S LRDFN=""
 .I $D(DIC),FLX']"" S DIC("A")="Enter "_$P(@(DIC_"0)"),"^",1)_": "
 .S FILE=$O(^DIC("B",FNAME,0)) I FILE="" S:$D(OLDFILE) FILE=OLDFILE W "  Unknown Entity" Q
 .;I '$D(^ZRKB("MFILE",FILE)) W !,*7,"  Now Constructing File " S FILENUM=FILE,EXECPROG="",ZFLG=1 D ADD^ZRGET W !,*7,"  File is now Built" S ^ZRKB("MFILE",FILE)=""
 .S DIC(0)=$S(FLX]"":"EQM",1:"AEQM") S:$D(FLX) X=FLX D ^DIC I +Y'>0 W "  Unknown Entry" Q
 .S (DRDA,DA)=+Y
 .S:FILE=52 DA=$P($G(^PSRX(+Y,0)),"^",2) Q:DA=""  S:FILE=2!(FILE=52) LRDFN=$G(^DPT(DA,"LR"))
DISPLAY ;
 I ARG["HEALTH SUMMARY",$P(ARG,"HEALTH",1)]"" D HEALTH^ZRESUTL1,END^GMTS G ASK
 I ARG="DRUG" S X=$P(ARG1," ",2) D DRUG^ZRESUTL1 S:$D(DRDA) DA=DRDA G ASK
 I ARG="LAST" S X=ARG1
 I CMD="DISPLAY"!(CMD="SHOW")!(CMD="GATHER") D  G ASK ;
 .S POS=$F(X," "),FIELDNAM=$E(X,POS,999) ;W !,"FIELDNAM=",FIELDNAM
 .I $D(^ZRKB(FIELDNAM,"GR")) D DISPLAY^ZRESUTL2 Q
 .I FIELDNAM["FOR" S FORV=$P(FIELDNAM," FOR ",2),FIELDNAM=$P(FIELDNAM," FOR ",1)
 .S D=0,F=0 F  S D=$O(^ZRKB(FIELDNAM,D)) Q:D'>0  S F=F+1,SEL=D I F>1 S AMBGU=1  
 .I F=0 S ERR=1 W *7,"  Field not known !!" Q
 .I AMBGU K SEL D AMBGU I SEL'=+SEL S ERR=1 Q
 .S KB=^ZRKB(FIELDNAM,SEL),FILEIN=$P(KB,"^",1)
 .I KB'["CALC;",'$D(FILE) S ERR=1 W *7,"  File needs to be Selected" Q
 .I FILEIN=63,LRDFN="" W !,"No lab data available" s ERR=1
 .I CMD="GATHER" K CHOICES
 .S CALC=$P(KB,";",1) I CALC="CALC" S PROG=$P(KB,";",2),ANS="" W " " D @PROG W " ",ANS Q
 .I KB="" W " Unknown field" S ERR=1
 .S EXECPROG=$P(KB,"~~",2) ;GET EXECUTABLE PROGRAM
 .I ERR=0 S FILENUM=$P(KB,"^",2),FIELDNUM=$P(KB,"^",3)
 .I ERR=0,FILE']"" W *7,"   File needs to be Selected" S ERR=1 Q
 .;I ERR=0,$P(KB,"^",1)'=63,$P(KB,"^",1)'=FILE W !,"The Entity "_$P(^DIC(FILE,0),"^",1)_" does not have field "_ARG_" defined" S ERR=1 Q
 .I ERR=0 S FDES=$P(^DD($P(KB,"^",2),.01,0),"^",2),FDESG="^"_$P(^DD($P(KB,"^",2),.01,0),"^",3)
 .I ERR=0,$P(KB,"^",15)=" " S DIC=FILENUM,DR=FIELDNUM,DIQ="ANS" D EN^DIQ1 W " ",ANS(FILENUM,DA,FIELDNUM) Q
 .I ERR=0 S GLB="^"_$P(KB,"^",5) D BLDSUB
 .I ERR=0 I $D(@GLB)=0 W !,"Data not available..." S ERR=1
 .I ERR=0 S Y=$P($G(@GLB),"^",$P(KB,"^",16)),C=$P(^DD($P(KB,"^",2),$P(KB,"^",3),0),"^",2) D Y^DIQ S ANS=Y W " =>",ANS
 .I ERR=1,CMD="GATHER" W " "_CNT_" Data Points Collected"
 .I ERR=0 I EXECPROG'="" W " " D @EXECPROG
PLOT I CMD="PLOT"!(CMD="GRAPH") D GRAPH^ZRESUTL1 G ASK
 I CMD="EXIT" G EXIT1
 W *7," I don't understand that input?"
 G ASK
MATHERR W " Error in calculation input!",*7 S $ZT="ERROR^ZRESP" G ASK
GETCHOIC ;GET USER TO PICK WHEN MULTIPLE CHOICES ARE AVAILABLE
 I CMD'="GATHER" R !,"Please Choose a NUMBER return for more: ",CHC
 I CHC'="" I $D(CHOICES(CHC))>0 S CHCF=1,$P(KB,"^",I+1)=CHOICES(CHC) Q
 Q
BLDSUB ;BUILD SUBSCRIPTS
 K SUB S SUBCNT=0
 F I=6:1:13 Q:$P(KB,"^",I)']""  S SUB(I-5)=$P(KB,"^",I) D:(I#2)'=0 CHKSUB
 S GLBSUB=""
 F D=1:1 Q:'$D(SUB(D))  S:'+SUB(D)&'(D#2) SUB(D)=$C(34)_SUB(D)_$C(34) S GLBSUB=GLBSUB_SUB(D)_","
 S GLB=GLB_GLBSUB_$P(KB,"^",15)_")"
 Q
CHKSUB ;CHECK SUB FOR AN ALGORITHM THAT MUST BE CONVERTED TO A VALUE.
 Q:$P(KB,"^",I)']""  ;THIS IS END OF KB ARRAY
 S GLBSUB="",GLBD=0
 F D=1:1 Q:'$D(SUB(D))  S GLBSUB=GLBSUB_$S('SUB(D)&'(D#2):$C(34)_SUB(D)_$C(34),1:SUB(D))_","
 Q:$D(CHCF)
 F  S GLBD=$O(@(GLB_GLBSUB_GLBD_")")) Q:GLBD'>0!$D(CHCF)  I $D(@(GLB_GLBSUB_GLBD_","_$P(KB,"^",15)_")")) S LNODE=GLBD D
 .S POS=$S(D=1:6,D=2:7,D=3:8,D=4:9,1:1) S TRANSFLG=$P(KB,"^",POS)
 .I ARG="LAST",TRANSFLG["9999" S $P(KB,"^",I+1)=GLBD,CHCF=1,CHC=1 Q
 .I CNT>1400 Q  ;CUTOFF FOR NUMBER OF RECORDS PER HIT
 .S CNT=CNT+1
 .S CHOICES(CNT)=GLBD_$S(CMD="GATHER":"^"_$P(@(GLB_GLBSUB_GLBD_","_$P(KB,"^",15)_")"),"^",$P(KB,"^",16)),1:"")
 .S SUBVAL=$P(@(GLB_GLBSUB_GLBD_","_0_")"),"^",1)
 .I TRANSFLG["9999",CMD="GATHER" S Y=SUBVAL X ^DD("DD") S $P(CHOICES(CNT),"^",3)=$P(Y,"@",1)
 .I CMD="GATHER" S ERR=1 Q
 .I FDES["P" S SUBVAL=$P(@(FDESG_SUBVAL_",0)"),"^",1)
 .I TRANSFLG["9999" S Y=SUBVAL X ^DD("DD") S SUBVAL=Y
 .I ARG'="LAST" W !,$J(CNT,4),"   ",SUBVAL
 .I CNT#5=0 D:ARG'="LAST" GETCHOIC I CHC="^" S ERR=1,CHCF=1
 I $D(TRANSFLG),TRANSFLG'="9999",ARG="LAST" S $P(KB,"^",$S(D=1:6,D=2:7,D=3:8,D=4:9,1:1))=LNODE,CHCF=1
 I '$D(CHCF)&((CNT#6)'=0) D:ARG'="LAST" GETCHOIC
 S:'$D(CHCF) ERR=1 K CHCF
 I CHC["^" S ERR=1,CHCF=1
 Q
ERROR ;
 W !,"Something I didn't expect was entered!",*7,!
 G ASK
EXIT1 ;
 K DIC,DIPGM,DOB,FILE,LRDFN,XX,X,Y,CHOICES,KB,DA,DRDA,OLDFILE
 W !,"Bye bye  ..."
EXIT K GLB,GLBSUB,GLBD,CHCF,FORV,SUB2,SUBVAL,GMTSQIT,MUSF,RET,HOLD,LRPARAM,FLX
 K ANYFL,ANYKEY,DRNAME,DRNUM,DRUGNAME,DRUGNUM,FILFLG,FNAME
 K MD,MEDCNT,RX,RX0,RX2,SIG,ST,ST0,WORD
 K ORIGIN,MIN,MAX,MAXX,K,Q,SCALE,SPREAD,TITLA,INCRE,PLOT,TITLE
 K AMBGU,ANS,ARG,ARG1,BADWORD,C,CALC,CHC,CMD,CNT,D,D1,ERR,EXECPROG,F
 K F,FDES,FDESG,FIELDNAM,FIELDNUM,FILEIN,FILENUM,GFIELD,GRP,HL,I
 K LABINFO,LABNORM,LNODE,LOWER,LP,NORMSS,PC,POS,PROG,REFHIGH
 K REFLOW,SEL,SFIELD,SFLG,SNUM,SPECTYPE,SUB,SUBCNT,TODAY,TRANSFLG
 K UNITS,UPPER,GMI,GMJ,GMPSAP,GMTSEG,GMW,GMTSTITL,GMTSTYP,HEALTH,GMTSEGC,GMTSEGI
 Q
AMBGU ;FIELD AMBGUITY RESOLVER (2 FIELDS HAVE SAME NAME)
 W !,"The "_FIELDNAM_" exists in the following categories:"
 S ML=0,COUNTF=0 F  S ML=$O(^ZRKB(FIELDNAM,ML)) Q:ML'>0  W !,ML,"   ",$P(^ZRKB(FIELDNAM,ML,"FRAME"),"^",2) S COUNTF=COUNTF+1
BACK R !,"Please Make a Selection: ",SEL I SEL<0!(SEL>COUNTF) G BACK
