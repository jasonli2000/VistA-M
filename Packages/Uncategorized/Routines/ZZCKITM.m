ZZCKITM ;JFR/HCIOFO - OR 49 CHECK UTILITIES
 ;;1.0
PROT ;LOOP 101 AND LOOK AT 10 FIELD FOR DUPS
 N TMPGBL,CTR,PTR,CTR1
 S (CTR1,ORZIEN)=0,TMPGBL="^TMP(""ZDUPS"",$J"
 S CHECK="Duplicate Items in PROTOCOL file"
 F  S ORZIEN=$O(^ORD(101,ORZIEN)) Q:'ORZIEN  D:$P(^(ORZIEN,0),"^",4)="D"
 . S ORZ10IEN=0
 . F  S ORZ10IEN=$O(^ORD(101,ORZIEN,10,"B",ORZ10IEN)) Q:'ORZ10IEN  D
 . . S (PTR,CTR)=0 
 . . F  S PTR=$O(^ORD(101,ORZIEN,10,"B",ORZ10IEN,PTR)) Q:'PTR  D 
 . . . S CTR=CTR+1 I CTR>1 S CTR1=CTR1+1 
 . . . I  S @(TMPGBL_","_CTR1_")")=$P(^ORD(101,ORZIEN,0),U)
 . . Q
 . Q 
 D PRINT(TMPGBL,CHECK)
 K @(TMPGBL_")"),TMPGBL,CTR1,CHECK,ORZIEN,ORZ10IEN,CTR,PTR
 Q
DLG ; FIND DUPS IN FILE 101.41
 N TMPGBL,CTR,PTR,CTR1
 S (CTR1,ORZIEN)=0,TMPGBL="^TMP(""ZDUPS"",$J"
 S CHECK="Duplicate Items in ORDER DIALOG file"
 F  S ORZIEN=$O(^ORD(101.41,ORZIEN)) Q:'ORZIEN  D:$P(^(ORZIEN,0),"^",4)="D"
 . S ORZ10IEN=0
 . F  S ORZ10IEN=$O(^ORD(101.41,ORZIEN,10,"D",ORZ10IEN)) Q:'ORZ10IEN  D
 . . S (PTR,CTR)=0 
 . . F  S PTR=$O(^ORD(101.41,ORZIEN,10,"D",ORZ10IEN,PTR)) Q:'PTR  D 
 . . . S CTR=CTR+1 I CTR>1 S CTR1=CTR1+1 
 . . . I  S @(TMPGBL_","_CTR1_")")=$P(^ORD(101.41,ORZIEN,0),U)
 . . Q
 . Q 
 D PRINT(TMPGBL,CHECK)
 K @(TMPGBL_")"),TMPGBL,CTR1,CHECK,ORZIEN,ORZ10IEN,CTR,PTR
 Q
GMRC ;LOOP TO FIND CONSULTS PROTOCOLS WITH IMPROPER NAMESPACE
 D DT^DICRW
 N TMPGBL,CTR,CHECK
 S ORZPKG=$O(^DIC(9.4,"C","GMRC",0)) Q:'ORZPKG  S (CTR,ORZIEN)=0
 S TMPGBL="^TMP(""ZGMRC"",$J",CHECK="Consult namespace check"
 F  S ORZIEN=$O(^ORD(101,ORZIEN)) Q:'ORZIEN  D:$$OK(ORZIEN)
 . S ORZNM=$E($P(^ORD(101,ORZIEN,0),U),1,5)
 . I ORZNM'="GMRCR"&(ORZNM'="GMRCT") D
 . . S CTR=CTR+1
 . . S @(TMPGBL_","_CTR_")")=$P(^ORD(101,ORZIEN,0),U)
 . . Q
 . Q
 D PRINT(TMPGBL,CHECK)
 K @(TMPGBL_")"),TMPGBL,CTR,CHECK,ORZPKG,ORZIEN,ORZNM
 Q
OK(PROT) ;SEE IF PROT NEEDS CHECKING ON CONSULT
 I $P(^ORD(101,PROT,0),U,12)'=ORZPKG Q 0
 I $P(^ORD(101,PROT,0),U,4)'="O" Q 0
 I $P(^ORD(101,PROT,0),U)["PLACE" Q 0
 I $P(^ORD(101,PROT,0),U)["URGENCY" Q 0
 I $P(^ORD(101,PROT,0),U)["GMRCO" Q 0
 Q 1
PRINT(GBL,HDR) ;PRINT THE STUFF
 I '$D(@(TMPGBL_")")) S @(TMPGBL_","_1_")")="No problems with "_HDR
 N CTR,PAGE
 D ^%ZIS Q:POP
 U IO
 W:IO=IO(0) @IOF
 D PAGE(0)
 S CTR=0 F  S CTR=$O(@(TMPGBL_","_CTR_")")) Q:'CTR!($D(DONE))  D
 . I $Y>(IOSL-5) D PAGE(1) Q:DONE
 . W !,@(TMPGBL_","_CTR_")")
 . Q
 D ^%ZISC
 K CTR,PAGE
 Q
FIND ; FIND ITEMS IN 101 AND THEIR POSITION
 I '$$PATCH^XPDUTL("OR*2.5*49") W !,"Can't use withou OR*2.5*49" Q  
 D DT^DICRW
 K DIC S DIC=101,DIC(0)="AEMNQ" D ^DIC
 I $D(DUOUT)!($D(DTOUT)) G QUIT
 W !!,$P($G(^ORD(101,+Y,0)),"^")
 I '$D(^ORD(101,"AD",+Y)) W !,?3,"Not contained on any menus!" G QUIT
 S ITEM=+Y
 S MEN=0 F  S MEN=$O(^ORD(101,"AD",ITEM,MEN)) Q:'MEN  D
 . W !,?5,"is part of ",$P($G(^ORD(101,MEN,0)),"^")
 . S ITPOS=$$FINDXUTL^ORCMEDT1(MEN,ITEM)
 . W ?50,"Column: ",$P(ITPOS,".",2),?65,"Row: ",$P(ITPOS,".")
 . Q
QUIT K DIC,ITEM,MEN,X,Y,ITPOS
 Q
PAGE(FEED) ; FEED ONE
 N DASH,DIR
 I FEED,$E(IOST,1,2)["C-" S DIR(0)="E" D ^DIR I Y<1 S DONE=1 Q
 W:FEED @IOF
 W HDR
 W ! F DASH=1:1:78 W "-"
 Q 
 
