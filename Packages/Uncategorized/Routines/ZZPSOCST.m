ZZPSOCST ;BHAM ISC/SAB - DAILY COST COMPILATION ROUTINE ; 06/03/92 15:05 [ 05/19/94  1:45 PM ]
 ;;6.0;OUTPATIENT PHARMACY;**47,72**;APRIL 1993
INIT K BEGDATE,ENDDATE S %DT(0)=$E(DT,1,5)_"00"
 W !!,"**** Date Range Selection ****" I '$O(^PS(59,0)) W *7,!,"PLEASE ENTER SITE PARAMETERS !!",!,*7 G EX
BEG W ! S %DT="APE",%DT("A")="   Beginning DATE : " D ^%DT G:"^"[X EX G:Y<0 BEG S (%DT(0),BEGDATE)=Y
EN W ! S %DT="APE",%DT("A")="   Ending    DATE : " D ^%DT K %DT G:"^"[X EX G:Y<0 EN W ! S ENDDATE=Y  I MJB<1  D 
 .S ZTIO="",ZTRTN="START^PSOCSTD",ZTDESC="DAILY COMPILATION ROUTINE" F G="BEGDATE","ENDDATE" S:$D(@G) ZTSAVE(G)=""
 .D ^%ZTLOAD W:$D(ZTSK) !,"AUTO COMPILATION INITIALIZED !" K G,BEGDATE,ENDDATE,ZTSAVE,ZTIO,ZTSK,ZTRTN,ZTDESC 
 .D QUE 
 Q
START B  S MJB=1 D INIT K MJB G:$E(DT,6,7)="01" MTH
 S PSG=0 F I=1:1 S X=$T(G+I) Q:$P(X,";",3)=""  S A(I)=$P(X,";",3),B(I)=$P(X,";",4),PSG=PSG+1
 S PSD=0 F I=1:1 S X=$T(D+I) Q:X=""  S C(I)=$P(X,";",3),D(I)=$P(X,";",4),PSD=PSD+1
 F PSDT=BEGDATE:1:ENDDATE K ^PSCST(PSDT),^PSCST("B",PSDT)
 S PSDT=BEGDATE-1
SRCH S PSDT=$O(^PSRX("AD",PSDT)) Q:'PSDT!(PSDT>ENDDATE)  S (OR,RF)=0 D SET,SRCH1,DFN G SRCH
 S PSOCNT=0 F PSDT=0:0 S PSDT=$O(^PSCST("B",PSDT)) Q:PSDT'>0  S PSD=PSDT,PSOCNT=PSOCNT+1
 S ^PSCST(0)="DRUG COST^50.9D^"_PSD_"^"_PSOCNT
EX  K ^TMP($J),FL,%DT,A,B,BEGDATE,COST,DATA,DATA1,DATA2,DRUG,DFN,ENDDATE,I,II,ML,OR,PAST,PHYS,PSOCNT,DIV,PSD,PSDT,PSFILL,PSG,QTY,RF,RX0,RX1,RX2,RXN,C,VALUE,VISITS,WD,X,X1,X2,Y,BDT,EDT S:$D(ZTQUEUED) ZTREQ="@" Q
SRCH1 F RXN=0:0 S RXN=$O(^PSRX("AD",PSDT,RXN)) Q:'RXN  D CHK
 Q
DFN F DFN=0:0 S DFN=$O(^TMP($J,"PAT",DFN)) Q:'DFN  S ^PSCST(PSDT,1)=DT_"^"_VISITS,VISITS=VISITS+1
 Q
CHK Q:'$D(^PSRX(RXN,0))  I '$D(^PSRX(RXN,2)) Q
 S RX0=^PSRX(RXN,0) S RX2=^PSRX(RXN,2)
 S DFN=+$P(RX0,"^",2) Q:'$D(^DPT(DFN,0))  S:$G(DRUG)'=$P(RX0,"^",6) (OR,RF)=0 S DRUG=+$P(RX0,"^",6) Q:'$D(^PSDRUG(DRUG,0))
 ;S CLASS=+$P(^(0),"^",2) Q:'$D(^PS(50.5,CLASS,0))
 S DIV=+$P(RX2,"^",9) Q:'$D(^PS(59,DIV,0))
 S PHYS=+$P(RX0,"^",4) Q:'$D(^VA(200,PHYS,0))  S PAST=+$P(RX0,"^",3) Q:'$D(^PS(53,PAST,0))
 S CLINIC=+$P(RX0,"^",5) K:'$D(^SC(CLINIC,0)) CLINIC
 S COST=$S(+$P(RX0,"^",17):+$P(RX0,"^",17),$D(^PSDRUG(DRUG,660)):+$P(^(660),"^",6),1:0)
 S OR=OR+1,QTY=+$P(RX0,"^",7),ML=$S($P(RX0,"^",11)="M":1,1:0),WD=$S($P(RX0,"^",11)="W":1,1:0),COST=QTY*COST
 I $P(RX2,"^",2)=PSDT,$D(^PSRX("AD",PSDT,RXN,0)) D SF
 D RF:$O(^PSRX("AD",PSDT,RXN,0))
 Q
SF S DATA="^"_OR_"^"_RF_"^"_COST_"^"_QTY_"^"_ML_"^"_WD,^TMP($J,"PAT",DFN)=""
 F I=1:1:PSG Q:('$D(CLINIC))&(I=PSG)  S DATA1=$S(($D(@(A(I)))#2):^(0),1:@(B(I))_"^0^0^0^0") S DATA2=+$P(DATA1,"^") F II=2:1:7 S VALUE=$P(DATA,"^",II)+$P(DATA1,"^",II),DATA2=DATA2_"^"_VALUE S:II=7 ^(0)=DATA2
 F I=1:1:PSD S DATA1=$S(($D(@(C(I)))#2):$G(^(0)),1:@(D(I))_"^0^0^0^0") S DATA2=+$P(DATA1,"^") F II=2:1:7 S VALUE=$P(DATA,"^",II)+$P(DATA1,"^",II),DATA2=DATA2_"^"_VALUE S:II=7 ^(0)=DATA2
 Q
SET S ^PSCST(PSDT,0)=PSDT,^PSCST("B",PSDT,PSDT)="",VISITS=0 K ^TMP($J,"PAT") Q
 ;
MTH S X1=DT,X2=-1 D C^%DTC S (BDT,EDT)=$E(X,1,5)_"00"
 F PSDT=(BDT+1):1:X K ^PSCST(PSDT),^PSCST("B",PSDT)
 D START^PSOCSTM
 G EX Q
QUE N BEGDATE,ENDDATE S ZTDTH=$H+1_",3600",ZTIO="",ZTRTN="START^PSOCSTD",ZTDESC="DAILY COMPILATION ROUTINE" S X1=DT,X2=-1 D C^%DTC S (BEGDATE,ENDDATE)=$S($E(DT,6,7)="01":DT,1:X),ZTREQ="@" F G="BEGDATE","ENDDATE" S:$D(@G) ZTSAVE(G)=""
 F ZTSK=0:0 S ZTSK=$O(^%ZTSCH(ZTDTH,ZTSK)) Q:'ZTSK  I $P($G(^%ZTSK(ZTSK,0)),"^",1,2)=ZTRTN S FL=1 K ZTSK,ZTRTN,ZTDESC,ZTIO,X1,X2,X,ZTSAVE Q
 D:'$G(FL) ^%ZTLOAD
 Q
RF F PSFILL=0:0 S PSFILL=$O(^PSRX("AD",PSDT,RXN,PSFILL)) Q:'PSFILL  I $D(^PSRX(RXN,1,PSFILL,0)) S RX1=^PSRX(RXN,1,PSFILL,0),DIV=$S($P(RX1,"^",9):$P(RX1,"^",9),1:$P(RX2,"^",9)) D REF
 Q
REF S OR=0,COST=$S(+$P(RX1,"^",11):+$P(RX1,"^",11),$D(^PSDRUG(DRUG,660)):+$P(^(660),"^",6),1:0)
 S RF=RF+1,QTY=+$P(RX1,"^",4),ML=$S($P(RX1,"^",2)="M":1,1:0),WD=$S($P(RX1,"^",2)="W":1,1:0) S COST=QTY*COST
 D SF
 Q
 ;
G ;;
 ;;^PSCST(PSDT,0);PSDT
 ;;^PSCST(PSDT,"P",PHYS,0);PHYS
 ;;^PSCST(PSDT,"P",PHYS,"D",DRUG,0);DRUG
 ;;^PSCST(PSDT,"D",DRUG,0);DRUG
 ;;^PSCST(PSDT,"D",DRUG,"P",PHYS,0);PHYS
 ;;^PSCST(PSDT,"PS",PAST,0);PAST
 ;;^PSCST(PSDT,"S",CLINIC,0);CLINIC
 ;;
D ;;
 ;;^PSCST(PSDT,"V",DIV,0);DIV
 ;;^PSCST(PSDT,"V",DIV,"D",DRUG,0);DRUG
 ;;^PSCST(PSDT,"V",DIV,"P",PHYS,0);PHYS
