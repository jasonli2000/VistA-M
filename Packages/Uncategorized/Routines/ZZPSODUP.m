ZZPSODUP ;BHAM ISC/RTR - DUPLICATE PRESCRIPTION DELETION ; 11/16/93
 ;;6.0;OUTPATIENT PHARMACY; ***NOT VERIFIED***
 D EXIT D ^PSOLSET
 W !! K DIR S PSOSEE=0,DIR(0)="Y",DIR("A")="ANSWER 'Y' OR 'N'",DIR("A",1)="Would you like to skip the duplicate prescriptions",DIR("A",2)="that already have at least one prescription deleted ?" D ^DIR G EXIT:Y["^" S:Y=1 PSOSEE=1 K DIR,Y
EN S RRR="" F  S RRR=$O(^PSRX("B",RRR)) Q:RRR=""!($G(PSOUT))  S (COUNT,RRR2)=0 F  S RRR2=$O(^PSRX("B",RRR,RRR2)) Q:'RRR2!($G(PSOUT))  S COUNT=COUNT+1 I COUNT>1 D SET
 ;
 G EXIT
SET S PSOCNT=0 F AA=0:0 S PSOCNT=PSOCNT+1 S AA=$O(^PSRX("B",RRR,AA)) Q:'AA  S RX(PSOCNT)=AA
 S RXONE=RX(1),RXTWO=RX(2),RXDUP=RRR
 S RXONE("ZERO")=$G(^PSRX(RXONE,0)),RXTWO("ZERO")=$G(^PSRX(RXTWO,0)),RXONE("TWO")=$G(^PSRX(RXONE,2)),RXTWO("TWO")=$G(^PSRX(RXTWO,2)),RXONE("THREE")=$G(^PSRX(RXONE,3)),RXTWO("THREE")=$G(^PSRX(RXTWO,3))
 S RXONE("COPAY")=$S(+$G(^PSRX(RXONE,"IB")):"COPAY",1:"NO COPAY"),RXTWO("COPAY")=$S(+$G(^PSRX(RXTWO,"IB")):"COPAY",1:"NO COPAY")
 S RXONE("REFILL")=$P(RXONE("ZERO"),"^",9),RXTWO("REFILL")=$P(RXTWO("ZERO"),"^",9)
 S RXONE("PAT")=$P(^DPT($P(RXONE("ZERO"),"^",2),0),"^"),RXONE("DRUG")=$P(^PSDRUG($P(RXONE("ZERO"),"^",6),0),"^"),RXONE("ISSD")=$P(RXONE("ZERO"),"^",13),RXONE("STAT")=$P(RXONE("ZERO"),"^",15),RXONE("REF")=$P(RXONE("ZERO"),"^",9)
 S RXONE("ORFIL")=$P(RXONE("TWO"),"^",2) S RFCNT=0 I $O(^PSRX(RXONE,1,0)) F ZZ=0:0 S ZZ=$O(^PSRX(RXONE,1,ZZ)) Q:'ZZ  S RFCNT=RFCNT+1 S RXONERF(RFCNT)=$P(^PSRX(RXONE,1,ZZ,0),"^")
 S RXONERL("ORFIL")=$P(^PSRX(RXONE,2),"^",13) F JJ=0:0 S JJ=$O(RXONERF(JJ)) Q:'JJ  S RXONEFRL(JJ)=$P(^PSRX(RXONE,1,JJ,0),"^",18)
 S RXTWO("PAT")=$P(^DPT($P(RXTWO("ZERO"),"^",2),0),"^"),RXTWO("DRUG")=$P(^PSDRUG($P(RXTWO("ZERO"),"^",6),0),"^"),RXTWO("ISSD")=$P(RXTWO("ZERO"),"^",13),RXTWO("STAT")=$P(RXTWO("ZERO"),"^",15),RXTWO("REF")=$P(RXTWO("ZERO"),"^",9)
 S RXTWO("ORFIL")=$P(RXTWO("TWO"),"^",2) S RFCNT=0 I $O(^PSRX(RXTWO,1,0)) F ZZ=0:0 S ZZ=$O(^PSRX(RXTWO,1,ZZ)) Q:'ZZ  S RFCNT=RFCNT+1 S RXTWORF(RFCNT)=$P(^PSRX(RXTWO,1,ZZ,0),"^")
 S RXTWORL("ORFIL")=$P(^PSRX(RXTWO,2),"^",13) F JJ=0:0 S JJ=$O(RXTWORF(JJ)) Q:'JJ  S RXTWOFRL(JJ)=$P(^PSRX(RXTWO,1,JJ,0),"^",18)
 ;
 I PSOSEE I RXONE("STAT")=13!(RXTWO("STAT")=13) G EXITONE
 ;
 W @IOF W ?27,"DUPLICATE PRESCRIPTION NUMBER ->  ",RXDUP,!
 W !,?20,1,?55,2,! F ZZ=1:1:80 W "_"
 W !,"Patient name:",?20,RXONE("PAT"),?55,RXTWO("PAT"),!,"Drug:",?9,$E(RXONE("DRUG"),1,30),?45,$E(RXTWO("DRUG"),1,30)
 W !,"Issue date:",?20,$E(RXONE("ISSD"),4,5),"/",$E(RXONE("ISSD"),6,7),"/",$E(RXONE("ISSD"),2,3),?55,$E(RXTWO("ISSD"),4,5),"/",$E(RXTWO("ISSD"),6,7),"/",$E(RXTWO("ISSD"),2,3)
 S STATUS=$P(^DD(52,100,0),"^",3),RXONE("STAT")=RXONE("STAT")_":",RXONE("STAT")=$S(RXONE("STAT")=":":"UNKNOWN",1:$P($P(STATUS,RXONE("STAT"),2),";")) W !,"Status:",?20,RXONE("STAT")
 S RXTWO("STAT")=RXTWO("STAT")_":",RXTWO("STAT")=$S(RXTWO("STAT")=":":"UNKNOWN",1:$P($P(STATUS,RXTWO("STAT"),2),";")) W ?55,RXTWO("STAT")
 W !,"Copay:",?20,RXONE("COPAY"),?55,RXTWO("COPAY")
 W !,"No. of refills:",?20,RXONE("REFILL"),?55,RXTWO("REFILL")
 W !,"Orig fill  ",$E(RXONE("ORFIL"),4,5),"/",$E(RXONE("ORFIL"),6,7),"/",$E(RXONE("ORFIL"),2,3),?20,$S(+RXONERL("ORFIL"):" RELEASED "_$E(RXONERL("ORFIL"),4,5)_"/"_$E(RXONERL("ORFIL"),6,7)_"/"_$E(RXONERL("ORFIL"),2,3),1:" NOT RELEASED")
 W ?42,"Orig fill  ",$E(RXTWO("ORFIL"),4,5),"/",$E(RXTWO("ORFIL"),6,7),"/",$E(RXTWO("ORFIL"),2,3),?62,$S(+RXTWORL("ORFIL"):" RELEASED "_$E(RXTWORL("ORFIL"),4,5)_"/"_$E(RXTWORL("ORFIL"),6,7)_"/"_$E(RXTWORL("ORFIL"),2,3),1:" NOT RELEASED")
 F Z=1:1:5 W !,"Refill ",Z,?11,$S(+$G(RXONERF(Z)):$E(RXONERF(Z),4,5)_"/"_$E(RXONERF(Z),6,7)_"/"_$E(RXONERF(Z),2,3),1:""),?21,$S(+$G(RXONEFRL(Z)):"RELEASED "_$E(RXONEFRL(Z),4,5)_"/"_$E(RXONEFRL(Z),6,7)_"/"_$E(RXONEFRL(Z),2,3),1:"NOT RELEASED") D
 .W ?42,"Refill ",Z,?53,$S(+$G(RXTWORF(Z)):$E(RXTWORF(Z),4,5)_"/"_$E(RXTWORF(Z),6,7)_"/"_$E(RXTWORF(Z),2,3),1:""),?63,$S(+$G(RXTWOFRL(Z)):"RELEASED "_$E(RXTWOFRL(Z),4,5)_"/"_$E(RXTWOFRL(Z),6,7)_"/"_$E(RXTWOFRL(Z),2,3),1:"NOT RELEASED")
 I RXONE("STAT")="DELETED"!(RXTWO("STAT")="DELETED") W !!!,"ONE OF THESE PRESCRIPTIONS IS ALREADY MARKED DELETED!",!,"PRESS <RET> TO CONTINUE: " R CONT:DTIME S:'$T!(CONT["^") PSOUT=1 G EXITONE
 W !! K DIR,Y S DIR(0)="SBO^1:1;2:2",DIR("A",1)="Press '1' to delete number 1, '2' to delete number 2,",DIR("A",2)="press '^' to exit, <ret> to move to the next duplicate prescription."
 S DIR("A")="Select prescription number"  D ^DIR I Y["^"!($D(DTOUT))!($D(DUOUT)) S PSOUT=1 Q
 I Y=1!(Y=2) S PSODEL=$S(Y=1:RXONE,1:RXTWO) K DIR S DIR(0)="Y",DIR("A")="ARE YOU SURE YOU WANT TO DELETE THIS PRESCRIPTION " D ^DIR G:Y'=1 EXITONE S DA=PSODEL D ENQ^ZZPSODEL K Y,PSODEL
EXITONE K RXONE,RXTWO,RXONERF,RXTWORF,RXONEFRL,RXTWOFRL
 K RXTWORL,RXONERL Q
EXIT K RXONE,RXTWO,RXONERF,RXTWORF,RXONEFRL,RXTWOFRL,PSOUT,DIR,DTOUT,DUOUT,Y,Z,PSODEL,RRR,RRR2,PSOSEE,RX,RXDUP,RXTWORL,RXONERL,STATUS,RFCNT
 D FINAL^PSOLSET W @IOF Q
