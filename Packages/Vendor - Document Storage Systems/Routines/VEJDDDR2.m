VEJDDDR2 ; blj/dss ;07/18/2000 20:48
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
 Q
 ; Call via line tags only
DD(RESULT,FILENUM) ; Retrieve data dictionary for a given file
 ; Input : FILENUM: File number
 ;
 ; Return : Data dictionary in .INI file format
 ;
 N LOOP,LOOP1,LOOP2,FIELDNM,FIELDTYP,ATTRIBUT
 S LOOP=0,ATTRIBUT="AUDIT;AUDIT CONDITION;COMPUTE ALGORITHM;DECIMAL DEFAULT;FIELD LENGTH"
 S ATTRIBUT=ATTRIBUT_"LABEL;MULTIPLE-VALUED;POINTER;SOURCE;SPECIFIER;TECHNICAL DESCRIPTION;TITLE;TYPE"
 S LOOP1=1,LOOP2=""
 F  S LOOP=$O(^DD(FILENUM,LOOP)) Q:+LOOP="0"  D
 . S ^TMP("VEJDDDR",$J,LOOP1)="["_$P($G(^DD(FILENUM,LOOP,0)),U)_"]",LOOP1=LOOP1+1
 . S ^TMP("VEJDDDR",$J,LOOP1)="NUMBER="_LOOP,LOOP1=LOOP1+1,FIELDNM=LOOP
 . D FIELD^DID(FILENUM,LOOP,"NZ",ATTRIBUT,"^TMP(""VEJDDDR1"",$J)")
 . F  S LOOP2=$O(^TMP("VEJDDDR1",$J,LOOP2)) Q:LOOP2=""  D
 .. S ^TMP("VEJDDDR",$J,LOOP1)=LOOP2_"="_$G(^TMP("VEJDDDR1",$J,LOOP2))
 .. I $G(^TMP("VEJDDDR1",$J,LOOP2))="FREE TEXT" D
 ... S LOOP1=LOOP1+1,^TMP("VEJDDDR",$J,LOOP1)="MAXLENGTH="_$$MAXLEN(FILENUM,FIELDNM)
 ... S LOOP1=LOOP1+1,^TMP("VEJDDDR",$J,LOOP1)="MINLENGTH="_$$MINLEN(FILENUM,FIELDNM)
 .. S LOOP1=LOOP1+1
 . K ^TMP("VEJDDDR1",$J)
 . S ^TMP("VEJDDDR",$J,LOOP1)="",LOOP1=LOOP1+1
 S RESULT=$NA(^TMP("VEJDDDR",$J))
 Q
MAXLEN(FILENUM,FIELDNUM) ;
 ; Returns the maximum available length from the Fileman DD for that file
 N MAXLEN,MAXLEN1,INPUTXFM S MAXLEN=0,MAXLEN1=""
 S INPUTXFM=$P($G(^DD(FILENUM,FIELDNUM,0)),U,5)
 FOR MAXLEN=$FIND(INPUTXFM,"$L(X)>"):1 Q:$E(INPUTXFM,MAXLEN)'?1N  S MAXLEN1=MAXLEN1_$E(INPUTXFM,MAXLEN)
 Q MAXLEN1
MINLEN(FILENUM,FIELDNUM) ;
 ; Returns the minimum available length from the Fileman DD for that file
 N MINLEN,MINLEN1,INPUTXFM S MINLEN=0,MINLEN1=""
 S INPUTXFM=$P($G(^DD(FILENUM,FIELDNUM,0)),U,5)
 FOR MINLEN=$FIND(INPUTXFM,"$L(X)<"):1 Q:$E(INPUTXFM,MINLEN)'?1N  S MINLEN1=MINLEN1_$E(INPUTXFM,MINLEN)
 Q MINLEN1
