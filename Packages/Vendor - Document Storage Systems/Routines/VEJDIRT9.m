VEJDIRT9 ;dss/DBB;RT REPORT
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
 Q
REPORTS(SUCCESS,PIEN,REPORT) ; Get RT reports for patient
 ; SUCCESS(0) = 0
 ;         = -1^Explanatory message if no SUCCESS
 ; SUCCESS(1...N) = Textual report
 ; REPORT = 'S'hort, 'L'ong report, 'M'issing (default = Short).
 ; PIEN = PATIENT IEN to retrieve
 K SUCCESS
 N INPUT D DT^DICRW
 S REPORT=$TR($G(REPORT),"mls","MLS")
 I REPORT'="L",REPORT'="M" S REPORT="S"
 S INPUT("RTN")="2^VEJDIRT9",INPUT("CTRL")=1,INPUT("DEL")=2
 S FILE=$$GET^DSICHFS("SUCCESS",.INPUT)
 I FILE<0 S SUCCESS(0)=FILE
 E  S SUCCESS(0)=$O(SUCCESS(""),-1)
 Q
2 ;;Short Inquiry
 S IOP="OR WORKSTATION;",%ZIS="N0",%ZIS("HFSNAME")=PATH_FILE,%ZIS("HFSMODE")="NW"
 S RTAPL=$O(^DIC(195.1,"B","MEDICAL ADMINISTRATION",0))
 I REPORT="M" G MISS
 S RTE=PIEN_";DPT("
 I REPORT="S" D SHORT G Q2
 D LONG
Q2 K RTAPL,RTSYS
 K RTE,RTESC,C,I,Y
 K %H,%I,DIC,DIY,N,POP,X,X1 Q
SHORT S RTINACFL="",RTDC("S")="I $P(^(0),U,4)="_+RTAPL D ^RTUTL2
 W ! ;C PATH_FILE
 K RTINACFL,RT1,RTC,RTDC Q
 ;
LONG S RTINFO="A"
 S RTPGM="START^RTNQ2",RTVAR="RTE^RTINFO^RTAPL"
 D START^RTNQ2 Q
 ;
MISS ;;Missing Records Report
 S L=0,DIC="^RTV(190.2,",FLDS="[RT MISSING]",BY="[RT MISSING]",FR="",TO="",DIS(0)="I $D(^RT(+^RTV(190.2,D0,0),0)),$P(^(0),U,4)=+RTAPL" K DTOUT D EN1^DIP K FLDS,BY,FR,TO,DIS(0),L,X,DHD Q
 Q
 ;
TESTI D REPORTS(.SUCCESS,441,"M")
 Q
