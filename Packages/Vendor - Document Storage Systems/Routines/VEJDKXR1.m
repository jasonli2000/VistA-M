VEJDKXR1 ;DSS/AMC-Document Storage Systems ;12/04/2002 14:04
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
TRT ;
 N PKG,SOURCE,TMP,VCNT,VTRT,VIEN,VTOT,VPOV
 S VIEN=0,VTRT=0
 S X="Checking V Treatment File for missing provider narratives."
 D MES^DSICXPDU(X,1),UPDSTAT^DSICXPDU(,,1)
 S PKG=$$PKG^DSICXPDU,VTOT=$P($G(^AUPNVTRT(0)),U,4)
 S SOURCE=+$O(^PX(839.7,"B","VEJD DSS PCE",0))
 F VCNT=1:1 S VTRT=$O(^AUPNVTRT(VTRT)) Q:'VTRT  D:'(VCNT#100) UPDSTAT^DSICXPDU(VTOT,VCNT) D
 .S VTRT(0)=$G(^AUPNVTRT(VTRT,0)),VTRT(812)=$G(^(812))
 .S VTRT(.01)=$P($G(^AUTTTRT(+VTRT(0),0)),U)
 .D:'$D(^AUTNPOV("B",VTRT(.01)))
 ..S X=VTRT(.01),DIC="^AUTNPOV(",DIC(0)="" D FILE^DICN
 .S VTRT(.06)=$O(^AUTNPOV("B",VTRT(.01),0)),DR=""
 .S:'$P(VTRT(0),U,6) DR=".06////"_VTRT(.06)
 .S:'$P(VTRT(812),U,2) DR=DR_";81202////"_PKG
 .S:'$P(VTRT(812),U,3) DR=DR_";81203////"_SOURCE
 .S DIE="^AUPNVTRT(",DA=VTRT D ^DIE K DR
 D UPDSTAT^DSICXPDU(VTOT,VTOT)
 Q
