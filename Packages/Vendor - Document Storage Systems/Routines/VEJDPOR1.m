VEJDPOR1 ;WPB/CAM Local calls used to populate DHCP from Delphi with RPC Broker
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
 ; CLONED FROM AXSRPCI  PTLD/JIT  4-13-99
 ;
F(AXAY,AXAX) ;Load vitals to file 120.5 AXA LOAD VITALS
 N I,X,Y,%,%H,%I,%DT,AXAD,AXAI,AXAL,DA,DFN,DIK,GDATE,GLAST,GMRDAT
 N GMRDATE,GMREDB,GMRENTY,GMRSITE,GMRSTR,GMRVHLOC,GMRVIDT,GMRVIT
 N GMRVITY,GMRVLST,GMRX
 S X=$P(AXAX,U)
 I X="" S AXAY(1)="NO SSN REQUESTED" Q
 D DFN I DFN="" S AXAY(0)="INCORRECT SSN" Q
 D IN5^VADPT S AXAL=$P(VAIP(5),U,2)
 S GMRVHLOC="" S:AXAL'="" GMRVHLOC=$O(^SC("B",AXAL,0))
 S AXAD=$$NOW^XLFDT,GMRVIDT=$P(AXAX,U,8)
 S %DT="RS" S X=GMRVIDT D ^%DT I Y,Y<AXAD S AXAD=Y
 ;Format of data is TTT.T-PPP-RR-BBB/BBB/BBB (or BBB/BBB)-HH-WWW.WW-PN
 ;This is loaded into variable GMRDAT
 S GMRVIDT=AXAD,GMREDB="P",GMRENTY=3,GLAST(1)=0
 F I=2,3,4,5,6,7,9 S AXAI=$P(AXAX,U,I) D:AXAI]""
 .I I=2 S X="T",GMRSITE("T")="ORAL^6"
 .I I=3 S X="P",GMRSITE("P")="RADIAL^23"
 .I I=4 S X="R",GMRSITE("R")="SPONTANEOUS^47"
 .I I=5 S X="BP",GMRSITE("BP")=""
 .I I=6 S X="HT",GMRSITE("HT")="ACTUAL^42"
 .I I=7 S X="WT",GMRSITE("WT")="ACTUAL^42"
 .I I=9 S X="PN",GMRSITE("PN")=""
 .S GMRDAT(X)=AXAI,GMRSTR(0)=";"_X_";"
 .N I D EN4^GMRVED2
 .I $G(DA)]"" S DIK="^GMR(120.5," D IX^DIK
 .Q
 S AXAY="DONE"
 D KVA^VADPT
 Q
 ;
DFN ;Get DFN for patient from SSN
 S DFN=$O(^DPT("SSN",X,0))
 Q
 ;
VC(X,VYC) ; CHECK VERIFY CODE AFTER F5
 ; X -  0=UNSUCCESFUL, 1=SUCCESSFUL
 ; VYC - UN-ENCRYPTED VERIFY CODE
 S X=VYC D ^XUSHSH S VYC=X I VYC="" S X=0 Q  ; ENCRYPTION
 S X=0
 I $P($G(^VA(200,DUZ,.1)),U,2)=VYC S X=1
 K VYC Q
 ;
PCP(DATA,DFN) ; GET PATIENT PRIMARY CARE PROVIDER FROM PCMM
 ; 11-15-03/ DBB
 ; replaced call to PCMM with NATIONAL API callS
P1 K DATA
 Q:$G(DFN)=""
 ; DATA=Primary Care Team^Primary Care Provider^Attending
 N PCT,PCP,ATT
 S PCT=$TR($$OUTPTTM^SDUTL3(DFN,DT),"^",":")
 S PCP=$TR($$OUTPTPR^SDUTL3(DFN,DT),"^",":")
 S ATT=$G(^DPT(DFN,.1041)) I ATT S ATT=ATT_":"_$P($G(^VA(200,ATT,0)),U)
 S X="",X=$$OUTPTAP^SDUTL3(DFN,DT) ; GET ASSOCIATE - 3-1-05/ DBB
 S DATA=PCT_U_PCP_U_ATT_U_$TR(X,"^",":")
 S DATA=PCT_U_PCP_U_ATT
 Q
PCMM ; GET PCP FROM PCMM FILE
 ;S (TPX,X)=""
 ;F TP=0:0 S TP=$O(^SCPT(404.43,"ADFN",DFN,TP)) Q:TP<1  D
 ;.F TPDT=0:0 S TPDT=$O(^SCPT(404.43,"ADFN",DFN,TP,TPDT)) Q:TPDT<1  D
 ;..F TPIEN=0:0 S TPIEN=$O(^SCPT(404.43,"ADFN",DFN,TP,TPDT,TPIEN)) Q:TPIEN<1  D
 ;...Q:$P($G(^SCPT(404.43,TPIEN,0)),U,4)]""  ; UNASSIGNED
 ;...I TPDT>TPX S X=TP,TPX=TPDT
 ;Q:X=""  ; NO TEAM POSITION FOR PATIENT
 ;S TPADT=$O(^SCTM(404.52,"AIDT",X,1,-9999999)) Q:TPADT=""
 ;S TPIFN=$O(^SCTM(404.52,"AIDT",X,1,TPADT,0)) Q:TPIFN=""
 ;S TPDUZ=$P($G(^SCTM(404.52,TPIFN,0)),U,3) Q:TPDUZ=""
 ;S VALMPCP(1)=TPDUZ
 ;S TPCL=$P($G(^SCTM(404.57,X,0)),U,9) ; CLINIC
 ;S VALMPCP(2)=TPCL
 ;K TPX,TP,TPDT,X,TPADT,TPIFN,TPDUZ,TPCL,TPIEN
 ;Q
