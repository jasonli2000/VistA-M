VEJDPX ;AMC-DSS;RPC Broker calls for PCE
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
DISPLAY(AXY,IFN,CCM) ;RPC - VEJD PCE VISIT RETRIEVE
 S AXY=$NA(^TMP("VEJDPX",$J)),CCM=+$G(CCM)
 I 'IFN S ^TMP("VEJDPX",$J,0)="0^MISSING IFN" Q
 I '$D(^AUPNVSIT(IFN,0)) S ^TMP("VEJDPX",$J,0)="0^NO ENTRY FOUND IN VISIT FILE"
 N CPT,DX,HF,ET,EX,IM,X,Y,Z,GET,FIL,AXX,IEN,ST,ASAPT,CST S AXX=1,ASAPT=0,GET=$NA(^TMP("GET",$J)),CST="" K ^TMP("GET",$J)
 S FIL=9000010,IEN=IFN_"," D GETS^DIQ(FIL,IEN,".07;.18;.22;15002;15003;80001:80007","EI",GET)
 I '$G(^TMP("GET",$J,FIL,IEN,.18,"I")) D
 .N LOC,PTML,CHKODT,ADMDT,DFN S LOC=+$G(^TMP("GET",$J,FIL,IEN,.22,"I")),ADMDT=+$G(^AUPNVSIT(+IEN,0)),DFN=+$P($G(^(0)),U,5)
 .S PTML=0 F  S PTML=$O(^SC(LOC,"S",ADMDT,1,PTML)) Q:'PTML!(+$G(^(+PTML,0))=DFN)
 .Q:'PTML  S CHKODT=$P($G(^SC(LOC,"S",ADMDT,1,PTML,"C")),U,3),^TMP("GET",$J,FIL,IEN,.18,"I")=CHKODT,ASAPT=1
 I '$G(GET(FIL,IEN,.18,"I")) D
 .N ENCN,CHKODT S ENCN=$O(^SCE("AVSIT",IFN,0)) Q:'ENCN  S CHKODT=$P($G(^SCE(ENCN,0)),U,7),GET(FIL,IEN,.18,"I")=CHKODT
 S ^TMP("VEJDPX",$J,0)=$G(^TMP("GET",$J,FIL,IEN,.07,"I"))_U_$G(^TMP("GET",$J,FIL,IEN,.18,"I"))_U_$G(^TMP("GET",$J,FIL,IEN,.22,"I"))_";"_$G(^("E")) F X=15002,15003,80001:1:80007 S ^TMP("VEJDPX",$J,0)=^TMP("VEJDPX",$J,0)_U_$G(^TMP("GET",$J,FIL,IEN,X,"I"))
 D
 .N D1,D0,ND0,II,NSTUS
 .I $D(^SCE("AVSIT",IFN)) D  Q
 ..S II=0 F  S II=$O(^SCE("AVSIT",IFN,II)) Q:'II  S ND0=$G(^SCE(II,0)),NSTUS=+$P(ND0,U,12) I NSTUS S CST=$P($G(^SD(409.63,NSTUS,0)),U) Q
 .S D1=$P(^AUPNVSIT(IFN,0),U),D0=$P(^AUPNVSIT(IFN,0),U,5) D CURRENT^SDAMU S CST=X
 S ^TMP("VEJDPX",$J,1)="~^"_ASAPT_U_CST
 S CPT=0,FIL=9000010.18 F  S CPT=$O(^AUPNVCPT("AD",IFN,CPT)) Q:'CPT  D
 .S IEN=CPT_"," D GETS^DIQ(FIL,IEN,"*","EI",GET)
 .S X="C^"_CPT_U_$P($G(^ICPT(+$G(^TMP("GET",$J,FIL,IEN,.01,"I")),0)),U,2)_U_$G(^TMP("GET",$J,FIL,IEN,.01,"E"))_U_$G(^("I"))_U_$G(^TMP("GET",$J,FIL,IEN,.16,"I"))
 .S AXX=AXX+1,^TMP("VEJDPX",$J,AXX)=X
 .N MOD,MODI,MODS,LNKE,LNKI S MOD=0,MODS="" F  S MOD=$O(^AUPNVCPT(CPT,1,MOD)) Q:'MOD  S:MODS]"" MODS=MODS_":" S MODI=+^(MOD,0),MODS=MODS_$TR($P($G(^DIC(81.3,MODI,0)),U,1,2),"^",";")
 .S:MODS]"" $P(^TMP("VEJDPX",$J,AXX),U,7)=MODS S $P(^TMP("VEJDPX",$J,AXX),U,9)=$S($G(^TMP("GET",$J,FIL,IEN,1204,"I")):^("I")_";"_$G(^("E")),1:"")
 .S (LNKE,LNKI)="" F I=.05,.09,.1,.11,.12,.13,.14,.15 S LNKE=LNKE_$S($G(^TMP("GET",$J,FIL,IEN,I,"E"))]"":^("E")_";",1:""),LNKI=LNKI_$S($G(^TMP("GET",$J,FIL,IEN,I,"I")):^("I")_";",1:"")
 .I LNKE]"" S:$E(LNKE,$L(LNKE))=";" LNKE=$E(LNKE,1,$L(LNKE)-1),LNKI=$E(LNKI,1,$L(LNKI)-1) S $P(^TMP("VEJDPX",$J,AXX),U,10)=LNKE,$P(^(AXX),U,11)=LNKI
 .S X=^TMP("VEJDPX",$J,AXX) D:$P(X,U,4)>99000 ENM(IFN,X)
 ;
 S HF=0,FIL=9000010.23 F  S HF=$O(^AUPNVHF("AD",IFN,HF)) Q:'HF  D
 .S IEN=HF_"," D GET(FIL,IEN,".01;.04;1201;1204","HF")
 .S AXX=AXX+1,^TMP("VEJDPX",$J,AXX)=X
 ;
 S IM=0,FIL=9000010.11 F  S IM=$O(^AUPNVIMM("AD",IFN,IM)) Q:'IM  D
 .S IEN=IM_"," D GET(FIL,IEN,".01;.04;.06;.07;1201;1204","IMM")
 .S X=X_U_$G(^TMP("GET",$J,FIL,IEN,.06,"I"))_";"_$G(^("E"))_U_$G(^TMP("GET",$J,FIL,IEN,.07,"I"))_";"_$G(^("E"))_U_$G(^TMP("GET",$J,FIL,IEN,1201,"I"))
 .S AXX=AXX+1,^TMP("VEJDPX",$J,AXX)=X
 ;
 S EX=0,FIL=9000010.13 F  S EX=$O(^AUPNVXAM("AD",IFN,EX)) Q:'EX  D
 .S IEN=EX_"," D GET(FIL,IEN,".01;.04;1201;1204","EX")
 .S AXX=AXX+1,^TMP("VEJDPX",$J,AXX)=X
 ;
 S ET=0,FIL=9000010.16 F  S ET=$O(^AUPNVPED("AD",IFN,ET)) Q:'ET  D
 .S IEN=ET_"," D GET(FIL,IEN,".01;.06;1201;1204","ET")
 .S X=X_U_$G(^TMP("GET",$J,FIL,IEN,.06,"I"))_";"_$G(^("E"))
 .S AXX=AXX+1,^TMP("VEJDPX",$J,AXX)=X
 ;
 S DX=0,FIL=9000010.07 F  S DX=$O(^AUPNVPOV("AD",IFN,DX)) Q:'DX  D
 .N GETPOV S IEN=DX_"," D GETS^DIQ(FIL,IEN,".01;.04;.06;.12;1204;80001:80006","EI","GETPOV")
 .S Y=$G(GETPOV(FIL,IEN,.01,"I")),Z=$P($G(^ICD9(+Y,0)),U,3)
 .S X="D^"_DX_U_Z_U_$G(GETPOV(FIL,IEN,.01,"E"))_U_Y_U_$G(GETPOV(FIL,IEN,.06,"I"))_";"_$G(GETPOV(FIL,IEN,.06,"E"))_U_$G(GETPOV(FIL,IEN,.12,"I"))_U_$G(GETPOV(FIL,IEN,1204,"I"))_";"_$G(GETPOV(FIL,IEN,1204,"E"))
 .F Y=80001:1:80006 S X=X_U_$G(GETPOV(FIL,IEN,Y,"I"))
 .I '$D(^DD(9000010.07,80005,0)),$P(^TMP("VEJDPX",$J,0),U,10)]"" S $P(X,U,13)=0
 .S $P(X,U,16)=$G(GETPOV(FIL,IEN,.04,"E"))
 .S AXX=AXX+1,^TMP("VEJDPX",$J,AXX)=X
 ;
 S ST=0,FIL=9000010.12 F  S ST=$O(^AUPNVSK("AD",IFN,ST)) Q:'ST  D
 .S IEN=ST_"," D GET(FIL,IEN,".01;.04:.06;1201;1204","ST")
 .S X=X_U_$G(^TMP("GET",$J,FIL,IEN,.05,"E"))_U_$G(^TMP("GET",$J,FIL,IEN,.06,"I"))
 .S AXX=AXX+1,^TMP("VEJDPX",$J,AXX)=X
 ;
 S PRV=0,FIL=9000010.06 F  S PRV=$O(^AUPNVPRV("AD",IFN,PRV)) Q:'PRV  D
 .S IEN=PRV_"," D GETS^DIQ(FIL,IEN,".01;.04;.05;81101","EI",GET)
 .S X="P^"_PRV_U_$G(^TMP("GET",$J,FIL,IEN,.01,"E"))_U_$G(^("I"))_U_$G(^TMP("GET",$J,FIL,IEN,.04,"I"))_U_U_$S($G(^TMP("GET",$J,FIL,IEN,.05,"I"))="A":"A",1:"")_U_$G(^TMP("GET",$J,FIL,IEN,81101,"E"))
 .S AXX=AXX+1,^TMP("VEJDPX",$J,AXX)=X
 ;
 S TRT=0,FIL=9000010.15 F  S TRT=$O(^AUPNVTRT("AD",IFN,TRT)) Q:'TRT  D
 .S IEN=TRT_"," D GET(FIL,IEN,".01;.04;.06;1201;1202;1204","TR")
 .S X=X_U_$G(^TMP("GET",$J,FIL,IEN,1202,"I"))_";"_$G(^("E"))_U_$G(^TMP("GET",$J,FIL,IEN,.06,"E"))
 .S AXX=AXX+1,^TMP("VEJDPX",$J,AXX)=X
 ;
 K ^TMP("GET",$J) Q
GET(FILE,IENS,FIELDS,RECHD)     ;
 Q:'$G(FILE)  N P04I S P04I=$S(FILE[.16:0,FILE[.15:-1,1:1)
 D GETS^DIQ(FILE,IENS,FIELDS,"IE",GET)
 N P04,P1204 S P04=$G(^TMP("GET",$J,FILE,IENS,.04,"I")),P1204=$G(^TMP("GET",$J,FILE,IENS,1204,"I"))
 S X=RECHD_U_$P(IENS,",")_U_$G(^TMP("GET",$J,FILE,IENS,.01,"E"))_U_$G(^TMP("GET",$J,FILE,IENS,.01,"I"))_U_$S(P04I>0:$S(P04]"":P04_";"_^TMP("GET",$J,FILE,IENS,.04,"E"),1:""),P04I<0:^TMP("GET",$J,FILE,IENS,.04,"E"),1:"")
 S X=X_U_$G(^TMP("GET",$J,FILE,IENS,1201,"I"))_U_$S(P1204:P1204_";"_^TMP("GET",$J,FILE,IENS,1204,"E"),1:"")
 Q
CPTMOD(AXY,CPTIFN)      ;RPC - VEJD CPT MODIFIERS LIST
 I $G(CPTIFN)="" S AXY(0)="-1^INVALID INPUT" Q
 S CPTIFN=$$NUM^ICPTAPIU(CPTIFN)
 N LWR,UPR,Y,YY S YY=0
 S LWR=0 F  S LWR=$O(^DIC(81.3,"M",LWR)) Q:'LWR!(LWR>CPTIFN)  D
 .S UPR=0 F  S UPR=$O(^DIC(81.3,"M",LWR,UPR)) Q:'UPR  I UPR'<CPTIFN D
 ..S Y=0 F  S Y=$O(^DIC(81.3,"M",LWR,UPR,Y)) Q:'Y  I '$P(^DIC(81.3,Y,0),U,5) S YY=YY+1,AXY(YY)=$TR($P(^DIC(81.3,Y,0),U,1,2),"^","-")
 I '$O(AXY(0)) S AXY(0)="-1^NO MODIFIERS FOUND" Q
 Q
ENM(VST,DATA) ;Check to see if this E & M code needs to be tracked
 Q:'CCM
 Q:'$D(^VEJD(19610.7,0))
 N PROV,DAT
 S PROV=+$P(DATA,U,9)
 Q:$D(^VEJD(19610.7,"AVSTP",VST,PROV))
 N X S X="VEJDENM" X ^%ZOSF("TEST") I '$T Q
 S $P(DATA,U,11)=0 D ENM^VEJDENM(VST,$P(DATA,U,2,11))
 Q
