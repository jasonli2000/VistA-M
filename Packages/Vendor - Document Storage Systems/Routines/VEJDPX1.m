VEJDPX1 ;AMC - Document Storage Systems;RPC Broker calls
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
399(AXYG,FIELDS,LDATE,XREF)     ;
 S AXYG=$NA(^TMP("VEJDPX13",$J))
 I $G(FIELDS)=""!($G(XREF)="")!'$G(LDATE) S ^TMP("VEJDPX13",$J,0)="-1^INVALID INPUT!" Q
 I '$O(^DGCR(399,XREF,LDATE,0)) S ^TMP("VEJDPX13",$J,0)="-1^No entries found on that date!" Q
 N DIC,XX,Y,FLDS,IENS,AXY,J S XX=0,FLDS=$TR(FIELDS,"^",";"),AXY=0
 F  S XX=$O(^DGCR(399,XREF,LDATE,XX)) Q:'XX  D:$G(^DGCR(399,XX,0))]""
 .N GET S IENS=XX_"," D GETS^DIQ(399,IENS,FLDS,"E","GET")
 .S Y=XX F J=1:1:$L(FIELDS,U) S:$G(GET(399,IENS,$P(FIELDS,U,J),"E"))]"" $P(Y,U,J+1)=GET(399,IENS,$P(FIELDS,U,J),"E")
 .S ^TMP("VEJDPX13",$J,AXY)=Y,AXY=AXY+1
 Q
PAT399(AXYG,FIELDS,DFN,STDT,ENDT,SORT)       ;
 S AXYG=$NA(^TMP("VEJDPX1P",$J))
 I $G(FIELDS)=""!'$G(DFN)!'$G(STDT) S ^TMP("VEJDPX1P",$J,0)="-1^INVALID INPUT!" Q
 I '$O(^DGCR(399,"C",DFN,0)) S ^TMP("VEJDPX1P",$J,0)="-1^No entries found for selected patient!" Q
 N DIC,XX,Y,FLDS,IENS,AXY,J,EVDT,NODE S XX=0,FLDS=$TR(FIELDS,"^",";"),AXY=0,SORT=$S($G(SORT):SORT,1:3),NODE=$S(SORT<10:0,1:"S") I '$G(ENDT) D NOW^%DTC S ENDT=X
 F  S XX=$O(^DGCR(399,"C",DFN,XX)) Q:'XX  D:$G(^DGCR(399,XX,0))]""
 .S EVDT=$P($G(^DGCR(399,XX,NODE)),U,SORT) Q:(EVDT<STDT)!(EVDT>ENDT)
 .N GET S IENS=XX_"," D GETS^DIQ(399,IENS,FLDS,"E","GET")
 .S Y=XX F J=1:1:$L(FIELDS,U) S:$G(GET(399,IENS,$P(FIELDS,U,J),"E"))]"" $P(Y,U,J+1)=GET(399,IENS,$P(FIELDS,U,J),"E")
 .S ^TMP("VEJDPX1P",$J,AXY)=Y,AXY=AXY+1
 I '$D(^TMP("VEJDPX1P",$J,0)) S ^TMP("VEJDPX1P",$J,0)="-1^No Bills/Claims found for date range!"
 Q
VISIT(AXY,FIELDS,LDATE,NOCNT,VDIV)   ;RPC - VEJD PCE VISIT LOOKUP
 S AXY=$NA(^TMP("VEJDPX1V",$J)),VDIV=$G(VDIV)
 I $G(FIELDS)=""!'$G(LDATE) S ^TMP("VEJDPX1V",$J,0)="-1^INVALID INPUT!" Q
 I $P($O(^AUPNVSIT("B",LDATE)),".")'=LDATE S ^TMP("VEJDPX1V",$J,0)="-1^No entries found on that date!" Q
 N DIC,XX,Y,FLDS,IENS,AXYY,J,VD,DIVS S DIVS=VDIV>0,VDIV=U_VDIV_U,NOCNT=$G(NOCNT),AXYY=0,FLDS=$TR(FIELDS,U,";"),XX=0,VD=LDATE,FLDS=FLDS_";.12"
 F  S VD=$O(^AUPNVSIT("B",VD)) Q:'VD!($P(VD,".")'=LDATE)  S XX=0 F  S XX=$O(^AUPNVSIT("B",VD,XX)) Q:'XX  D:$G(^AUPNVSIT(XX,0))]""
 .I DIVS,VDIV'[(U_+$$VSTDIVX(XX)_U) Q
 .N GET Q:'$$COUNT(XX)
 .S IENS=XX_"," D GETS^DIQ(9000010,IENS,FLDS,"E","GET") Q:$G(GET(9000010,IENS,.12,"E"))]""
 .S Y=XX F J=1:1:$L(FIELDS,U) S:$G(GET(9000010,IENS,$P(FIELDS,U,J),"E"))]"" $P(Y,U,J+1)=GET(9000010,IENS,$P(FIELDS,U,J),"E")
 .S ^TMP("VEJDPX1V",$J,AXYY)=Y,AXYY=AXYY+1
 S:'$D(^TMP("VEJDPX1V",$J,0)) ^TMP("VEJDPX1V",$J,0)="-1^No entries found!" Q
COUNT(VSIT)     ;
 Q:'$G(VSIT) 0
 N IEN44,Y44,X44
 S IEN44=+$P(^AUPNVSIT(VSIT,0),U,22),Y44=$G(^SC(IEN44,0)) Q:Y44="" 0 Q:NOCNT="" 1
 S X44=$P(Y44,U,17)
 I NOCNT="N" Q:"N"[X44 1
 Q:NOCNT=X44 1
 Q 0
CLAIMS(AXY,FIELDS,DFN,STDT,ENDT)        ;
 S AXY=$NA(^TMP("VEJDPX1C",$J)) I '$G(DT) D NOW^%DTC S DT=X
 I $G(FIELDS)=""!'$G(DFN)!'$G(STDT) S ^TMP("VEJDPX1C",$J,0)="-1^INVALID INPUT!" Q
 I '$O(^IBT(356,"C",DFN,0)) S ^TMP("VEJDPX1C",$J,0)="-1^No entries found for patient!" Q
 N DIC,XX,Y,FLDS,IENS,J S (XX,AXYY)=0,FLDS=$TR(FIELDS,U,";"),ENDT=$S($G(ENDT):ENDT,1:DT)
 F  S XX=$O(^IBT(356,"C",DFN,XX)) Q:'XX  D:$G(^IBT(356,XX,0))]""
 .N GET,EPDT
 .S EPDT=+$P(^IBT(356,XX,0),U,6) Q:'EPDT
 .Q:EPDT<STDT!(EPDT>(ENDT+1))
 .S IENS=XX_"," D GETS^DIQ(356,IENS,FLDS,"E","GET")
 .S Y=XX F J=1:1:$L(FIELDS,U) S:$G(GET(356,IENS,$P(FIELDS,U,J),"E"))]"" $P(Y,U,J+1)=GET(356,IENS,$P(FIELDS,U,J),"E")
 .S ^TMP("VEJDPX1C",$J,AXYY)=Y,AXYY=AXYY+1
 S:'$D(^TMP("VEJDPX1C",$J,0)) ^TMP("VEJDPX1C",$J,0)="-1^No entries found!" Q
 Q
GET399(AXY,IFN) ;
 I '$G(IFN) S AXY(0)="-1^INVALID INPUT!" Q
 N XX,YY,FIL S (AXY,XX)=0,FIL=399.0304
 F  S XX=$O(^DGCR(399,IFN,"CP",XX)) Q:'XX  D
 .N GET,IENS,II S IENS=XX_","_IFN_"," D GETS^DIQ(FIL,IENS,"*","EI","GET")
 .I GET(FIL,IENS,8,"I") S GET(FIL,IENS,8,"E")=GET(FIL,IENS,8,"E")_" - "_$P($G(^IBE(353.1,GET(FIL,IENS,8,"I"),0)),U,3)
 .I GET(FIL,IENS,9,"I") S GET(FIL,IENS,9,"E")=GET(FIL,IENS,9,"E")_" - "_$P($G(^IBE(353.2,GET(FIL,IENS,9,"I"),0)),U,3)
 .I GET(FIL,IENS,14,"I") S GET(FIL,IENS,14,"E")=GET(FIL,IENS,14,"I")_";"_GET(FIL,IENS,14,"E")_" - "_$E($P($G(^DIC(81.3,GET(FIL,IENS,14,"I"),0)),U,2),1,10)
 .D:$O(^DGCR(399,IFN,"CP",XX,"MOD",0))
 ..N ZZ,MOD,MODS,MODIF S ZZ=0,MODS=""
 ..F  S ZZ=$O(^DGCR(399,IFN,"CP",XX,"MOD",ZZ)) Q:'ZZ  D
 ...S MOD=+$P(^(ZZ,0),U,2)
 ...S MODIF=$G(^DIC(81.3,MOD,0))
 ...S MOD=MOD_";"_$P(MODIF,U)_" - "_$P(MODIF,U,2)_";"_$E($P(^DGCR(399,IFN,"CP",XX,"MOD",ZZ,0),U),1,10)
 ...S MODS=MODS_MOD_":"
 ..S GET(FIL,IENS,14,"E")=$E(MODS,1,$L(MODS)-1)
 .S YY=XX_U_$G(GET(FIL,IENS,.01,"E"))_U_$G(GET(FIL,IENS,1,"E"))_U_$G(GET(FIL,IENS,3,"E"))_U
 .F II=4,5,6,8:1:13 S YY=YY_$S($G(GET(FIL,IENS,II,"E"))]"":GET(FIL,IENS,II,"I")_";"_GET(FIL,IENS,II,"E"),1:"")_U
 .S YY=YY_GET(FIL,IENS,14,"E")_U_$S($G(GET(FIL,IENS,18,"E"))]"":GET(FIL,IENS,18,"I")_";"_GET(FIL,IENS,18,"E"),1:"")
 .S AXY(AXY)=$E(YY,1,$L(YY)-1),AXY=AXY+1
 Q
GETLOG(AXY,IFN,FIL,FIELDS)      ;
 S AXY=$NA(^TMP("VEJDPX1L",$J)) I '$G(FIL)!'$G(IFN)!($G(FIELDS)="") S ^TMP("VEJDPX1L",$J,0)="-1^INVALID INPUT!" Q
 N NOD,XX,YY,FLDS,AXYY S NOD="VEJDPX1L",(AXYY,XX)=0,FLDS=$TR(FIELDS,U,";")
 F  S XX=$O(^VEJD(FIL,"ALOG",IFN,XX)) Q:'XX  D
 .N GET,IENS,II S IENS=XX_"," D GETS^DIQ(FIL,IENS,FLDS,"E","GET")
 .S YY=XX
 .F II=1:1:$L(FIELDS,U) S:$G(GET(FIL,IENS,$P(FIELDS,U,II),"E"))]"" $P(YY,U,II+1)=GET(FIL,IENS,$P(FIELDS,U,II),"E")
 .S ^TMP(NOD,$J,AXYY)=YY,AXYY=AXYY+1
 Q
RENDPRV(AXY,IFN)        ;
 I '$G(IFN) S AXY="-1^Invalid Input!" Q
 N PRV,XX,YY,ZZ S AXY=""
 S YY=0 F  S YY=$O(^DGCR(399,IFN,"PRV",YY)) Q:'YY  S XX=$G(^(YY,0)) I +XX=3 D  Q
 .S PRV=$P(XX,U,2)
 .I PRV?.E1" (".N1")" S PRV=$P($P(PRV,")"),"(",2)_";"_$P(PRV," (")
 .I 'PRV,$E(PRV)'="*" S PRV="*"_PRV
 .S AXY=YY_U_PRV
 S:AXY="" AXY="0^No Rendering Provider Found!"
 Q
VSTDIVX(VST) ;Extrinsic Function to return Division from a Visit File entry
 I '$G(VST)!'$D(^AUPNVSIT(VST,0)) Q "-1^Unknown/Invalid Visit File reference!"
 N LOC,DIV
 S LOC=+$P($G(^AUPNVSIT(VST,0)),U,22) I 'LOC!'$D(^SC(LOC,0)) Q "-1^Unknown/Invalid Hospital Location reference!"
 S DIV=+$P($G(^SC(LOC,0)),U,15) I 'DIV!'$D(^DG(40.8,DIV,0)) Q "-1^Unknown/Invalid Division reference!"
 Q DIV_U_$P(^DG(40.8,DIV,0),U)
