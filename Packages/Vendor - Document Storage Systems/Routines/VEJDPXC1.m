VEJDPXC1 ;AMC - Document Storage Systems  RPC Broker calls for adding new visit
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
EDPRV(XX)   ;
 N DATA,DA,DIK,CLS,VEJD,IENS,FIL,NEW,ERR,VIEN S DATA=ARRY("P",XX)
 I $P(DATA,U,5) S DA=$P(DATA,U),DIK="^AUPNVPRV(" D ^DIK Q
 S NEW='DATA,IENS=$S(DATA:+DATA,1:"+1")_",",FIL=9000010.06
 I NEW S VEJD(FIL,IENS,.02)=DFN,VEJD(FIL,IENS,.03)=VISIT
 S VEJD(FIL,IENS,.01)=$P(DATA,U,3)
 I $P(DATA,U,4)]"" S VEJD(FIL,IENS,.04)=$P(DATA,U,4) S:$P(DATA,U,4)="P" ENCPRV=$P(DATA,U,3)
 S VEJD(FIL,IENS,.05)=$S($P(DATA,U,6)]"":"A",1:"@")
 S:$P(DATA,U,7)]"" VEJD(FIL,IENS,81101)=$P(DATA,U,7)
 D PERSCLS(.CLS,$P(DATA,U,3),APPTDT) I $G(CLS) S VEJD(FIL,IENS,.06)=CLS
 D:NEW UPDATE^DIE(,"VEJD","VIEN","ERR") D:'NEW FILE^DIE(,"VEJD","ERR")
 Q
 ;
EDPOV(XX)       ;
 N DATA,DIK,DA,VEJD,FIL,NEW,ERR,VIEN,VEJDA,II S DATA=ARRY("D",XX) Q:'$P(DATA,U)&'$P(DATA,U,4)
 I $P(DATA,U,14) S DA=$P(DATA,U),DIK="^AUPNVPOV(" D ^DIK Q
 I $P(DATA,U,15)=""!($P(DATA,U,15)=0) S $P(DATA,U,15)=$S($G(^ICD9(+$P(DATA,U,4),1))]"":^(1),$P($G(^ICD9(+$P(DATA,U,4),0)),U,3)]"":$P(^(0),U,3),1:"UNKNOWN NARRATIVE")
 S NEW='DATA,IENS=$S(DATA:+DATA,1:"+1")_",",FIL=9000010.07
 I NEW S VEJD(FIL,IENS,.02)=DFN,VEJD(FIL,IENS,.03)=VISIT
 S VEJD(FIL,IENS,.01)=$P(DATA,U,4)
 S:$P(DATA,U,6)]"" VEJD(FIL,IENS,.12)=$E("SP",$P(DATA,U,6)+1)
 S VEJD(FIL,IENS,1204)=+$P(DATA,U,7)
 S VEJD(FIL,IENS,80001)=$P(DATA,U,8),VEJD(FIL,IENS,80002)=$P(DATA,U,9),VEJD(FIL,IENS,80003)=$P(DATA,U,10),VEJD(FIL,IENS,80004)=$P(DATA,U,11)
 S VEJD(FIL,IENS,80005)=$P(DATA,U,13),VEJD(FIL,IENS,80006)=$P(DATA,U,16)
 F II=1:1:6 I $G(VEJD(FIL,IENS,80000+II)) S VEJDA(9000010,VISIT_",",80000+II)=1
 I $D(VEJDA) D FILE^DIE(,"VEJDA","ERR")
 S VEJD(FIL,IENS,.04)=$$NEWPRNA^VEJDPXCX($P(DATA,U,15),FIL)
 D:NEW UPDATE^DIE(,"VEJD","VIEN","ERR") D:'NEW FILE^DIE(,"VEJD","ERR")
 Q
ADDCPT(XX)      ;
 ;
 N DATA,YY,CPT,NMODS,NMOD,JJ,POVNA,VEJD,FIL,IENS,VIEN,LINKS S DATA=ARRY("C",XX),FIL=9000010.18,IENS="+1,"
 I '$G(SOURCEI) S SOURCEI=$O(^PX(839.7,"B",SOURCE,0))
 S POVNA=$$NEWPRNA^VEJDPXCX($P($G(^ICPT(+$P(DATA,U,4),0)),U,2),FIL)
 S VEJD(FIL,IENS,.01)=$P(DATA,U,4)
 S VEJD(FIL,IENS,.02)=DFN,VEJD(FIL,IENS,.03)=VISIT,VEJD(FIL,IENS,.04)=POVNA
 S:$P(DATA,U,5) VEJD(FIL,IENS,.16)=$P(DATA,U,5)
 S:$P(DATA,U,8) VEJD(FIL,IENS,1204)=+$P(DATA,U,8)
 S:PACKAGE VEJD(FIL,IENS,81202)=PACKAGE S:SOURCEI VEJD(FIL,IENS,81203)=SOURCEI
 I $$PATCH^XPDUTL("PX*1.0*124"),$P(DATA,U,9)]"" S LINKS=$P(DATA,U,9) F JJ=1:1:8 S VEJD(FIL,IENS,$P(".05^.09^.1^.11^.12^.13^.14^.15",U,JJ))=$S($P(LINKS,";",JJ)="@":"",1:$O(^ICD9("BA",$P(LINKS,";",JJ)_$S($P(LINKS,";",JJ)[".":"",1:".")_" ",0)))
 D
 .S NMODS=$P(ARRY("C",XX),U,6) Q:NMODS=""  F JJ=1:1:$L(NMODS,":") S NMOD=$P($P(NMODS,":",JJ),";"),VEJD(FIL_1,"+"_(JJ+1)_",+1,",.01)=$$ACTMOD(NMOD) ;$O(^DIC(81.3,"B",NMOD,0))
 .;S JJ=0 F YY=2:1 S JJ=$O(NMODS(JJ)) Q:JJ=""  S VEJD(FIL_1,"+"_YY_",+1,",.01)=NMODS(JJ)
 D UPDATE^DIE(,"VEJD","VIEN","ERR")
 D:$$ENM(DATA) ENM^VEJDENM(VISIT,DATA)
 Q
ACTMOD(X) ;
 Q:$G(X)="" ""
 N Y S Y=0 F  S Y=$O(^DIC(81.3,"B",X,Y)) Q:'Y!'$P($G(^DIC(81.3,Y,0)),U,5)
 Q:'Y ""
 Q Y
EDENC ;
 N DIE,DA,DR,X,Y,DATA,JJ,VEJD,FIL,IENS S FIL=9000010,IENS=VISIT_","
 S DATA=ARRAY(1)
 S VEJD(FIL,IENS,.07)=$P(DATA,U) S:$P($G(^AUPNVSIT(VISIT,0)),U,23) VEJD(FIL,IENS,.13)=DT S:'$P(DATA,U,4) VEJD(FIL,IENS,.18)=CHKOUT
 S:SDCL&($P(^AUPNVSIT(VISIT,0),U,22)'=SDCL) VEJD(FIL,IENS,.22)=SDCL
 F JJ=6:1:12 S:$P(DATA,U,JJ)]"" VEJD(FIL,IENS,79995+JJ)=$P(DATA,U,JJ)
 D FILE^DIE(,"VEJD","ERR")
 Q
PERSCLS(AXY,IFN,TRDT)   ;
 I '$G(IFN)!'$G(TRDT) S AXY="-1^INVALID INPUT!" Q
 N CLS,MM,EDT,TDT
 S MM=0,AXY="" F  S MM=$O(^VA(200,IFN,"USC1",MM)) Q:'MM  I $G(^(MM,0))]"" D  Q:AXY]""
 .S CLS=^(0),EDT=$P(CLS,U,2),TDT=$P(CLS,U,3),CLS=+CLS
 .I TRDT'<EDT,(TRDT'>TDT!'TDT) S AXY=CLS
 Q
 ;
PKG ;  return package pointer for VEJD or 0 if failed
 Q $$PKG^VEJDXPDU
 ;
SOURCE ;  ien to SOURCE file or 0 if failed
 Q +$O(^PX(839.7,"B","VEJD DSS PCE",0))
 ; 
ENM(XX) ;Check to see if this CPT code is an E&M code
 N X S X="VEJDENM" X ^%ZOSF("TEST") I '$T Q 0
 Q:'$D(^VEJD(19610.7,0)) 0
 Q:$P(XX,U,3)>99000 1
 Q 0
