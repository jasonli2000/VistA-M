VEJDPXCO        ;AMC-Document Storage Systems; RPC Broker calls for adding new visit
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
UPDATE(AXY,DFN,SDCL,APPTDT,SDCOD,VISIT,ARRAY) ;Update the visit and related v files
 I '$G(DFN)!'$G(APPTDT)!'$G(SDCL) S AXY=0 Q
 I '$O(ARRAY(0)) S AXY=$G(VISIT) Q
 S APPTDT=+$E(APPTDT,1,12),CHKOUT=$P(ARRAY(1),U,2),ZTQUEUED=1,VALQUIET=1
 S:'CHKOUT CHKOUT=$$NOW^XLFDT
 N XX,TYPE,VEJDERR,IEN,ARRY,VEJDED,PACKAGE,SOURCE,SOURCEI,VNEW
 S VNEW='VISIT,VEJDERR="",PACKAGE=$$PKG^DSICXPDU,SOURCE="VEJD DSS PCE"
 I 'PACKAGE S AXY="-1^NO PACKAGE INFO" Q
 S X=1 F  S X=$O(ARRAY(X)) Q:X=""  S ARRY($P(ARRAY(X),U),X+1)=$P(ARRAY(X),U,2,999)
 ;
 N VEJDARY,VEJDARYD,VEJD,ENCED,VEJDPRVC S ENCED=0,VEJDPRVC="" K ^TMP("NEW PXKCO",$J)
 ;
 I $G(VISIT) S ENCED=1 S:'$$BEFORE^VEJDPXBA(VISIT) VEJDERR="BEFORE" G:VEJDERR]"" ERROR D EDENC^VEJDPXC1 G ENCA
 S VEJDARY("ENCOUNTER",1,"ENC D/T")=APPTDT
 S VEJDARY("ENCOUNTER",1,"PATIENT")=DFN
 S VEJDARY("ENCOUNTER",1,"HOS LOC")=SDCL
 S VEJDARY("ENCOUNTER",1,"SERVICE CATEGORY")=$P(ARRAY(1),U)
 S VEJDARY("ENCOUNTER",1,"ENCOUNTER TYPE")="P"
 S:'$P(ARRAY(1),U,4) VEJDARY("ENCOUNTER",1,"CHECKOUT D/T")=CHKOUT
 S:$P(ARRAY(1),U,6)]"" VEJDARY("ENCOUNTER",1,"SC")=$P(ARRAY(1),U,6)
 S:$P(ARRAY(1),U,7)]"" VEJDARY("ENCOUNTER",1,"AO")=$P(ARRAY(1),U,7)
 S:$P(ARRAY(1),U,8)]"" VEJDARY("ENCOUNTER",1,"IR")=$P(ARRAY(1),U,8)
 S:$P(ARRAY(1),U,9)]"" VEJDARY("ENCOUNTER",1,"EC")=$P(ARRAY(1),U,9)
 S:$P(ARRAY(1),U,10) VEJDARY("ENCOUNTER",1,"MST")=$P(ARRAY(1),U,10)
 S:$P(ARRAY(1),U,11) VEJDARY("ENCOUNTER",1,"HNC")=$P(ARRAY(1),U,11)
 S:$P(ARRAY(1),U,12) VEJDARY("ENCOUNTER",1,"CV")=$P(ARRAY(1),U,12)
ENCA ;
 D EN^VEJDPXCY I $O(VEJDPRVC(0)) D PRVCMT
 I 'VISIT S VEJDERR="DATA2PCE" G ERROR
 D:'ENCED EDENC^VEJDPXC1
 ;
 D EN^VEJDPXCX
 S:'$$AFTER^VEJDPXBA(VISIT) VEJDERR="AFTER" G:VEJDERR]"" ERROR M ^TMP("PXKCO",$J)=^TMP("NEW PXKCO",$J) D EVENT^PXKMAIN K ^TMP("PXKCO",$J),^TMP("NEW PXKCO",$J)
 I $D(ARRY("~")) D EN^VEJDPXCW(VISIT,VNEW)
 S AXY=VISIT Q
 ;
ERROR ;
 S AXY="-1^"_VEJDERR_" record type error!" Q
PREC ;
 S DELFLG=$P(ARRAY(XX),U,3)="@",P04=$P($P(ARRAY(XX),U,5),";"),P1201=$P(ARRAY(XX),U,6),P1204=$P($P(ARRAY(XX),U,7),";"),P001=$P(ARRAY(XX),U,2)
 S:'P1204&$G(ENCPRV) P1204=ENCPRV
 S:'P1201&CHKOUT P1201=CHKOUT
 Q
EDIT(FILE,FIELDS,IENS)  ;
 N BEFORE,DIC,DR,DIE,DA,FLD,I,VAR,CPT
 D GETS^DIQ(FILE,IENS,FIELDS,"I","BEFORE")
 S DIE=^DIC(FILE,0,"GL"),DR="",(CPT,DA)=IENS,IENS=IENS_","
 F I=1:1:$L(FIELDS,";") S FLD=$P(FIELDS,";",I),VAR="P"_$TR(FLD,".","") I @VAR'=BEFORE(FILE,IENS,FLD,"I") S DR=DR_FLD_"///"_$S(@VAR]"":"/",1:"@")_@VAR_";"
 I DR]"" S DR=$E(DR,1,$L(DR)-1) D ^DIE
 Q
IMDECPT ;
 N IMM,IEN,DIK,CPT,DA,DEL D GETCPT
 S IMM=P01_";AUTTIMM(",IEN=0 F  S IEN=$O(^PXD(811.1,"B",IMM,IEN)) Q:'IEN  D
 .S IMM(0)=^PXD(811.1,IEN,0),CPT=+$P(IMM(0),U,2),DEL=$P(IMM(0),U,4)="CPT"
 .I DEL,$G(CPT(CPT)) S DIK="^AUPNVCPT(",DA=CPT(CPT) D ^DIK
 Q
SKDECPT ;
 N SK,IEN,DIK,CPT,DA,DEL D GETCPT
 S SK=P01_";AUTTSK(",IEN=0 F  S IEN=$O(^PXD(811.1,"B",SK,IEN)) Q:'IEN  D
 .S SK(0)=^PXD(811.1,IEN,0),CPT=+$P(SK(0),U,2),DEL=$P(SK(0),U,4)="CPT"
 .I DEL,$G(CPT(CPT)) S DIK="^AUPNVCPT(",DA=CPT(CPT) D ^DIK
 Q
GETCPT ;
 N X S X=0
 F  S X=$O(^AUPNVCPT("AD",VISIT,X)) Q:'X  S CPT($P(^AUPNVCPT(X,0),U))=X
 Q
PRVCMT ;
 Q:'VISIT
 N VIS,PRV,PRVAR,CMT
 S PRV=0 F  S PRV=$O(VEJDPRVC(PRV)) Q:'PRV  S PRVAR(+$P(VEJDPRVC(PRV),U,3))=$P(VEJDPRVC(PRV),U,7)
 S VIS=0 F  S VIS=$O(^AUPNVPRV("AD",VISIT,VIS)) Q:'VIS  D:$D(PRVAR($P($G(^AUPNVPRV(VIS,0)),U)))
 .N DIE,DA,DR
 .S PRV=+^AUPNVPRV(VIS,0),CMT=PRVAR(PRV)
 .S DIE="^AUPNVPRV(",DA=VIS,DR="81101////"_CMT D ^DIE
 Q
