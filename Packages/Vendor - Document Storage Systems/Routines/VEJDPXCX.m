VEJDPXCX ;AMC - Document Storage Systems ;RPC Broker call extension
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
EN ;
 S SOURCEI=$O(^PX(839.7,"B",SOURCE,0))
 S XX=0 F  S XX=$O(ARRAY(XX)) Q:'XX  D  G:$G(VEJDERR)]"" ERROR^VEJDPXCO
 .S TYPE=$P(ARRAY(XX),U),P01=$P(ARRAY(XX),U,4) Q:TYPE="A"
 .I TYPE="HF" D  Q
 ..D PREC^VEJDPXCO
 ..I P001&'DELFLG D EDIT^VEJDPXCO(9000010.23,".01;.04;1201;1204",P001) Q
 ..I P001 S DIK="^AUPNVHF(",DA=P001 D ^DIK Q
 ..S (DIE,DIC)="^AUPNVHF(",DIC(0)="",X=P01 K DD,DO D FILE^DICN I Y<0 S VEJDERR="Health Factor" Q
 ..S DA=+Y,DR=".02////"_DFN_";.03////"_VISIT S:P04]"" DR=DR_";.04////"_P04
 ..S:P1204]"" DR=DR_";1204////"_P1204 S:P1201]"" DR=DR_";1201////"_P1201
 ..S:PACKAGE DR=DR_";81202////"_PACKAGE S:SOURCEI DR=DR_";81203////"_SOURCEI
 ..D ^DIE K DR,P01,P04,P1201,P1204
 .;
 .I TYPE="IMM" D  Q
 ..D PREC^VEJDPXCO
 ..S P06=$P($P(ARRAY(XX),U,8),";"),P07=$P($P(ARRAY(XX),U,9),";")
 ..I P001&'DELFLG D EDIT^VEJDPXCO(9000010.11,".01;.04;.06;.07;1201;1204",P001) Q
 ..I P001 S DIK="^AUPNVIMM(",DA=P001 D ^DIK,IMDECPT^VEJDPXCO Q
 ..S:$D(^PXD(811.1,"B",P01_";AUTTIMM(")) VEJD("IMM",P01_";AUTTIMM(")=P1201_U_P1204
 ..S (DIE,DIC)="^AUPNVIMM(",DIC(0)="",X=P01 K DD,DO D FILE^DICN I Y<0 S VEJDERR="Immunization" Q
 ..S DA=+Y,DR=".02////"_DFN_";.03////"_VISIT S:P04]"" DR=DR_";.04////"_P04 S:P06]"" DR=DR_";.06////"_P06
 ..S:P07]"" DR=DR_";.07////"_P07
 ..S:P1201 DR=DR_";1201////"_P1201 S:P1204 DR=DR_";1204////"_P1204
 ..S:PACKAGE DR=DR_";81202////"_PACKAGE S:SOURCEI DR=DR_";81203////"_SOURCEI
 ..D ^DIE K DR,P01,P04,P06,P07,P1201,P1204
 .;
 .I TYPE="EX" D  Q
 ..D PREC^VEJDPXCO
 ..I P001&'DELFLG D EDIT^VEJDPXCO(9000010.13,".01;.04;1201;1204",P001) Q
 ..I P001 S DIK="^AUPNVXAM(",DA=P001 D ^DIK Q
 ..S (DIE,DIC)="^AUPNVXAM(",DIC(0)="",X=P01 K DD,DO D FILE^DICN I Y<0 S VEJDERR="Exam" Q
 ..S DA=+Y,DR=".02////"_DFN_";.03////"_VISIT S:P04]"" DR=DR_";.04////"_P04
 ..S:P1201 DR=DR_";1201////"_P1201 S:P1204 DR=DR_";1204////"_P1204
 ..S:PACKAGE DR=DR_";81202////"_PACKAGE S:SOURCEI DR=DR_";81203////"_SOURCEI
 ..D ^DIE K DR,P01,P04,P1201,P1204
 .;
 .I TYPE="ET" D  Q
 ..D PREC^VEJDPXCO
 ..S P06=$P($P(ARRAY(XX),U,8),";")
 ..I P001&'DELFLG D EDIT^VEJDPXCO(9000010.16,".01;.06;1201;1204",P001) Q
 ..I P001 S DIK="^AUPNVPED(",DA=P001 D ^DIK Q
 ..S (DIE,DIC)="^AUPNVPED(",DIC(0)="",X=P01 K DD,DO D FILE^DICN I Y<0 S VEJDERR="Patient Education" Q
 ..S DA=+Y,DR=".02////"_DFN_";.03////"_VISIT S:P06]"" DR=DR_";.06////"_P06
 ..S:P1201 DR=DR_";1201////"_P1201 S:P1204 DR=DR_";1204////"_P1204
 ..S:PACKAGE DR=DR_";81202////"_PACKAGE S:SOURCEI DR=DR_";81203////"_SOURCEI
 ..D ^DIE K DR,P01,P04,P06,P1201,P1204
 .;
 .I TYPE="ST" D  Q
 ..D PREC^VEJDPXCO
 ..S P05=$P(ARRAY(XX),U,8),P06=$P(ARRAY(XX),U,9)
 ..I P001&'DELFLG D EDIT^VEJDPXCO(9000010.12,".01;.04;.05;.06;1201;1204",P001) Q
 ..I P001 S DIK="^AUPNVSK(",DA=P001 D ^DIK,SKDECPT^VEJDPXCO Q
 ..S (DIE,DIC)="^AUPNVSK(",DIC(0)="",X=P01 K DD,DO D FILE^DICN I Y<0 S VEJDERR="Skin Test" Q
 ..S DA=+Y,DR=".02////"_DFN_";.03////"_VISIT S:P04]"" DR=DR_";.04////"_P04
 ..S:P05]"" DR=DR_";.05////"_P05 S:P06]"" DR=DR_";.06////"_P06
 ..S:P1201 DR=DR_";1201////"_P1201 S:P1204 DR=DR_";1204////"_P1204
 ..S:PACKAGE DR=DR_";81202////"_PACKAGE S:SOURCEI DR=DR_";81203////"_SOURCEI
 ..S:$D(^PXD(811.1,"B",P01_";AUTTSK(")) VEJD("SK",P01_";AUTTSK(")=P1201_U_P1204
 ..D ^DIE K DR,P01,P04,P05,P1201,P1202,P1204
 .;
 .I TYPE="TR" D  Q
 ..D PREC^VEJDPXCO
 ..S P1202=+$P(ARRAY(XX),U,8),P06=$S($P(ARRAY(XX),U,9)]"":$P(ARRAY(XX),U,9),1:$P(ARRAY(XX),U,3)) S:P06'=+P06 P06=$$NEWPRNA(P06,9000010.15)
 ..I P001&'DELFLG D EDIT^VEJDPXCO(9000010.15,".01;.04;.06;1201;1202;1204",P001) Q
 ..I P001 S DIK="^AUPNVTRT(",DA=P001 D ^DIK Q
 ..S (DIE,DIC)="^AUPNVTRT(",DIC(0)="",X=P01 K DD,DO D FILE^DICN I Y<0 S VEJDERR="Treatment" Q
 ..S DA=+Y,DR=".02////"_DFN_";.03////"_VISIT S:P04 DR=DR_";.04////"_P04
 ..S:P06 DR=DR_";.06////"_P06 S:P1201 DR=DR_";1201////"_P1201
 ..S:P1202]"" DR=DR_";1202////"_P1202 S:P1204 DR=DR_";1204////"_P1204
 ..S:PACKAGE DR=DR_";81202////"_PACKAGE S:SOURCEI DR=DR_";81203////"_SOURCEI
 ..S PXKLAYGO=9999999.27 D ^DIE K DR,P04,P1201,P1202,P1204
 ;
 D:$D(VEJD("IMM"))!$D(VEJD("SK"))
 .N IMM,SK,IEN,DIC,DIE,DR,DA
 .S IMM="" F  S IMM=$O(VEJD("IMM",IMM)) Q:IMM=""  D
 ..S P1201=VEJD("IMM",IMM),P1204=$P(P1201,U,2),P1201=$P(P1201,U)
 ..S IEN=0 F  S IEN=$O(^PXD(811.1,"B",IMM,IEN)) Q:'IEN  D:$P($G(^PXD(811.1,IEN,0)),U,5)
 ...S IMM(0)=^PXD(811.1,IEN,0) D:$P(IMM(0),U,4)="CPT"&'$D(VEJD("CPT",+$P(IMM(0),U,2)))
 ....S X=+$P(IMM(0),U,2),CPTNAR=$$CPTNAR(X)
 ....S DIC="^AUPNVCPT(",DIC(0)="" K DD,DO D FILE^DICN I Y<0 S VEJDERR="PCE Code Map IMM" Q
 ....S DIE="^AUPNVCPT(",DA=+Y,DR=".02////"_DFN_";.03////"_VISIT_";.04////"_CPTNAR_";.16////1" S:P1201 DR=DR_";1201////"_P1201 S:P1204 DR=DR_";1204////"_P1204
 ....S:PACKAGE DR=DR_";81202////"_PACKAGE S:SOURCEI DR=DR_";81203////"_SOURCEI
 ....D ^DIE K DR
 .S SK="" F  S SK=$O(VEJD("SK",SK)) Q:SK=""  D
 ..S P1201=VEJD("SK",SK),P1204=$P(P1201,U,2),P1201=$P(P1201,U)
 ..S IEN=0 F  S IEN=$O(^PXD(811.1,"B",SK,IEN)) Q:'IEN  D:$P($G(^PXD(811.1,IEN,0)),U,5)
 ...S SK(0)=^PXD(811.1,IEN,0) D:$P(SK(0),U,4)="CPT"&'$D(VEJD("CPT",+$P(SK(0),U,2)))
 ....S X=+$P(SK(0),U,2),CPTNAR=$$CPTNAR(X)
 ....S DIC="^AUPNVCPT(",DIC(0)="" K DD,DO D FILE^DICN I Y<0 S VEJDERR="PCE Code Map SK" Q
 ....S DIE="^AUPNVCPT(",DA=+Y,DR=".02////"_DFN_";.03////"_VISIT_";.04////"_CPTNAR_";.16////1" S:P1201 DR=DR_";1201////"_P1201 S:P1204 DR=DR_";1204////"_P1204
 ....S:PACKAGE DR=DR_";81202////"_PACKAGE S:SOURCEI DR=DR_";81203////"_SOURCEI
 ....D ^DIE K DR
 Q
NEWPRNA(X,FIL) ;
 N VDATA,NARA,ERR S NARA=0 Q:$G(X)="" 0
 I $O(^AUTNPOV("B",$E(X,1,30),0)) F  S NARA=$O(^AUTNPOV("B",$E(X,1,30),NARA)) Q:'NARA  Q:$P(^AUTNPOV(NARA,0),U,1)=X
 Q:NARA NARA
 S X=$TR(X,"^",""),VDATA(9999999.27,"+1,",.01)=X,VDATA(9999999.27,"+1,",75702)=$G(FIL)
 D UPDATE^DIE(,"VDATA","NARA","ERR")
 I $D(DIERR) Q 0
 Q NARA(1)
CPTNAR(X) ;
 N CPTDESC
 S CPTDESC=$P($G(^ICPT(X,0)),U,2) I CPTDESC="" S CPTDESC="UNKNOWN VCPT DESCRIPTION"
 Q $$NEWPRNA(CPTDESC,9000010.18)
