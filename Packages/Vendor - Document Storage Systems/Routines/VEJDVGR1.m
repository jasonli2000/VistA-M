VEJDVGR1 ;DSS/DCN RPC FOR INTRINSIQ ORDERS; VISTA GATEWAY
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
 Q
EN(RET,ORVP,ORNP,ORL,UDIV,ES,ORDIALOG) ;ORDER ENTRY POINT. var ORDIALOG is ARRAY based on UDIV value
 I '$G(ORVP) S RET(0)="-1^No Patient Specified" Q
 I '$D(^DPT(+ORVP)) S RET(0)="-1^Patient does not exist" Q
 I '$G(ORNP) S RET(0)="-1^No Provider Specified" Q
 I '$D(^VA(200,+ORNP,0)) S RET(0)="-1^Provider does not exist" Q
 I ES'="" D VALIDSIG^VEJDVGR0(.RET,+$G(ORNP),$G(ES)) I RET<0 S RET(0)="-1^Invalid ES code" Q  ;VALIDATE ELECTRONIC SIGNATURE
 S RET(0)="-1^NOT DEFINED CORRECTLY" ;ERROR MESSAGE
 S (REC,ORIFN)="" ;INIT VAR'S
 I UDIV="UD" S DLG="PSJ OR PAT OE",DRUG=ORDIALOG(4,1)
 I UDIV="IV" S DLG="PSJI OR PAT FLUID OE"
 I UDIV="RAD" S DLG="RA OERR EXAM"
 I UDIV="OPM" S DLG="PSO OERR"
 I UDIV="WP" S DLG="OR GXTEXT WORD PROCESSING ORDER"
 I UDIV="LAB" S DLG="LR OTHER LAB TESTS"
 I UDIV="CON" S DLG="GMRCOR CONSULT"
 I UDIV="PROC" S DLG="GMRCOR REQUEST"
 I UDIV="DIET" S DLG="FHW1",ORDIALOG("ORTRAIL")="Diet"
 ;CHANGE THE CHECKING LOGIC DCN/DSS MAR 10,2005
 S DLGI=$$FIND1^DIC(101.41,"","O",DLG) I +DLGI<1 S RET(0)="Order Dialog '"_DLG_"', does not exist in file 101.41" Q
 S ORDG=$$GET1^DIQ(101.41,DLGI,5,"I"),(ORIT,ORDIALOG)=DLGI
 ;S I=$O(^ORD(101.41,"B",DLG,0)) I 'I S RET(0)="Order Dialog '"_DLG_"', does not exist in file 101.41" Q
 ;S X=$G(^ORD(101.41,I,0)) I X="" S RET(0)="Order Dialog '"_DLG_"', does not exist in file 101.41" Q
 ;S (ORIT,ORDIALOG)=I,ORDG=$P(X,"^",5)
 ;LOOK FOR MULTIPLES
 I $G(ORDIALOG(131,1))["^" D
 . S I=0,DATA=ORDIALOG(131,1) F  S I=I+1,VAL=$P(DATA,"^",I) Q:VAL=""  S ORDIALOG(131,I)=VAL
 I $G(ORDIALOG(30,1))["^" D
 . S I=0,DATA=ORDIALOG(30,1) F  S I=I+1,VAL=$P(DATA,"^",I) Q:VAL=""  S ORDIALOG(30,I)=VAL
 I $G(ORDIALOG(152,1))["^" D
 . S I=0,DATA=ORDIALOG(152,1) F  S I=I+1,VAL=$P(DATA,"^",I) Q:VAL=""  S ORDIALOG(152,I)=VAL
 D GETDLG1^ORCD(ORDIALOG) ;GET THE ARRAY
 D SAVE^ORWDX(.REC,ORVP,ORNP,ORL,DLG,ORDG,ORIT,ORIFN,.ORDIALOG)
 S ROOT=+$P(REC(1),"~",2)
 D GETORDER^ORCD(ROOT,"ORDIALOG")
 S ORIFN=ROOT ;PUT ORDER NUMBER IN CORRECT VAR
 D RESPONSE^ORCSAVE ;SAVE THE RESPONSES
 D ORDTEXT^ORCSAVE1(ORIFN_";1") ;ADD TO NEW ORDER
 S RET(0)=ORIFN_"^Order placed^"_$G(REC(1))_"^"_$P($G(REC(2)),"*UNSIGNED*")
 ;now setup for ES
 S ORWLST="",ORWREC(1)=ORIFN_";"_"1^1^1^E" ;ACTION^SIG STAT^REL STAT^NAT OF ORDER
 D SEND^ORWDX(.ORWLST,ORVP,ORNP,ORL,ES,.ORWREC)
 I $G(FLAG)="D",$P($G(ORWLST(1)),"^",2)="E" S RET(0)="-1^"_$P($G(ORWLST(1)),"^",4)
 ;NOW CHECK FOR IV's THAT HAVE A ROUTE THAT IS NOT IV--ADDED MAR 22, 2005 DCN/DSS
 I UDIV="IV" D
 . S IVROUTE=$$FIND1^DIC(51.2,,"X","INTRAVENOUS") Q:IVROUTE<1
 . S IVTYDIA=$$FIND1^DIC(101.41,,"X","ORZ VEJD INTQ IV TYPE") Q:IVTYDIA<1
 . I $G(ORDIALOG(137,1))="" Q  ;S ORDIALOG(137,1)=IVROUTE
 . ;I $G(ORDIALOG(IVTYDIA,1))="" Q  ;NEEDS AN IV TYPE TO CONTINUE 
 . ;I ORDIALOG(137,1)'=IVROUTE  ;RUN THE NEXT LINE WITHOUT THIS SCREEN
 . D UPDTRT(ORIFN,ORDIALOG(137,1),ORDIALOG(IVTYDIA,1)) ;BRING OVER THE ARRAY
 Q
DEL(RET,ORIFN,ORNP,ORL,REA,ES) ;DELETE ORDER RPC
 ;ORIFN NEEDS VERSION, IE, 12345;1
 ;REA IS SET OF CODES FOR REASON TO DELETE
 S RET(0)="-1^NO DELETION OCCURED"
 I +$G(ORIFN)<1 S RET(0)="-1^ORDER NUMBER NOT DEFINED" Q
 I '$G(ORNP) S RET(0)="-1^No Provider Specified" Q
 I '$D(^VA(200,+ORNP,0)) S RET(0)="-1^Provider does not exist" Q
 I '$G(ORL) S RET(0)="-1^ORDERING LOCATION NOT DEFINED" Q
 I '$D(^SC(ORL,0)) S RET(0)="-1^ORDERING LOCATION NOT FOUND" Q
 I ES="" S RET(0)="-1^NO ES code entered" Q
 I $G(^OR(100,+ORIFN,0))="" S RET(0)="-1^ORDER DOES NOT EXIST" Q  ;ADDED DCN/DSS 3/8/05
 D VALIDSIG^VEJDVGR0(.RET,+$G(ORNP),$G(ES)) I RET<0 S RET(0)="-1^Invalid ES code" Q  ;VALIDATE ELECTRONIC SIGNATURE
 S VAL="",VER=$P($G(^OR(100,+ORIFN,8,0)),"^",3),ORIFN=+ORIFN_";"_VER
 S ORVP=+$P(^OR(100,+ORIFN,0),"^",2) ;GET THE PATIENT DFN
 D VALID^ORWDXA(VAL,ORIFN,"DC",ORNP) I VAL]"" S RET(0)="-1^"_VAL Q
 S REC="" ;14 is Requesting Physician Cancelled
 D DC^ORWDXA(.REC,ORIFN,ORNP,ORL,14)
 S NIFN=+ORIFN_";"_(VER+1)
 ;now setup for ES
ES S ORWLST="",ORWREC(1)=NIFN_"^1^1^E" ;ACTION^SIG STAT^REL STAT^NAT OF ORDER
 D SEND^ORWDX(.ORWLST,ORVP,ORNP,ORL,ES,.ORWREC)
 S RET(0)=$G(ORWLST(1)) ;BRING BACK SUCCESS INFO
 Q
ORINFO(RET,ORIFN) ;FOR AN ORDER NUMBER, DETERMINE THE CURRENT VERSION AND RETURN
 ;THE ORDER TEXT FOR THAT VERSION IN THE RETURN ARRAY.  3/2/04
 ;UPDATED BY DCN/DSS 3/11/05 
 S RET(1)="-1^NO ORDER NUMBER RECEIVED" Q:+ORIFN<1
 I ORIFN[";" S VER=$P(ORIFN,";",2) I +VER G ON
 I $G(^OR(100,+ORIFN,0))="" S RET(1)="-1^Order does NOT exist" Q
 S VER=$O(^OR(100,+ORIFN,8,9999),-1) I VER="" S RET(1)="-1^Order NODE 8 does NOT exist" Q
ON I $G(^OR(100,+ORIFN,8,VER,.1,0))="" S RET(1)=+ORIFN_";"_VER_"^"_"NO TEXT" Q  ;ADDED DCN/DSS MAR 9, 2005
 S EN=0 F  S EN=$O(^OR(100,+ORIFN,8,VER,.1,EN)) Q:EN=""  S TEXT=^(EN,0) S RET(EN)=+ORIFN_";"_VER_"^"_TEXT
 Q
OICONV(RET,OIEN) ;CONVERT ORDERABLE ITEM (100.43) TO PHARM ORDERABLE (51.7)
 S RET(1)="-1^NO ORDERABLE ITEM NUMBER" S OIDATA=$G(^ORD(101.43,OIEN,0)) Q:OIDATA=""  S ID=$P(OIDATA,"^",2)
 I ID'["PS" S RET(1)="-1^NOT A PHARMACY ORDERABLE" Q
 I ID["PS" S RET(1)=+ID_"^"_$P($G(^PS(50.7,+ID,0)),"^")
 Q
UPDTRT(ORIFN,ROUTE,IVTYPE) ;UPDATE THE ROUTE FOR IV'S THAT HAVE A DIFFERENT VALUE FOR ROUTE THAN IV
 ; MODULE ADDED MAR 22, 2005 DCN/DSS
 N OR4,LC,IENS,PROUTE
 S OR4=$G(^OR(100,+ORIFN,4)) I +OR4<1  Q  ;NO PACKAGE REFERENCE FOUND
 S LC=$L(OR4) I LC<2 Q  ;MUST BE ONE OR MORE NUMBERS FOLLOWIED BY A 'P'
 I $E(OR4,LC)'="P" Q  ;SEE ABOVE
 S IENS=+OR4,PROUTE=$$GET1^DIQ(53.1,IENS,3,"I") I PROUTE'=ROUTE S VEJDFDA(53.1,IENS_",",3)=ROUTE ;ADDED TO FIX ROUTE
 I $L(IVTYPE) S VEJDFDA(53.1,IENS_",",53)=IVTYPE
 D FILE^DIE("K","VEJDFDA","VEJDMES") ;SET INTO FILE
 Q
 ;Either a RADIOLOGY CPT code, or IEN into the ORDERABLE ITEMS file
         ;if an IEN, must be of the form "I"nnnn where nnnn is the IEN
         ;Returns IEN^DESCRIPTION
CPTLOOK(RET,CPTCODE) ;LOOKUP CPT CODE IN ORDERABLE ITEM FILE & RETURN IEN^DESCRIPTION
 S RET="" I CPTCODE?1"i"1.N S CPTCODE=$TR(CPTCODE,"i","I")
 I CPTCODE?1"I"1.9N!(CPTCODE'?5N) D  Q:RET<0  G R2
 . S I=$S(CPTCODE?1"I".E:$E(CPTCODE,2,9),1:+CPTCODE) ; IEN into ORDERABLE ITEMS FILE
 . I $D(^ORD(101.43,+I,9,"B","RAD"))!$D(^("XRAY"))=0 S RET="-1^IEN '"_I_"' is not in the ORDERABLE ITEMS FILE as an XRAY Procedure" Q
 D  I $G(RET)<0 S RET="-1^CPT Code '"_CPTCODE_"' is not in the ORDERABLE ITEMS FILE as an XRAY Procedure" Q
 . I CPTCODE?1"I"1.9N S X=+$E(CPTCODE,2,9) ; this is IEN into ORDERABLE ITEMS FILE
 . S X=$O(^ORD(101.43,"C.XRAY",CPTCODE_" ")) I X="" S RET=-1 Q
 . S X=$O(^ORD(101.43,"C.XRAY",X,"")) I 'X S RET=-1 Q
 . I X S X0=$G(^ORD(101.43,+X,0))  I +$P(X0,"^",3)'=+CPTCODE S RET=-1 Q
 . S I=+X
R2 S X=$G(^ORD(101.43,I,0)),X=$P(X,"^")
 S RET=I_"^"_X Q
