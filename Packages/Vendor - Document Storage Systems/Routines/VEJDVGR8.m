VEJDVGR8 ;DSS/DBB; VISTA GATEWAY - various RPCs.
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
 ; PLACE DIET ORDER
DIET(AXY,PATID,PROVID,OIEN,DELIVERY,EFFDATE,EXDATE,TFLAG,ESIG,ORDTEXT) ;
 S ORL="" I $G(DELIVERY)="" S AXY(0)="No DELIVERY service specified" G Q
 I "T^C^D"'[DELIVERY S AXY(0)="DELIVERY service must be Tray, Cafeteria, or Dining Room" G Q
 S TFLAG=$G(TFLAG) I "C"'[TFLAG S AXY(0)="TFLAG must be NULL or 'C'" G Q
 D NOW^%DTC S NOW=%,DT=NOW\1 K %,%H,%I
 I EFFDATE<NOW S AXY(0)="-1^Cannot be effective before now!" G Q
 I EXDATE'>EFFDATE S AXY(0)="-1^Cannot end before effective date!" G Q
 F I=1:1:$L(OIEN,"^") S X=$P(OIEN,"^",I),ORDIALOG(4,I)=X I '$D(^ORD(101.43,X))!'X S AXY(0)="-1^Orderable Item is null or invalid" G Q
 S ORDIALOG(6,1)=EFFDATE,ORDIALOG(24,1)=EXDATE
 S ORDIALOG(18,1)=DELIVERY,ORDIALOG(175,1)=TFLAG="C"
 S FLAG="D" G GO
 ; PLACE CONSULT OR CLINICAL PROCEDURE ORDER
CONSULT(AXY,PATID,PROVID,OIEN,URGENCY,CATEGORY,INPLACE,ATTEN,DIAG,ORL,ESIG,ORDTEXT) N RET
 K ORDIALOG S OIEN=$G(OIEN) N LIST,LST
 I '$D(^ORD(101.43,+OIEN))!'OIEN S AXY(0)="-1^Orderable Item is null or invalid" G Q
 S ORDIALOG(4,1)=+OIEN ; THIS IS THE ACTUAL "ORDERABLE ITEM"
 S URGENCY=$G(URGENCY),CATEGORY=$G(CATEGORY),INPLACE=$G(INPLACE),ATTEN=$G(ATTEN),DIAG=$G(DIAG),SERV=""
 I CATEGORY["^" S SERV=$P(CATEGORY,"^",2),CATEGORY=$P(CATEGORY,"^")
 I OIEN["P",SERV,'$D(^GMR(123.5,+SERV,0)) S AXY(0)="SERVICE '"_SERV_"' IS NOT IN FILE 123.5" G Q
 I OIEN["P",SERV="",OIEN'["P" S AXY(0)="-1^NO SERVICE SPECIFIED" G Q
 ;I DIAG="" S AXY(0)="-1^DIAGNOSIS MUST BE SPECIFIED"
 S X="" I DIAG?.E1"^".ANP S X=$P(DIAG,"^",2),DIAG=$P(DIAG,"^") ; GET TEXTUAL DISCRIPTION
 I OIEN'["P",DIAG'="",'$D(^ICD9("AB",DIAG_" ")) S AXY(0)="-1^INVALID DIAGNOSIS: "_DIAG_"." G Q
 I X="",DIAG'="" S I=$O(^ICD9("AB",DIAG_" ",0)) I I S X=$P($G(^ICD9(I,0)),"^",3)
 S ORDIALOG(20,1)=X
 I '$F("IO",CATEGORY) S AXY(0)="CATEGORY MUST BE 'I' OR 'O'" G Q
 S ORDIALOG(10,1)=CATEGORY
 ;I '$D(^VA(200,+ATTEN,0)) S AXY(0)="-1^'ATTENTION TO' ENTRY ("_ATTEN_") IS NOT IN FILE 200" G Q
 I '$D(^ORD(101.43,+OIEN))!'OIEN S AXY(0)="-1^Orderable Item is null or invalid" G Q
 S ORDIALOG(4,1)=OIEN ; THIS IS THE ACTUAL "ORDERABLE ITEM"
 S ORL=+$G(ORL) I 'ORL S AXY(0)="-1^LOCATION is NULL or ZERO" G Q
 I URGENCY="" S AXY(0)="-1^Urgency Code is null" G Q
 K LST,LST2 D DEF^ORWDCN32(.LST,$S(OIEN'["P":"C",1:"P"))
 S I=0 F  S I=$O(LST2(I)) Q:'I  S LST($O(LST(""),-1)+1)=LST2(I)
 K LST2 S I=0,L=0 F  S I=$O(LST(I)) Q:'I  D
 . S X=LST(I),X1="~Inpt Place1~Outpt Place2~Inpt "_$S(OIEN["P":"Proc",1:"Cslt")_" Urgencies3"
 . I X1[X S L=+$E(X1,$F(X1,X),99) S LIST(L)=X
 . I $E(X)["~",'L Q
 . I X?1.L1.4E1"^".E S J=$E($P(X,"^"),2,9),LIST(L,J)=X
 I '$D(LIST(1,INPLACE)) S AXY(0)="-1^IN Place Code '"_INPLACE_"' is not valid" G Q
 I '$D(^SC(ORL)) S AXY(0)="-1^HOSPITAL LOCATION: "_ORL_", is invalid" G Q
 I '$D(LIST(3,URGENCY)) S AXY(0)="-1^Urgency Code '"_URGENCY_"' is not valid" G Q
 I $G(AXY(0)) G Q
 S ORDIALOG(15,1)="ORDIALOG(""WP"",15,1)"
 S I="",J=0 F  S I=$O(ORDTEXT(I)) Q:I=""  S J=J+1,ORDIALOG("WP",15,1,J,0)=ORDTEXT(I)
 S ORDIALOG(7,1)=URGENCY,ORDIALOG(10,1)=CATEGORY
 S ORDIALOG(140,1)=INPLACE
 S ORDIALOG(166,1)=SERV
 S ORDIALOG(173,1)=DIAG
 S FLAG=""
 G GO
 Q
 ;VEJDVG LAB PLACE ORDER.
 ;INPUT: PatientIFN,     ;       CPT code of Radiology order
 ;       PROVIDER (defaults to DUZ)
 ;       OIEN = Orderable Items IEN
 ;       SEND = Send Patient code
 ;       Urgency Code
 ;       COLLECT = COLLECTION CODE
 ;       SCHED = SCHEDULE
 ;       ORL=HOSPITAL LOCATION IEN
 ;       Electronic Signature
LABORD(AXY,PATID,PROVID,OIEN,SEND,URGENCY,COLLECT,SAMPLE,SPEC,SCHED,ORL,REQDATE,HOWLONG,ESIG,ORDTEXT) N RET
 K ORDIALOG S OIEN=+$G(OIEN),SAMPLE=$G(SAMPLE) N LIST,LST
 I '$D(^ORD(101.43,+OIEN))!'OIEN S AXY(0)="-1^Orderable Item is null or invalid" G Q
 I OIEN["P",'$$ACTIVE^ORDD43(Y) S AXY(0)="-1^Orderable Item"_+OIEN_" is not ACTIVE" G Q
 S ORDIALOG(4,1)=+OIEN ; THIS IS THE ACTUAL "ORDERABLE ITEM"
 S ORL=+$G(ORL) I 'ORL S AXY(0)="LOCATION is NULL or ZERO" G Q
 S SPEC=$G(SPEC) I SPEC="" S AXY(0)="SPECIMEN is NULL" G Q
 I '$D(^LAB(61,+SPEC,0)) S AXY(0)="-1^SPECIMEN CODE '"_SPEC_"' NOT IN FILE 61" G Q
 ;S SEND=$G(SEND) I SEND="" S AXY(0)="-1^SEND CODE IS NULL" G Q
 S COLLECT=$G(COLLECT) I COLLECT="" S AXY(0)="-1^COLLECT CODE IS NULL" G Q
 I URGENCY="" S AXY(0)="-1^Urgency Code is null" G Q
 K LST,LST2 D DEF^ORWDLR32(.LST,OIEN) D LOAD^ORWDLR32(.LST2,OIEN) K LIST
 S I=0 F  S I=$O(LST2(I)) Q:'I  S LST($O(LST(""),-1)+1)=LST2(I)
 K LST2 S I=0,L=0 F  S I=$O(LST(I)) Q:'I  D
 . S X=LST(I),X1="~Urgencies1~Collection Types2~Send Patient Times3"
 . I X1[X S L=+$E(X1,$F(X1,X),99) S LIST(L)=X
 . I $E(X)["~",'L Q
 . I X?1.L1.4E1"^".E S J=$E($P(X,"^"),2,9),LIST(L,J)=X
 I REQDATE="",OIEN'["P" S AXY(0)="-1^No requested DATE/TIME specified" G Q
 ;I '$D(LIST(3,SEND)) S AXY(0)="-1^SEND Code '"_SEND_"' is not valid" G Q
 I '$D(LIST(1,URGENCY)) S AXY(0)="-1^Urgency Code '"_URGENCY_"' is not valid" G Q
 I '$D(LIST(2,COLLECT)) S AXY(0)="-1^Collection Code '"_COLLECT_"' is not valid" G Q
 I '$D(^SC(ORL)) S AXY(0)="-1^HOSPITAL LOCATION: "_ORL_", is invalid" G Q
 ;I SPEC,'$D(LIST(4,SPEC)) S AXY(0)="-1^Specimen Code '"_SPEC_"' is not valid" G Q
 I $G(AXY(0)) G Q
 S ORDIALOG(15,1)="ORDIALOG(""WP"",15,1)"
 S I="",J=0 F  S I=$O(ORDTEXT(I)) Q:I=""  S J=J+1,ORDIALOG("WP",15,1,J,0)=ORDTEXT(I)
 S ORDIALOG(6,1)=REQDATE,ORDIALOG(180,1)=URGENCY ;ORDIALOG(9,1)=TRANS,ORDIALOG(10,1)=CATEGORY
 S ORDIALOG(153,1)=HOWLONG,ORDIALOG(127,1)=SPEC,ORDIALOG(126,1)=SAMPLE
 S ORDIALOG(29,1)=SCHED
 S ORDIALOG(4)="1^ORDERABLE",ORDIALOG(4,0)="P^101.43:EQSC"
 S ORDIALOG(126,1)=COLLECT,FLAG="L"
GO I '$G(PROVID) S PROVID=$G(DUZ) I 'DUZ S AXY(0)="-1^No provider specified" G Q
 I ESIG="" S AXY(0)="-1^ELECTRONIC SIGNATURE IS NULL" G Q
 I $$DECRYP^XUSRB1(ESIG)="" S ESIG=""
 E  D VALIDSIG^VEJDVGR0(.ESOK,PROVID,ESIG) I ESOK<1 S AXY(0)=ESOK G Q
 S ORDIALOG(17,1)=PROVID
 ;PLACE ORDER HERE
 K RET D EN^VEJDVGR1(.RET,PATID,PROVID,ORL,$S(FLAG="D":"DIET",FLAG="L":"LAB",OIEN["P":"PROC",1:"CON"),ESIG,.ORDIALOG) I $D(RET(0)) S AXY(0)=RET(0) Q
 ;S AXY(0)="0^ORDER PLACED"
Q Q
TESTCON D DT^DICRW
 I '$G(DUZ) S DUZ=10000000032
 S TXT(1)="LINE 1",TXT(2)="LINE 2",TXT(9)="LINE 3"
 K ZZ D CONSULT(.ZZ,421,10000000032,3492,2,"I^","B",2,692.9,56,",aAAZ:4?R/",.TXT)
 Q
TESTPRO D DT^DICRW
 I '$G(DUZ) S DUZ=10000000034
 S TXT(1)="LINE 1",TXT(2)="LINE 2",TXT(9)="LINE 3"
 K ZZ D CONSULT(.ZZ,441,10000000034,"111P",2,"I^40","B",2,692.9,56,"%lbf/tttV ",.TXT)
 Q
TESTDIET D DT^DICRW ;(PATID,PROVID,OIEN,DELIVERY,EFFDATE,EXDATE,ESIG,ORDTEXT
 I '$G(DUZ) S DUZ=10000000032
 S TXT(1)="LINE 1",TXT(2)="LINE 2",TXT(9)="LINE 3"
 K ZZ D DIET(.ZZ,421,10000000032,"3431^136","C",3041228.11,3041231.10,"","&C#",.TXT) ;SIG ,aAAZ:4?R/
 Q
TESTLAB D DT^DICRW
 I '$G(DUZ) S DUZ=10000000032
 S TXT(1)="LINE 1",TXT(2)="LINE 2",TXT(9)="LINE 3"
 K ZZ D LABORD(.ZZ,441,10000000032,210,"LT",2,"SP","O",2,99,56,"T+1@2P","",",aAAZ:4?R/",.TXT) ;SIG FOR "SMITH,DENNIS"
 Q
NXT() S ILST=ILST+1 Q ILST  ; Increment index of LST
REQDET() ; Are "broad" procedures allowed for this division?
 N TMPDIV,RESULT
 S TMPDIV=DUZ(2)
 I $D(EVTDIV),$G(EVTDIV) S DUZ(2)=EVTDIV
 S RESULT=$$GET^XPAR("ALL","RA REQUIRE DETAILED",1,"Q")
 S DUZ(2)=TMPDIV
 Q RESULT
 ;
REQAPPR(IEN) ;  does procedure require radiologist approval?
 N RAIEN,X
 S RAIEN=$P($P($G(^ORD(101.43,IEN,0)),U,2),";",1)
 I +RAIEN=0 Q ""
 Q $P($G(^RAMIS(71,RAIEN,0)),U,11)
 ;
IMTYPE(DGRP) ; return the mnemonic for the imaging type
 Q $P(^ORD(100.98,DGRP,0),U,3)
IMTYPSEL(Y,DUMMY) ;return list of active imaging types
 N X,I,IEN,DGRP,MNEM,NAME
 S X=""
 F I=1:1  S X=$O(^RA(79.2,"C",X)) Q:X=""  D
 . I '$D(^ORD(101.43,"S."_X)) Q
 . S IEN=$O(^RA(79.2,"C",X,0))
 . S NAME=$P(^RA(79.2,IEN,0),U,1)
 . S MNEM=$P(^RA(79.2,IEN,0),U,3)
 . S DGRP=$O(^ORD(100.98,"B",MNEM,0))
 . S Y(I)=IEN_U_NAME_U_MNEM_U_DGRP
 Q
TESTIMT K LIST D IMTYPSEL(.LIST,"")
 Q
