VEJDVSI ;DBB/DSS; VITAL SIGNS
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
 ; RPC: VEJDVS GET QUAL
 ; INPUT: NONE
 ; OUTPUT:
 ; LIST OF QUALIFIERS IN THE FORMAT:
 ;       ien:CATEGORY^ien:VITAL TYPE text^ien:QUALIFIER TEXT ^ ien:default qualifier
 ;       EXAMPLES:
 ;1:LOCATION^1:BLOOD PRESSURE^0:^4:L LEG"
 ;1:LOCATION^2:TEMPERATURE^6:ORAL^46:SKIN"
 ;1:LOCATION^5:PULSE^23:RADIAL^58:ULNAR"
 ;1:LOCATION^20:CIRCUMFERENCE/GIRTH^0:^69:THIGH"
 ;2:METHOD^1:BLOOD PRESSURE^59:PALPATED^65:NON-INVASIVE"
 ;2:METHOD^1:BLOOD PRESSURE^59:PALPATED^60:DOPPLER"
 ;2:METHOD^3:RESPIRATION^47:SPONTANEOUS^49:ASSISTED VENTILATOR"
 ;5:SITE^5:PULSE^0:^63:LEFT
GETQUAL(AXY) ;VITAL TYPE, CATEGORY
 N VEJDMES,VEJDDATA,VEJDFIL
 K AXY2 F FIL=120.51,120.52,120.53 D
 . N VEJDMES,VEJDDATA
 . D FILE^DID(FIL,"N","NAME","VEJDDATA","VEJDMES") S VEJDFIL(FIL)=VEJDDATA("NAME")
 . K VEJDDATA,VEJDMES D LIST^DIC(FIL,,"","","*",,,,,,"VEJDDATA","VEJDMES")
 . F I=1:1:VEJDDATA("DILIST",0) S X=VEJDDATA("DILIST",2,I)_"^"_VEJDDATA("DILIST",1,I),VEJDFIL(FIL,+X)=$P(X,"^",2) D:FIL=120.53
 .. S J=0 ;,X=^GMRD(120.53,I,0)
 .. F  S J=$O(^GMRD(120.53,I,1,J)) Q:'J  S X0=$G(^(J,0)) Q:X0=""  S K=$P(X0,"^",7) I K S VEJDFIL(120.53,I,+X0)=K
 . ;M VEJDFIL(FIL)=VEJDDATA("DILIST",1)
 S I=0 F  S I=$O(VEJDFIL(120.51,I)) Q:'I  S AXY2(200+I)="0^"_I_":"_VEJDFIL(120.51,I)
 S I=0 F  S I=$O(^GMRD(120.52,I)) Q:'I  D
 . S J=0 F  S J=$O(^GMRD(120.52,I,1,J)) Q:'J  D
 .. S X0=$G(^GMRD(120.52,I,0)),X1=^GMRD(120.52,I,1,J,0) S VQUAL=I_":"_$P(X0,"^")
 .. S VTYP=$P(X1,"^"),VCAT=$P(X1,"^",2)
 .. S VTYP=$$GET(120.51,VTYP),VCAT=$$GET(120.53,VCAT)
 .. S X=$G(VEJDFIL(120.53,+VCAT,+VTYP)),VDEF=+X_":"_$G(VEJDFIL(120.52,+X)) ;B:+VTYP=5
 .. K AXY2(200+VTYP)
 .. S AXY2(+VTYP,+VCAT,I,J)=VCAT_"^"_VTYP_"^"_VQUAL_"^"_VDEF
 I $D(AXY2)<10 S AXY(0)="-1^NO VITAL TYPE INFO" Q
 S X="AXY2(0)"
 K AXY F I=1:1 S X=$Q(@X) Q:X=""  S AXY(I)=@X
 K AXY2 Q
GET(FIL,X) ;
 Q +X_":"_$G(VEJDFIL(FIL,+X))
TEST K ZZ D GETQUAL(.ZZ)
 Q
