VEJDWPB6 ;WPB/CAM routine modified for dental GUI;8/1/98;7/10/98
 ;;3.5;VEJD DSS CORE RPCS;;Jan 03, 2006
 ;Copyright 1995-2006, Document Storage Systems, Inc., All Rights Reserved
 ;; SLC/MKB - Problem List utilities for OE/TIU ;12/9/97  12:06
 ;GMPLUTL2;2.0;Problem List;**10**;Aug 25, 1994
LIST(GMPL,GMPDFN,GMPSTAT,GMPCOMM) ; -- Returns list of problems for patient GMPDFN
 ; in GMPL(#)=ifn^status^description^ICD^onset^last modified^SC^SpExp^*^$
 ;    GMPL(#,C#)=Comments
 ;  & GMPL(0)=number of problems returned
 ;
 N I,IFN,CNT,GMPL0,GMPL1,SP,ST,NUM,ONSET,ICD,LASTMOD,SC,GMPLIST,GMPLVIEW,GMPARAM,GMPTOTAL
 Q:$G(GMPDFN)'>0  S CNT=0,SP=""
 S GMPARAM("QUIET")=1,GMPARAM("REV")=$P($G(^GMPL(125.99,1,0)),U,5)="R"
 S GMPLVIEW("ACT")=GMPSTAT,GMPLVIEW("PROV")=0,GMPLVIEW("VIEW")=""
 D GETPLIST^VEJDWPB7(.GMPLIST,.GMPTOTAL,.GMPLVIEW)
 F NUM=0:0 S NUM=$O(GMPLIST(NUM)) Q:NUM'>0  D
 . S IFN=+GMPLIST(NUM) Q:IFN'>0
 . S GMPL0=$G(^AUPNPROB(IFN,0)),GMPL1=$G(^(1)),CNT=CNT+1
 . S ICD=$P($G(^ICD9(+GMPL0,0)),U),LASTMOD=$P(GMPL0,U,3)
 . S ST=$P(GMPL0,U,12),ONSET=$P(GMPL0,U,13)
 . S SC=$S(+$P(GMPL1,U,10):"SC",$P(GMPL1,U,10)=0:"NSC",1:"")
 . S SP="" F I=11,12,13 S:$P(GMPL1,U,I) SP=SP_$S(I=11:"A",I=12:"I",1:"P")
 . S GMPL(CNT)=IFN_U_ST_U_$$PROBTEXT^GMPLX(IFN)_U_ICD_U_ONSET_U_LASTMOD_U_SC_U_SP_U_$S($P(GMPL1,U,14)="A":"*",1:"")_U_$S('$P($G(^GMPL(125.99,1,0)),U,2):"",$P(GMPL1,U,2)'="T":"",1:"$")
 . I $G(GMPCOMM) D  ; if comments should be returned
 . . N FAC,NIFN,NOTE,NOTECNT
 . . S NOTECNT=0
 . . S FAC=$O(^AUPNPROB(IFN,11,"B",+$G(DUZ(2)),0)) Q:'FAC
 . . F NIFN=0:0 S NIFN=$O(^AUPNPROB(IFN,11,FAC,11,"B",NIFN)) Q:NIFN'>0  D
 . . . S NOTE=$P($G(^AUPNPROB(IFN,11,FAC,11,NIFN,0)),U,3)
 . . . S NOTECNT=NOTECNT+1,GMPL(CNT,NOTECNT)=NOTE
 S GMPL(0)=CNT
 Q
 ;
DETAIL(IFN,GMPL) ; -- Returns detailed data for problem IFN
 ;   in GMPL("DATA NAME")=external format of value
 ;
 N GMPL0,GMPL1,X,I,FAC,CNT,GMPVAMC
 Q:'$D(^AUPNPROB(IFN,0))  ; no such problem
 S GMPL0=$G(^AUPNPROB(IFN,0)),GMPL1=$G(^(1))
 S GMPL("DIAGNOSIS")=$P($G(^ICD9(+GMPL0,0)),U)
 S GMPL("PATIENT")=$P($G(^DPT(+$P(GMPL0,U,2),0)),U)
 S GMPL("MODIFIED")=$$EXTDT^GMPLX($P(GMPL0,U,3))
 S GMPL("NARRATIVE")=$$PROBTEXT^GMPLX(IFN)
 S GMPL("ENTERED")=$$EXTDT^GMPLX($P(GMPL0,U,8))_U_$P($G(^VA(200,+$P(GMPL1,U,3),0)),U)
 S X=$P(GMPL0,U,12),GMPL("STATUS")=$S(X="A":"ACTIVE",1:"INACTIVE")
 S X=$S(X'="A":"",1:$P(GMPL1,U,14)),GMPL("PRIORITY")=$S(X="A":"ACUTE",X="C":"CHRONIC",1:"")
 S GMPL("ONSET")=$$EXTDT^GMPLX($P(GMPL0,U,13))
 S GMPL("PROVIDER")=$P($G(^VA(200,+$P(GMPL1,U,5),0)),U)
 S GMPL("RECORDED")=$$EXTDT^GMPLX($P(GMPL1,U,9))_U_$P($G(^VA(200,+$P(GMPL1,U,4),0)),U)
 S GMPL("CLINIC")=$P($G(^SC(+$P(GMPL1,U,8),0)),U)
 S GMPL("SC")=$S($P(GMPL1,U,10):"YES",$P(GMPL1,U,10)=0:"NO",1:"UNKNOWN")
 S GMPL("EXPOSURE")=0
 I $P(GMPL1,U,11) S X=GMPL("EXPOSURE")+1,GMPL("EXPOSURE",X)="AGENT ORANGE",GMPL("EXPOSURE")=X
 I $P(GMPL1,U,12) S X=GMPL("EXPOSURE")+1,GMPL("EXPOSURE",X)="RADIATION",GMPL("EXPOSURE")=X
 I $P(GMPL1,U,13) S X=GMPL("EXPOSURE")+1,GMPL("EXPOSURE",X)="ENV CONTAMINANTS",GMPL("EXPOSURE")=X
 S CNT=0,GMPL("COMMENT")=0
 S GMPVAMC=+$G(DUZ(2)),FAC=$O(^AUPNPROB(IFN,11,"B",+GMPVAMC,0)) Q:'FAC
 F I=0:0 S I=$O(^AUPNPROB(IFN,11,FAC,11,"B",I)) Q:I'>0  D
 . S X=$G(^AUPNPROB(IFN,11,FAC,11,I,0)) ;Q:$P(X,U,6)'=DUZ;must be author
 . S CNT=CNT+1,GMPL("COMMENT",CNT)=$$EXTDT^GMPLX($P(X,U,5))_U_$P($G(^VA(200,+$P(X,U,6),0)),U)_U_$P(X,U,3) ; date^author^text of note
 S GMPL("COMMENT")=CNT
 Q
 ;
ADD(DFN,LOC,GMPROV) ; -- Interactive LMgr action to add new problem
 N X,Y,GMPDFN,GMPVA,GMPVAMC,GMPSC,GMPAGTOR,GMPION,GMPGULF,GMPARAM,GMPLVIEW,GMPLUSER,GMPCLIN,GMPLSLST,GMPQUIT,VALMCC,GMPSAVED
 Q:'DFN  Q:'LOC  D SETVARS
 S GMPLSLST=$P($G(^VA(200,DUZ,125)),U,2),VALMCC=0
 I 'GMPLSLST,GMPCLIN,$D(^GMPL(125,"C",+GMPCLIN)) S GMPLSLST=$O(^(+GMPCLIN,0))
 I GMPLSLST D  Q
 . S $P(GMPLSLST,U,2)=$P($G(^GMPL(125,+GMPLSLST,0)),U)
 . D EN^VALM("GMPL LIST MENU")
 F  D ADD^GMPL1 Q:$D(GMPQUIT)  K DUOUT,DTOUT,GMPSAVED W !!,">>>  Please enter another problem, or press <return> to exit."
 Q
 ;
SETVARS ; -- Define GMP* variables used in ADD and EDIT
 N VA,VADM,VAEL,VASV
 Q:'DFN  D DEM^VADPT,7^VADPT
 S GMPDFN=DFN_U_VADM(1)_U_$E(VADM(1))_VA("BID")_$S(VADM(6):U_+VADM(6),1:""),AUPNSEX=$P(VADM(5),U),GMPVA=1,GMPSC=VAEL(3),GMPAGTOR=VASV(2),GMPION=VASV(3)
 S X=$P($G(^DPT(DFN,.322)),U,10),GMPGULF=$S(X="Y":1,X="N":0,1:"")
 S GMPLVIEW("VIEW")=$S($P($G(^SC(+$G(LOC),0)),U,3)="C":"C",1:"S")
 S GMPCLIN="" I $G(LOC),GMPLVIEW("VIEW")="C" S GMPCLIN=+LOC_U_$P(^SC(+LOC,0),U)
 S X=$$PARAM,GMPARAM("VER")=+$P(X,U,2),GMPARAM("CLU")=+$P(X,U,4),GMPARAM("REV")=+$P(X,U,5)
 S:+GMPROV=DUZ GMPLUSER=1 S GMPVAMC=+$G(DUZ(2)),GMPLIST(0)=0
 Q
 ;
EDIT(DFN,LOC,GMPROV,GMPIFN) ; Interactive LMgr action to edit a problem
 N GMPARAM,GMPDFN,GMPVA,GMPSC,GMPAGTOR,GMPION,GMPGULF,GMPLVIEW,GMPCLIN,GMPLJUMP,GMPQUIT,GMPLUSER,GMPLVAMC,AUPNSEX
 L +^AUPNPROB(GMPIFN,0):1 I '$T W $C(7),!!,$$LOCKED^GMPLX,! H 2 Q
 D SETVARS,EN^VALM("GMPL EDIT PROBLEM")
 L -^AUPNPROB(GMPIFN,0)
 Q
 ;
REMOVE(GMPIFN,GMPROV,TEXT,PLY) ; -- Remove problem GMPIFN
 N GMPVAMC,CHANGE
 S GMPVAMC=+$G(DUZ(2)),PLY=-1,PLY(0)=""
 I '$L($G(^AUPNPROB(GMPIFN,0))) S PLY(0)="Invalid problem" Q
 I '$D(^VA(200,+$G(GMPROV),0)) S PLY(0)="Invalid provider" Q
 I $L($G(TEXT)) S GMPFLD(10,"NEW",1)=TEXT D NEWNOTE^GMPLSAVE
 S CHANGE=GMPIFN_"^1.02^"_$$HTFM^XLFDT($H)_U_DUZ_U_$P($G(^AUPNPROB(GMPIFN,1)),U,2)_"^H^Deleted^"_+$G(GMPROV),$P(^AUPNPROB(GMPIFN,1),U,2)="H",PLY=GMPIFN
 D AUDIT^GMPLX(CHANGE,""),DTMOD^GMPLX(GMPIFN)
 Q
 ;
PARAM() ; -- Returns parameter values from 125.99
 Q $G(^GMPL(125.99,1,0))
